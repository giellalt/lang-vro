! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root
< %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
 NUMBERSECTION ;
!+NumNum NUMBERSECTION ;
!+NumNum COMMASECTION ;


LEXICON NUMBERSECTION
HUNDREDSM ;		! 200M
sada:1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
sada:1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tuhat:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
katś:2 CUODIM ;
kolm:3 CUODIM ;
neli:4 CUODIM ;
nelli+Use/NG:4 CUODIM ;
viiś:5 CUODIM ;
kuuś:6 CUODIM ;
säidse:7 CUODIM ;
katõsa:8 CUODIM ;
katõssa+Use/NG:8 CUODIM ;
ütesä:9 CUODIM ;
ütessä+Use/NG:9 CUODIM ;

LEXICON CUODIM
sada: HUNDREDM ;

LEXICON HUNDREDM
% :      TENSM ;
% :      TEENSM ;
% :%0   ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
ütśtõist:11 LOHKAIM ;
katśtõist:12 LOHKAIM ;
kolmtõist:13 LOHKAIM ;
nelitõist:14 LOHKAIM ;
nellitõist+Use/NG:14 LOHKAIM ;
viiśtõist:15 LOHKAIM ;
kuuśtõist:16 LOHKAIM ;
säidsetõist:17 LOHKAIM ;
katõsatõist:18 LOHKAIM ;
katõssatõist+Use/NG:18 LOHKAIM ;
ütesätõist:19 LOHKAIM ;
ütessätõist+Use/NG:19 LOHKAIM ;


LEXICON LOHKAIM
 MILJON ;
kümme+Use/NG: MILJON ;
kümmend+Use/NG: MILJON ;

LEXICON TENSM
kümme:1%0 MILJON ;
katś:2 LUHKIEM ;
kolm:3 LUHKIEM ;
neli:4 LUHKIEM ;
nelli+Use/NG:4 LUHKIEM ;
viiś:5 LUHKIEM ;
kuuś:6 LUHKIEM ;
säidse:7 LUHKIEM ;
katõsa:8 LUHKIEM ;
katõssa+Use/NG:8 LUHKIEM ;
ütesä:9 LUHKIEM ;
ütessä+Use/NG:9 LUHKIEM ;


LEXICON LUHKIEM
kümmend:%0 MILJON ;
kümmend% :   ONESM ;

LEXICON ONESM
ütś% millʼon% :1 OVERTHOUSANDS ;
katś:2 MILJON ;
kolm:3 MILJON ;
neli:4 MILJON ;
nelli+Use/NG:4 MILJON ;
viiś:5 MILJON ;
kuuś:6 MILJON ;
säidse:7 MILJON ;
katõsa:8 MILJON ;
katõssa+Use/NG:8 MILJON ;
ütesä:9 MILJON ;
ütessä+Use/NG:9 MILJON ;


LEXICON MILJON
% millʼonat% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
sada:1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
% :%0 TEENST ;		!  10.000.000
:%0%0 ONEST ;		!   2.000.000
tuhat:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	!  x000000

LEXICON HUNDREDST
katś:2   CUODIT ;	! 200000-299999
kolm:3    CUODIT ;	! 300000-399999
neli:4 CUODIT ;	! 400000-499999
nelli+Use/NG:4  CUODIT ;
viiś:5   CUODIT ;	! 500000-599999
kuuś:6   CUODIT ;	! 600000-699999
säidse:7 CUODIT ;	! 700000-799999
katõssa+Use/NG:8 CUODIT ;	! 800000-899999
katõsa:8 CUODIT ;	! 800000-899999
ütessä+Use/NG:9   CUODIT ;	! 900000-999999
ütesä:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
sada: HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
% : TENST ;			! X2XYYY, X10YYY
% : TEENST ;		! X1XYYY
% ütś:%01 THOUSANDS ;
% :%0 XONEST ;		! XX0YYY
% :%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
ütśtõist:11 TEENT ;
katśtõist:12 TEENT ;
kolmtõist:13 TEENT ;
nelitõist:14 TEENT ;
nellitõist+Use/NG:14 TEENT ;
viiśtõist:15 TEENT ;
kuuśtõist:16 TEENT ;
säidsetõist:17 TEENT ;
katõsatõist:18 TEENT ;
katõssatõist+Use/NG:18 TEENT ;
ütesätõist:19 TEENT ;
ütessätõist+Use/NG:19 TEENT ;
kümme:1%0 THOUSANDS ;

LEXICON TEENT
 LOHKAIT ;
kümme+Use/NG: LOHKAIT ;
kümmend+Use/NG: LOHKAIT ;

LEXICON LOHKAIT
THOUSANDS ;

LEXICON TENST
kümme:1%0 THOUSANDS ;
kümnes:1%0%. THOUSANDS ;
katś:2 LUHKIET ;
kolm:3 LUHKIET ;
neli:4 LUHKIET ;
nelli+Use/NG:4 LUHKIET ;
viiś:5 LUHKIET ;
kuuś:6 LUHKIET ;
säidse:7 LUHKIET ;
katõsa:8 LUHKIET ;
katõssa+Use/NG:8 LUHKIET ;
ütesä:9 LUHKIET ;
ütessä+Use/NG:9 LUHKIET ;

LEXICON LUHKIET
kümmend% :%0 THOUSANDS ;
ütś% tuhat:1 THOUSAND ;
kümmend% : XONEST ;

LEXICON XONEST
katś% :2 THOUSANDS ;
kolm% :3 THOUSANDS ;
neli% :4 THOUSANDS ;
nelli+Use/NG% :4 THOUSANDS ;
viiś% :5 THOUSANDS ;
kuuś% :6 THOUSANDS ;
säidse% :7 THOUSANDS ;
katõsa% :8 THOUSANDS ;
katõssa+Use/NG% :8 THOUSANDS ;
ütesä% :9 THOUSANDS ;
ütessä+Use/NG% :9 THOUSANDS ;

LEXICON ONEST
katś:2 THOUSANDS ;
kolm:3 THOUSANDS ;
neli:4 THOUSANDS ;
nelli+Use/NG:4 THOUSANDS ;
viiś:5 THOUSANDS ;
kuuś:6 THOUSANDS ;
säidse:7 THOUSANDS ;
katõsa:8 THOUSANDS ;
katõssa+Use/NG:8 THOUSANDS ;
ütesä:9 THOUSANDS ;
ütessä+Use/NG:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
tuhat% : THOUSAND ;

LEXICON THOUSAND
% : UNDERTHOUSAND ;	   ! 1100-1999
% :%0 TENS ;	   ! 1020-1099
% :%0 TEENS ;	   ! 1010-1019
% :%0%0 ONES ;	   ! 1001-1009
:%0%0%0 COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
sada:1 HUNDRED ;
tjuatede:1  CUODAAT ;

LEXICON HUNDREDS
katś:2 CUODI ;
kolm:3 CUODI ;
neli:4 CUODI ;
nelli+Use/NG:4 CUODI ;
viiś:5 CUODI ;
kuuś:6 CUODI ;
säidse:7 CUODI ;
katõsa:8 CUODI ;
katõssa+Use/NG:8 CUODI ;
ütesä:9 CUODI ;
ütessä+Use/NG:9 CUODI ;


LEXICON CUODI
sada:   HUNDRED ;
tjuatede:  CUODAAT ;

LEXICON HUNDRED
% :      TENS ;
% :      TEENS ;
% :%0   ONES ;
:%0%0 COMMA ;

LEXICON CUODAAT
:%0%0%.    COMMA ;

LEXICON TEENS
ütśtõist:11 TEENS-VARIATION ;
katśtõist:12 TEENS-VARIATION ;
kolmtõist:13 TEENS-VARIATION ;
nelitõist:14 TEENS-VARIATION ;
nellitõist+Use/NG:14 TEENS-VARIATION ;
viiśtõist:15 TEENS-VARIATION ;
kuuśtõist:16 TEENS-VARIATION ;
säidsetõist:17 TEENS-VARIATION ;
katõsatõist:18 TEENS-VARIATION ;
katõssatõist+Use/NG:18 TEENS-VARIATION ;
ütesätõist:19 TEENS-VARIATION ;
ütessätõist+Use/NG:19 TEENS-VARIATION ;


LEXICON TEENS-VARIATION
 COMMA ;
kümme+Use/NG: COMMA ;
kümmend+Use/NG: COMMA ;


LEXICON TENS
kümme:1%0 COMMA ;
kümnes:1%0%. COMMA ;
katś:2 LUHKIE ;
kolm:3 LUHKIE ;
neli:4 LUHKIE ;
nelli+Use/NG:4 LUHKIE ;
viiś:5 LUHKIE ;
kuuś:6 LUHKIE ;
säidse:7 LUHKIE ;
katõsa:8 LUHKIE ;
katõssa+Use/NG:8 LUHKIE ;
ütesä:9 LUHKIE ;
ütessä+Use/NG:9 LUHKIE ;

LEXICON LUHKIE
kümmend:%0 COMMA ;
!låhkede:%0%. COMMA ;
kümmend% : ONES ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
ütś:1 COMMA ;
katś:2 COMMA ;
kolm:3 COMMA ;
neli:4 COMMA ;
nelli+Use/NG:4 COMMA ;
viiś:5 COMMA ;
kuuś:6 COMMA ;
säidse:7 COMMA ;
katõsa:8 COMMA ;
katõssa+Use/NG:8 COMMA ;
ütesä:9 COMMA ;
ütessä+Use/NG:9 COMMA ;

LEXICON ORDINAL
voestes:1%.     COMMA ;
mubpie:2%.   COMMA ;
gåalmede:3%.   COMMA ;
njealjede:4%. COMMA ;
vïjhtede:5%.   COMMA ;
govhtede:6%.   COMMA ;
tjïjhtjede:7%.    COMMA ;
gaaktsede:8%.   COMMA ;
åktsede:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
 % la% :%=         Root ; 
 % gráda% :%°      Root ; 
 % paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
 % ja% :&          Root ; 

LEXICON ENDLEX
 # ;
