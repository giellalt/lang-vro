!! Verb inflection
! ---------------
!! Võro language verbs inflect for person and number.

LEXICON V_EI
+V+Neg+Prs:i # ;
+V+Neg+Prt:s # ;
+V+Neg+Prt:siq # ;

LEXICON V_48KASUMA
!! There are other verbs here, cf. V_ELÄMÄ
!! * Yaml: __V-kasuma48__
+V:%{back%} V_48KASUMA/XX ;

LEXICON V_48KASUMA/XX
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGStem V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_i ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>õ%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_48KUIOMA
!! There are other verbs here, cf. V_ELÄMÄ
!! * Yaml: __V-kuioma48__
+V:%{back%} V_48KUIOMA/XX ;

LEXICON V_48KUIOMA/XX
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGStem V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_i ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>õ%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_48PALAMA
!! There are other verbs here, cf. V_ELÄMÄ
!! * Yaml: __V-palama48__
+V:%{back%} V_48PALAMA/ELÄMÄ ;
LEXICON V_48PALAMA/ELÄMÄ
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGStem V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_i ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>%{eõ%}%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_48ELÄMÄ
!! There are other verbs here, cf. V_ELÄMÄ
!! * Yaml: ____
+V:%{front%} V_48PALAMA/ELÄMÄ ;


LEXICON V_ELÄMÄ !!≈ * __@CODE@__ elämä:elä
!! * Yaml: __elaemae48__
+V:%{front%} V_48PALAMA/ELÄMÄ ;

LEXICON V_48SADAMA
!! There are other verbs here, cf. V_ELÄMÄ
!! * Yaml: __V-sadama48__
+V:%{back%} V_48SADAMA/XX ;
LEXICON V_48SADAMA/XX
:%^StrGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^XStrGStem%>%{eõ%} ACT-IND-PRT-1/2_ZERO ;
!! Act_Prt_Sg1: sattõ
: ACT-IND-CONNEG ;
:%^XStrGStem%>%{eõ%} ACT-IND-PRT-SG3_ZERO ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>%{aä%} Harm-Neutr_PSS-PRFPRC_t ;
!! Pss_PrfPrc: sadat

LEXICON V_49LÕPMA !!≈ * __@CODE@__ lõpma:lõpp
+V:%{back%} V_49LÕPMA/XX ;
LEXICON V_49LÕPMA/XX !!≈ * __@CODE@__ lõpma:lõpp
:%^WGStem Harm-Neutr_INF_mA ;
:%^WGStem Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
!lõp
+Act+Ind+Prs+Sg1:%^WGStem%>%{eõ%} K ;
+Act+Ind+Prs+Sg2:%^WGStem%>%{eõ%}t K ;
+Use/NG+Act+Ind+Prs+Sg2:%^WGStem%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prs+Pl1:%^WGStem%>%{eõ%}miq K ;
+Act+Ind+Prs+Pl2:%^WGStem%>%{eõ%}tiq K ;

+Act+Ind+Prs+Sg3:%^WGStem%>%{eõ%}s K ;
+Act+Ind+Prs+Pl3:%^WGStem%>v%{aä%}q K ;

! lõpp
:%{eõ%} Harm-Neutr_INF_ZEROq ;
+Act+Ind+Prt+Sg1:%>i K ;
+Act+Ind+Prt+Sg2:%>it K ;
+Use/NG+Act+Ind+Prt+Sg2:%>iq K ;
+Use/NG+Act+Ind+Prt+Sg2:%>id%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>imiq K ;
+Act+Ind+Prt+Pl2:%>itiq K ;
+Act+Ind+Prt+Sg3:%>i K ;

:%>i ACT-IND-PRT-PL3_q/vAq ;

:%^WGStem ACT-PRSPRC_vA ;
:%^WGStem%>%{eõ%} ACT-IND-CONNEG ;
:%^WGStem MUTUAL_JUS_kuq ;


LEXICON V_50TONISÕMA !!≈ * __@CODE@__ tonisõma:tonis
!! * Yaml: __toniswma50__
+V:%{back%} V_50TONISÕMA/HELISEMÄ ;
LEXICON V_50TONISÕMA/HELISEMÄ !!≈ * __@CODE@__ tonisõma:tonis
:%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: V0-IND-PRT-SC-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_i ;
!+V:%{XC%} Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
: MUTUAL_JUS_kuq ;

LEXICON V_50HELISEMÄ !!≈ * __@CODE@__ helisemä:helis
!! * Yaml: __helisemae50__
+V:%{front%} V_50TONISÕMA/HELISEMÄ ;


LEXICON V_51NÕSÕMA !!≈ * __@CODE@__ nõsõma:nõs
!! * Yaml: __nwswma51__
!! cf. tulõma
+V:%{back%} V_51NÕSÕMA/PÄSEMÄ ;
LEXICON V_51PÄSEMÄ !!≈ * __@CODE@__ päsemä:päs
!! * Yaml: __paesemae51__
!! cf. tulõma
+V:%{front%} V_51NÕSÕMA/PÄSEMÄ ;

LEXICON V_51NÕSÕMA/PÄSEMÄ !!≈ * __@CODE@__ nõsõma:nõs
:%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%{XC%} V0-IND-PRT-SC-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
:%{XC%} ACT-IND-PRT-SG3_i ;
!:%{XC%} Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_52ALOSTÕLÕMA !!≈ * __@CODE@__ alostõlõma:alostõll
!! * Yaml: __alostwllwma52__
+V:%{back%} V_52ALOSTÕLÕMA/EHITELEMÄ ;
LEXICON V_52EHITELEMÄ !!≈ * __@CODE@__ ehitelemä:ehitell
!! ehitelemä:ehitell
!! * Yaml: __ehitellemae52__
+V:%{front%} V_52ALOSTÕLÕMA/EHITELEMÄ ;

LEXICON V_52ALOSTÕLÕMA/EHITELEMÄ !!≈ * __@CODE@__ alostõllõma:alostõl
:%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^WGStem%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%^WGStem%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%^WGStem PSS-IND-PRS_dA/SG1 ;
+Act+Ind+Prt+Pl3:%^WGStem%>iq K ;
+Act+Ind+Prt+Pl3:%^WGStem%>iv%{aä%}q K ;

:%^WGStem V0-IND-PRT-SC-12_i ;
: V0-IND-PRT-SC-12_i ;
:%^WGStem%{eõ%} ACT-IND-CONNEG ;
:%^WGStem ACT-IND-PRT-SG3_i ;
: ACT-IND-PRT-SG3_i ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^WGStem%{eõ%} Harm-Neutr_INF_mA ;
+Use/NG:%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_Aq ;
:%^WGStem%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;

:%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;
:%^WGStem PSS-PRFPRC_dU-SG-NOM ;
: MUTUAL_JUS ;


LEXICON V_53KAEMA !!≈ * __@CODE@__ 
+V:%{back%} V_53KAEMA/JÄÄMÄ ;
LEXICON V_53JÄÄMÄ !!≈ * __@CODE@__ 
+V:%{front%} V_53KAEMA/JÄÄMÄ ;
LEXICON V_53KAEMA/JÄÄMÄ !!≈ * __@CODE@__ 
:%^VOWRaise V_77JUUMA/SÜÜMÄ_RAISED-VOWEL ; !!= * @CODE@ jää kii
: V_77JUUMA/SÜÜMÄ_NOT-RAISED-VOWEL ; !!= * @CODE@ jäänüq keenüq
:%^VOWRaise%^VowRM%>i V_77JUUMA/SÜÜMÄ_RAISED-VOWEL-TENSE ; !!= * @CODE@ jäi kii
:%^VowRM%>i V_77JUUMA/SÜÜMÄ_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ jäi kei
! CHECKME 2016-08-28
:%^VOWRaise ACT-IND-PRS-3_s/sEq ; !!= * @CODE@ (5) RAISED jääs jääseq 
! Act+Ind+Prs+Sg3

: ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) joovaq sööväq


LEXICON V_54ISTMA !!≈ * __@CODE@__ 
+V+WORK: # ;

LEXICON V_55KÕNÕLÕMA !!≈ * __@CODE@__ kõnõlõma:kõ̭nõl
!! * Yaml: __kwnwlwma55__
+V:%{back%} V_55KÕNÕLÕMA/HÄRELEMÄ ;
LEXICON V_55HÄRELEMÄ !!≈ * __@CODE@__ härelemä:härel
!! * Yaml: __haerelemae55__
+V:%{front%} V_55KÕNÕLÕMA/HÄRELEMÄ ;

LEXICON V_55KÕNÕLÕMA/HÄRELEMÄ
:%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
+Use/NG:%{aä%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%{aä%} ACT-IND-PRS-3_s/sEq ;
: PSS-IND-PRS_dA/SG1 ;

: V0-IND-PRT-SC-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
+Use/NG:%{aä%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-PRT-SG3_i ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: PSS-PRFPRC_d ;
: PSS-PRFPRC_dU-SG-NOM ;
: MUTUAL_JUS ;


LEXICON V_56HIIBÕLÕMA !!≈ * __@CODE@__ hiibõlõma:hiibõl
!! * Yaml: __hiibwlwma56__
+V:%{back%} V_56HIIBÕLÕMA/HEIKELEMÄ ;
LEXICON V_56HEIKELEMÄ !!≈ * __@CODE@__ heikelemä:heikel
!! * Yaml: __heikelemae56__
+V:%{front%} V_56HIIBÕLÕMA/HEIKELEMÄ ;

LEXICON V_56HIIBÕLÕMA/HEIKELEMÄ !!≈ * __@CODE@__ hiibõlõma:hiibõl
:%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:ʼ ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_iAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: V0-IND-PRT-SC-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
!: ACT-IND-PRT-SG3_i ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;


LEXICON V_57TAPLÕMA
!! taplõma:tapõl
!! * Yaml: __taplwma57__
+V:%{back%} V_57TAPLÕMA/ÜTLEMÄ ;
LEXICON V_57ÜTLEMÄ 
!! ütlemä:ütel
!! * Yaml: __kiirdlemae57__
+V:%{front%} V_57TAPLÕMA/ÜTLEMÄ ;

LEXICON V_57TAPLÕMA/ÜTLEMÄ
:%^Pen%^VowRM%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^VowRM%{eõ%} ACT-IND-PRS-3_s/sEq ;
: PSS-IND-PRS_dA/SG1 ;

:%^Pen%^VowRM V0-IND-PRT-SC-12_i ;
:%^Pen%^VowRM%>%{eõ%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;

:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_INF_mA ;
:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_INF_dAq ;
: Harm-Neutr_PSS-PRFPRC_dU ;
: MUTUAL_JUS ;

LEXICON V_57VÕITLÕMA
!! võitlõma:võitõl
!! * Yaml: __taplwma57__
+V:%{back%} V_57VÕITLÕMA/KIIRDLEMÄ ;
LEXICON V_57KIIRDLEMÄ 
!! kiirdlemä:keerdel
!! * Yaml: __kiirdlemae57__
+V:%{front%} V_57VÕITLÕMA/KIIRDLEMÄ ;

LEXICON V_57VÕITLÕMA/KIIRDLEMÄ
:%^Pen%^VowRM%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^VowRM%{eõ%} ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^VowRM V0-IND-PRT-SC-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;

:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_INF_mA ;
:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;

:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_dU ;


LEXICON V_58HIRNAHTAMA !!≈ * __@CODE@__ hirnahtama:hirnahta
!! * Yaml: __V-hirnahtama58__
+V:%{back%} V_58HIRNAHTAMA/TÄHENDÄMÄ ;

LEXICON V_58TÄHENDÄMÄ !!≈ * __@CODE@__ tähendämä:tähendä
!! * Yaml: __V-hirnahtama58, V-taehendaemae__
!! front
+V:%{front%} V_58HIRNAHTAMA/TÄHENDÄMÄ ;

LEXICON V_58HIRNAHTAMA/TÄHENDÄMÄ !!≈ * __@CODE@__ hirnahtama:hirnahta, tähendämä:tähend
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^VowRM V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^VowRM%>e Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>e Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_59KAOTAMA !!≈ * __@CODE@__ kaotama:kaota
!!
+V:%{back%} V_59KAOTAMA/KERGÜTÄMÄ ;
LEXICON V_59KERGÜTÄMÄ
!! Based on 59KAOTAMA
!! * Yaml: __kerguetaemae__
+V:%{front%} V_59KAOTAMA/KERGÜTÄMÄ ;

LEXICON V_59KAOTAMA/KERGÜTÄMÄ !!≈ * __@CODE@__ kaotama:kaota
:%^Pen%^WGStem Harm-Neutr_ACT-IND-PRS-SG1_ZERO_USUALLY-WEAK ; !!= * @CODE@ +Act+Ind+Prs+Sg1: kaoda
!! +Act+Ind+Prs+Sg1, +Act+Ind+ConNegII, +Act+Imprt+Sg2

:%^Pen%^WGStem%^VowRM%>i   Harm-Neutr_ACT-IND-PRT-SG1_ZERO_USUALLY-WEAK ; !!= * @CODE@ kaodi
!! +Act+Ind+Prt+Sg1, +Use/NG+Act+Ind+Prt+Sg2, +Act+Ind+Prt+Pl3

: Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG ; !!= * @CODE@ kaota
!! +Act+Ind+Prs+Sg2, +Use/NG+Act+Ind+Prs+Sg2, 
!! +Act+Ind+Prs+Pl1, +Act+Ind+Prs+Pl2

: ACT-IND-PRS-3_s/sEq ; !!= * @CODE@ 
!! +Act+Ind+Prs+Sg3, +Act+Ind+Prs+Pl3

: MUTUAL_JUS ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;

: ACT-IND-CONNEG ;
!: Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;

:%^VowRM%>i Harm-Neutr_ACT-IND-PRT-SG2_USUALLY-STRONG ; !!= * @CODE@ kaoti
!! +Act+Ind+Prt+Sg2, +Use/NG+Act+Ind+Prt+Sg2, +Act+Ind+Prt+Pl1,
!! +Act+Ind+Prt+Pl2, +Use/NG+Act+Ind+Prt+Pl3

:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ; !!= * @CODE@ +Act+Ind+Prt+Sg3: kaotʼ
!! +V+Act+Ind+Prt+Sg3: kaotʼ Harm-Neutr_K ;

:%^VowRM%>%{eõ%} Harm-Neutr_KAOTAMA-PSS-DISTRIBUTION ; != * @CODE@ kaotõ
!! +Pss+Ind+Prs+Sg1, +Pss+Ind+Prt+Sg1
!! +Pss+PrsPrc, +Pss+PrfPrc
:%^VowRM%>%{eõ%} PSS-PRFPRC_dU-SG-NOM ;


LEXICON V_60MANITSÕMA !!≈ * __@CODE@__ manitsõma:mani
!! * Yaml: __manitswma60,manitswma61>manitswma__
+V:%{back%} V_60MANITSÕMA/EHITSEMÄ ;
LEXICON V_60EHITSEMÄ !!≈ * __@CODE@__ ehitsemä:ehii
!! * Yaml: __manitswma60,manitswma61>manitswma__
+V:%{front%} V_60MANITSÕMA/EHITSEMÄ ;

LEXICON V_60MANITSÕMA/EHITSEMÄ !!≈ * __@CODE@__ manitsõma:mani
:ds%{eõ%} ACT-IND-PRS-SG1_ZERO ;
:ts%{eõ%} Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG ;
:ts%{eõ%} ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:ds V0-IND-PRT-SC-12_i ;
:ds%{eõ%} ACT-IND-CONNEG ;
:ts ACT-IND-PRT-SG3_APOSTROPHE ;
!: Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;

:ts%{eõ%} PSS-IND-PRS_dA/SG1 ;
:ts%{eõ%} PSS-IND-PRT-SG1_di ;

:ts%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:ts Harm-Neutr_INF_Aq ;

:ts%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:ts Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;

:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:ts%{eõ%}%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:ts%{eõ%}%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:  MUTUAL_JUS ;

LEXICON V_62ARGNÕMA
!! argnõma:arg
!! * Yaml: __argnwma62__
+V:%{back%} V_62ARGNÕMA/TÄRKNEMÄ ;
LEXICON V_62TÄRKNEMÄ
!! tärknemä:tärk
!! * Yaml: __taerknemae62__
+V:%{front%} V_62ARGNÕMA/TÄRKNEMÄ ;

LEXICON V_62ARGNÕMA/TÄRKNEMÄ 
:n%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:n%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%{eõ%}%^Pen%^WGStem PSS-IND-PRS_tA/SG1 ;

:s V0-IND-PRT-SC-12_i ;
:n%{eõ%} ACT-IND-CONNEG ;
:%{eõ%} Harm-Neutr_IND-PRT-SC3_s ;
!: Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:n%{eõ%} Harm-Neutr_INF_mA ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%{eõ%}%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%{eõ%}%^Pen%^WGStem MUTUAL_JUS_kuq ;

LEXICON V_63SÕIMAMA !!≈ * __@CODE@__ sõimama:sõima
! Ind.Prs.ScSg3 in -s
! vowel_harmony: back
!! * Yaml: __V-swimama63__
+V:%{back%} V_63SÕIMAMA/XX ; 
LEXICON V_63SÕIMAMA/XX !!≈ * __@CODE@__ sõimama:sõima
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^VowRM%>s ACT-IND-PRT-12_si ;
:%>s ACT-IND-PRT-12_si ;

: ACT-IND-CONNEG ;
: Harm-Neutr_IND-PRT-SC3_sʼ ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^WGStem Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
: MUTUAL_JUS_kuq ;

LEXICON V_63VISKAMA !!≈ * __@CODE@__ viskama:viska
! from V_63SÕIMAMA 
! Ind.Prs.ScSg3 in -s
! vowel_harmony: back
!! gradation: yes
!! * Yaml: __V-viskama63__
+V:%{back%} V_63VISKAMA/HINGÄMÄ ;
LEXICON V_63HINGÄMÄ !!≈ * __@CODE@__ hingämä:hingä
! Ind.Prs.ScSg3 in -s
! vowel_harmony: front
!! gradation: no
!! * Yaml: __V-hingaemae63__
+V:%{front%} V_63VISKAMA/HINGÄMÄ ;

LEXICON V_63VISKAMA/HINGÄMÄ !!≈ * __@CODE@__ viskama:viska
: ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-PRS-SG2_t ;
: ACT-IND-PRS-SG2_dEq ;
+Act+Ind+Prs+Sg3:%>s K ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;
+Act+Ind+Prs+Pl3:%>s%{eõ%}q K ;
 ACT-IND-CONNEG ;
+Err/Orth+Use/NG+Act+Ind+Prs+Sg2:%>d%{eõ%} K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Act+Ind+Prs+Pl1:%>mi K ;	!!= * @CODE@ no	q
+Err/Orth+Use/NG+Act+Ind+Prs+Pl2:%>ti K ;	!!= * @CODE@ no	q
+Err/Orth+Use/NG+Act+Ind+Prs+Pl3:%>s%{eõ%} K ;	!!= * @CODE@ no	q

+Act+Ind+Prt+Sg3:%^Pen%^WGStem%>sʼ K ;
+Act+Ind+Prt+Sg1:%^Pen%^WGStem%>ssi K ;
+Act+Ind+Prt+Sg2:%^Pen%^WGStem%>ssit K ;
+Act+Ind+Prt+Sg2:%^Pen%^WGStem%>ssiq K ;
+Act+Ind+Prt+Sg2:%^Pen%^WGStem%>ssid%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%^Pen%^WGStem%>ssimiq K ;
+Act+Ind+Prt+Pl2:%^Pen%^WGStem%>ssitiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>ssiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>ssiv%{aä%}q K ;


+Act+Ind+Prt+Pl3:%^VowRM%>siq K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siv%{aä%}q K ;

:%^VowRM ACT-IND-PRT-12_si ; 

: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
!! (6) Ind_Prs_Sg2, Ind_Prs_Sg3, Ind_Prs_Pl2, Ind_Prs_Pl3,
!! Ind_Prt_OcPl3
!! ActPrsPrc, PssPrsPrc

: ACT-IND-CONNEG ;

:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%^Pen%^WGStem%>di K ; !!= * @CODE@ no    q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%^Pen%^WGStem%>di K ; !!= * @CODE@ no    q
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%^Pen%^WGStem%>tid%{eõ%} K ; !!= * @CODE@ no    q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl1:%^Pen%^WGStem%>timi K ; !!= * @CODE@ no    q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl2:%^Pen%^WGStem%>titi K ; !!= * @CODE@ no    q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%^Pen%^WGStem%>tiv%{aä%} K ; !!= * @CODE@ no    q


:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
!! +Pss+Ind+Prt +Sg1-+Pl3, ConNeg

: Harm-Neutr_INF_mA ;
: Harm-Neutr_ACT-PRSPRC_v ;

:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;






!! THIS FAR 2016-08-27
LEXICON V_64PUHKSAMA
! puhksama:puhkas
!! * Yaml: __V-puhksama64__
+V:%{back%} V_64PUHKSAMA/REHKSÄMÄ ;
LEXICON V_64REHKSÄMÄ
! rehksämä:rehkäs
!! * Yaml: __V-rehksaemae64__
+V:%{front%} V_64PUHKSAMA/REHKSÄMÄ ;

LEXICON V_64PUHKSAMA/REHKSÄMÄ
:%^Pen%^VowRM%>%{aä%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^VowRM%>%{aä%} ACT-IND-PRS-3_s/sEq ;

:%^Pen%^VowRM ACT-IND-PRT-12_si-NO-s ;
+Act+Ind+Prt+Sg3:%^Pen%^WGStem%>ʼ K ;
:%^Pen%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;

:%^Pen%^VowRM%>%{aä%} ACT-IND-CONNEG ;

:%^Pen%^WGStem PSS-IND-PRS_tA/SG1 ;
:%^Pen%^WGStem PSS-IND-PRT-SG1_ti ;

:%^Pen%^VowRM%>%{aä%} V2N_minE ;
:%^Pen%^VowRM%>%{aä%} Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_tAq ;
:%^Pen%^VowRM%>%{aä%} Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;

:%^Pen%^WGStem MUTUAL_JUS_kuq ;

LEXICON V_65SIBAHAMA !!≈ * __@CODE@__ >>88SIBAMA >>SIBAMA

+V+WORK: # ;

LEXICON V_67HARINÕMA !!≈ * __@CODE@__ harinõma:hari
!! * Yaml: __harinwma66,harinwma67 =>harinwma__
+V:%{back%} V_67HARINÕMA/NÄGÜNEMÄ ;
LEXICON V_67NÄGÜNEMÄ !!≈ * __@CODE@__ nägünemä:nägü
!! * Yaml: __harinwma66,harinwma67 =>harinwma__
+V:%{front%} V_67HARINÕMA/NÄGÜNEMÄ ;

LEXICON V_67HARINÕMA/NÄGÜNEMÄ !!≈ * __@CODE@__ harinõma:hari
:n%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:n%{eõ%} ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: ACT-IND-PRT-12_si ;
:n%{eõ%} ACT-IND-CONNEG ;
: Harm-Neutr_IND-PRT-SC3_si_WEAKGRADE ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:n%{eõ%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:n%{eõ%} Harm-Neutr_INF_dAq ;
!+Err/Orth:n%{eõ%} Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:n%{eõ%}%^WGStem Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_HARINÕMA
+V+WORK: # ;
LEXICON V_68HARKUMA
!! * Yaml: __V-harkuma68__
+V:%{back%} V_68HARKUMA/NÄLGÜMÄ ;
LEXICON V_68NÄLGÜMÄ
!! * Yaml: __V-naelguemae68__
+V:%{front%} V_68HARKUMA/NÄLGÜMÄ ;

LEXICON V_68HARKUMA/NÄLGÜMÄ
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;

: PSS-IND-PRS_tA/SG1 ;

: ACT-IND-PRT-1/2_ZERO ;
: ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_ZERO ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;
+Act+Ind+Prt+Pl3:%>q K ;
: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
: PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_69HAARDUMA !!≈ * __@CODE@__ haarduma:haardu
+V:%{back%} V_69HAARDUMA/SÜNDÜMÄ ;
LEXICON V_69SÜNDÜMÄ !!≈ * __@CODE@__ sündümä:sündü
+V:%{front%} V_69HAARDUMA/SÜNDÜMÄ ;
LEXICON V_69HAARDUMA/SÜNDÜMÄ !!≈ * __@CODE@__ sündümä:sündü
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
!! Act_Ind_Prs_Pl3: essüseq
:%^Pen%^WGStem PSS-IND-PRS_tA/SG1 ;

: ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_ZERO ;
: ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem PSS-IND-PRT-SG1_ti ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_69SUURDUMA !!≈ * __@CODE@__ suurduma:suurdu
+V:%{back%} V_69SUURDUMA/HÜÜRDÜMÄ ;
LEXICON V_69HÜÜRDÜMÄ !!≈ * __@CODE@__ hüürdümä:höördü
+V:%{front%} V_69SUURDUMA/HÜÜRDÜMÄ ;
LEXICON V_69SUURDUMA/HÜÜRDÜMÄ !!≈ * __@CODE@__ hüürdümä:höördü vowel raising Required 2016-09-15
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;

:%^Pen%^WGStem PSS-IND-PRS_tA/SG1 ;

:%^Pen%^VOWRaise ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^VOWRaise ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
!! V_Inf/mA: miildümä 
:%^Pen%^VOWRaise Harm-Neutr_INF_dAq ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_70UNÕHUMA !!≈ * __@CODE@__ unõhuma:unõhtu
!! * Yaml: __V-unwhuma__
+V:%{back%} V_70UNÕHUMA/ÄNGÄHÜMÄ ;
LEXICON V_70ÄNGÄHÜMÄ !!≈ * __@CODE@__ ängähümä:ängähtü
!! * Yaml: __V-eraelduemae__
+V:%{front%} V_70UNÕHUMA/ÄNGÄHÜMÄ ;

LEXICON V_70UNÕHUMA/ÄNGÄHÜMÄ !!≈ * __@CODE@__ unõhuma:unõhtu
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
+Use/NG: Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
+Use/NG: ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem PSS-IND-PRS_dA/SG1 ;
+Use/NG: PSS-IND-PRS_dA/SG1 ;


:%^Pen%^WGStem ACT-IND-PRT-1/2_ZERO ;
+Use/NG: ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
: ACT-IND-CONNEG ;
:%^Pen%^WGStem ACT-IND-PRT-SG3_ZERO ;
: ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
: Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
!: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>q K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>v%{aä%}q K ;
+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;
:%^Pen%^WGStem MUTUAL_JUS ;
: MUTUAL_JUS ;

LEXICON V_70VIPÕRDUMA !!≈ * __@CODE@__ unõhtuma:unõhtu
!! * Yaml: __V-unwhtuma__
+V:%{back%} V_70VIPÕRDUMA/ERÄLDÜMÄ ;
LEXICON V_70ERÄLDÜMÄ !!≈ * __@CODE@__ eräldümä:eräldü
!! * Yaml: __V-eraelduemae__
+V:%{front%} V_70VIPÕRDUMA/ERÄLDÜMÄ ;

LEXICON V_70VIPÕRDUMA/ERÄLDÜMÄ !!≈ * __@CODE@__ unõhtuma:unõhtu
: Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-PRT-1/2_ZERO ;
: ACT-IND-CONNEG ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRT-SG3_ZERO ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;



LEXICON V_71HALLÕTUMA !!≈ * __@CODE@__ hallõtuma:hallõtu
!! * Yaml: __V-hallwtuma__
+V:%{back%} V_71HALLÕTUMA/ERÄTÜMÄ ;
LEXICON V_71ERÄTÜMÄ !!≈ * __@CODE@__ erätümä:erätü
!! * Yaml: __V-eraetuemae__
+V:%{front%} V_71HALLÕTUMA/ERÄTÜMÄ ;

LEXICON V_71HALLÕTUMA/ERÄTÜMÄ !!≈ * __@CODE@__ hallõtuma:hallõtu
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^WGStem ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_ZERO ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_72VAOMA !!≈ * __@CODE@__ vaoma:vao
!! * Yaml: __vaoma__
+V:%{back%} V_72VAOMA/TÄÜMÄ ;
LEXICON V_72TÄÜMÄ !!≈ * __@CODE@__ täümä:täü
!! * Yaml: __haeoemae__
+V:%{front%} V_72VAOMA/TÄÜMÄ ;

LEXICON V_72VAOMA/TÄÜMÄ !!≈ * __@CODE@__ vaoma:vao
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGStem V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_i ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_73VAOMA !!= * @CODE@ vaoma:vao
+V:%{back%} V_73VAOMA/XX ;
LEXICON V_73VAOMA/XX
+Work: K ;
!+Inf/mA: vaoma
!+Inf: [vaodaq, vaioq]
!+Act+PrfPrc: vaonuq
!+Pss+PrfPrc: [vaot, vaotu]

!+Act+Ind+Prs+Sg1: vao
!+Act+Ind+Prs+Sg2: [vaot, vaodõq]
!+Act+Ind+Prs+Sg3: vaos
!+Act+Ind+Prs+Pl1: vaomiq
!+Act+Ind+Prs+Pl2: vaotiq
!+Act+Ind+Prs+Pl3: vaosõq
!+Act+Ind+ConNegII: vao

!+Act+Ind+Prt+Sg1: vaio
!+Act+Ind+Prt+Sg2: [vaiot, vaioq, vaiodõq]
!+Act+Ind+Prt+Sg3: vaio
!+Act+Ind+Prt+Pl1: vaiomiq
!+Act+Ind+Prt+Pl2: vaiotiq
!+Act+Ind+Prt+Pl3: [vaioq, vaiovaq]

!+Pss+Ind+Prs+Sg3: vaotas

!+Act+PrsPrc+Sg+Nom: vaova

!+Jus: vaoguq

LEXICON V_74KULUMA !!≈ * __@CODE@__ kuluma:kullu
!! * Yaml: __V-kuluma__
+V:%{back%} V_74KULUMA/VÄSÜMÄ ;
LEXICON V_74VÄSÜMÄ !!≈ * __@CODE@__ väsümä:vässü
!! * Yaml: __V-vaesuemae__
+V:%{front%} V_74KULUMA/VÄSÜMÄ ;

LEXICON V_74KULUMA/VÄSÜMÄ !!≈ * __@CODE@__ kuluma:kullu
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_ZERO ;
: ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^WGStem MUTUAL_JUS ;

LEXICON V_75VALAMA !!≈ * __@CODE@__ valama:vala
!! * Yaml: __V-valama75__
!! vala, valla, vali, valõ
!!test
+V:%{back%} V_75VALAMA/JÄRÄMÄ ;
LEXICON V_75JÄRÄMÄ !!≈ * __@CODE@__ järämä:järä
!! * Yaml: __V-jaeraemae75__
+V:%{front%} V_75VALAMA/JÄRÄMÄ ;

LEXICON V_75VALAMA/JÄRÄMÄ !!≈ * __@CODE@__ valama:vala

!!
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-PL3_vAq ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^WGStem%^VowRM V0-IND-PRT-SC-12_i ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^WGStem%^VowRM ACT-IND-PRT-SG3_i ;
:%^Pen%^WGStem%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;

:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;

:%^Pen%^WGStem MUTUAL_JUS ;


LEXICON V_76HIGOMA !!≈ * __@CODE@__ higoma:hi%{kg0%}o
!! * Yaml: __V-higoma76__
+V:%{back%} V_76HIGOMA/PÜGÄMÄ ;
LEXICON V_76PÜGÄMÄ !!≈ * __@CODE@__ pügämä:pü%{kg0%}ä
!! * Yaml: __V-puegaemae76__
+V:%{front%} V_76HIGOMA/PÜGÄMÄ ;

LEXICON V_76HIGOMA/PÜGÄMÄ !!≈ * __@CODE@__ higoma:hi%{kg0%}o

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-PRSPRC_v_LUGÕMA ; !!= * @CODE@ (1) lugõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
: MUTUAL_JUS ;
!! +Jus


!! Strengthen consonant retain stem vowel
:%^Pen%^StrGStem Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) lukõq
:%^Pen%^StrGStem Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) lukõv
!! Inf, Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-PRFPRC_v_LUGÕMA ; !!= * @CODE@ (3) lugõnuq
!! +Act+PrfPrc

!! Weaken consonant and semi-retension of stem vowel
:%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%} Harm-Neutr_LUGÕMA_PSS-PRC ; !!= * @CODE@ (4) loet
!! Pss+PrfPrc, Pss+PrsPrc

!! Weaken consonant and semi-retension of stem vowel
:%^Pen%^WGStem ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_t ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_dEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL1_miq ;
:%^Pen%^WGStem ACT-IND-PRS-PL2_tiq ;
:%^Pen%^WGStem ACT-IND-CONNEG ;

!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-IND-PRS-3_LUGÕMA ; !!= * @CODE@ (6) lugõma
!! Ind+Prs+ 3

!! Weaken consonant and replace stem vowel with i
:%^VOWLower%^Pen%^WGStem%^VowRM%>i Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Retain consonant remove stem vowel and add i
:%^VowRM%>i Harm-Neutr_LUGÕMA_IND-PRT-SG3 ; !!= * @CODE@ (8) lugi
!! Act+Ind+Prt+Sg3

LEXICON V_76LUGÕMA !!≈ * __@CODE@__ lugõma:lu%{kg0%}õ
!! * Yaml: __V-higoma76__
+V:%{back%} V_76LUGÕMA/XX ;


LEXICON V_76LUGÕMA/XX !!≈ * __@CODE@__ lugõma:lu%{kg0%}õ

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-PRSPRC_v_LUGÕMA ; !!= * @CODE@ (1) lugõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
: MUTUAL_JUS ;
!! +Jus


!! Strengthen consonant retain stem vowel
:%^Pen%^StrGStem Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) lukõq
:%^Pen%^StrGStem Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) lukõv
!! Inf, Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-PRFPRC_v_LUGÕMA ; !!= * @CODE@ (3) lugõnuq
!! +Act+PrfPrc

!! Weaken consonant and semi-retension of stem vowel
:%^VOWLower%^Pen%^WGStem%^VowRM%>e Harm-Neutr_LUGÕMA_PSS-PRC ; !!= * @CODE@ (4) loet
!! Pss+PrfPrc, Pss+PrsPrc

!! Weaken consonant and semi-retension of stem vowel
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG1_ZERO ;
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG2_t ;
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG2_dEq ;
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-PL1_miq ;
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-PL2_tiq ;
:%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-CONNEG ;

!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-IND-PRS-3_LUGÕMA ; !!= * @CODE@ (6) lugõma
!! Ind+Prs+ 3

!! Weaken consonant and replace stem vowel with i
:%^VOWLower%^Pen%^WGStem%^VowRM%>i Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Retain consonant remove stem vowel and add i
:%^VowRM%>i Harm-Neutr_LUGÕMA_IND-PRT-SG3 ; !!= * @CODE@ (8) lugi
!! Act+Ind+Prt+Sg3



LEXICON V_77JUUMA !!≈ * __@CODE@__ juuma:joo
!! * Yaml: __juuma__
+V:%{back%} V_77JUUMA/SÜÜMÄ ;
LEXICON V_77SÜÜMÄ !!≈ * __@CODE@__ süümä:süü
!! * Yaml: __sueuemae__
+V:%{front%} V_77JUUMA/SÜÜMÄ ;

LEXICON V_77JUUMA/SÜÜMÄ !!≈ * __@CODE@__ juuma:joo

:%^VOWRaise V_77JUUMA/SÜÜMÄ_RAISED-VOWEL ; !!= * @CODE@ (1) juu, süü
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA

: V_77JUUMA/SÜÜMÄ_NOT-RAISED-VOWEL ; !!= * @CODE@ (2) joo- söö-
!

:%^VOWRaise%^VowRM%>vv V_77JUUMA/SÜÜMÄ_RAISED-VOWEL-TENSE ; !!= * @CODE@ (3) juvv
!

:%^OO2Õ%>i V_77JUUMA/SÜÜMÄ_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ (4) jõi- poi- sei- möi-
!

:%^VOWRaise ACT-IND-PRS-SG3_ZERO ; !!= * @CODE@ (5) RAISED juu süü
! Act+Ind+Prs+Sg3
:%^VOWRaise%>s ACT-COND-CONNEG ;
:%^VOWRaise%^VowRM%>vv%{aä%} PSS-COND-CONNEG ;
: ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) joovaq sööväq


!:%^VOWRaise%^VowRM%>vv Harm-Neutr_IND-PRT-OC1-2_i ; !!= * @CODE@ juvv süvv



LEXICON V_77JUUMA/SÜÜMÄ_RAISED-VOWEL
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA, ACT-PRFPRC-OBL,
! ACT-PRSPRC
: Harm-Neutr_V0-PSS-PRSPRC_dAv ;
: PSS-PRFPRC_d ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_INF_mA ;
: Harm-Neutr_ACT-PRFPRC-OBL_nU ;
: ACT-IND-PRS-SG1_ZERO ;
: ACT-IND-PRS-SG2_t ;
+Use/NG: ACT-IND-PRS-SG2_dEq ;
+Use/NG: ACT-IND-PRS-PL2_dEq ;
: ACT-IND-PRS-PL2_tiq ;
 ACT-IND-CONNEG ;
 Harm-Neutr_77JUUMA/SÜÜMÄ-PSS-IND-PRT_di ; !!= * @CODE@ only 3rd person

LEXICON V_77JUUMA/SÜÜMÄ_NOT-RAISED-VOWEL !!= * @CODE@ joo- söö-
! Act+PrfPrc+Sg+Nom, Act+Ind+Prs+Sg3, Act+Ind+Prs+Pl1, Act+Ind+Prs+Pl3
: Harm-Neutr_ACT-PRFPRC_nUq ; !!= * @CODE@ joonuq
+Use/NG: ACT-IND-PRS-SG3_sE ;
 ACT-IND-PRS-PL1_miq ;
 ACT-COND-CONNEG ;

LEXICON V_77JUUMA/SÜÜMÄ_RAISED-VOWEL-TENSE !!= * @CODE@ juvv
 Harm-Neutr_INF_Aq ; !!= * @CODE@ juvvaq süvväq
:%>%{aä%} Harm-Neutr_JUVVA_PSS-IND-PRS ; !!= * @CODE@ juvva süvvä
:%>i Harm-Neutr_JUVVA_PSS-IND-PRT-1/2 ; !!= * @CODE@ juvvi süvvi

LEXICON V_77JUUMA/SÜÜMÄ_NON-RAISED-VOWEL-TENSE !!= * @CODE@ jõi-
: ACT-IND-PRT-1/2_ZERO ;
+Use/NG: Harm-Neutr_ACT-IND-PRT-1/2_E ;
: ACT-IND-PRT-SG3_ZERO ;
+Use/NG: ACT-IND-PRT-SG3_E ;
: ACT-IND-PRT-PL3_q/vAq ;
+Use/NG:%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;

LEXICON V_77KÄÜMÄ !!≈ * __@CODE@__ käümä:käü
!! * Yaml: __sueuemae__
+V:%{front%} V_77XX/KÄÜMÄ ;

LEXICON V_77XX/KÄÜMÄ !!≈ * __@CODE@__ käümä:käü

:%^VOWRaise V_77JUUMA/SÜÜMÄ_RAISED-VOWEL ; !!= * @CODE@ (1) käü
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA

: V_77JUUMA/SÜÜMÄ_NOT-RAISED-VOWEL ; !!= * @CODE@ (2) käü-
!

:%^VowRM%>vv V_77JUUMA/SÜÜMÄ_RAISED-VOWEL-TENSE ; !!= * @CODE@ (3) kävv
!

:%^VowRM%>ve V_77JUUMA/SÜÜMÄ_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ (4) käve
!

: ACT-IND-PRS-SG3_ZERO ; !!= * @CODE@ (5) RAISED käü
! Act+Ind+Prs+Sg3

: ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) käüväq



LEXICON V_78VÕIMA !!≈ * __@CODE@__ võima:või
!! * Yaml: __V-vwima__
+V:%{back%} V_VÕIMA/XX ;
LEXICON V_VÕIMA/XX !!≈ * __@CODE@__ võima:või
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: ACT-IND-CONNEG ;
:%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
+Use/NG:%>ds%{eõ%} ACT-IND-PRT-1/2_ZERO ;
:%>s%{eõ%} ACT-IND-PRT-SG3_ZERO ;
:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
+Use/NG:%>ds%{eõ%} ACT-IND-PRT-SG3_ZERO ;
+Use/NG:%>ds%{eõ%} ACT-IND-PRT-PL3_q/vAq ;

: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_dAq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
: PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_78SAAMA
!! * Yaml: __saama__
+V:%{back%} V_78SAAMA/XX ;
LEXICON V_78SAAMA/XX
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;

: Harm-Neutr_IND-PRT-SCSG1_iE ;
: V0-IND-PRT-SC-12_i ;
: Harm-Neutr_V0-IND-PRT-SC-12_iE ;

: ACT-IND-CONNEG ;

: ACT-IND-PRT-SG3_i ;
: ACT-IND-PRT-SG3_iE ;
:%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
+Use/NG:%^VowRM%>i%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>i%{aä%} Harm-Neutr_JUVVA_PSS-IND-PRS ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_iAq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: PSS-PRFPRC_d ;
: PSS-PRFPRC_dU-SG-NOM ;
: MUTUAL_JUS ;

LEXICON V_78VIIMÄ
!! * Yaml: __V-viimae__
+V:%{front%}  V_78XX/VIIMÄ ; 
LEXICON V_78XX/VIIMÄ
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-SG3_sE ;
: ACT-IND-PRS-PL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: Harm-Neutr_IND-PRT-SCSG1_iE ;
: V0-IND-PRT-SC-12_i ;
: ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_i ;
: ACT-IND-PRT-SG3_iE ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_iAq ;
: Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_MINEMÄ !!≈ * __@CODE@__ minemä:min
!! * Yaml: __V-minemae__
+V:%{front%} V_XX/MINEMÄ ;
LEXICON V_XX/MINEMÄ !!≈ * __@CODE@__ minemä:min
:%>%{eõ%} Harm-Neutr_INF_mA ;
+Inf:%>n%{aä%}q K ;
+Pss+PrfPrc:%>t K ;
+Use/NG+Pss+PrfPrc:%>t%{uü%} K ;
+Pss+Ind+Prs+Sg3:%>d%{aä%}s K ;
:%>n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
: MUTUAL_JUS_kuq ;
!! Remainder is in exceptions.lexc



LEXICON V_79TULÕMA !!≈ * __@CODE@__ tulõma:tul
!! * Yaml: __tulwma__
+V:%{back%} V_79TULÕMA/XX ;
LEXICON V_79TULÕMA/XX !!≈ * __@CODE@__ tulõma:tul
!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v_LUGÕMA ; !!= * @CODE@ (1) tulõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
: MUTUAL_JUS ;
!! +Jus, 

!! Strengthen consonant
:%{XC%} Harm-Neutr_INF_Aq ; !!= * @CODE@ (2) tullaq
!! Inf 
!! Strengthen consonant 
:%{XC%}%>%{eõ%} Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) tullõv
!! Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
: Harm-Neutr_ACT-PRFPRC_v_LUGÕMA ; !!= * @CODE@ (3) tulnuq
!! +Act+PrfPrc

!! Retain consonant 
: Harm-Neutr_LUGÕMA_PSS-PRC ; !!= * @CODE@ (4) tul
!! Pss+PrfPrc, Pss+PrsPrc, 


!! Retain consonant and add õ
:%{eõ%} Harm-Neutr_LUGÕMA_IND-CONNEGII ; !!= * @CODE@ (5) tulõ
!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-IND-PRS-3_LUGÕMA ; !!= * @CODE@ (6) tulõma
!! Ind+Prs+ 3

!! Strengthen consonant and replace stem vowel with i
:%{XC%}%>i TULÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Strengthen consonant and add ʼ
:%{XC%}%>ʼ Harm-Neutr_LUGÕMA_IND-PRT-SG3 ; !!= * @CODE@ (8) tullʼ
!! Act+Ind+Prt+Sg3

LEXICON V_79OLÕMA !!≈ * __@CODE@__ olõma:o
+V:%{back%} V_79OLÕMA/XX ;

LEXICON V_79OLÕMA/XX !!≈ * __@CODE@__ olõma:o
:l%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
+Act+Ind+Prs+Sg3:m K ;
+Act+Ind+Prs+Pl3:mm%{aä%}q K ;
:mm%{aä%} ACT-IND-PRS-PL3_vAq ;
:l PSS-IND-PRS_dA/SG1 ;

:ll V0-IND-PRT-SC-12_i ;
:l%{eõ%} ACT-IND-CONNEG ;
+Act+Ind+Prt+Sg3:llʼ K ;
+Act+Ind+Prt+Sg3:lle K ;
:lli ACT-IND-PRT-PL3_q/vAq ;
:l Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:l%{eõ%} Harm-Neutr_INF_mA ;
:ll Harm-Neutr_INF_Aq ;
:ll%{eõ%} Harm-Neutr_ACT-PRSPRC-SG-NOM_v ;
:l%{eõ%} Harm-Neutr_ACT-PRSPRC_v_LUGÕMA ;
:l%{eõ%} MUTUAL_JUS ;
:l Harm-Neutr_ACT-PRFPRC_v_LUGÕMA ;
:l Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:l Harm-Neutr_PSS-PRFPRC_t ;
:l PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_80RAPAHUTMA !!≈ * __@CODE@__ rapahutma:rapahuta
+V:%{back%} V_80RAPAHUTMA/HÄMMÄHÜTMÄ ;
LEXICON V_80HÄMMÄHÜTMÄ !!≈ * __@CODE@__ hämmähütmä:hämmähütä
+V:%{front%} V_80RAPAHUTMA/HÄMMÄHÜTMÄ ;
+V:%^WGStem%>%{aä%} Harm-Neutr_V0-IND-PRS-SC-12 ;
+V: ACT-IND-PRS-SG3_ZERO ;

+V:%{aä%} ACT-IND-PRS-PL3_vAq ;
+V: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

+V:%^WGStem V0-IND-PRT-SC-12_i ;
+V+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+V+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+V+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;

+V: ACT-IND-PRT-SG3_APOSTROPHE ;
+V:%>i ACT-IND-PRT-PL3_q/vAq ;
+V+Use/NG:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
+V: Harm-Neutr_VISKAMA_PSS-IND-PRS ;
+V: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

+V: Harm-Neutr_INF_Aq ;
+V:%{aä%} Harm-Neutr_ACT-PRSPRC_v ;
+V: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
+V: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
+V:%^WGStem%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
+V:%^WGStem%{eõ%} PSS-PRFPRC_tU-SG-NOM ;


LEXICON V_80RAPAHUTMA/HÄMMÄHÜTMÄ !!= * @CODE@ rapahutma:rapahuta
!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-PRSPRC_v_LUGÕMA ; !!= * @CODE@ (1) rapahutma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
:%^VowRM MUTUAL_JUS_kuq ;
!! +Jus, 

!! Strengthen consonant
: Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) rapahutaq
!! Inf 
!! Strengthen consonant 
:%^VowRM Harm-Neutr_ACT-PRSPRC-SG-NOM_vA ; !!= * @CODE@ (2b) rapahutva
!! Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-PRFPRC_v_LUGÕMA ; !!= * @CODE@ (3) rapahutnuq
!! +Act+PrfPrc

!! Retain consonant 
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_LUGÕMA_PSS-PRC ; !!= * @CODE@ (4) rapahudõ
!! Pss+PrfPrc, Pss+PrsPrc, 

!! Retain consonant and add õ
:%^Pen%^WGStem Harm-Neutr_LUGÕMA_IND-CONNEGII ; !!= * @CODE@ (5) rapahuda
!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind
:%^Pen%^WGStem ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_t ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_dEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL1_miq ;
:%^Pen%^WGStem ACT-IND-PRS-PL2_tiq ;
:%^Pen%^WGStem ACT-IND-CONNEG ;

!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-IND-PRS-3_LUGÕMA ; !!= * @CODE@ (6) rapahut
!! Ind+Prs+ 3

!! Strengthen consonant and replace stem vowel with i
:%^Pen%^WGStem%^VowRM%>i TULÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) rapahudi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Strengthen consonant and add ʼ
:%^VowRM%>ʼ Harm-Neutr_LUGÕMA_IND-PRT-SG3 ; !!= * @CODE@ (8) tullʼ
!! Act+Ind+Prt+Sg3


LEXICON V_81TEGEMÄ !!≈ * __@CODE@__ tegemä:teg
!! * Yaml: __tegemä__
!! Work
+V:%{front%} V_81XX/TEGEMÄ ;
LEXICON V_81XX/TEGEMÄ !!≈ * __@CODE@__ tegemä:teg
:%^WGStem%>%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{eõ%} ACT-IND-PRS-SG3_ZERO ;
!:%{aä%} ACT-IND-PRS-PL3_vAq ;
:%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^WGStem V0-IND-PRT-SC-12_i ;
:%^WGStem Harm-Neutr_V0-IND-PRT-SC-12_iE ;
:%^StrGStem Harm-Neutr_V0-IND-PRT-SC-12_sE ;
:%^StrGStem Harm-Neutr_IND-PRT-SCSG1_sE ;
:%^WGStem%{eõ%} ACT-IND-CONNEG ;
:%^XStrGStem ACT-IND-PRT-SG3_APOSTROPHE ;
:%^StrGStem ACT-IND-PRT-SG3_sE ;
:%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
:%^WGStem Harm-Neutr_INF_tAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^WGStem%>n Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^WGStem Harm-Neutr_V0-PSS-PRSPRC_ttAv ;
:%^WGStem Harm-Neutr_V0-PSS-PRFPRC_tt ;



LEXICON V_82ANDMA !!≈ * __@CODE@__ andma:and
!! * Yaml: __andma__
+V:%{back%} V_82ANDMA/PÜÜDMÄ ;
LEXICON V_82PÜÜDMÄ !!≈ * __@CODE@__ püüdmä:püüd
!! * Yaml: __pueuedmae__
+V:%{front%} V_82ANDMA/PÜÜDMÄ ;

LEXICON V_82ANDMA/PÜÜDMÄ !!≈ * __@CODE@__ andma:and
:%^WGStem%>%{aä%} Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;
:%^WGStem%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
:%^WGStem%>i ACT-IND-PRT-1/2_ZERO ;
!:%^WGStem V0-IND-PRT-SC-12_i ;
+Act+Ind+Prt+Sg1:%>s%{eõ%} K ;
: Harm-Neutr_IND-PRT-SCSG1_sE ;
:%^WGStem%>%{aä%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-PRT-SG3_sE ;
:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_Aq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;
: MUTUAL_JUS_kuq ;

LEXICON V_83NÕSTMA  !!= * @CODE@ nõstma:nõst
!! gradation: no
!! * Yaml: __nwstma__
+V:%{back%} V_83NÕSTMA/PÄSTMÄ  ;
LEXICON V_83PÄSTMÄ  !!= * @CODE@ nõstma:nõst
!! gradation: no
!! * Yaml: __nwstma__
+V:%{front%} V_83NÕSTMA/PÄSTMÄ  ;

LEXICON V_83NÕSTMA/PÄSTMÄ  !!= * @CODE@ nõstma:nõst
:%{aä%} Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;

: V0-IND-PRT-SC-12_i ;
: Harm-Neutr_V0-IND-PRT-SC-12_sE ;
: Harm-Neutr_IND-PRT-SCSG1_sE ;
:%{aä%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-PRT-SG3_sE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
: MUTUAL_JUS_kuq ;

: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_Aq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%{eõ%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%{eõ%} PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_83SÕITMA  !!= * @CODE@ sõitma:sõit
!! gradation: yes
!! * Yaml: __switma__
+V:%{back%} V_83SÕITMA/HEITMÄ ;
LEXICON V_83HEITMÄ  !!= * @CODE@ heitmä:heit
!! gradation: yes
!! * Yaml: __heitmae__
+V:%{front%} V_83SÕITMA/HEITMÄ ;

LEXICON V_83SÕITMA/HEITMÄ  !!= * @CODE@ sõitma:sõit
+Act+Ind+Prs+Sg1:%^WGStem%>%{aä%} K ; 
+Act+Ind+Prs+Sg2:%^WGStem%>%{aä%}t K ; 
+Use/NG+Act+Ind+Prs+Sg2:%^WGStem%>%{aä%}d%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^WGStem%>%{aä%}miq K ; 
+Act+Ind+Prs+Pl2:%^WGStem%>%{aä%}tiq K ; 
+Act+Ind+Prs+Sg3: K ; 
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 
:%^WGStem%>%{aä%} ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^WGStem%>i K ; 
+Use/NG+Act+Ind+Prt+Sg1:%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^WGStem%>it K ;
+Use/NG+Act+Ind+Prt+Sg2:%^WGStem%>iq K ;
+Use/NG+Act+Ind+Prt+Sg2:%^WGStem%>id%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
+Use/NG+Act+Ind+Prt+Sg3:%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^WGStem%>imiq K ;
+Use/NG+Act+Ind+Prt+Pl1:%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^WGStem%>itiq K ;
+Use/NG+Act+Ind+Prt+Pl2:%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^WGStem%>iq K ;
+Use/NG+Act+Ind+Prt+Pl3:%^WGStem%>iv%{aä%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>s%{eõ%}v%{aä%}q K ;


:%^WGStem%>%{eõ%} Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;

: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02

: MUTUAL_JUS_kuq ;
: Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_Aq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{eõ%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;

LEXICON V_83LAULMA  !!= * @CODE@ laulma:laul
!! gradation: yes
!! * Yaml: __switma__
+V:%{back%} V_83LAULMA/XX ;


LEXICON V_83LAULMA/XX  !!= * @CODE@ laulma:laul
+Act+Ind+Prs+Sg1:%^WGStem%>%{aä%} K ; 
+Act+Ind+Prs+Sg2:%^WGStem%>%{aä%}t K ; 
+Use/NG+Act+Ind+Prs+Sg2:%^WGStem%>%{aä%}d%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^WGStem%>%{aä%}miq K ; 
+Act+Ind+Prs+Pl2:%^WGStem%>%{aä%}tiq K ; 
+Act+Ind+Prs+Sg3: K ; 
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 
:%^WGStem%>%{aä%} ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^WGStem%>i K ; 
+Use/NG+Act+Ind+Prt+Sg1:%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^WGStem%>it K ;
+Use/NG+Act+Ind+Prt+Sg2:%^WGStem%>iq K ;
+Use/NG+Act+Ind+Prt+Sg2:%^WGStem%>id%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
:%{aä%} ACT-IND-CONNEG ;
+Use/NG+Act+Ind+Prt+Sg3:%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^WGStem%>imiq K ;
+Use/NG+Act+Ind+Prt+Pl1:%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^WGStem%>itiq K ;
+Use/NG+Act+Ind+Prt+Pl2:%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^WGStem%>iq K ;
+Use/NG+Act+Ind+Prt+Pl3:%^WGStem%>iv%{aä%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>s%{eõ%}v%{aä%}q K ;


:%^WGStem%>%{eõ%} Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;

: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02

: MUTUAL_JUS_kuq ;
: Harm-Neutr_INF_mA ;
:d Harm-Neutr_INF_Aq ;  !! HERE is the distinction 2016-10-04
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{eõ%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;

LEXICON V_83ATMA  !!= * @CODE@ atma:atta
!! gradation: yes
!! * Yaml: __switma__
+V:%{back%} V_83ATMA/PETMÄ ;
LEXICON V_83PETMÄ  !!= * @CODE@ petmä:pettä
!! gradation: yes
!! * Yaml: __heitmae__
+V:%{front%} V_83ATMA/PETMÄ ;

LEXICON V_83ATMA/PETMÄ  !!= * @CODE@ sõitma:sõit
+Act+Ind+Prs+Sg1:%^Pen%^WGStem K ; 
+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>t K ; 
+Use/NG+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>d%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^Pen%^WGStem%>miq K ; 
+Act+Ind+Prs+Pl2:%^Pen%^WGStem%>tiq K ; 

+Act+Ind+Prs+Sg3: K ; 
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 

:%^Pen%^WGStem ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^Pen%^WGStem%^VowRM%>i K ; 
+Use/NG+Act+Ind+Prt+Sg1:%^Pen%^CC2C%^VowRM%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>it K ;
+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>iq K ;
+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>id%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}t K ;
+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}d%{eõ%}q K ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-CONNEG ;
+Use/NG+Act+Ind+Prt+Sg3:%^Pen%^CC2C%^VowRM%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^Pen%^WGStem%^VowRM%>imiq K ;
+Use/NG+Act+Ind+Prt+Pl1:%^Pen%^CC2C%^VowRM%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^Pen%^WGStem%^VowRM%>itiq K ;
+Use/NG+Act+Ind+Prt+Pl2:%^Pen%^CC2C%^VowRM%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iq K ;
+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iv%{aä%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^CC2C%^VowRM%>s%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^CC2C%^VowRM%>s%{eõ%}v%{aä%}q K ;


:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;

:%^Pen%^CC2C%^VowRM Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02

:%^Pen%^CC2C%^VowRM MUTUAL_JUS_kuq ;
:%^Pen%^CC2C%^VowRM Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
:%^Pen%^CC2C%^VowRM ACT-PRSPRC_vA ;
:%^Pen%^CC2C%^VowRM Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^Pen%^CC2C%^VowRM Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;


LEXICON V_84LASKMA !!= * @CODE@ laskma:lask
!! * Yaml: __nwstma__
+V:%{back%} V_84LASKMA/KÄSKMÄ ;
LEXICON V_84KÄSKMÄ !!= * @CODE@ käskmä:käsk
!! * Yaml: __nwstma__
+V:%{front%} V_84LASKMA/KÄSKMÄ ;
LEXICON V_84LASKMA/KÄSKMÄ !!= * @CODE@ laskma:lask
!! * Yaml: __nwstma__
:%^WGStem%>%{eõ%} Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;

:%^WGStem V0-IND-PRT-SC-12_i ;
: Harm-Neutr_V0-IND-PRT-SC-12_sE ;
:%^WGStem%>%{eõ%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-PRT-SG3_sE ;
:%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem PSS-IND-PRS_tA/SG1 ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%>%{eõ%} Harm-Neutr_INF_ZEROq ;
: ACT-PRSPRC_vA ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem PSS-PRFPRC_t-ONLY ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^WGStem MUTUAL_JUS_kuq ;

LEXICON V_85HIRNMA !!≈ * __@CODE@__ kakma:kaku hirnma:hirnu
!! * Yaml: __kakma__
+V:%{back%} V_85HIRNMA/XX ;
LEXICON V_85HIRNMA/XX !!≈ * __@CODE@__ kakma:kaku hirnma:hirnu
:%{uü%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%{XC%} ACT-IND-PRS-SG3_ZERO ;
: ACT-IND-PRS-PL3_vAq ;
:%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^WGStem V0-IND-PRT-SC-12_i ;
: Harm-Neutr_IND-PRT-SCSG1_sE ;
:%{uü%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
: ACT-IND-PRT-SG3_sE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
:%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%{uü%} Harm-Neutr_INF_ZEROq ;
:%{uü%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%{uü%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;

!
LEXICON V_85HAUDMA !!≈ * __@CODE@__  haudma:haud
!! * Yaml: __haudma__
+V:%{back%} V_85HAUDMA/VÄLKMÄ ;
LEXICON V_85VÄLKMÄ !!≈ * __@CODE@__  haudma:haud
!! * Yaml: __vaelkmae__
+V:%{front%} V_85HAUDMA/VÄLKMÄ ;
LEXICON V_85HAUDMA/VÄLKMÄ !!≈ * __@CODE@__ kakma:kakk haudma:haud
:%^WGStem%>%{uü%} Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-SG3_ZERO ;
:%^CC2C ACT-IND-PRS-PL3_vAq ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%^CC2C Harm-Neutr_INF_mA ;


:%^WGStem V0-IND-PRT-SC-12_i ;
:%^CC2C Harm-Neutr_IND-PRT-SCSG1_sE ;
:%^WGStem%>%{uü%} ACT-IND-CONNEG ;
: ACT-IND-PRT-SG3_APOSTROPHE ;
:%^CC2C ACT-IND-PRT-SG3_sE ;
:%^CC2C%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{uü%} Harm-Neutr_INF_ZEROq ;
:%^CC2C ACT-PRSPRC_vA ;
:%^CC2C Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^WGStem%>%{uü%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;




LEXICON V_86ISTMA !!≈ * __@CODE@__ istma:istu
!! * Yaml: __istma__
+V:%{back%} V_86ISTMA/XX ;
LEXICON V_86ISTMA/XX !!≈ * __@CODE@__ istma:istu
: Harm-Neutr_V0-IND-PRS-SC-12 ;
: ACT-IND-PRS-3_s/sEq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^VowRM V0-IND-PRT-SC-12_i ;
:%^VowRM Harm-Neutr_ACT-IND-PRT-1/2_E ; !2014-06-27 Is this correct
: ACT-IND-CONNEG ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VowRM ACT-IND-PRT-SG3_E ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VowRM Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^VowRM Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_86JAHTJMA !!≈ * __@CODE@__ jahtʼma:jahti
!! * Yaml: __jahtjma__
+V:%{back%} V_86JAHTJMA/EHTJMÄ ;
LEXICON V_86EHTJMÄ !!≈ * __@CODE@__ ehtʼmä:ehti
!! * Yaml: __V-ehtjmae__
+V:%{front%} V_86JAHTJMA/EHTJMÄ ;
LEXICON V_86JAHTJMA/EHTJMÄ !!≈ * __@CODE@__ ehtʼmä:ehti
: Harm-Neutr_V0-IND-PRS-SC-12 ;
+Act+Ind+Prs+Sg3:%^I2ʼ K ;
:%^I2ʼ ACT-IND-PRS-PL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

!:%^VowRM V0-IND-PRT-SC-12_i ;
:%^VowRM Harm-Neutr_ACT-IND-PRT-1/2_E ; !2014-06-27 Is this correct
:%^I2ʼ%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
: ACT-IND-CONNEG ;

:%^VowRM ACT-IND-PRT-SG3_E ;
+Act+Ind+Prt+Sg3:%^I2ʼ%>s%{eõ%} K ;
:%^I2ʼ%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^CC2C%^I2ʼ Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
:%^I2ʼ ACT-PRSPRC_vA ;
:%^I2ʼ Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
: PSS-PRFPRC_tU-SG-NOM ;
:%^I2ʼ MUTUAL_JUS_kuq ;

LEXICON V_87KOSIMA !!≈ * __@CODE@__ kosima:kosi
!! * Yaml: __V-kosima__
+V:%{back%} V_87KOSIMA/KERIMÄ ;
LEXICON V_87KERIMÄ !!≈ * __@CODE@__ kerimä:keri
!! * Yaml: __V-kerimae__
+V:%{front%} V_87KOSIMA/KERIMÄ ;

LEXICON V_87KOSIMA/KERIMÄ !!≈ * __@CODE@__ kosima:kosi
:%^Pen%^WGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^Pen%^WGStem ACT-IND-PRS-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL3_vAq ;

:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^WGStem%^VowRM Harm-Neutr_ACT-IND-PRT-1/2_E ; 
:%^Pen%^WGStem ACT-IND-PRT-12_si ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^WGStem%^VowRM ACT-IND-PRT-SG3_E ;
:%^Pen%^WGStem%>si ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem%>si ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_ZEROq ;
: Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^WGStem MUTUAL_JUS ;


LEXICON V_88SIBAHAMA !!= * @CODE@ sibama:siba
!! * Yaml: __V-sibama88,sibahama__
+V:%{back%} V_88SIBAHAMA/JÜVÄHÄMÄ ;
LEXICON V_88JÜVÄHÄMÄ
!! * Yaml: __V-juevaemae88__
+V:%{front%} V_88SIBAHAMA/JÜVÄHÄMÄ ;
LEXICON V_88SIBAHAMA/JÜVÄHÄMÄ !!= * @CODE@ sibama:siba
:%^StrGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^StrGStem Harm-Neutr_V0-IND-PRS-SC-12 ;
:%>h%{XV%} Harm-Neutr_V0-IND-PRS-SC-12 ;
:%^StrGStem ACT-IND-PRS-SG3_ZERO ;
:%^StrGStem ACT-IND-PRS-3_s/sEq ;
:%>h%{XV%} ACT-IND-PRS-3_s/sEq ;



: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

: ACT-IND-PRT-12_si ;
:%^StrGStem ACT-IND-CONNEG ;
: IND-PRT-SC3_si ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_mA ;
:%>h%{XV%} Harm-Neutr_INF_mA ;
: Harm-Neutr_INF_dAq ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
:%^StrGStem Harm-Neutr_ACT-PRSPRC_v ;
+Use/NG:%>h%{XV%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC-ALL_nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
: Harm-Neutr_PSS-PRFPRC_t ;
: PSS-PRFPRC_tU-SG-NOM ;
: MUTUAL_JUS ;

!LEXICON V-89
!+V+WORK: # ;
!90
!manitsõma:mani
LEXICON V-manitsõma
+V+Act+Ind+Prs+Sg1:ds%{eõ%} # ;
+V+Act+Ind+Prs+Sg3:ts%{eõ%}s # ;
+V+Act+Ind+Prt+Sg1:dsi # ;
+V+Act+Ind+Prt+Sg3:tsʼ # ;
+V+Inf:daq # ;
+V+Inf:tsaq # ;
+V+Act+PrfPrc:nuq # ;
+V+Act+PrfPrc:tsnuq # ;
+V+Pss+PrfPrc:t # ;
+V+Pss+PrfPrc:ts%{eõ%}t # ;

!! !INDICATIVE PRESENT SUBJECT CONJUGATION
LEXICON Harm-Neutr_V0-IND-PRS-SC-12
: ACT-IND-PRS-SG1_ZERO ;

 ACT-IND-PRS-SG2_t ;
+Use/NG: ACT-IND-PRS-SG2_dEq ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;
!+Use/NG+Act+Ind+Prs+Pl2:%>d%{eõ%}q K ; These will be dealt with in cg
!+Use/NG+Act+Ind+Prs+Pl2:%>t K ; These will be dealt with in cg
ACT-IND-CONNEG ;

LEXICON Harm-Neutr_ACT-IND-PRS-SG1_ZERO_USUALLY-WEAK
: ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-CONNEG ;
+Act+Imprt+Sg2: K ;


LEXICON Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG
 ACT-IND-PRS-SG2_t ;
+Use/NG: ACT-IND-PRS-SG2_dEq ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;



LEXICON ACT-IND-PRS-SG1_ZERO
+Act+Ind+Prs+Sg1: K ;

LEXICON ACT-IND-PRS-SG2_t
+Act+Ind+Prs+Sg2:%>t K ;

LEXICON ACT-IND-PRS-SG2_dEq
+Act+Ind+Prs+Sg2:%>d%{eõ%}q K ;

LEXICON ACT-IND-PRS-SG3_ZERO
+Act+Ind+Prs+Sg3: K ;

LEXICON ACT-IND-PRS-PL1_miq
+Act+Ind+Prs+Pl1:%>miq K ;

LEXICON ACT-IND-PRS-PL2_tiq
+Act+Ind+Prs+Pl2:%>tiq K ;
LEXICON ACT-IND-PRS-PL2_t
+Act+Ind+Prs+Pl2:%>t K ;
LEXICON ACT-IND-PRS-PL2_dEq
+Act+Ind+Prs+Pl2:%>d%{eõ%}q K ;

LEXICON ACT-IND-PRS-SG3_sE
+Act+Ind+Prs+Sg3:%>s%{eõ%} K ;

LEXICON ACT-IND-PRS-PL3_iAq
+Act+Ind+Prs+Pl3:%>i%{aä%}q K ;

LEXICON ACT-IND-PRS-PL3_vAq
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ;


LEXICON ACT-IND-PRS-3_s/sEq
+Act+Ind+Prs+Sg3:%>s K ;
+Act+Ind+Prs+Pl3:%>s%{eõ%}q K ;

LEXICON ACT-IND-CONNEG
+Act: IND-CONNEG ;
LEXICON PSS-IND-CONNEG
+Pss: IND-CONNEG ;
LEXICON IND-CONNEG
+Ind+Prs+Neg:%-%{XV%}iq K ; !!= * @CODE@ This gives stress
+Use/NG+Ind+Prs+Neg:%-iq K ; !!= * @CODE@ This gives stress
+Use/NG+Ind+Prs+Neg:%-%{XV%}i K ; !!= * @CODE@ more neutral without q
+Use/NG+Ind+Prs+Neg:%-i K ; !!= * @CODE@ more neutral without q
+Ind+ConNegI:%>q K ;
+Ind+ConNegII: K ;
+Ind+Prt+Neg:%-%{XV%}s K ;
+Use/NG+Ind+Prt+Neg:%-s K ;

LEXICON ACT-COND-CONNEG
+Act: COND-CONNEG ;
LEXICON PSS-COND-CONNEG
+Pss: COND-CONNEG ;
LEXICON COND-CONNEG
+Cond+Prs+Neg:si%-iq K ;
+Cond+Prs+Neg:si%-i K ;
+Cond+Prt+Neg:si%-is K ;
+Cond+ConNegI:siq K ;
+Cond+ConNegII:si K ;



!! !JUS
LEXICON MUTUAL_JUS !!= * @CODE@ regardless of harmony this is back
+Jus:%>guq%{back%} K ;
LEXICON MUTUAL_JUS_kuq 
+Jus:%>kuq%{back%} K ;

!! CHECK THIS
!! !PASSIVE INDICATIVE PRESENT CONJUGATION

LEXICON PSS-IND-PRS_tA/SG1
+Pss+Ind+Prs+Sg1:%>t%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>t%{aä%} PSS-IND-CONNEG ;

LEXICON PSS-IND-PRS_dA/SG1
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>d%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>d%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}tiq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;


LEXICON Harm-Neutr_VISKAMA_PSS-IND-PRS
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;


LEXICON Harm-Neutr_JUVVA_PSS-IND-PRS !!= * @CODE@ juvva süvvä
+Pss+Ind+Prs+Sg1: K ;
+Pss+Ind+Prs+Sg2:%>t K ;
+Use/NG+Pss+Ind+Prs+Sg2:%>d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>s K ;
+Pss+Ind+Prs+Pl1:%>miq K ;
+Pss+Ind+Prs+Pl2:%>tiq K ;
+Pss+Ind+Prs+Pl3:%>s%{eõ%}q K ;
: PSS-IND-CONNEG ;


LEXICON Harm-Neutr_JUVVA_PSS-IND-PRT-1/2 !!= * @CODE@ juvvi süvvi
+Pss+Ind+Prt+Sg1: K ;
+Pss+Ind+Prt+Sg2:%>t K ;
+Use/NG+Pss+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Use/NG+Pss+Ind+Prt+Sg2:%>q K ;
+Pss+Ind+Prt+Pl1:%>miq K ;
+Pss+Ind+Prt+Pl2:%>tiq K ;
!+Use/NG+Pss+Ind+Prt+Pl2:%>d%{eõ%}q K ; These will be dealt with in cg




!! !INDICATIVE PRETERIT SUBJECT CONJUGATION
LEXICON Harm-Neutr_IND-PRT-SCSG1_iE
+Act+Ind+Prt+Sg1:%>i%{eõ%} K ;

LEXICON Harm-Neutr_IND-PRT-SCSG1_sE
+Act+Ind+Prt+Sg1:%>s%{eõ%} K ;

LEXICON ACT-IND-PRT-1/2_ZERO
+Act+Ind+Prt+Sg1: K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
!+Use/NG+Act+Ind+Prt+Pl1: K ; ! These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>tiq K ;

LEXICON Harm-Neutr_ACT-IND-PRT-SG1_ZERO_USUALLY-WEAK
+Act+Ind+Prt+Sg1: K ;
+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Act+Ind+Prt+Pl3:%>q K ; 


LEXICON Harm-Neutr_ACT-IND-PRT-SG2_USUALLY-STRONG
+Act+Ind+Prt+Sg2:%>t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


LEXICON V0-IND-PRT-SC-12_i
+Act+Ind+Prt+Sg1:%>i K ;
+Act+Ind+Prt+Sg2:%>iq K ;
+Act+Ind+Prt+Sg2:%>it K ;
+Act+Ind+Prt+Sg2:%>id%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>imiq K ;
!+Act+Ind+Prt+Pl1:%>i K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>itiq K ;
!+Act+Ind+Prt+Pl2:%>it K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>iq K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>id%{eõ%}q K ; These will be dealt with in cg

LEXICON FRONT_V0-IND-PRT-SC-12_ie
+Act+Ind+Prt+Sg1:%>ie K ;
+Act+Ind+Prt+Sg2:%>ieq K ;
+Act+Ind+Prt+Sg2:%>iet K ;
+Act+Ind+Prt+Sg2:%>iedeq K ;
+Act+Ind+Prt+Pl1:%>iemiq K ;
+Act+Ind+Prt+Pl2:%>ietiq K ;
!+Act+Ind+Prt+Pl2:%>iet K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>ieq K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>iedeq K ; These will be dealt with in cg

LEXICON Harm-Neutr_V0-IND-PRT-SC-12_iE
+Act+Ind+Prt+Sg1:%>i%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>i%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>i%{eõ%}tiq K ;
!+Act+Ind+Prt+Pl2:%>i%{eõ%}t K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>i%{eõ%}q K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>i%{eõ%}d%{eõ%}q K ; These will be dealt with in cg

LEXICON FRONT_V0-IND-PRT-SC-12_se
+Act+Ind+Prt+Sg1:%>se K ;
+Act+Ind+Prt+Sg2:%>seq K ;
+Act+Ind+Prt+Sg2:%>set K ;
+Act+Ind+Prt+Sg2:%>sedeq K ;
+Act+Ind+Prt+Pl1:%>semiq K ;
+Act+Ind+Prt+Pl2:%>setiq K ;
!+Act+Ind+Prt+Pl2:%>set K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>seq K ; These will be dealt with in cg
!+Act+Ind+Prt+Pl2:%>sedeq K ; These will be dealt with in cg

LEXICON Harm-Neutr_V0-IND-PRT-SC-12_sE
+Act+Ind+Prt+Sg1:%>s%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>s%{eõ%}tiq K ;
!+Act+Ind+Prt+Pl2:%>s%{eõ%}t K ; These will be dealt with in cg


LEXICON Harm-Neutr_ACT-IND-PRT-1/2_E
+Act+Ind+Prt+Sg1:%>%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>%{eõ%}miq K ;
!+Act+Ind+Prt+Pl1:%>%{eõ%} K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>%{eõ%}tiq K ;

LEXICON ACT-IND-PRT-12_si
+Act+Ind+Prt+Sg1:%>si K ;
+Act+Ind+Prt+Sg2:%>siq K ;
+Act+Ind+Prt+Sg2:%>sit K ;
+Act+Ind+Prt+Sg2:%>sid%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>simiq K ;
!+Act+Ind+Prt+Pl1:%>si K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>sitiq K ;

LEXICON ACT-IND-PRT-12_si-NO-s
+Act+Ind+Prt+Sg1:%>i K ;
+Act+Ind+Prt+Sg2:%>iq K ;
+Act+Ind+Prt+Sg2:%>it K ;
+Act+Ind+Prt+Sg2:%>idõq K ;
+Act+Ind+Prt+Pl1:%>imiq K ;
!+Act+Ind+Prt+Pl1:%>i K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>itiq K ;


LEXICON ACT-IND-PRT-SG3_ZERO
+Act+Ind+Prt+Sg3: K ;
LEXICON ACT-IND-PRT-SG3_i
+Act+Ind+Prt+Sg3:%>i K ;

LEXICON ACT-IND-PRT-SG3_APOSTROPHE
+Act+Ind+Prt+Sg3:%>ʼ K ;

LEXICON ACT-IND-PRT-SG3_iE
+Act+Ind+Prt+Sg3:%>i%{eõ%} K ;

LEXICON ACT-IND-PRT-SG3_E
+Act+Ind+Prt+Sg3:%>%{eõ%} K ;

LEXICON ACT-IND-PRT-SG3_sE
+Act+Ind+Prt+Sg3:%>s%{eõ%} K ;

LEXICON ACT-IND-PRT-PL3_q/vAq
+Act+Ind+Prt+Pl3:%>q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


LEXICON Harm-Neutr_IND-PRT-SC3_s
+Act+Ind+Prt+Sg3:%^WGStem%>s K ;
+Act+Ind+Prt+Pl3:%>siq K ;
+Act+Ind+Prt+Pl3:%>siv%{aä%}q K ;

LEXICON Harm-Neutr_IND-PRT-SC3_sʼ
+Act+Ind+Prt+Sg3:%^WGStem%>sʼ K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siq K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siv%{aä%}q K ;

LEXICON Harm-Neutr_IND-PRT-SC3_sʼ-NO-s
+Act+Ind+Prt+Sg3:%^WGStem%>ʼ K ;
+Act+Ind+Prt+Pl3:%^VowRM%>iq K ;
+Act+Ind+Prt+Pl3:%^VowRM%>iv%{aä%}q K ;

LEXICON Harm-Neutr_IND-PRT-SC3_si_WEAKGRADE
+Act+Ind+Prt+Sg3:%^WGStem%>si K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siq K ;
+Act+Ind+Prt+Pl3+Err/Orth:%^VowRM%>siv%{aä%}q K ;

LEXICON IND-PRT-SC3_si
+Act+Ind+Prt+Sg3:%>si K ;
+Act+Ind+Prt+Pl3:%>siq K ;
+Use/NG+Act+Ind+Prt+Pl3:%>siv%{aä%}q K ;



!! !PASSIVE INDICATIVE PRETERIT CONJUGATION

LEXICON PSS-IND-PRT-SG1_di
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>diq K ;
+Pss+Ind+Prt+Sg2:%>dit K ;
+Pss+Ind+Prt+Sg2:%>did%{eõ%}q K ;
+Pss+Ind+Prt+Pl1:%>dimiq K ;
+Pss+Ind+Prt+Pl2:%>ditiq K ;
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;

LEXICON PSS-IND-PRT-SG1_ti
+Pss+Ind+Prt+Sg1:%>ti K ;
+Pss+Ind+Prt+Sg2:%>tiq K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
+Pss+Ind+Prt+Pl2:%>titiq K ;
:%>t%{aä%} PSS-IND-CONNEG ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl3:%>tiq K ;
+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;

LEXICON Harm-Neutr_VISKAMA_PSS-IND-PRT_ti
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>diq K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
!+Pss+Ind+Prt+Pl1:%>di K ; These will be dealt with in cg
!+Pss+Ind+Prt+Pl2:%>diq K ; These will be dealt with in cg
+Pss+Ind+Prt+Pl2:%>titiq K ;
!+Pss+Ind+Prt+Pl2:%>tit K ; These will be dealt with in cg
!+Pss+Ind+Prt+Pl2:%>tid%{eõ%}q K ; These will be dealt with in cg
+Pss+Ind+Prt+Pl3:%>diq K ;
+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;
!:%>d%{aä%} PSS-IND-CONNEG ; ! CHECKME 2017-02-02


LEXICON Harm-Neutr_IND-PRT-OC1-2_i !!= * @CODE@ juvv süvv
!! usually weak grade
+Pss+Ind+Prt+Sg1:i K ;
+Pss+Ind+Prt+Sg2:it K ;
+Use/NG+Pss+Ind+Prt+Sg2:iq K ;
+Use/NG+Pss+Ind+Prt+Sg2:ideq K ;
+Pss+Ind+Prt+Pl1:imiq K ;
+Pss+Ind+Prt+Pl2:itiq K ;
!+Use/NG+Pss+Ind+Prt+Pl2:ideq K ; These will be dealt with in cg
:%{aä%} PSS-IND-CONNEG ; !CHECKME 2017-02-02

LEXICON Harm-Neutr_IND-PRT-OC3_i !!= * @CODE@ süüd
!! usually strong grade
+Pss+Ind+Prt+Sg3:%>i K ;
+Pss+Ind+Prt+Pl3:%>iq K ;
+Use/NG+Pss+Ind+Prt+Pl3:%>iv%{aä%}q K ;

LEXICON Harm-Neutr_PSS-PRFPRC_dU
+Pss+PrfPrc:d%{uü%} K ;

!! !NON-FINITES
LEXICON Harm-Neutr_INF_mA
+Inf/mA:%>m%{aä%} K ;
 V2N_minE ;

LEXICON Harm-Neutr_INF_dAq
+Inf:%>d%{aä%}q K ;
+Err/Orth+Use/NG+Inf:%>d%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_iAq
+Inf:%>i%{aä%}q K ;
+Err/Orth+Use/NG+Inf:%>i%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_vAq
+Inf:%>v%{aä%}q K ;
+Err/Orth+Use/NG+Inf:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_Aq
+Inf:%>%{aä%}q K ;
+Err/Orth+Use/NG+Inf:%>%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_tAq
+Inf:%>t%{aä%}q K ;
+Err/Orth+Use/NG+Inf:%>t%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_ZEROq
+Inf:%>q K ;
+Err/Orth+Use/NG+Inf: K ; !!= * @CODE@ no q

LEXICON V2N_minE
+Der/minE+N:%>mis ATTESTED-NOUN-STEM-CONTINUED ;
+Der/minE+N:%>mi NMN_16ABILINÕ/INEMINE ;
+Der/JA+N:%>j NMN_9KIPÕNʼ/ELLÄI ; !!≈ * __@CODE@__ This should have a +Der/JA tag


LEXICON Harm-Neutr_ACT-PRSPRC_v
+Act+PrsPrc:v K ;
+Act+PrsPrc:v A_9KIPÕNʼ/ELLÄI ;
+Act+PrsPrc+Sg+Nom:v K ;
+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON ACT-PRSPRC_vA
+Act+PrsPrc:%>v%{aä%} K ;		! juuva süüvä
+Act+PrsPrc:%>v%{aä%} A_9KIPÕNʼ/ELLÄI ;	! MUST BE FIXED 2015-06-09
+Act+PrsPrc+Sg+Nom:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_ACT-PRFPRC-ALL_nUq
 Harm-Neutr_ACT-PRFPRC_nUq ;
 Harm-Neutr_ACT-PRFPRC-OBL_nU ;

LEXICON Harm-Neutr_ACT-PRFPRC_nUq
+Act+PrfPrc:%>n%{uü%}q K ;
+Err/Orth+Use/NG+Act+PrfPrc:%>n%{uü%} K ; !!= * @CODE@ no q
+Act+PrfPrc+Sg+Nom:%>n%{uü%}q K ;
+Err/Orth+Use/NG+Act+PrfPrc+Sg+Nom:%>n%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_ACT-PRFPRC-OBL_nU !!= * @CODE@ only oblique cases and Pl+Nom
+Act+PrfPrc+Sg+Gen:%>n%{uü%} K ;
+Act+PrfPrc+Sg+Par:%>n%{uü%}t K ;
+Act+PrfPrc+Sg+Ill:%>n%{uü%}he K ;
+Act+PrfPrc+Pl+Nom:%>n%{uü%}q K ;
+Act+PrfPrc+Pl+Gen:%>n%{uü%}id%{eõ%} K ;
+Use/NG+Act+PrfPrc+Pl+Gen:%>n%{uü%}isi K ;
+Act+PrfPrc+Pl+Par:%>n%{uü%}id K ;
+Use/NG+Act+PrfPrc+Pl+Par:%>n%{uü%}isi K ;
+Act+PrfPrc+Pl+Ill:%>n%{uü%}ihe K ;
+Use/NG+Act+PrfPrc+Pl+Ill:%>n%{uü%}isihe K ;
+Err/Orth+Use/NG+Act+PrfPrc+Pl+Nom:%>n%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq
+Act+PrfPrc:%^WGStem%>n%{uü%}q K ;
+Act+PrfPrc:%^WGStem%>n%{uü%} NMN_2KASUNUQ/ELÄNÜQ ;
+Err/Orth+Use/NG+Act+PrfPrc:%^WGStem%>n%{uü%} K ; !!= * @CODE@ no q


!! PASSIVE DISTRIBUTION
LEXICON Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION
Harm-Neutr_NÕSTMA-PSS-IND-PRS_dA ; !!= * @CODE@ +Pss+Ind+Prs+Sg1: sõidõda
!Harm-Neutr_KAOTAMA-PSS-IND-PRT_di ; !!= * @CODE@ +Pss+Ind+Prt+Sg1: sõidõdi
!Harm-Neutr_V0-PSS-PRSPRC_dAv ; !!= * @CODE@ +Pss+PrsPrc+Sg+Nom: kaotõdav
Harm-Neutr_KAOTAMA-PSS-PRFPRC_t ; !!= * @CODE@ +Pss+PrfPrc+Sg+Nom: kaotõt

LEXICON Harm-Neutr_KAOTAMA-PSS-DISTRIBUTION
Harm-Neutr_KAOTAMA-PSS-IND-PRS_dA ; !!= * @CODE@ +Pss+Ind+Prs+Sg1: kaotõda
Harm-Neutr_KAOTAMA-PSS-IND-PRT_di ; !!= * @CODE@ +Pss+Ind+Prt+Sg1: kaotõdi
Harm-Neutr_V0-PSS-PRSPRC_dAv ; !!= * @CODE@ +Pss+PrsPrc+Sg+Nom: kaotõdav
Harm-Neutr_KAOTAMA-PSS-PRFPRC_t ; !!= * @CODE@ +Pss+PrfPrc+Sg+Nom: kaotõt


LEXICON Harm-Neutr_NÕSTMA-PSS-IND-PRS_dA
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Use/NG+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%} K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl1:%>t%{aä%}mi K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl2:%>t%{aä%}ti K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_KAOTAMA-PSS-IND-PRS_dA
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}t K ;
+Use/NG+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>d%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>d%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%} K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl1:%>d%{aä%}mi K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl2:%>d%{aä%}ti K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_77JUUMA/SÜÜMÄ-PSS-IND-PRT_di
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Use/NG+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;

LEXICON Harm-Neutr_KAOTAMA-PSS-IND-PRT_di !!= * @CODE@ +Pss+Ind+Prt+Sg3: kaotõdi
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>dit K ;
+Use/NG+Pss+Ind+Prt+Sg2:%>diq K ;
+Use/NG+Pss+Ind+Prt+Sg2:%>did%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl1:%>dimiq K ;
+Pss+Ind+Prt+Pl2:%>ditiq K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Use/NG+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>di K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>did%{eõ%} K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl1:%>dimi K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl2:%>diti K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>di K ; !!= * @CODE@ no q
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>div%{aä%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_V0-PSS-PRSPRC_tAv
+Pss+PrsPrc:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>t%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>t%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>t%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>vihe K ;
+Use/NG+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>viid%{eõ%} K ;
+Use/NG+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>viid K ;
+Use/NG+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>viihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_V0-PSS-PRSPRC_ttAv
+Pss+PrsPrc:%>tt%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>tt%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>tt%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>tt%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>tt%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>tt%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>tt%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>tt%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>tt%{aä%}%>vihe K ;
+Use/NG+Pss+PrsPrc+Pl+Gen:%>tt%{aä%}%>viid%{eõ%} K ;
+Use/NG+Pss+PrsPrc+Pl+Par:%>tt%{aä%}%>viid K ;
+Use/NG+Pss+PrsPrc+Pl+Ill:%>tt%{aä%}%>viihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Nom:%>tt%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_V0-PSS-PRSPRC_dAv
+Pss+PrsPrc:%>d%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>d%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>d%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>d%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>d%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>d%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>d%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>d%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>d%{aä%}%>vihe K ;
+Use/NG+Pss+PrsPrc+Pl+Gen:%>d%{aä%}%>viid%{eõ%} K ;
+Use/NG+Pss+PrsPrc+Pl+Par:%>d%{aä%}%>viid K ;
+Use/NG+Pss+PrsPrc+Pl+Ill:%>d%{aä%}%>viihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Nom:%>d%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_KAOTAMA-PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_d
+Pss+PrfPrc:%>d K ;
+Pss+PrfPrc+Sg+Nom:%>d K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Use/NG+Pss+PrfPrc+Sg+Nom:%>d%{uü%} K ; !!= * @CODE@
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_t-ONLY
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>t%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;


LEXICON Harm-Neutr_PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_VISKAMA-PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_dU-SG-NOM
+Pss+PrfPrc:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Nom:%>d%{uü%} K ;

LEXICON Harm-Neutr_JUUMA-PSS-PRFPRC_d
+Pss+PrfPrc:%>d K ;
+Pss+PrfPrc+Sg+Nom:%>d K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_V0-PSS-PRFPRC_tt
+Pss+PrfPrc:%>tt K ;
+Pss+PrfPrc+Sg+Nom:%>tt K ;
+Pss+PrfPrc+Sg+Gen:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>tt%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>tt%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>t%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>tt%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Gen:%>tt%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>tt%{uü%}id K ;
+Pss+PrfPrc+Pl+Par:%>tt%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>tt%{uü%}isihe K ;
+Pss+PrfPrc+Pl+Ill:%>tt%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>t%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_tU-SG-NOM
+Pss+PrfPrc:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Nom:%>t%{uü%} K ;

!!
LEXICON Harm-Neutr_ACT-PRSPRC_v_LUGÕMA !!= * @CODE@ lugõma:lugõ
!! Retain consonant and stem vowel
+Inf/mA:%>m%{aä%} K ;
 V2N_minE ;
!

+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q
!


LEXICON Harm-Neutr_ACT-IND-PRS-3_LUGÕMA !!= * @CODE@ (6) lugõma:lugõ
: ACT-IND-PRS-SG3_ZERO ;
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 

LEXICON Harm-Neutr_ACT-PRFPRC_v_LUGÕMA
+Act+PrfPrc:%>n%{uü%}q K ;
+Act+PrfPrc:%>n%{uü%} NMN_2KASUNUQ/ELÄNÜQ ; !FIX ME
+Err/Orth+Use/NG+Act+PrfPrc:%>n%{uü%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_LUGÕMA_IND-PRT-SG3
+Act+Ind+Prt+Sg3: K ;

LEXICON Harm-Neutr_ACT-PRSPRC-SG-NOM_v
+Act+PrsPrc+Sg+Nom:%>v K ;
LEXICON Harm-Neutr_ACT-PRSPRC-SG-NOM_vA
+Act+PrsPrc+Sg+Nom:%>v%{aä%} K ;

LEXICON Harm-Neutr_LUGÕMA_IND-CONNEGII
!
: ACT-IND-PRS-SG1_ZERO ;
: ACT-IND-PRS-SG2_t ;
: ACT-IND-PRS-SG2_dEq ;
: ACT-IND-PRS-SG3_ZERO ;
 ACT-IND-PRS-PL1_miq ;
: ACT-IND-PRS-PL2_tiq ;
 ACT-IND-CONNEG ;

LEXICON Harm-Neutr_LUGÕMA_PSS-PRC
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Use/NG+Pss+PrfPrc:%>t%{uü%} K ;
+Use/NG+Pss+PrfPrc+Sg+Nom:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q
!
+Pss+PrsPrc:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>t%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>t%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>t%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>vihe K ;
+Use/NG+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>viid%{eõ%} K ;
+Use/NG+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>viid K ;
+Use/NG+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>viihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q
!
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Use/NG+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;

+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Use/NG+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
+Pss+Ind+Prt+Pl2:%>titiq K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Use/NG+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;


LEXICON Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1
+Act+Ind+Prt+Sg1: K ;
+Use/NG+Act+Ind+Prt+Sg1:%>e K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Use/NG+Act+Ind+Prt+Pl1:%>%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Use/NG+Act+Ind+Prt+Pl2:%>%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>%{eõ%}q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>%{eõ%}v%{aä%}q K ;

LEXICON TULÕMA_ACT-IND-PRT-SG1
+Act+Ind+Prt+Sg1: K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


! vim: set ft=xfst-lexc:

