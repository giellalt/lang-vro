!! Verb inflection
! ---------------
!! Võro language verbs inflect for person and number.

LEXICON V_EI
+V+Neg+Prs:i # ;
+V+Neg+Prt:s # ;
+V+Neg+Prt:siq # ;

LEXICON V_48KASUMA !kasuma:ka%{sØ%}s
!! There are other verbs here, cf. V_ELAEMAE
!! * Yaml: __V-kasuma48__
:%{back%} V_48KASUMA/XX ;

LEXICON V_48KASUMA/XX
:%^WGStem%>%{uü%} ACT-IND-PRS-12 ;
:%^WGStem%>%{uü%} ACT-IND-PRS-3_s/sEq ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^WGStem%>v ACT-IND-PRT-12_i ;
:%^WGStem%>%{uü%} ACT-IND-CONNEG ;
:%^WGStem%>v ACT-IND-PRT-SG3_i ;
:%^WGStem%>vi ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem%>%{uü%} Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^WGStem%>%{uü%} Harm-Neutr_INF_mA ;
:%>%{uü%} Harm-Neutr_INF_ZEROq ;
:%>%{uü%} Harm-Neutr_ACT-PRSPRC_v ;
:%^WGStem%>%{uü%} Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^WGStem%>%{uü%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{uü%} Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem%>%{uü%} PSS-PRFPRC_tU-SG-NOM ;
:%^WGStem%>%{uü%} MUTUAL_JUS ;

LEXICON V_48KUIOMA
!! There are other verbs here, cf. V_ELAEMAE
!! * Yaml: __V-kuioma48__
:%{back%} V_48KUIOMA/XX ;

LEXICON V_48KUIOMA/XX
 ACT-IND-PRS-12 ;
 ACT-IND-PRS-3_s/sEq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGStem ACT-IND-PRT-12_i ;
 ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_i ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
 Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VowRM%>õ%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_48PALAMA
!! There are other verbs here, cf. V_ELAEMAE
!! * Yaml: __V-palama48__
:%{back%} V_48PALAMA/ELAEMAE ;
LEXICON V_48PALAMA/ELAEMAE
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;

:%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^VowRM ACT-IND-PRT-SG3_i ;
:%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^WGStem MUTUAL_JUS ;

LEXICON V_48ELAEMAE
!! There are other verbs here, cf. V_ELAEMAE
!! * Yaml: ____
:%{front%} V_48PALAMA/ELAEMAE ;


!LEXICON V_ELAEMAE !!≈ * __@CODE@__ elämä:elä
!!! * Yaml: __elaemae48__
!:%{front%} V_48PALAMA/ELAEMAE ;

!LEXICON V_48SADAMA
!!! There are other verbs here, cf. V_ELAEMAE
!!! * Yaml: __V-sadama48__
!:%{back%} V_48SADAMA/XX ;

!LEXICON V_48SADAMA/XX
!:%^StrGStem ACT-IND-PRS-12 ;
! ACT-IND-PRS-3_s/sEq ;
! Harm-Neutr_VISKAMA_PSS-IND-PRS ;

!:%^XStrGStem%>%{eõ%} ACT-IND-PRT-1/2_ZERO ;
!!! Act_Prt_Sg1: sattõ
! ACT-IND-CONNEG ;
!:%^XStrGStem%>%{eõ%} ACT-IND-PRT-SG3_ZERO ;
! Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

! Harm-Neutr_INF_mA ;
!:%^StrGStem Harm-Neutr_INF_ZEROq ;
! Harm-Neutr_ACT-PRSPRC_v ;
! Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
! Harm-Neutr_V0-PSS-PRSPRC_tAv ;
!:%^VowRM%>%{aä%} Harm-Neutr_PSS-PRFPRC_t ;
!!! Pss_PrfPrc: sadat

LEXICON V_49LWPMA !!≈ * __@CODE@__ lõpma:lõpp
:%{back%} V_49LWPMA/XX ;
LEXICON V_49LWPMA/XX !!≈ * __@CODE@__ lõpma:lõpp
:%^WGStem Harm-Neutr_INF_mA ;
:%^WGStem ACT-PRFPRC-ALL_nUq ;
!lõp
+Act+Ind+Prs+Sg1:%^WGStem%>%{eõ%}%{PrsSg1%} K ;
+Act+Ind+Prs+Sg2:%^WGStem%>%{eõ%}t K ;
+Err/Orth+Use/NG+Act+Ind+Prs+Sg2:%^WGStem%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prs+Pl1:%^WGStem%>%{eõ%}miq K ;
+Act+Ind+Prs+Pl2:%^WGStem%>%{eõ%}tiq K ;

+Act+Ind+Prs+Sg3:%^WGStem%>%{eõ%}s K ;
+Act+Ind+Prs+Pl3:%^WGStem%>v%{aä%}q K ;

! lõpp
:%{eõ%} Harm-Neutr_INF_ZEROq ;
+Act+Ind+Prt+Sg1:%>i K ;
+Act+Ind+Prt+Sg2:%>it K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>id%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>imiq K ;
+Act+Ind+Prt+Pl2:%>itiq K ;
+Act+Ind+Prt+Sg3:%>i K ;

:%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem%>%{eõ%} PSS-IND-PRS-ALL_tA ;
:%^WGStem%>%{eõ%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;


:%^WGStem ACT-PRSPRC_vA ;
:%^WGStem%>%{eõ%} ACT-IND-CONNEG ;
:%^WGStem MUTUAL_JUS_kuq ;


LEXICON V_50TONISWMA !!≈ * __@CODE@__ tonisõma:tonis
!! * Yaml: __toniswma50__
:%{back%} V_50TONISWMA/HELISEMAE ;
LEXICON V_50TONISWMA/HELISEMAE !!≈ * __@CODE@__ tonisõma:tonis
:%{eõ%} ACT-IND-PRS-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;

 ACT-IND-PRT-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_i ;
:%>i ACT-IND-PRT-PL3_q/vAq ;

:%{eõ%} PSS-IND-PRS-ALL_dA ;
:%{eõ%} PSS-IND-PRT-ALL_di ;

:%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
 MUTUAL_JUS_kuq ;

LEXICON V_50HELISEMAE !!≈ * __@CODE@__ helisemä:helis
!! * Yaml: __helisemae50__
:%{front%} V_50TONISWMA/HELISEMAE ;


LEXICON V_51NWSWMA !!≈ * __@CODE@__ nõsõma:nõs
!! * Yaml: __nwswma51__
!! cf. tulõma
:%{back%} V_51NWSWMA/PAESEMAE ;
LEXICON V_51PAESEMAE !!≈ * __@CODE@__ päsemä:päs
!! * Yaml: __paesemae51__
!! cf. tulõma
:%{front%} V_51NWSWMA/PAESEMAE ;

LEXICON V_51NWSWMA/PAESEMAE !!≈ * __@CODE@__ nõsõma:nõs
:%{eõ%} ACT-IND-PRS-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^StrGSt ACT-IND-PRT-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
:%^StrGSt ACT-IND-PRT-SG3_i ;
!:%^StrGSt Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;

LEXICON V_52ALOSTWLWMA !!≈ * __@CODE@__ alostõlõma:alostõll
!! * Yaml: __alostwllwma52__
:%{back%} V_52ALOSTWLWMA/EHITELEMAE ;
LEXICON V_52EHITELEMAE !!≈ * __@CODE@__ ehitelemä:ehitell
!! ehitelemä:ehitell
!! * Yaml: __ehitellemae52__
:%{front%} V_52ALOSTWLWMA/EHITELEMAE ;

LEXICON V_52ALOSTWLWMA/EHITELEMAE !!≈ * __@CODE@__ alostõllõma:alostõl
:%{eõ%} ACT-IND-PRS-12 ;
:%^WGStem%{eõ%} ACT-IND-PRS-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%^WGStem%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%^WGStem PSS-IND-PRS-ALL_dA ;
+Act+Ind+Prt+Pl3:%^WGStem%>iq K ;
+Act+Ind+Prt+Pl3:%^WGStem%>iv%{aä%}q K ;

:%^WGStem ACT-IND-PRT-12_i ;
 ACT-IND-PRT-12_i ;
:%^WGStem%{eõ%} ACT-IND-CONNEG ;
:%^WGStem ACT-IND-PRT-SG3_i ;
 ACT-IND-PRT-SG3_i ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^WGStem%{eõ%} Harm-Neutr_INF_mA ;
+Err/Orth+Use/NG:%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_Aq ;
:%^WGStem%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;

:%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;
:%^WGStem PSS-PRFPRC_dU-SG-NOM ;
:%^WGStem MUTUAL_JUS ;


LEXICON V_53KAEMA !!≈ * __@CODE@__ 
:%{back%} V_53KAEMA/JAEAEMAE ;
LEXICON V_53JAEAEMAE !!≈ * __@CODE@__ 
:%{front%} V_53KAEMA/JAEAEMAE ;
LEXICON V_53KAEMA/JAEAEMAE !!≈ * __@CODE@__ 
:%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_dAv ;
:%^VOWRaise PSS-PRFPRC_d ;
:%^VOWRaise PSS-PRFPRC_dU-SG-NOM ;
:%^VOWRaise ACT-PRSPRC_vA ;
:%^VOWRaise Harm-Neutr_INF_mA ;
:%^VOWRaise ACT-PRFPRC-OBL_nU ;
:%^VOWRaise ACT-IND-PRS-SG1_ZERO ;
:%^VOWRaise ACT-IND-PRS-SG2_t ;
+Err/Orth+Use/NG:%^VOWRaise ACT-IND-PRS-SG2_dEq ;
!+Err/Orth+Use/NG:%^VOWRaise ACT-IND-PRS-PL2_dEq ;
:%^VOWRaise ACT-IND-PRS-PL2_tiq ;
:%^VOWRaise ACT-IND-CONNEG ;
!:%^VOWRaise ACT-IND-PRS-SG3_ZERO ;
:%^VOWRaise Harm-Neutr_77JUUMA/SUEUEMAE-PSS-IND-PRT_di ; !!= * @CODE@ only 3rd person

 ACT-PRFPRC_nUq-SG-NOM ; !!= * @CODE@ jäänüq keenüq
+Err/Orth+Use/NG: ACT-IND-PRS-SG3_sE ;
 ACT-IND-PRS-PL1_miq ;
 ACT-COND-CONNEG ;
 ACT-IND-PRS-PL3_vAq ;

:%^VOWRaise%^VowRM%>i V_77JUUMA/SUEUEMAE_RAISED-VOWEL-TENSE ; !!= * @CODE@ jäi kii
:%^VowRM%>i V_77JUUMA/SUEUEMAE_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ jäi kei
! CHECKME 2016-08-28
:%^VOWRaise ACT-IND-PRS-3_s/sEq ; !!= * @CODE@ (5) RAISED jääs jääseq 
! Act+Ind+Prs+Sg3

 ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) joovaq sööväq
: MUTUAL_JUS ;

!LEXICON V_54ISTMA !!≈ * __@CODE@__ 
!+WORK: # ;

LEXICON V_55KWNWLWMA !!≈ * __@CODE@__ kõnõlõma:kõ̭nõl
!! * Yaml: __kwnwlwma55__
:%{back%} V_55KWNWLWMA/HAERELEMAE ;
LEXICON V_55HAERELEMAE !!≈ * __@CODE@__ härelemä:härel
!! * Yaml: __haerelemae55__
:%{front%} V_55KWNWLWMA/HAERELEMAE ;

LEXICON V_55KWNWLWMA/HAERELEMAE
:%{eõ%} ACT-IND-PRS-12 ;
+Err/Orth+Use/NG:%{aä%} ACT-IND-PRS-12 ;
:%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%{aä%} ACT-IND-PRS-3_s/sEq ;
 PSS-IND-PRS-ALL_dA ;

 ACT-IND-PRT-12_i ;
:%{eõ%} ACT-IND-CONNEG ;
+Err/Orth+Use/NG:%{aä%} ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_APOSTROPHE ;
 ACT-IND-PRT-SG3_i ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 PSS-PRFPRC_d ;
 PSS-PRFPRC_dU-SG-NOM ;
 MUTUAL_JUS ;


LEXICON V_56HIIBWLWMA !!≈ * __@CODE@__ hiibõlõma:hiibõl
!! * Yaml: __hiibwlwma56__
:%{back%} V_56HIIBWLWMA/HEIKELEMAE ;
LEXICON V_56HEIKELEMAE !!≈ * __@CODE@__ heikelemä:heikel
!! * Yaml: __heikelemae56__
:%{front%} V_56HIIBWLWMA/HEIKELEMAE ;

LEXICON V_56HIIBWLWMA/HEIKELEMAE !!≈ * __@CODE@__ hiibõlõma:hiibõl
:%^Pen%^VOWRaise%{eõ%} ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise%{eõ%} ACT-IND-PRS-3_s/sEq ;


:%^Pen%^VOWRaise ACT-IND-PRT-12_i ;
:%^Pen%^VOWRaise%{eõ%} ACT-IND-CONNEG ;
:%^Pen%^VOWRaise ACT-IND-PRT-SG3_i ;
!:%^Pen%^VOWRaise ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VOWRaise%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise PSS-IND-PRS-ALL_dA ;
:%^Pen%^VOWRaise PSS-IND-PRT-ALL_di ;

:%^Pen%^VOWRaise%{eõ%} Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_INF_dAq ;
:%^Pen%^VOWRaise%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;
:%^Pen%^VOWLower%^Pen%^WGStem PSS-PRFPRC_dU-SG-NOM ;
:%^Pen%^VOWRaise MUTUAL_JUS ;

LEXICON V_57TAPLWMA
!! taplõma:tapõl
!! * Yaml: __taplwma57__
:%{back%} V_57TAPLWMA/UETLEMAE ;
LEXICON V_57UETLEMAE 
!! ütlemä:ütel
!! * Yaml: __kiirdlemae57__
:%{front%} V_57TAPLWMA/UETLEMAE ;

LEXICON V_57TAPLWMA/UETLEMAE
:%^Pen%^VowRM%{eõ%} ACT-IND-PRS-12 ;
:%^Pen%^VowRM%{eõ%} ACT-IND-PRS-3_s/sEq ;
 PSS-IND-PRS-ALL_dA ;

:%^Pen%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^VowRM%>%{eõ%} ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;

:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_INF_mA ;
:%^Pen%^VowRM%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_INF_dAq ;
 Harm-Neutr_PSS-PRFPRC_dU ;
 MUTUAL_JUS ;

LEXICON V_57VWITLWMA
!! võitlõma:võitõl
!! * Yaml: __taplwma57__
:%{back%} V_57VWITLWMA/KIIRDLEMAE ;
LEXICON V_57KIIRDLEMAE 
!! kiirdlemä:keerdel
!! * Yaml: __kiirdlemae57__
:%{front%} V_57VWITLWMA/KIIRDLEMAE ;

LEXICON V_57VWITLWMA/KIIRDLEMAE
:%^Pen%^VOWRaise%^Pen%^VowRM%{eõ%} ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise%^Pen%^VowRM%{eõ%} ACT-IND-PRS-3_s/sEq ;
:%^Pen%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^VOWRaise%^Pen%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^VOWRaise%{eõ%} ACT-IND-CONNEG ;
:%^Pen%^VOWRaise ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VOWRaise%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^VOWRaise Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_JUUMA-PSS-PRFPRC_d ;

:%^Pen%^VOWRaise%^Pen%^VowRM%>%{eõ%} Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise%^Pen%^VowRM%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;

:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_dU ;


LEXICON V_58HIRNAHTAMA !!≈ * __@CODE@__ hirnahtama:hirnahta
!! * Yaml: __V-hirnahtama58__
:%{back%} V_58HIRNAHTAMA/TAEHENDAEMAE ;

LEXICON V_58TAEHENDAEMAE !!≈ * __@CODE@__ tähendämä:tähendä
!! * Yaml: __V-hirnahtama58, V-taehendaemae__
!! front
:%{front%} V_58HIRNAHTAMA/TAEHENDAEMAE ;

LEXICON V_58HIRNAHTAMA/TAEHENDAEMAE !!≈ * __@CODE@__ hirnahtama:hirnahta, tähendämä:tähend
:%^Pen%^VOWRaise ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise ACT-IND-PRS-3_s/sEq ;

:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^VOWRaise ACT-IND-CONNEG ;
:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VOWRaise%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} PSS-IND-PRS-ALL_dA ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} PSS-IND-PRT-ALL_di ;
:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_INF_ZEROq ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} PSS-PRFPRC_dU-SG-NOM ;
:%^Pen%^VOWRaise MUTUAL_JUS ;

LEXICON V_59KAOTAMA !!≈ * __@CODE@__ kaotama:kaota
!!
:%{back%} V_59KAOTAMA/KERGUETAEMAE ;
LEXICON V_59KERGUETAEMAE
!! Based on 59KAOTAMA
!! * Yaml: __kerguetaemae__
:%{front%} V_59KAOTAMA/KERGUETAEMAE ;

LEXICON V_59KAOTAMA/KERGUETAEMAE !!≈ * __@CODE@__ kaotama:kaota
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_ACT-IND-PRS-SG1_ZERO_USUALLY-WEAK ; !!= * @CODE@ +Act+Ind+Prs+Sg1:%^Pen%^VOWRaise kaoda
!! +Act+Ind+Prs+Sg1, +Act+Ind+ConNegII, +Act+Imprt+Sg2

:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i   ACT-IND-PRT-SG1_ZERO/SG2_q/PL3_q ; !!= * @CODE@ kaodi
!! +Act+Ind+Prt+Sg1, +Err/Orth+Use/NG+Act+Ind+Prt+Sg2, +Act+Ind+Prt+Pl3

:%^Pen%^VOWRaise Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG ; !!= * @CODE@ kaota
!! +Act+Ind+Prs+Sg2, +Err/Orth+Use/NG+Act+Ind+Prs+Sg2, 
!! +Act+Ind+Prs+Pl1, +Act+Ind+Prs+Pl2

:%^Pen%^VOWRaise ACT-IND-PRS-3_s/sEq ; !!= * @CODE@ 
!! +Act+Ind+Prs+Sg3, +Act+Ind+Prs+Pl3

:%^Pen%^VOWRaise MUTUAL_JUS ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise ACT-PRFPRC-ALL_nUq ;

!:%^Pen%^VOWRaise Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_INF_ZEROq ;

:%^Pen%^VOWRaise%^VowRM%>i Harm-Neutr_ACT-IND-PRT-SG2_USUALLY-STRONG ; !!= * @CODE@ kaoti
!! +Act+Ind+Prt+Sg2, +Err/Orth+Use/NG+Act+Ind+Prt+Sg2, +Act+Ind+Prt+Pl1,
!! +Act+Ind+Prt+Pl2, +Err/Orth+Use/NG+Act+Ind+Prt+Pl3

:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ; !!= * @CODE@ +Act+Ind+Prt+Sg3:%^Pen%^VOWRaise kaot́
!! +Act+Ind+Prt+Sg3:%^Pen%^VOWRaise kaot́ Harm-Neutr_K ;

:%^Pen%^VOWRaise%^VowRM%>%{eõ%} Harm-Neutr_KAOTAMA-PSS-DISTRIBUTION ; != * @CODE@ kaotõ
!! +Pss+Ind+Prs+Sg1, +Pss+Ind+Prt+Sg1
!! +Pss+PrsPrc, +Pss+PrfPrc
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} PSS-PRFPRC_dU-SG-NOM ;


LEXICON V_60MANITSWMA !!≈ * __@CODE@__ manitsõma:mani
!! * Yaml: __manitswma60,manitswma61>manitswma__
:%{back%} V_60MANITSWMA/EHITSEMAE ;
LEXICON V_60EHITSEMAE !!≈ * __@CODE@__ ehitsemä:ehii
!! * Yaml: __manitswma60,manitswma61>manitswma__
:%{front%} V_60MANITSWMA/EHITSEMAE ;

LEXICON V_60MANITSWMA/EHITSEMAE !!≈ * __@CODE@__ manitsõma:mani

:ds%{eõ%} ACT-IND-PRS-SG1_ZERO ;
:ts%{eõ%} Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG ;
:ts%{eõ%} ACT-IND-PRS-3_s/sEq ;

:ds ACT-IND-PRT-SG1_i ;
:ds ACT-IND-PRT-SG2_iq-USUALLY-WEAK ;
:ts ACT-IND-PRT-SG2/PL1/PL2_i-USUALLY-STRONG ;
:ds%{eõ%} ACT-IND-CONNEG ;
:t%{sś%} ACT-IND-PRT-SG3_APOSTROPHE ;
:tsi ACT-IND-PRT-PL3_vAq ;
:dsi ACT-IND-PRT-PL3_q ;

:ts%{eõ%} PSS-IND-PRS-ALL_dA ;
:ts%{eõ%} PSS-IND-PRT-ALL_di ;

:ts%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:ts Harm-Neutr_INF_Aq ;

:ts%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:ts Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;

:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:ts%{eõ%}%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:ts%{eõ%}%^WGStem PSS-PRFPRC_tU-SG-NOM ;
  MUTUAL_JUS ;
:ts  MUTUAL_JUS_kuq ;

LEXICON V_62ARGNWMA
!! argnõma:arg
!! * Yaml: __argnwma62__
:%{back%} V_62ARGNWMA/TAERKNEMAE ;
LEXICON V_62TAERKNEMAE
!! tärknemä:tärk
!! * Yaml: __taerknemae62__
:%{front%} V_62ARGNWMA/TAERKNEMAE ;

LEXICON V_62ARGNWMA/TAERKNEMAE 
:%>n%{eõ%} ACT-IND-PRS-12 ;
:%>n%{eõ%} ACT-IND-PRS-3_s/sEq ;

:%>s ACT-IND-PRT-12_i ;
:%>n%{eõ%} ACT-IND-CONNEG ;
:%^WGStem%>%{eõ%} ACT-IND-PRT-SG3_sJ ;
:%>si ACT-IND-PRT-PL3_q/vAq ;

!: Harm-Neutr_ACT-IND-PRT-SCPL3_vAq ;
:%^WGStem%>%{eõ%} PSS-IND-PRS-ALL_tA ;
:%^WGStem%>%{eõ%} PSS-IND-PRT-ALL_ti ;

:n%{eõ%} Harm-Neutr_INF_mA ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%{eõ%}%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%{eõ%}%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%{eõ%}%^Pen%^WGStem MUTUAL_JUS_kuq ;

LEXICON V_63SWIMAMA !!≈ * __@CODE@__ sõimama:sõima
! Ind.Prs.ScSg3 in -s
! vowel_harmony: back
!! * Yaml: __V-swimama63__
:%{back%} V_63SWIMAMA/XX ; 
LEXICON V_63SWIMAMA/XX !!≈ * __@CODE@__ sõimama:sõima
:%^Pen%^VOWRaise ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise ACT-IND-PRS-3_s/sEq ;

 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^VOWRaise%^VowRM%>s ACT-IND-PRT-12_si ;
!sõimssi
:%>s ACT-IND-PRT-12_si ;
!sõimassi

:%^Pen%^VOWRaise ACT-IND-CONNEG ;

 ACT-IND-PRT-SC3_sJ ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;

:%^WGStem Harm-Neutr_INF_dAq ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;

 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
 MUTUAL_JUS_kuq ;

LEXICON V_63VISKAMA !!≈ * __@CODE@__ viskama:viska
! from V_63SWIMAMA 
! Ind.Prs.ScSg3 in -s
! vowel_harmony: back
!! gradation: yes
!! * Yaml: __V-viskama63__
:%{back%} V_63VISKAMA/HINGAEMAE ;
LEXICON V_63HINGAEMAE !!≈ * __@CODE@__ hingämä:hingä
! Ind.Prs.ScSg3 in -s
! vowel_harmony: front
!! gradation: no
!! * Yaml: __V-hingaemae63__
:%{front%} V_63VISKAMA/HINGAEMAE ;

LEXICON V_63VISKAMA/HINGAEMAE !!≈ * __@CODE@__ viskama:viska
:%^Pen%^VOWRaise ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^VOWRaise ACT-IND-PRS-SG2_t ;
:%^Pen%^VOWRaise ACT-IND-PRS-SG2_dEq ;
+Act+Ind+Prs+Sg3:%^Pen%^VOWRaise%>s K ;
:%^Pen%^VOWRaise ACT-IND-PRS-PL1_miq ;
:%^Pen%^VOWRaise ACT-IND-PRS-PL2_tiq ;
+Act+Ind+Prs+Pl3:%^Pen%^VOWRaise%>s%{eõ%}q K ;
:%^Pen%^VOWRaise ACT-IND-CONNEG ;
+Use/NG+Err/Orth+Act+Ind+Prs+Sg2:%^Pen%^VOWRaise%>d%{eõ%} K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Act+Ind+Prs+Pl1:%^Pen%^VOWRaise%>mi K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Act+Ind+Prs+Pl2:%^Pen%^VOWRaise%>ti K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Act+Ind+Prs+Pl3:%^Pen%^VOWRaise%>s%{eõ%} K ; !!= * @CODE@ no q

+Act+Ind+Prt+Sg3:%^Pen%^VOWLower%^Pen%^WGStem%>ś K ;
+Act+Ind+Prt+Sg1:%^Pen%^VOWLower%^Pen%^WGStem%>ssi K ;
+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%>ssit K ;
+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%>ssiq K ;
+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%>ssid%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%^Pen%^VOWLower%^Pen%^WGStem%>ssimiq K ;
+Act+Ind+Prt+Pl2:%^Pen%^VOWLower%^Pen%^WGStem%>ssitiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%>ssiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%>ssiv%{aä%}q K ;


+Act+Ind+Prt+Pl3:%^Pen%^VOWRaise%^VowRM%>siq K ;
+Act+Ind+Prt+Pl3:%^Pen%^VOWRaise%^VowRM%>siv%{aä%}q K ;

:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-12_si ; 

:%^Pen%^VOWRaise ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise ACT-IND-PRS-3_s/sEq ;
!! (6) Ind_Prs_Sg2, Ind_Prs_Sg3, Ind_Prs_Pl2, Ind_Prs_Pl3,
!! Ind_Prt_OcPl3
!! ActPrsPrc, PssPrsPrc

:%^Pen%^VOWRaise ACT-IND-CONNEG ;

:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

+Use/NG+Err/Orth+Pss+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%>di K ; !!= * @CODE@ no    q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%>di K ; !!= * @CODE@ no    q
+Use/NG+Err/Orth+Pss+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%>tid%{eõ%} K ; !!= * @CODE@ no    q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl1:%^Pen%^VOWLower%^Pen%^WGStem%>timi K ; !!= * @CODE@ no    q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl2:%^Pen%^VOWLower%^Pen%^WGStem%>titi K ; !!= * @CODE@ no    q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%>tiv%{aä%} K ; !!= * @CODE@ no    q


:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
!! +Pss+Ind+Prt +Sg1-+Pl3, ConNeg

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;

:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_INF_dAq ;
:%^Pen%^VOWLower%^Pen%^WGStem ACT-PRFPRC-ALL_nUq ;
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^Pen%^VOWLower%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;






!! THIS FAR 2016-08-27
LEXICON V_64PUHKSAMA
! puhksama:puhkas
!! * Yaml: __V-puhksama64__
:%{back%} V_64PUHKSAMA/REHKSAEMAE ;
LEXICON V_64REHKSAEMAE
! rehksämä:rehkäs
!! * Yaml: __V-rehksaemae64__
:%{front%} V_64PUHKSAMA/REHKSAEMAE ;

LEXICON V_64PUHKSAMA/REHKSAEMAE
:%^Pen%^VowRM%>%{aä%} ACT-IND-PRS-12 ;
:%^Pen%^VowRM%>%{aä%} ACT-IND-PRS-3_s/sEq ;

:%^Pen%^VowRM ACT-IND-PRT-12_i ;
+Act+Ind+Prt+Sg3:%^Pen%^WGStem%^PAL K ;
+Err/Orth-no-pal+Act+Ind+Prt+Sg3:%^Pen%^WGStem K ;
:%^Pen%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;

:%^Pen%^VowRM%>%{aä%} ACT-IND-CONNEG ;

:%^Pen%^WGStem PSS-IND-PRS-ALL_tA ;
:%^Pen%^WGStem PSS-IND-PRT-ALL_ti ;

:%^Pen%^VowRM%>%{aä%} V2N_minE ;
:%^Pen%^VowRM%>%{aä%} Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_tAq ;
:%^Pen%^VowRM%>%{aä%} Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;

:%^Pen%^WGStem MUTUAL_JUS_kuq ;

!LEXICON V_65SIBAHAMA !!≈ * __@CODE@__ >>88SIBAMA >>SIBAMA
!
!+WORK: # ;

LEXICON V_67HARINWMA !!≈ * __@CODE@__ harinõma:hari
!! * Yaml: __harinwma66,harinwma67 =>harinwma__
:%{back%} V_67HARINWMA/NAEGUENEMAE ;
LEXICON V_67NAEGUENEMAE !!≈ * __@CODE@__ nägünemä:nägü
!! * Yaml: __harinwma66,harinwma67 =>harinwma__
:%{front%} V_67HARINWMA/NAEGUENEMAE ;

LEXICON V_67HARINWMA/NAEGUENEMAE !!≈ * __@CODE@__ harinõma:hari
:n%{eõ%} ACT-IND-PRS-12 ;
:n%{eõ%} ACT-IND-PRS-3_s/sEq ;

 ACT-IND-PRT-12_si ;
:n%{eõ%} ACT-IND-CONNEG ;
 ACT-IND-PRT-SC3_si ;
!:%>i ACT-IND-PRT-PL3_q/vAq ;
:n%{eõ%} PSS-IND-PRS-ALL_dA ;
:n%{eõ%} PSS-IND-PRT-ALL_di ;

:n%{eõ%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:n%{eõ%} Harm-Neutr_INF_dAq ;
!+Err/Orth:n%{eõ%} Harm-Neutr_INF_dAq ;

:n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:n%{eõ%}%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:n%{eõ%}%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:n%{eõ%} MUTUAL_JUS ;

!LEXICON V_HARINWMA
!+WORK: # ;

LEXICON V_68HARKUMA
!! * Yaml: __V-harkuma68__
:%{back%} V_68HARKUMA/NAELGUEMAE ;
LEXICON V_68NAELGUEMAE
!! * Yaml: __V-naelguemae68__
:%{front%} V_68HARKUMA/NAELGUEMAE ;

LEXICON V_68HARKUMA/NAELGUEMAE
:%^VOWRaise ACT-IND-PRS-12 ;
:%^VOWRaise ACT-IND-PRS-3_s/sEq ;

:%^VOWRaise PSS-IND-PRS-ALL_tA ;

:%^VOWRaise ACT-IND-PRT-1/2_ZERO ;
:%^VOWRaise ACT-IND-CONNEG ;
:%^VOWRaise ACT-IND-PRT-SG3_ZERO ;
:%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

+Act+Ind+Prt+Pl3:%^VOWRaise%>v%{aä%}q K ;
+Act+Ind+Prt+Pl3:%^VOWRaise%>q K ;
:%^VOWRaise Harm-Neutr_INF_mA ;
:%^VOWRaise Harm-Neutr_INF_dAq ;
:%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^VOWRaise ACT-PRFPRC-ALL_nUq ;
:%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VOWRaise Harm-Neutr_PSS-PRFPRC_t ;
:%^VOWRaise PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_69HAARDUMA !!≈ * __@CODE@__ haarduma:haardu
:%{back%} V_69HAARDUMA/SUENDUEMAE ;
LEXICON V_69SUENDUEMAE !!≈ * __@CODE@__ sündümä:sündü
:%{front%} V_69HAARDUMA/SUENDUEMAE ;
LEXICON V_69HAARDUMA/SUENDUEMAE !!≈ * __@CODE@__ sündümä:sündü
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
!! Act_Ind_Prs_Pl3: essüseq
:%^Pen%^WGStem PSS-IND-PRS-ALL_tA ;

 ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_ZERO ;
 ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem PSS-IND-PRT-ALL_ti ;

 Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
 Harm-Neutr_ACT-PRSPRC_v ;
 ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_69SUURDUMA !!≈ * __@CODE@__ suurduma:suurdu
:%{back%} V_69SUURDUMA/HUEUERDUEMAE ;
LEXICON V_69HUEUERDUEMAE !!≈ * __@CODE@__ hüürdümä:höördü
:%{front%} V_69SUURDUMA/HUEUERDUEMAE ;
LEXICON V_69SUURDUMA/HUEUERDUEMAE !!≈ * __@CODE@__ hüürdümä:höördü vowel raising Required 2016-09-15
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;

:%^Pen%^WGStem PSS-IND-PRS-ALL_tA ;

:%^Pen%^VOWRaise ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^VOWRaise ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
!! V_Inf/mA: miildümä 
:%^Pen%^VOWRaise Harm-Neutr_INF_dAq ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_70UNWHUMA !!≈ * __@CODE@__ unõhuma:unõhtu
!! * Yaml: __V-unwhuma__
:%{back%} V_70UNWHUMA/ÄNGÄHÜMÄ ;
LEXICON V_70ÄNGÄHÜMÄ !!≈ * __@CODE@__ ängähümä:ängähtü
!! * Yaml: __V-eraelduemae__
:%{front%} V_70UNWHUMA/ÄNGÄHÜMÄ ;

LEXICON V_70UNWHUMA/ÄNGÄHÜMÄ !!≈ * __@CODE@__ unõhuma:unõhtu
:%^Pen%^WGStem ACT-IND-PRS-12 ;
+Err/Orth+Use/NG: ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
+Err/Orth+Use/NG: ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem PSS-IND-PRS-ALL_dA ;
+Err/Orth+Use/NG: PSS-IND-PRS-ALL_dA ;


:%^Pen%^WGStem ACT-IND-PRT-1/2_ZERO ;
+Err/Orth+Use/NG: ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
 ACT-IND-CONNEG ;
:%^Pen%^WGStem ACT-IND-PRT-SG3_ZERO ;
 ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
 Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRSPRC_v ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem ACT-PRFPRC-ALL_nUq ;
 ACT-PRFPRC-ALL_nUq ;
!: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>q K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%>v%{aä%}q K ;
+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;
:%^Pen%^WGStem MUTUAL_JUS ;
 MUTUAL_JUS ;

LEXICON V_70VIPWRDUMA !!≈ * __@CODE@__ unõhtuma:unõhtu
!! * Yaml: __V-unwhtuma__
:%{back%} V_70VIPWRDUMA/ERAELDUEMAE ;
LEXICON V_70ERAELDUEMAE !!≈ * __@CODE@__ eräldümä:eräldü
!! * Yaml: __V-eraelduemae__
:%{front%} V_70VIPWRDUMA/ERAELDUEMAE ;

LEXICON V_70VIPWRDUMA/ERAELDUEMAE !!≈ * __@CODE@__ unõhtuma:unõhtu
 ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-12 ;
 ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

 ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-PRT-1/2_ZERO ;
 ACT-IND-CONNEG ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRT-SG3_ZERO ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_INF_mA ;
:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:%^Pen%^WGStem Harm-Neutr_INF_dAq ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRSPRC_v ;
 ACT-PRFPRC-ALL_nUq ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;



LEXICON V_71HALLWTUMA !!≈ * __@CODE@__ hallõtuma:hallõtu
!! * Yaml: __V-hallwtuma__
:%{back%} V_71HALLWTUMA/ERAETUEMAE ;
LEXICON V_71ERAETUEMAE !!≈ * __@CODE@__ erätümä:erä%{td%}ü
!! * Yaml: __V-eraetuemae__
:%{front%} V_71HALLWTUMA/ERAETUEMAE ;

LEXICON V_71HALLWTUMA/ERAETUEMAE !!≈ * __@CODE@__ hallõtuma:hallõtu
:%^Pen%^VOWLower%^Pen%^WGStem Harm-Neutr_ACT-IND-PRS-SG1_ZERO_USUALLY-WEAK ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG ;
:%^Pen%^VOWRaise ACT-IND-PRS-3_s/sEq ;

:%^Pen%^VOWLower%^Pen%^WGStem ACT-IND-PRT-SG1_ZERO/SG2_q/PL3_q ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-IND-PRT-SG2_USUALLY-STRONG ;
:%^Pen%^VOWLower%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^VOWLower%^Pen%^WGStem ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^VOWRaise PSS-IND-PRS-ALL_dA ;
:%^Pen%^VOWRaise PSS-IND-PRT-ALL_di ;

:%^Pen%^VOWRaise Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_INF_dAq ;
:%^Pen%^VOWRaise Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise ACT-PRFPRC-ALL_nUq ;
:%^Pen%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWRaise Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^VOWRaise PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^VOWRaise MUTUAL_JUS ;

LEXICON V_72VAOMA !!≈ * __@CODE@__ vaoma:%{ˋØ%}va%^I7o
!! * Yaml: __vaoma__
:%{back%} V_72VAOMA/TAEUEMAE ;
LEXICON V_72TAEUEMAE !!≈ * __@CODE@__ täümä:täü
!! * Yaml: __haeoemae__
:%{front%} V_72VAOMA/TAEUEMAE ;

LEXICON V_72VAOMA/TAEUEMAE !!≈ * __@CODE@__ vaoma:vao
 ACT-IND-PRS-12 ;
 ACT-IND-PRS-3_s/sEq ;

:%^StrGStem ACT-IND-PRT-12_ZERO ;
 ACT-IND-CONNEG ;
:%^StrGStem ACT-IND-PRT-SG3_ZERO ;
:%^StrGStem ACT-IND-PRT-PL3_q/vAq ;
 Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
  Harm-Neutr_INF_dAq ;
 ACT-PRSPRC_vA ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRS ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
 PSS-PRFPRC_tU-SG-NOM ;
 MUTUAL_JUS ;

!LEXICON V_73VAOMA !!= * @CODE@ vaoma:vao
!:%{back%} V_73VAOMA/XX ;

!LEXICON V_73VAOMA/XX
!+Work: K ;
!!+Inf/mA: vaoma
!!+Inf: [vaodaq, vaioq]
!!+Act+PrfPrc: vaonuq
!!+Pss+PrfPrc: [vaot, vaotu]

!!+Act+Ind+Prs+Sg1: vao
!!+Act+Ind+Prs+Sg2: [vaot, vaodõq]
!!+Act+Ind+Prs+Sg3: vaos
!!+Act+Ind+Prs+Pl1: vaomiq
!!+Act+Ind+Prs+Pl2: vaotiq
!!+Act+Ind+Prs+Pl3: vaosõq
!!+Act+Ind+ConNegII: vao

!!+Act+Ind+Prt+Sg1: vaio
!!+Act+Ind+Prt+Sg2: [vaiot, vaioq, vaiodõq]
!!+Act+Ind+Prt+Sg3: vaio
!!+Act+Ind+Prt+Pl1: vaiomiq
!!+Act+Ind+Prt+Pl2: vaiotiq
!!+Act+Ind+Prt+Pl3: [vaioq, vaiovaq]

!!+Pss+Ind+Prs+Sg3: vaotas

!!+Act+PrsPrc+Sg+Nom: vaova

!!+Jus: vaoguq

LEXICON V_74KULUMA !!≈ * __@CODE@__ kuluma:kullu
!! * Yaml: __V-kuluma__
:%{back%} V_74KULUMA/VAESUEMAE ;
LEXICON V_74VAESUEMAE !!≈ * __@CODE@__ väsümä:vässü
!! * Yaml: __V-vaesuemae__
:%{front%} V_74KULUMA/VAESUEMAE ;

LEXICON V_74KULUMA/VAESUEMAE !!≈ * __@CODE@__ kuluma:kullu
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

 ACT-IND-PRT-1/2_ZERO ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
 ACT-IND-PRT-SG3_ZERO ;
 ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^WGStem MUTUAL_JUS ;

LEXICON V_75VALAMA !!≈ * __@CODE@__ valama:vala
!! * Yaml: __V-valama75__
!! vala, valla, vali, valõ
!!test
:%{back%} V_75VALAMA/JAERAEMAE ;
LEXICON V_75JAERAEMAE !!≈ * __@CODE@__ järämä:järä
!! * Yaml: __V-jaeraemae75__
:%{front%} V_75VALAMA/JAERAEMAE ;

LEXICON V_75VALAMA/JAERAEMAE !!≈ * __@CODE@__ valama:vala

!!
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-PL3_vAq ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^WGStem%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^WGStem%^VowRM ACT-IND-PRT-SG3_i ;
:%^Pen%^WGStem%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;

:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;

:%^Pen%^WGStem MUTUAL_JUS ;


LEXICON V_76HIGOMA !!≈ * __@CODE@__ higoma:hi%{kg0%}o
!! * Yaml: __V-higoma76__
:%{back%} V_76HIGOMA/PUEGAEMAE ;
LEXICON V_76PUEGAEMAE !!≈ * __@CODE@__ pügämä:pü%{kg0%}ä
!! * Yaml: __V-puegaemae76__
:%{front%} V_76HIGOMA/PUEGAEMAE ;

LEXICON V_76HIGOMA/PUEGAEMAE !!≈ * __@CODE@__ higoma:hi%{kg0%}o

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRSPRC_v_LUGWMA ; !!= * @CODE@ (1) lugõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
 MUTUAL_JUS ;
!! +Jus


!! Strengthen consonant retain stem vowel
:%^Pen%^StrGStem Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) lukõq
:%^Pen%^StrGStem Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) lukõv
!! Inf, Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRFPRC_v_LUGWMA ; !!= * @CODE@ (3) lugõnuq
!! +Act+PrfPrc

!! Weaken consonant and semi-retension of stem vowel
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%} Harm-Neutr_LUGWMA_PSS-PRC ; !!= * @CODE@ (4) loet
!! Pss+PrfPrc, Pss+PrsPrc
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;


!! Weaken consonant and semi-retension of stem vowel
:%^Pen%^WGStem ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_t ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_dEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL1_miq ;
:%^Pen%^WGStem ACT-IND-PRS-PL2_tiq ;
:%^Pen%^WGStem ACT-IND-CONNEG ;

!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-IND-PRS-3_LUGWMA ; !!= * @CODE@ (6) lugõma
!! Ind+Prs+ 3

!! Weaken consonant and replace stem vowel with i
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Retain consonant remove stem vowel and add i
:%^VowRM%>i Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) lugi
!! Act+Ind+Prt+Sg3

LEXICON V_76LUGWMA !!≈ * __@CODE@__ lugõma:lu%{kg0%}õ
!! * Yaml: __V-higoma76__
:%{back%} V_76LUGWMA/XX ;


LEXICON V_76LUGWMA/XX !!≈ * __@CODE@__ lugõma:lu%{kg0%}õ

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRSPRC_v_LUGWMA ; !!= * @CODE@ (1) lugõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
 MUTUAL_JUS ;
!! +Jus


!! Strengthen consonant retain stem vowel
:%^Pen%^StrGStem Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) lukõq
:%^Pen%^StrGStem Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) lukõv
!! Inf, Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRFPRC_v_LUGWMA ; !!= * @CODE@ (3) lugõnuq
!! +Act+PrfPrc

!! Weaken consonant and semi-retension of stem vowel
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e Harm-Neutr_LUGWMA_PSS-PRC ; !!= * @CODE@ (4) loet
!! Pss+PrfPrc, Pss+PrsPrc

!! Weaken consonant and semi-retension of stem vowel
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG2_t ;
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-SG2_dEq ;
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-PL1_miq ;
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-PRS-PL2_tiq ;
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>e ACT-IND-CONNEG ;

!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-IND-PRS-3_LUGWMA ; !!= * @CODE@ (6) lugõma
!! Ind+Prs+ 3

!! Weaken consonant and replace stem vowel with i
:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Retain consonant remove stem vowel and add i
:%^VowRM%>i Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) lugi
!! Act+Ind+Prt+Sg3



LEXICON V_77JUUMA !!≈ * __@CODE@__ juuma:joo
!! * Yaml: __juuma__
:%{back%} V_77JUUMA/SUEUEMAE ;
LEXICON V_77SUEUEMAE !!≈ * __@CODE@__ süümä:süü
!! * Yaml: __sueuemae__
:%{front%} V_77JUUMA/SUEUEMAE ;

LEXICON V_77JUUMA/SUEUEMAE !!≈ * __@CODE@__ juuma:joo

:%^VOWRaise V_77JUUMA/SUEUEMAE_RAISED-VOWEL ; !!= * @CODE@ (1) juu, süü
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA

:%^VOWLower V_77JUUMA/SUEUEMAE_NOT-RAISED-VOWEL ; !!= * @CODE@ (2) joo- söö-
!

:%^VOWRaise%^VowRM%>vv V_77JUUMA/SUEUEMAE_RAISED-VOWEL-TENSE ; !!= * @CODE@ (3) juvv
!

:%^OO2Õ%>i V_77JUUMA/SUEUEMAE_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ (4) jõi- poi- sei- möi-
!

:%^VOWRaise ACT-IND-PRS-SG3_ZERO ; !!= * @CODE@ (5) RAISED juu süü
! Act+Ind+Prs+Sg3
:%^VOWRaise%>s ACT-COND-CONNEG ;
:%^VOWRaise%^VowRM%>vv%{aä%} PSS-COND-CONNEG ;
 ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) joovaq sööväq


!:%^VOWRaise%^VowRM%>vv Harm-Neutr_IND-PRT-OC1-2_i ; !!= * @CODE@ juvv süvv



LEXICON V_77JUUMA/SUEUEMAE_RAISED-VOWEL
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA, ACT-PRFPRC-OBL,
! ACT-PRSPRC
 Harm-Neutr_V0-PSS-PRSPRC_dAv ;
 PSS-PRFPRC_d ;
 ACT-PRSPRC_vA ;
 Harm-Neutr_INF_mA ;
 ACT-PRFPRC-OBL_nU ;
 ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-PRS-SG2_t ;
+Err/Orth+Use/NG: ACT-IND-PRS-SG2_dEq ;
+Err/Orth+Use/NG: ACT-IND-PRS-PL2_dEq ;
 ACT-IND-PRS-PL2_tiq ;
 ACT-IND-CONNEG ;
 ACT-IND-PRS-SG3_ZERO ;
 Harm-Neutr_77JUUMA/SUEUEMAE-PSS-IND-PRT_di ; !!= * @CODE@ only 3rd person

LEXICON V_77JUUMA/SUEUEMAE_NOT-RAISED-VOWEL !!= * @CODE@ joo- söö-
! Act+PrfPrc+Sg+Nom, Act+Ind+Prs+Sg3, Act+Ind+Prs+Pl1, Act+Ind+Prs+Pl3
 ACT-PRFPRC_nUq-SG-NOM ; !!= * @CODE@ joonuq
+Err/Orth+Use/NG: ACT-IND-PRS-SG3_sE ;
 ACT-IND-PRS-PL1_miq ;
 ACT-COND-CONNEG ;
 ACT-IND-PRS-PL3_vAq ; 

LEXICON V_77JUUMA/SUEUEMAE_RAISED-VOWEL-TENSE !!= * @CODE@ juvv
 Harm-Neutr_INF_Aq ; !!= * @CODE@ juvvaq süvväq
:%>%{aä%} Harm-Neutr_JUVVA_PSS-IND-PRS ; !!= * @CODE@ juvva süvvä
:%>i Harm-Neutr_JUVVA_PSS-IND-PRT-1/2 ; !!= * @CODE@ juvvi süvvi

LEXICON V_77JUUMA/SUEUEMAE_NON-RAISED-VOWEL-TENSE !!= * @CODE@ jõi-
 ACT-IND-PRT-1/2_ZERO ;
+Err/Orth+Use/NG: ACT-IND-PRT-12_E ;
 ACT-IND-PRT-SG3_ZERO ;
+Err/Orth+Use/NG: ACT-IND-PRT-SG3_E ;
 ACT-IND-PRT-PL3_q/vAq ;
+Err/Orth+Use/NG:%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;

LEXICON V_77KAEUEMAE !!≈ * __@CODE@__ käümä:käü
!! * Yaml: __sueuemae__
:%{front%} V_77XX/KAEUEMAE ;

LEXICON V_77XX/KAEUEMAE !!≈ * __@CODE@__ käümä:käü

:%^VOWRaise V_77JUUMA/SUEUEMAE_RAISED-VOWEL ; !!= * @CODE@ (1) käü
! PSS-PRSPRC, PSS-PRFPRC, ACT-PRSPRC, INF_mA

 V_77JUUMA/SUEUEMAE_NOT-RAISED-VOWEL ; !!= * @CODE@ (2) käü-
!

:%^VowRM%>vv V_77JUUMA/SUEUEMAE_RAISED-VOWEL-TENSE ; !!= * @CODE@ (3) kävv
!

:%^VowRM%>ve V_77JUUMA/SUEUEMAE_NON-RAISED-VOWEL-TENSE ; !!= * @CODE@ (4) käve
!

 ACT-IND-PRS-SG3_ZERO ; !!= * @CODE@ (5) RAISED käü
! Act+Ind+Prs+Sg3

 ACT-IND-PRS-PL3_vAq ; !!= * @CODE@ (6) käüväq



LEXICON V_78VWIMA !!≈ * __@CODE@__ võima:või
!! * Yaml: __V-vwima__
:%{back%} V_VWIMA/XX ;
LEXICON V_VWIMA/XX !!≈ * __@CODE@__ võima:või
 ACT-IND-PRS-12 ;
 ACT-IND-PRS-SG3_ZERO ;
 ACT-IND-PRS-PL3_vAq ;
 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

 ACT-IND-CONNEG ;
:%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
+Err/Orth+Use/NG:%>ds%{eõ%} ACT-IND-PRT-1/2_ZERO ;
:%>s%{eõ%} ACT-IND-PRT-SG3_ZERO ;
:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
+Err/Orth+Use/NG:%>ds%{eõ%} ACT-IND-PRT-SG3_ZERO ;
+Err/Orth+Use/NG:%>ds%{eõ%} ACT-IND-PRT-PL3_q/vAq ;

 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_INF_mA ;
:%^StrGStem Harm-Neutr_INF_dAq ;
 ACT-PRSPRC_vA ;
 Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
 PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_78SAAMA
!! * Yaml: __saama__
:%{back%} V_78SAAMA/XX ;
LEXICON V_78SAAMA/XX
 ACT-IND-PRS-12 ;
 ACT-IND-PRS-SG3_ZERO ;
 ACT-IND-PRS-PL3_vAq ;

 Harm-Neutr_IND-PRT-SCSG1_iE ;
 ACT-IND-PRT-12_i ;
 ACT-IND-PRT-12_iE ;

 ACT-IND-CONNEG ;

 ACT-IND-PRT-SG3_i ;
 ACT-IND-PRT-SG3_iE ;
:%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
+Err/Orth+Use/NG:%^VowRM%>i%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>i%{aä%} Harm-Neutr_JUVVA_PSS-IND-PRS ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_iAq ;
 ACT-PRSPRC_vA ;
 ACT-PRFPRC-ALL_nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 PSS-PRFPRC_d ;
 PSS-PRFPRC_dU-SG-NOM ;
 MUTUAL_JUS ;

!LEXICON V_78VIIMÄ
!!! * Yaml: __V-viimae__
!:%{front%}  V_78XX/VIIMÄ ; 

!LEXICON V_78XX/VIIMÄ
! ACT-IND-PRS-12 ;
! ACT-IND-PRS-SG3_ZERO ;
! ACT-IND-PRS-SG3_sE ;
! ACT-IND-PRS-PL3_vAq ;
! Harm-Neutr_VISKAMA_PSS-IND-PRS ;

! Harm-Neutr_IND-PRT-SCSG1_iE ;
! ACT-IND-PRT-12_i ;
! ACT-IND-CONNEG ;
! ACT-IND-PRT-SG3_i ;
! ACT-IND-PRT-SG3_iE ;
! Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

! Harm-Neutr_INF_mA ;
! Harm-Neutr_INF_iAq ;
! Harm-Neutr_ACT-PRSPRC_v ;
! Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
! Harm-Neutr_V0-PSS-PRSPRC_tAv ;
!:%^WGStem Harm-Neutr_PSS-PRFPRC_t ;


LEXICON V_78MINEMAE !!≈ * __@CODE@__ minemä:min
!! * Yaml: __V-minemae__
:%{front%} V_XX/MINEMAE ;
LEXICON V_XX/MINEMAE !!≈ * __@CODE@__ minemä:min
:%>%{eõ%} Harm-Neutr_INF_mA ;
+Inf:%>n%{aä%}q K ;

 PSS-IND-PRS-ALL_dA ;
 Harm-Neutr_PSS-PRFPRC_t ;
 PSS-PRFPRC_tU-SG-NOM ;

:n%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
 MUTUAL_JUS_kuq ;
!! Remainder is in exceptions.lexc



LEXICON V_79TULWMA !!≈ * __@CODE@__ tulõma:tul
!! * Yaml: __tulwma__
:%{back%} V_79TULWMA/XX ;
LEXICON V_79TULWMA/XX !!≈ * __@CODE@__ tulõma:tul
!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v_LUGWMA ; !!= * @CODE@ (1) tulõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
 MUTUAL_JUS ;
!! +Jus, 

!! Strengthen consonant
:%^StrGSt Harm-Neutr_INF_Aq ; !!= * @CODE@ (2) tullaq
!! Inf 
!! Strengthen consonant 
:%^StrGSt%>%{eõ%} Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) tullõv
!! Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRFPRC_v_LUGWMA ; !!= * @CODE@ (3) tulnuq
!! +Act+PrfPrc

!! Retain consonant 
 Harm-Neutr_LUGWMA_PSS-PRC ; !!= * @CODE@ (4) tul
!! Pss+PrfPrc, Pss+PrsPrc, 


!! Retain consonant and add õ
:%{eõ%} Harm-Neutr_LUGWMA_IND-CONNEGII ; !!= * @CODE@ (5) tulõ
!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-IND-PRS-3_LUGWMA ; !!= * @CODE@ (6) tulõma
!! Ind+Prs+ 3

!! Strengthen consonant and replace stem vowel with i
:%^StrGSt%>i TULWMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) loi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Strengthen consonant and add ʼ
:%^StrGSt%^PAL Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) tulĺ
+Err/Orth-no-pal:%^StrGSt Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) tulĺ
!! Act+Ind+Prt+Sg3

LEXICON V_79PURWMA !!≈ * __@CODE@__ purõma:pur
!! * Yaml: __purwma__
:%{back%} V_79PURWMA/XX ;
LEXICON V_79PURWMA/XX !!≈ * __@CODE@__ purõma:pur
!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-PRSPRC_v_LUGWMA ; !!= * @CODE@ (1) purõma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
 MUTUAL_JUS ;
!! +Jus, 

!! Strengthen consonant
:%^StrGSt Harm-Neutr_INF_Aq ; !!= * @CODE@ (2) purraq
!! Inf 
!! Strengthen consonant 
:%^StrGSt%>%{eõ%} Harm-Neutr_ACT-PRSPRC-SG-NOM_v ; !!= * @CODE@ (2b) purrõv
!! Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
 Harm-Neutr_ACT-PRFPRC_v_LUGWMA ; !!= * @CODE@ (3) purnuq
!! +Act+PrfPrc

!! Retain consonant 
 Harm-Neutr_LUGWMA_PSS-PRC ; !!= * @CODE@ (4) pur
!! Pss+PrfPrc, Pss+PrsPrc, 


!! Retain consonant and add õ
:%{eõ%} Harm-Neutr_LUGWMA_IND-CONNEGII ; !!= * @CODE@ (5) purõ
!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind

!! Retain consonant and stem vowel
:%{eõ%} Harm-Neutr_ACT-IND-PRS-3_LUGWMA ; !!= * @CODE@ (6) purõma
!! Ind+Prs+ 3

!! Strengthen consonant and replace stem vowel with i
:%>i TULWMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) puri
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! consonant and add i
:%>i Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) puri
!! Act+Ind+Prt+Sg3

LEXICON V_79OLWMA !!≈ * __@CODE@__ olõma:o
:%{back%} V_79OLWMA/XX ;

LEXICON V_79OLWMA/XX !!≈ * __@CODE@__ olõma:o
:l%{eõ%} ACT-IND-PRS-12 ;
+Act+Ind+Prs+Sg3:m K ;
+Act+Ind+Prs+Pl3:mm%{aä%}q K ;
:mm%{aä%} ACT-IND-PRS-PL3_vAq ;
:l PSS-IND-PRS-ALL_dA ;

:ll ACT-IND-PRT-12_i ;
:l%{eõ%} ACT-IND-CONNEG ;
+Act+Ind+Prt+Sg3:lĺ K ;
+Act+Ind+Prt+Sg3:lle K ;
:lli ACT-IND-PRT-PL3_q/vAq ;
:l Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:l%{eõ%} Harm-Neutr_INF_mA ;
:ll Harm-Neutr_INF_Aq ;
:ll%{eõ%} Harm-Neutr_ACT-PRSPRC-SG-NOM_v ;
:l%{eõ%} Harm-Neutr_ACT-PRSPRC_v_LUGWMA ;
:l%{eõ%} MUTUAL_JUS ;
:l Harm-Neutr_ACT-PRFPRC_v_LUGWMA ;
:l Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:l Harm-Neutr_PSS-PRFPRC_t ;
:l PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_80RAPAHUTMA !!≈ * __@CODE@__ rapahutma:rapahuta
:%{back%} V_80RAPAHUTMA/HAEMMAEHUETMAE ;
LEXICON V_80HAEMMAEHUETMAE !!≈ * __@CODE@__ hämmähütmä:hämmähütä
:%{front%} V_80RAPAHUTMA/HAEMMAEHUETMAE ;
:%^WGStem%>%{aä%} ACT-IND-PRS-12 ;
: ACT-IND-PRS-SG3_ZERO ;

:%{aä%} ACT-IND-PRS-PL3_vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^WGStem ACT-IND-PRT-12_i ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;

: ACT-IND-PRT-SG3_APOSTROPHE ;
:%>i ACT-IND-PRT-PL3_q/vAq ;
+Err/Orth+Use/NG:%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
: Harm-Neutr_VISKAMA_PSS-IND-PRS ;
: Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

: Harm-Neutr_INF_Aq ;
:%{aä%} Harm-Neutr_ACT-PRSPRC_v ;
: Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq ;
: Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^WGStem%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^WGStem%{eõ%} PSS-PRFPRC_tU-SG-NOM ;


LEXICON V_80RAPAHUTMA/HAEMMAEHUETMAE !!= * @CODE@ rapahutma:rapahuta
!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-PRSPRC_v_LUGWMA ; !!= * @CODE@ (1) rapahutma
!! +Act+PrsPrc+Sg OBL, Inf/mA, 
:%^VowRM MUTUAL_JUS_kuq ;
!! +Jus, 

!! Strengthen consonant
 Harm-Neutr_INF_ZEROq ; !!= * @CODE@ (2) rapahutaq
!! Inf 
!! Strengthen consonant 
:%^VowRM Harm-Neutr_ACT-PRSPRC-SG-NOM_vA ; !!= * @CODE@ (2b) rapahutva
!! Act+PrsPrc+Sg+Nom

!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-PRFPRC_v_LUGWMA ; !!= * @CODE@ (3) rapahutnuq
!! +Act+PrfPrc

!! Retain consonant 
:%^Pen%^WGStem%^VowRM%>%{eõ%} Harm-Neutr_LUGWMA_PSS-PRC ; !!= * @CODE@ (4) rapahudõ
!! Pss+PrfPrc, Pss+PrsPrc, 

!! Retain consonant and add õ
:%^Pen%^WGStem Harm-Neutr_LUGWMA_IND-CONNEGII ; !!= * @CODE@ (5) rapahuda

!! Act+Ind+Prs+Sg1/Sg2/Pl1/Pl2, Ind+ConNegII, Ind+Prs+ConNeg
!! Pss+Ind
:%^Pen%^WGStem ACT-IND-PRS-SG1_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_t ;
:%^Pen%^WGStem ACT-IND-PRS-SG2_dEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL1_miq ;
:%^Pen%^WGStem ACT-IND-PRS-PL2_tiq ;
:%^Pen%^WGStem ACT-IND-CONNEG ;

!! Retain consonant and stem vowel
:%^VowRM Harm-Neutr_ACT-IND-PRS-3_LUGWMA ; !!= * @CODE@ (6) rapahut
!! Ind+Prs+ 3

!! Strengthen consonant and replace stem vowel with i
:%^Pen%^WGStem%^VowRM%>i TULWMA_ACT-IND-PRT-SG1 ; !!= * @CODE@ (7) rapahudi
!! Act+Ind+PrtSg1/Sg2/Pl1-3

!! Strengthen consonant and add ʼ
:%^VowRM%^PAL Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) rapahut
+Err/Orth-no-pal:%^VowRM Harm-Neutr_LUGWMA_IND-PRT-SG3 ; !!= * @CODE@ (8) rapahut
!! Act+Ind+Prt+Sg3


LEXICON V_81TEGEMAE !!≈ * __@CODE@__ tegemä:teg
!! * Yaml: __tegemä__
!! Work
:%{front%} V_81XX/TEGEMAE ;
LEXICON V_81XX/TEGEMAE !!≈ * __@CODE@__ tegemä:teg
:%^WGStem%>e ACT-IND-PRS-12 ; ! The e must be retained or the%{eõ%}:i rule fixed
:%{eõ%} ACT-IND-PRS-SG3_ZERO ;
:%{eõ%} ACT-IND-PRS-PL3_vAq ;

:%^WGStem ACT-IND-PRT-12_i ;
:%^WGStem ACT-IND-PRT-12_iE ;
:%^StrGStem ACT-IND-PRT-12_sE ;
:%^StrGStem Harm-Neutr_IND-PRT-SCSG1_sE ;
 ACT-IND-PRT-SG3_i ;
:%^StrGStem%>%{kḱ%} ACT-IND-PRT-SG3_APOSTROPHE ;
:%^StrGStem ACT-IND-PRT-SG3_sE ;
:%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem%>i%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^StrGStem%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^WGStem PSS-IND-PRS-ALL_tA ;

:%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%{eõ%} Harm-Neutr_INF_mA ;
:%^WGStem Harm-Neutr_INF_tAq ;
:%^StrGStem%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^WGStem%>n ACT-PRFPRC-ALL_nUq ;
:%^WGStem Harm-Neutr_V0-PSS-PRSPRC_ttAv ;
:%^WGStem PSS-PRFPRC_tt ;
:%^WGStem PSS-PRFPRC_ttU-SG-NOM ;
:%^WGStem MUTUAL_JUS_kuq ;
:%^StrGStem MUTUAL_JUS_kuq ;

LEXICON V_82ANDMA !!≈ * __@CODE@__ andma:and
!! * Yaml: __andma__
:%{back%} V_82ANDMA/PUEUEDMAE ;
LEXICON V_82PUEUEDMAE !!≈ * __@CODE@__ püüdmä:püüd
!! * Yaml: __pueuedmae__
:%{front%} V_82ANDMA/PUEUEDMAE ;

LEXICON V_82ANDMA/PUEUEDMAE !!≈ * __@CODE@__ andma:%{ˋØ%}an%{dd́n%}
! jõõrdma:j%{õy%}%{õy%}r%{dd́Ø%}
:%^VOWLower%^WGStem%>%{aä%} ACT-IND-PRS-12 ;
:%^VOWRaise ACT-IND-PRS-SG3_ZERO ;
:%^VOWRaise ACT-IND-PRS-PL3_vAq ;
:%^VOWLower%^WGStem%>%{eõ%} Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^VOWRaise%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
:%^VOWLower%^WGStem%>i ACT-IND-PRT-1/2_ZERO ;
!:%^VOWLower%^WGStem ACT-IND-PRT-12_i ;
+Act+Ind+Prt+Sg1:%^VOWRaise%>s%{eõ%} K ;
:%^VOWRaise Harm-Neutr_IND-PRT-SCSG1_sE ;
:%^VOWLower%^WGStem%>%{aä%} ACT-IND-CONNEG ;
:%^VOWRaise ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VOWRaise ACT-IND-PRT-SG3_sE ;
:%^VOWRaise%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VOWLower%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
:%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VOWRaise Harm-Neutr_INF_mA ;
:%^VOWRaise Harm-Neutr_INF_Aq ;
:%^VOWRaise ACT-PRSPRC_vA ;
:%^VOWRaise ACT-PRFPRC-ALL_nUq ;
:%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VOWLower%^WGStem%>%{eõ%} Harm-Neutr_PSS-PRFPRC_t ;
:%^VOWLower%^WGStem%>%{eõ%} PSS-PRFPRC_tU-SG-NOM ;
:%^VOWRaise MUTUAL_JUS_kuq ;

LEXICON V_83NÕSTMA  !!= * @CODE@ nõstma:nõst
!! gradation: no
!! * Yaml: __nwstma__
:%{back%} V_83NÕSTMA/PÄSTMÄ  ;
LEXICON V_83PÄSTMÄ  !!= * @CODE@ nõstma:nõst
!! gradation: no
!! * Yaml: __nwstma__
:%{front%} V_83NÕSTMA/PÄSTMÄ  ;

LEXICON V_83NÕSTMA/PÄSTMÄ  !!= * @CODE@ nõstma:nõst
 ACT-IND-PRS-12 ;
:%^VowRM ACT-IND-PRS-SG3_ZERO ;
:%^VowRM ACT-IND-PRS-PL3_vAq ;

:%^VowRM ACT-IND-PRT-12_i ;
:%^VowRM ACT-IND-PRT-12_sE ;
:%^VowRM Harm-Neutr_IND-PRT-SCSG1_sE ;
 ACT-IND-CONNEG ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VowRM ACT-IND-PRT-SG3_sE ;
:%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM MUTUAL_JUS_kuq ;

:%^VowRM Harm-Neutr_INF_mA ;
:%^VowRM Harm-Neutr_INF_Aq ;
:%^VowRM ACT-PRSPRC_vA ;
:%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^PSS Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%^PSS Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;
:%^PSS Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^PSS Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^PSS PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_83SÕITMA  !!= * @CODE@ sõitma:sõit
!! gradation: yes
!! * Yaml: __switma__
:%{back%} V_83SÕITMA/HEITMAE ;
LEXICON V_83HEITMAE  !!= * @CODE@ heitmä:heit
!! gradation: yes
!! * Yaml: __heitmae__
:%{front%} V_83SÕITMA/HEITMAE ;

LEXICON V_83SÕITMA/HEITMAE  !!= * @CODE@ sõitma:sõit
+Act+Ind+Prs+Sg1:%^Pen%^VOWLower%^Pen%^WGStem K ; 
+Act+Ind+Prs+Sg2:%^Pen%^VOWLower%^Pen%^WGStemt K ; 
+Err/Orth+Use/NG+Act+Ind+Prs+Sg2:%^Pen%^VOWLower%^Pen%^WGStemd%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^Pen%^VOWLower%^Pen%^WGStemmiq K ; 
+Act+Ind+Prs+Pl2:%^Pen%^VOWLower%^Pen%^WGStemtiq K ; 
+Act+Ind+Prs+Sg3:%^Pen%^VOWRaise%^VowRM K ; 
+Act+Ind+Prs+Pl3:%^Pen%^VOWRaise%^VowRM%>v%{aä%}q K ; 
:%^Pen%^VOWLower%^Pen%^WGStem ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i K ; 
+Err/Orth+Use/NG+Act+Ind+Prt+Sg1:%^Pen%^VOWRaise%^VowRM%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>it K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>id%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}d%{eõ%}q K ;
:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg3:%^Pen%^VOWRaise%^VowRM%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>imiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl1:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>itiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl2:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>iv%{aä%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^VOWRaise%^VowRM%>s%{eõ%}v%{aä%}q K ;



:%^Pen%^VOWRaise%^VowRM MUTUAL_JUS_kuq ;
:%^Pen%^VOWRaise%^VowRM Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise%^VowRM Harm-Neutr_INF_Aq ;
:%^Pen%^VOWRaise%^VowRM ACT-PRSPRC_vA ;
:%^Pen%^VOWRaise%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^Pen%^VOWRaise%^VowRM Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWLower%^Pen%^WGStem%^PSS Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;
:%^Pen%^VOWRaise%^PSS Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02
:%^Pen%^VOWLower%^Pen%^WGStem%^PSS Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^Pen%^VOWLower%^Pen%^WGStem%^PSS PSS-PRFPRC_tU-SG-NOM ;

LEXICON V_83LAULMA  !!= * @CODE@ laulma:laul
!! gradation: yes
!! * Yaml: __switma__
:%{back%} V_83LAULMA/XX ;


LEXICON V_83LAULMA/XX  !!= * @CODE@ laulma:laul
+Act+Ind+Prs+Sg1:%^Pen%^WGStem%> K ; 
+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>t K ; 
+Err/Orth+Use/NG+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>d%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^Pen%^WGStem%>miq K ; 
+Act+Ind+Prs+Pl2:%^Pen%^WGStem%>tiq K ; 
+Act+Ind+Prs+Sg3:%^VowRM K ; 
+Act+Ind+Prs+Pl3:%^VowRM%>v%{aä%}q K ; 
:%^Pen%^WGStem ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^Pen%^WGStem%^VowRM%>i K ; 
+Err/Orth+Use/NG+Act+Ind+Prt+Sg1:%^VowRM%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>it K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>id%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^VowRM%>s%{eõ%}t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^VowRM%>s%{eõ%}d%{eõ%}q K ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
 ACT-IND-CONNEG ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg3:%^VowRM%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^Pen%^WGStem%^VowRM%>imiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl1:%^VowRM%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^Pen%^WGStem%^VowRM%>itiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl2:%^VowRM%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iv%{aä%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^VowRM%>s%{eõ%}v%{aä%}q K ;


:%^Pen%^WGStem%^PSS Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;

:%^PSS Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02

:%^VowRM MUTUAL_JUS_kuq ;
:%^VowRM Harm-Neutr_INF_mA ;
:%^VowRM%>d Harm-Neutr_INF_Aq ;  !! HERE is the distinction 2016-10-04
:%^VowRM ACT-PRSPRC_vA ;
:%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^VowRM Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^PSS Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;

LEXICON V_83ATMA  !!= * @CODE@ atma:atta
!! gradation: yes
!! * Yaml: __switma__
:%{back%} V_83ATMA/PETMÄ ;
LEXICON V_83PETMÄ  !!= * @CODE@ petmä:pettä
!! gradation: yes
!! * Yaml: __heitmae__
:%{front%} V_83ATMA/PETMÄ ;

LEXICON V_83ATMA/PETMÄ  !!= * @CODE@ atma:att%{ae%}
+Act+Ind+Prs+Sg1:%^Pen%^WGStem K ; 
+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>t K ; 
+Err/Orth+Use/NG+Act+Ind+Prs+Sg2:%^Pen%^WGStem%>d%{eõ%}q K ; 
+Act+Ind+Prs+Pl1:%^Pen%^WGStem%>miq K ; 
+Act+Ind+Prs+Pl2:%^Pen%^WGStem%>tiq K ; 

+Act+Ind+Prs+Sg3: K ; 
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 

:%^Pen%^WGStem ACT-IND-CONNEG ; !! sõida

+Act+Ind+Prt+Sg1:%^Pen%^WGStem%^VowRM%>i K ; 
+Err/Orth+Use/NG+Act+Ind+Prt+Sg1:%^Pen%^CC2C%^VowRM%>s%{eõ%} K ; 

+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>it K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^WGStem%^VowRM%>id%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%^Pen%^CC2C%^VowRM%>s%{eõ%}d%{eõ%}q K ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
 ACT-IND-CONNEG ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg3:%^Pen%^CC2C%^VowRM%>s%{eõ%} K ;
+Act+Ind+Prt+Pl1:%^Pen%^WGStem%^VowRM%>imiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl1:%^Pen%^CC2C%^VowRM%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%^Pen%^WGStem%^VowRM%>itiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl2:%^Pen%^CC2C%^VowRM%>s%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^WGStem%^VowRM%>iv%{aä%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^CC2C%^VowRM%>s%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%^Pen%^CC2C%^VowRM%>s%{eõ%}v%{aä%}q K ;



:%^Pen%^CC2C%^VowRM MUTUAL_JUS_kuq ;
:%^Pen%^CC2C%^VowRM Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
:%^Pen%^CC2C%^VowRM ACT-PRSPRC_vA ;
:%^Pen%^CC2C%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem%^PSS Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION ;

:%^Pen%^CC2C%^PSS Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ; !! IS THIS RIGHT? 2015-09-02
:%^Pen%^CC2C%^PSS Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem%^PSS Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;


LEXICON V_84LASKMA !!= * @CODE@ laskma:lask
!! * Yaml: __nwstma__
:%{back%} V_84LASKMA/KÄSKMÄ ;
LEXICON V_84KÄSKMÄ !!= * @CODE@ käskmä:käsk
!! * Yaml: __nwstma__
:%{front%} V_84LASKMA/KÄSKMÄ ;
LEXICON V_84LASKMA/KÄSKMÄ !!= * @CODE@ laskma:lask
!! * Yaml: __nwstma__
:%^VOWLower%^WGStem%>%{eõ%} ACT-IND-PRS-12 ;
:%^VOWRAise ACT-IND-PRS-SG3_ZERO ;
:%^VOWRAise ACT-IND-PRS-PL3_vAq ;

:%^VOWLower%^WGStem ACT-IND-PRT-12_i ;
:%^VOWRAise ACT-IND-PRT-12_sE ;
:%^VOWLower%^WGStem%>%{eõ%} ACT-IND-CONNEG ;
:%^VOWRAise ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VOWRAise ACT-IND-PRT-SG3_sE ;
:%^VOWLower%^WGStem%>i ACT-IND-PRT-PL3_q/vAq ;
:%^VOWRAise%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VOWLower%^WGStem PSS-IND-PRS-ALL_tA ;
:%^VOWRAise Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VOWRAise Harm-Neutr_INF_mA ;
:%^VOWRAise%>%{eõ%} Harm-Neutr_INF_ZEROq ;
:%^VOWRAise ACT-PRSPRC_vA ;
:%^VOWRAise ACT-PRFPRC-ALL_nUq ;
:%^VOWRAise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VOWLower%^WGStem PSS-PRFPRC_t-ONLY ;
:%^VOWLower%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^VOWLower%^WGStem MUTUAL_JUS_kuq ;

LEXICON V_85HIRNMA !!≈ * __@CODE@__ kakma:kaku hirnma:hirnu
!! * Yaml: __kakma__
:%{back%} V_85HIRNMA/XX ;
LEXICON V_85HIRNMA/XX !!≈ * __@CODE@__ kakma:kaku hirnma:hirnu
:%^Pen%^VOWRaise ACT-IND-PRS-12 ;
:%^Pen%^VOWRaise%^VowRM ACT-IND-PRS-SG3_ZERO ;
:%^Pen%^VOWRaise%^Pen%^CC2C%^VowRM ACT-IND-PRS-PL3_vAq ;

:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-12_i ;
:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-12_E ;
:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-12_sE ;

:%^Pen%^VOWLower%^WGStem ACT-IND-CONNEG ;

:%^Pen%^VOWRaise%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^VOWRaise%^Pen%^CC2C%^VowRM ACT-IND-PRT-SG3_sE ;

:%^Pen%^VOWRaise%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise%^Pen%^CC2C%^VowRM%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%^Pen%^VOWRaise Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^VOWRaise%^Pen%^CC2C%^VowRM Harm-Neutr_INF_mA ;
:%^Pen%^VOWRaise Harm-Neutr_INF_ZEROq ;
:%^Pen%^VOWRaise%^VowRM%>%{eõ%} Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^VOWRaise%^Pen%^CC2C%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^Pen%^VOWRaise Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^VOWRaise Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^Pen%^VOWRaise PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^VOWRaise%^VowRM MUTUAL_JUS_kuq ;

!
LEXICON V_85HAUDMA !!≈ * __@CODE@__  kakma:%{ˋØ%}ka%{kØ%}%{kḱ%}u 
!! * Yaml: __haudma__
:%{back%} V_85HAUDMA/VAELKMAE ;
LEXICON V_85VAELKMAE !!≈ * __@CODE@__  haudma:haud
!! * Yaml: __vaelkmae__
:%{front%} V_85HAUDMA/VAELKMAE ;
LEXICON V_85HAUDMA/VAELKMAE !!≈ * __@CODE@__ kakma:kakk haudma:haud
:%^VOWLower%^WGStem ACT-IND-PRS-12 ;
:%^VOWRaise%^VowRM ACT-IND-PRS-SG3_ZERO ;
:%^VOWRaise%^CC2C%^VowRM ACT-IND-PRS-PL3_vAq ;
:%^VOWLower%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;
:%^VOWRaise%^CC2C%^VowRM Harm-Neutr_INF_mA ;

:%^VOWLower%^WGStem%^VowRM ACT-IND-PRT-12_i ;
:%^VOWRaise%^CC2C%^VowRM%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
:%^VOWRaise%^CC2C%^VowRM Harm-Neutr_IND-PRT-SCSG1_sE ;
:%^VOWLower%^WGStem ACT-IND-CONNEG ;
:%^VOWRaise%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VOWRaise%^CC2C%^VowRM ACT-IND-PRT-SG3_sE ;
:%^VOWRaise%^CC2C%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^VOWRaise%^CC2C%^VowRM%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VOWLower%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VOWRaise Harm-Neutr_INF_ZEROq ;
:%^VOWRaise%^CC2C%^VowRM ACT-PRSPRC_vA ;
:%^VOWRaise%^CC2C%^VowRM ACT-PRFPRC-ALL_nUq ;
:%^VOWLower%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VOWLower%^WGStem Harm-Neutr_VISKAMA-PSS-PRFPRC_t ;
:%^VOWLower%^WGStem PSS-PRFPRC_tU-SG-NOM ;

:%^VOWRaise%^CC2C%^VowRM MUTUAL_JUS_kuq ;


LEXICON V_86ISTMA !!≈ * __@CODE@__ istma:istu
!! * Yaml: __istma__
:%{back%} V_86ISTMA/XX ;
LEXICON V_86ISTMA/XX !!≈ * __@CODE@__ istma:istu
 ACT-IND-PRS-12 ;
 ACT-IND-PRS-3_s/sEq ;
:%^VowRM ACT-IND-PRS-SG3_ZERO ;
:%^VowRM  ACT-IND-PRS-PL3_vAq ;

:%^VowRM ACT-IND-PRT-12_i ;
:%^VowRM ACT-IND-PRT-12_sE ;
:%^VowRM ACT-IND-PRT-12_E ; !2014-06-27 Is this correct
 ACT-IND-CONNEG ;
:%^VowRM ACT-IND-PRT-SG3_APOSTROPHE ;
:%^VowRM ACT-IND-PRT-SG3_E ;
:%^VowRM%>s ACT-IND-PRT-SG3_E ;
:%^VowRM%>i ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VowRM%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
 PSS-IND-PRS-ALL_tA ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VowRM Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
:%^VowRM ACT-PRSPRC_vA ;
:%^VowRM ACT-PRFPRC-ALL_nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
 PSS-PRFPRC_tU-SG-NOM ;

:%^VowRM MUTUAL_JUS_kuq ;


LEXICON V_86JAHTJMA !!≈ * __@CODE@__ jaht́ma:ˋjah%{tØ%}i
!! * Yaml: __jahtjma__
:%{back%} V_86JAHTJMA/EHTJMÄ ;
LEXICON V_86EHTJMÄ !!≈ * __@CODE@__ eht́mä:ehti
!! * Yaml: __V-ehtjmae__
:%{front%} V_86JAHTJMA/EHTJMÄ ;

LEXICON V_86JAHTJMA/EHTJMÄ !!≈ * __@CODE@__ eht́mä:eh%{tt́%}
:%^VOWLower%^WGStem%>i ACT-IND-PRS-12 ;
+Act+Ind+Prs+Sg3:%^VOWRaise%^PAL K ;
+Err/Orth-no-pal+Act+Ind+Prs+Sg3:%^VOWRaise K ;
:%^VOWRaise%^PAL ACT-IND-PRS-PL3_vAq ;
+Err/Orth-no-pal:%^VOWRaise ACT-IND-PRS-PL3_vAq ;
:%^VOWLower%^WGStem%>i Harm-Neutr_VISKAMA_PSS-IND-PRS ;

!:%^VOWRaise ACT-IND-PRT-12_i ;
:%^VOWLower%^WGStem%>e ACT-IND-PRT-1/2_ZERO ;
:%^VOWRaise%^PAL%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;
+Err/Orth-no-pal:%^VOWRaise%>s%{eõ%} ACT-IND-PRT-1/2_ZERO ;

:%^VOWRaise ACT-IND-PRT-SG3_E ;
+Act+Ind+Prt+Sg3:%^VOWRaise%^PAL%>s%{eõ%} K ;
+Err/Orth-no-pal+Act+Ind+Prt+Sg3:%^VOWRaise%>s%{eõ%} K ;
:%^VOWRaise%^PAL%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
+Err/Orth-no-pal:%^VOWRaise%>s%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^VOWLower%^WGStem%>e ACT-IND-PRT-PL3_q/vAq ;
:%^VOWLower%^WGStem%>i Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^VOWRaise%^CC2C%^PAL Harm-Neutr_INF_mA ;
+Err/Orth-no-pal:%^VOWRaise%^CC2C Harm-Neutr_INF_mA ;
:%^VOWRaise%>i Harm-Neutr_INF_ZEROq ;
:%^VOWRaise%^PAL ACT-PRSPRC_vA ;
+Err/Orth-no-pal:%^VOWRaise ACT-PRSPRC_vA ;
:%^VOWRaise%^PAL ACT-PRFPRC-ALL_nUq ;
+Err/Orth-no-pal:%^VOWRaise ACT-PRFPRC-ALL_nUq ;
:%^VOWLower%^WGStem%>i Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^VOWLower%^WGStem%>i Harm-Neutr_PSS-PRFPRC_t ;
:%^VOWLower%^WGStem%>i PSS-PRFPRC_tU-SG-NOM ;
:%^VOWRaise%^PAL MUTUAL_JUS_kuq ;
+Err/Orth-no-pal:%^VOWRaise MUTUAL_JUS_kuq ;

LEXICON V_87KOSIMA !!≈ * __@CODE@__ kosima:ko%{sØ%}si
!! * Yaml: __V-kosima__
:%{back%} V_87KOSIMA/KERIMAE ;
LEXICON V_87KERIMAE !!≈ * __@CODE@__ kerimä:keri
!! * Yaml: __V-kerimae__
:%{front%} V_87KOSIMA/KERIMAE ;

LEXICON V_87KOSIMA/KERIMAE !!≈ * __@CODE@__ kosima:kosi
:%^Pen%^WGStem ACT-IND-PRS-12 ;
:%^Pen%^WGStem ACT-IND-PRS-SG3_ZERO ;
:%^Pen%^WGStem ACT-IND-PRS-3_s/sEq ;
:%^Pen%^WGStem ACT-IND-PRS-PL3_vAq ;

:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRS ;

:%^Pen%^WGStem%^VowRM ACT-IND-PRT-12_E ; 
:%^Pen%^WGStem ACT-IND-PRT-12_si ;
:%^Pen%^WGStem ACT-IND-CONNEG ;
:%^Pen%^WGStem%^VowRM ACT-IND-PRT-SG3_E ;
:%^Pen%^WGStem%>si ACT-IND-PRT-SG3_ZERO ;
:%^Pen%^WGStem%^VowRM%>%{eõ%} ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem%>si ACT-IND-PRT-PL3_q/vAq ;
:%^Pen%^WGStem Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

:%^Pen%^WGStem Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_ZEROq ;
 Harm-Neutr_ACT-PRSPRC_v ;
:%^Pen%^WGStem ACT-PRFPRC-ALL_nUq ;
:%^Pen%^WGStem Harm-Neutr_V0-PSS-PRSPRC_tAv ;
:%^Pen%^WGStem Harm-Neutr_PSS-PRFPRC_t ;
:%^Pen%^WGStem PSS-PRFPRC_tU-SG-NOM ;
:%^Pen%^WGStem MUTUAL_JUS ;


LEXICON V_88SIBAHAMA !!= * @CODE@ sibama:siba
!! * Yaml: __V-sibama88,sibahama__
:%{back%} V_88SIBAHAMA/JUEVAEHAEMAE ;
LEXICON V_88JUEVAEHAEMAE
!! * Yaml: __V-juevaemae88__
:%{front%} V_88SIBAHAMA/JUEVAEHAEMAE ;
LEXICON V_88SIBAHAMA/JUEVAEHAEMAE !!= * @CODE@ sibama:siba
:%^StrGStem ACT-IND-PRS-12 ;
:%^StrGStem ACT-IND-PRS-12 ;
:%>h%{XV%} ACT-IND-PRS-12 ;
:%^StrGStem ACT-IND-PRS-SG3_ZERO ;
:%^StrGStem ACT-IND-PRS-3_s/sEq ;
:%>h%{XV%} ACT-IND-PRS-3_s/sEq ;



 Harm-Neutr_VISKAMA_PSS-IND-PRS ;

 ACT-IND-PRT-12_si ;
:%^StrGStem ACT-IND-CONNEG ;
 ACT-IND-PRT-SC3_si ;
 Harm-Neutr_VISKAMA_PSS-IND-PRT_ti ;

 Harm-Neutr_INF_mA ;
:%>h%{XV%} Harm-Neutr_INF_mA ;
 Harm-Neutr_INF_dAq ;
:%^StrGStem Harm-Neutr_INF_ZEROq ;
:%^StrGStem Harm-Neutr_ACT-PRSPRC_v ;
+Err/Orth+Use/NG:%>h%{XV%} Harm-Neutr_ACT-PRSPRC_v ;
 ACT-PRFPRC-ALL_nUq ;
 Harm-Neutr_V0-PSS-PRSPRC_tAv ;
 Harm-Neutr_PSS-PRFPRC_t ;
 PSS-PRFPRC_tU-SG-NOM ;
 MUTUAL_JUS ;

!LEXICON V-89
!+WORK: # ;
!90
!manitsõma:mani
!LEXICON V-manitsõma
!+Act+Ind+Prs+Sg1:ds%{eõ%} # ;
!+Act+Ind+Prs+Sg3:ts%{eõ%}s # ;
!+Act+Ind+Prt+Sg1:dsi # ;
!+Act+Ind+Prt+Sg3:tś # ;
!+Inf:daq # ;
!+Inf:tsaq # ;
!+Act+PrfPrc:nuq # ;
!+Act+PrfPrc:tsnuq # ;
!+Pss+PrfPrc:t # ;
!+Pss+PrfPrc:ts%{eõ%}t # ;

!! !INDICATIVE PRESENT SUBJECT CONJUGATION
LEXICON ACT-IND-PRS-12
 ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-CONNEG ;

 ACT-IND-PRS-SG2_t ;
+Err/Orth+Use/NG: ACT-IND-PRS-SG2_dEq ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;
!+Err/Orth+Use/NG+Act+Ind+Prs+Pl2:%>d%{eõ%}q K ; These will be dealt with in cg
!+Err/Orth+Use/NG+Act+Ind+Prs+Pl2:%>t K ; These will be dealt with in cg

LEXICON Harm-Neutr_ACT-IND-PRS-SG1_ZERO_USUALLY-WEAK
 ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-CONNEG ;
+Act+Imprt+Sg2: K ;


LEXICON Harm-Neutr_ACT-IND-PRS-SG2_USUALLY-STRONG
 ACT-IND-PRS-SG2_t ;
+Err/Orth+Use/NG: ACT-IND-PRS-SG2_dEq ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;



LEXICON ACT-IND-PRS-SG1_ZERO
+Act+Ind+Prs+Sg1: K ;

LEXICON ACT-IND-PRS-SG2_t
+Act+Ind+Prs+Sg2:%>t K ;

LEXICON ACT-IND-PRS-SG2_dEq
+Act+Ind+Prs+Sg2:%>d%{eõ%}q K ;

LEXICON ACT-IND-PRS-SG3_ZERO
+Act+Ind+Prs+Sg3: K ;

LEXICON ACT-IND-PRS-PL1_miq
+Act+Ind+Prs+Pl1:%>miq K ;

LEXICON ACT-IND-PRS-PL2_tiq
+Act+Ind+Prs+Pl2:%>tiq K ;

!LEXICON ACT-IND-PRS-PL2_t
!+Act+Ind+Prs+Pl2:%>t K ;

LEXICON ACT-IND-PRS-PL2_dEq
+Act+Ind+Prs+Pl2:%>d%{eõ%}q K ;

LEXICON ACT-IND-PRS-SG3_sE
+Act+Ind+Prs+Sg3:%>s%{eõ%} K ;

!LEXICON ACT-IND-PRS-PL3_iAq
!+Act+Ind+Prs+Pl3:%>i%{aä%}q K ;

LEXICON ACT-IND-PRS-PL3_vAq
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ;


LEXICON ACT-IND-PRS-3_s/sEq
+Act+Ind+Prs+Sg3:%>s K ;
+Act+Ind+Prs+Pl3:%>s%{eõ%}q K ;

LEXICON ACT-IND-CONNEG
+Act: IND-CONNEG ;
LEXICON PSS-IND-CONNEG
+Pss: IND-CONNEG ;
LEXICON IND-CONNEG
+Ind+Prs+Neg:%-%{XV%}iq K ; !!= * @CODE@ This gives stress
+Err/Orth+Use/NG+Ind+Prs+Neg:%-iq K ; !!= * @CODE@ This gives stress
+Err/Orth+Use/NG+Ind+Prs+Neg:%-%{XV%}i K ; !!= * @CODE@ more neutral without q
+Err/Orth+Use/NG+Ind+Prs+Neg:%-i K ; !!= * @CODE@ more neutral without q
+Ind+ConNeg:%>q K ;
+Ind+ConNegII: K ;
+Ind+Prt+Neg:%-%{XV%}s K ;
+Err/Orth+Use/NG+Ind+Prt+Neg:%-s K ;

LEXICON ACT-COND-CONNEG
+Act: COND-CONNEG ;
LEXICON PSS-COND-CONNEG
+Pss: COND-CONNEG ;
LEXICON COND-CONNEG
+Cond+Prs+Neg:%>si%-iq K ;
+Cond+Prs+Neg:%>si%-i K ;
+Cond+Prt+Neg:%>si%-is K ;
+Cond+ConNeg:%>siq K ;
+Cond+ConNegII:%>si K ;



!! !JUS
LEXICON MUTUAL_JUS !!= * @CODE@ regardless of harmony this is back
+Jus:%>guq%{back%} K ;
+Jus+ConNeg:%>guq-ui%{back%} K ;
LEXICON MUTUAL_JUS_kuq 
+Jus:%>kuq%{back%} K ;
+Jus+ConNeg:%>kuq-ui%{back%} K ;

!! CHECK THIS
!! !PASSIVE INDICATIVE PRESENT CONJUGATION

LEXICON PSS-IND-PRS-ALL_tA
+Pss+Ind+Prs+Sg1:%>t%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>t%{aä%} PSS-IND-CONNEG ;

LEXICON PSS-IND-PRS-ALL_dA
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>d%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>d%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}tiq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;


LEXICON Harm-Neutr_VISKAMA_PSS-IND-PRS
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;


LEXICON Harm-Neutr_JUVVA_PSS-IND-PRS !!= * @CODE@ juvva süvvä
+Pss+Ind+Prs+Sg1: K ;
+Pss+Ind+Prs+Sg2:%>t K ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>s K ;
+Pss+Ind+Prs+Pl1:%>miq K ;
+Pss+Ind+Prs+Pl2:%>tiq K ;
+Pss+Ind+Prs+Pl3:%>s%{eõ%}q K ;
 PSS-IND-CONNEG ;


LEXICON Harm-Neutr_JUVVA_PSS-IND-PRT-1/2 !!= * @CODE@ juvvi süvvi
+Pss+Ind+Prt+Sg1: K ;
+Pss+Ind+Prt+Sg2:%>t K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>q K ;
+Pss+Ind+Prt+Pl1:%>miq K ;
+Pss+Ind+Prt+Pl2:%>tiq K ;
!+Err/Orth+Use/NG+Pss+Ind+Prt+Pl2:%>d%{eõ%}q K ; These will be dealt with in cg




!! !INDICATIVE PRETERIT SUBJECT CONJUGATION
LEXICON Harm-Neutr_IND-PRT-SCSG1_iE
+Act+Ind+Prt+Sg1:%>i%{eõ%} K ;

LEXICON Harm-Neutr_IND-PRT-SCSG1_sE
+Act+Ind+Prt+Sg1:%>s%{eõ%} K ;

LEXICON ACT-IND-PRT-1/2_ZERO
+Act+Ind+Prt+Sg1: K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
!+Err/Orth+Use/NG+Act+Ind+Prt+Pl1: K ; ! These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>tiq K ;

LEXICON ACT-IND-PRT-SG1_ZERO/SG2_q/PL3_q
+Act+Ind+Prt+Sg1: K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Act+Ind+Prt+Pl3:%>q K ; 


LEXICON Harm-Neutr_ACT-IND-PRT-SG2_USUALLY-STRONG
+Act+Ind+Prt+Sg2:%>t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


LEXICON ACT-IND-PRT-12_i
 ACT-IND-PRT-SG1_i ;
 ACT-IND-PRT-SG2/PL1/PL2_i ;
LEXICON ACT-IND-PRT-SG1_i
+Act+Ind+Prt+Sg1:%>i K ;
LEXICON ACT-IND-PRT-SG2/PL1/PL2_i
 ACT-IND-PRT-SG2_iq-USUALLY-WEAK ;
 ACT-IND-PRT-SG2/PL1/PL2_i-USUALLY-STRONG ;

LEXICON ACT-IND-PRT-SG2_iq-USUALLY-WEAK
+Act+Ind+Prt+Sg2:%>iq K ;
LEXICON ACT-IND-PRT-SG2/PL1/PL2_i-USUALLY-STRONG
+Act+Ind+Prt+Sg2:%>it K ;
+Act+Ind+Prt+Sg2:%>id%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>imiq K ;
!+Act+Ind+Prt+Pl1:%>i K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>itiq K ;


LEXICON ACT-IND-PRT-12_ZERO
+Act+Ind+Prt+Sg1: K ;

+Act+Ind+Prt+Sg2:%>q K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;

LEXICON ACT-IND-PRT-12_iE
+Act+Ind+Prt+Sg1:%>i%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>i%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>i%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>i%{eõ%}tiq K ;

LEXICON ACT-IND-PRT-12_sE
+Act+Ind+Prt+Sg1:%>s%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>s%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>s%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>s%{eõ%}tiq K ;
!+Act+Ind+Prt+Pl2:%>s%{eõ%}t K ; These will be dealt with in cg


LEXICON ACT-IND-PRT-12_E
+Act+Ind+Prt+Sg1:%>%{eõ%} K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}q K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}t K ;
+Act+Ind+Prt+Sg2:%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>%{eõ%}miq K ;
!+Act+Ind+Prt+Pl1:%>%{eõ%} K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>%{eõ%}tiq K ;

LEXICON ACT-IND-PRT-12_si
+Act+Ind+Prt+Sg1:%>si K ;
+Act+Ind+Prt+Sg2:%>siq K ;
+Act+Ind+Prt+Sg2:%>sit K ;
+Act+Ind+Prt+Sg2:%>sid%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>simiq K ;
!+Act+Ind+Prt+Pl1:%>si K ; These will be dealt with in cg
+Act+Ind+Prt+Pl2:%>sitiq K ;


LEXICON ACT-IND-PRT-SG3_ZERO
+Act+Ind+Prt+Sg3: K ;
LEXICON ACT-IND-PRT-SG3_i
+Act+Ind+Prt+Sg3:%>i K ;

LEXICON ACT-IND-PRT-SG3_APOSTROPHE
+Act+Ind+Prt+Sg3:%^PAL K ;
+Err/Orth-no-pal+Act+Ind+Prt+Sg3: K ;

LEXICON ACT-IND-PRT-SG3_iE
+Act+Ind+Prt+Sg3:%>i%{eõ%} K ;

LEXICON ACT-IND-PRT-SG3_E
+Act+Ind+Prt+Sg3:%>%{eõ%} K ;

LEXICON ACT-IND-PRT-SG3_sE
+Act+Ind+Prt+Sg3:%>s%{eõ%} K ;

LEXICON ACT-IND-PRT-PL3_q/vAq
 ACT-IND-PRT-PL3_q ;
 ACT-IND-PRT-PL3_vAq ;
LEXICON ACT-IND-PRT-PL3_q
+Act+Ind+Prt+Pl3:%>q K ;
LEXICON ACT-IND-PRT-PL3_vAq
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


!LEXICON ACT-IND-PRT-SC3_s
!+Act+Ind+Prt+Sg3:%^WGStem%>s K ;
!+Act+Ind+Prt+Pl3:%>siq K ;
!+Act+Ind+Prt+Pl3:%>siv%{aä%}q K ;

LEXICON ACT-IND-PRT-SC3_sJ
+Act+Ind+Prt+Sg3:%^WGStem%>ś K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siq K ;
+Act+Ind+Prt+Pl3:%^VowRM%>siv%{aä%}q K ;
LEXICON ACT-IND-PRT-SG3_sJ
+Act+Ind+Prt+Sg3:%^WGStem%>ś K ;

!LEXICON ACT-IND-PRT-SC3_sJ-NO-s
!+Act+Ind+Prt+Sg3:%^WGStem%>%^PAL K ;
!+Act+Ind+Prt+Pl3:%^VowRM%>iq K ;
!+Act+Ind+Prt+Pl3:%^VowRM%>iv%{aä%}q K ;

LEXICON ACT-IND-PRT-SC3_si
+Act+Ind+Prt+Sg3:%>si K ;
+Act+Ind+Prt+Pl3:%>siq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>siv%{aä%}q K ;



!! !PASSIVE INDICATIVE PRETERIT CONJUGATION

LEXICON PSS-IND-PRT-ALL_di
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>diq K ;
+Pss+Ind+Prt+Sg2:%>dit K ;
+Pss+Ind+Prt+Sg2:%>did%{eõ%}q K ;
+Pss+Ind+Prt+Pl1:%>dimiq K ;
+Pss+Ind+Prt+Pl2:%>ditiq K ;
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;

LEXICON PSS-IND-PRT-ALL_ti
+Pss+Ind+Prt+Sg1:%>ti K ;
+Pss+Ind+Prt+Sg2:%>tiq K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
+Pss+Ind+Prt+Pl2:%>titiq K ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl3:%>tiq K ;
+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;

LEXICON Harm-Neutr_VISKAMA_PSS-IND-PRT_ti
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>diq K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
!+Pss+Ind+Prt+Pl1:%>di K ; These will be dealt with in cg
!+Pss+Ind+Prt+Pl2:%>diq K ; These will be dealt with in cg
+Pss+Ind+Prt+Pl2:%>titiq K ;
!+Pss+Ind+Prt+Pl2:%>tit K ; These will be dealt with in cg
!+Pss+Ind+Prt+Pl2:%>tid%{eõ%}q K ; These will be dealt with in cg
+Pss+Ind+Prt+Pl3:%>diq K ;
+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;


!LEXICON Harm-Neutr_IND-PRT-OC1-2_i !!= * @CODE@ juvv süvv
!!! usually weak grade
!+Pss+Ind+Prt+Sg1:i K ;
!+Pss+Ind+Prt+Sg2:it K ;
!+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:iq K ;
!+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:ideq K ;
!+Pss+Ind+Prt+Pl1:imiq K ;
!+Pss+Ind+Prt+Pl2:itiq K ;
!!+Err/Orth+Use/NG+Pss+Ind+Prt+Pl2:ideq K ; These will be dealt with in cg
!:%{aä%} PSS-IND-CONNEG ; !CHECKME 2017-02-02

!LEXICON Harm-Neutr_IND-PRT-OC3_i !!= * @CODE@ süüd
!!! usually strong grade
!+Pss+Ind+Prt+Sg3:%>i K ;
!+Pss+Ind+Prt+Pl3:%>iq K ;
!+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>iv%{aä%}q K ;

LEXICON Harm-Neutr_PSS-PRFPRC_dU
+Pss+PrfPrc:d%{uü%} K ;

!! !NON-FINITES
LEXICON Harm-Neutr_INF_mA
+Inf/mA:%>m%{aä%} K ;
 V2N_minE ;
+Act+Sup+Abe:%>m%{aä%}ldaq%{back%} K ;
+Act+Sup+Ine:%>m%{aä%}n K ;
+Use/NG+Act+Sup+Ine:%>m%{aä%}h K ;
+Use/NG+Act+Sup+Ine:%>m%{aä%}hn K ;
 

!LEXICON Harm-Neutr_INF_DAq
!+Inf:%>d%{aä%}q%^Pen%^WGStem K ;

LEXICON Harm-Neutr_INF_dAq
+Inf:%>d%{aä%}q K ;
+Use/NG+Err/Orth+Inf:%>d%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_iAq
+Inf:%>i%{aä%}q K ;
+Use/NG+Err/Orth+Inf:%>i%{aä%} K ; !!= * @CODE@ no q

!LEXICON Harm-Neutr_INF_vAq
!+Inf:%>v%{aä%}q K ;
!+Use/NG+Err/Orth+Inf:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_Aq
+Inf:%>%{aä%}q K ;
+Use/NG+Err/Orth+Inf:%>%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_tAq
+Inf:%>t%{aä%}q K ;
+Use/NG+Err/Orth+Inf:%>t%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_INF_ZEROq
+Inf:%>q K ;
+Use/NG+Err/Orth+Inf: K ; !!= * @CODE@ no q

LEXICON V2N_minE
+Der/minE+N:%>mis ATTESTED-NOUN-STEM-CONTINUED ;
+Der/minE+N:%>mi NMN_16ABILINW/INEMINE ;
+Der/JA+N:%>j NMN_9KIPWN1/ELLAEI ; !!≈ * __@CODE@__ This should have a +Der/JA tag


LEXICON Harm-Neutr_ACT-PRSPRC_v
+Act+PrsPrc:v K ;
+Act+PrsPrc:v A_9KIPWN1/ELLAEI ;
+Act+PrsPrc+Sg+Nom:v K ;
+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Use/NG+Err/Orth+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON ACT-PRSPRC_vA
+Act+PrsPrc:%>v%{aä%} K ;  ! juuva süüvä
+Act+PrsPrc:%>v%{aä%} A_9KIPWN1/ELLAEI ; ! MUST BE FIXED 2015-06-09
+Act+PrsPrc+Sg+Nom:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Use/NG+Err/Orth+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON ACT-PRFPRC-ALL_nUq
 ACT-PRFPRC_nUq-SG-NOM ;
 ACT-PRFPRC-OBL_nU ;

LEXICON ACT-PRFPRC_nUq-SG-NOM
+Act+PrfPrc:%>n%{uü%}q K ;
+Act+PrfPrc+Sg+Nom:%>n%{uü%}q K ;


LEXICON ACT-PRFPRC-OBL_nU !!= * @CODE@ only oblique cases and Pl+Nom
+Act+PrfPrc+Sg+Gen:%>n%{uü%} K ;
+Act+PrfPrc+Sg+Par:%>n%{uü%}t K ;
+Act+PrfPrc+Sg+Ill:%>n%{uü%}he K ;
+Act+PrfPrc+Pl+Nom:%>n%{uü%}q K ;
+Act+PrfPrc+Pl+Gen:%>n%{uü%}id%{eõ%} K ;
+Err/Orth+Use/NG+Act+PrfPrc+Pl+Gen:%>n%{uü%}isi K ;
+Act+PrfPrc+Pl+Par:%>n%{uü%}id K ;
+Err/Orth+Use/NG+Act+PrfPrc+Pl+Par:%>n%{uü%}isi K ;
+Act+PrfPrc+Pl+Ill:%>n%{uü%}ihe K ;
+Err/Orth+Use/NG+Act+PrfPrc+Pl+Ill:%>n%{uü%}isihe K ;
+Use/NG+Err/Orth+Act+PrfPrc+Pl+Nom:%>n%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_ACT-PRFPRC_WEAKGRADE-nUq
+Act+PrfPrc:%^WGStem%>n%{uü%}q K ;
+Act+PrfPrc:%^WGStem%>n%{uü%} NMN_2KASUNUQ/ELAENUEQ ;
+Use/NG+Err/Orth+Act+PrfPrc:%^WGStem%>n%{uü%} K ; !!= * @CODE@ no q


!! PASSIVE DISTRIBUTION
LEXICON Harm-Neutr_NÕSTMA-PSS-DISTRIBUTION
Harm-Neutr_NÕSTMA-PSS-IND-PRS_dA ; !!= * @CODE@ +Pss+Ind+Prs+Sg1: sõidõda
!Harm-Neutr_KAOTAMA-PSS-IND-PRT_di ; !!= * @CODE@ +Pss+Ind+Prt+Sg1: sõidõdi
!Harm-Neutr_V0-PSS-PRSPRC_dAv ; !!= * @CODE@ +Pss+PrsPrc+Sg+Nom: kaotõdav
Harm-Neutr_KAOTAMA-PSS-PRFPRC_t ; !!= * @CODE@ +Pss+PrfPrc+Sg+Nom: kaotõt

LEXICON Harm-Neutr_KAOTAMA-PSS-DISTRIBUTION
Harm-Neutr_KAOTAMA-PSS-IND-PRS_dA ; !!= * @CODE@ +Pss+Ind+Prs+Sg1: kaotõda
Harm-Neutr_KAOTAMA-PSS-IND-PRT_di ; !!= * @CODE@ +Pss+Ind+Prt+Sg1: kaotõdi
Harm-Neutr_V0-PSS-PRSPRC_dAv ; !!= * @CODE@ +Pss+PrsPrc+Sg+Nom: kaotõdav
Harm-Neutr_KAOTAMA-PSS-PRFPRC_t ; !!= * @CODE@ +Pss+PrfPrc+Sg+Nom: kaotõt


LEXICON Harm-Neutr_NÕSTMA-PSS-IND-PRS_dA
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;
+Use/NG+Err/Orth+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%} K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl1:%>t%{aä%}mi K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl2:%>t%{aä%}ti K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_KAOTAMA-PSS-IND-PRS_dA
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>d%{aä%}t K ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>d%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>d%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>d%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;
+Use/NG+Err/Orth+Pss+Ind+Prs+Sg2:%>d%{aä%}d%{eõ%} K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl1:%>d%{aä%}mi K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl2:%>d%{aä%}ti K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prs+Pl3:%>d%{aä%}s%{eõ%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_77JUUMA/SUEUEMAE-PSS-IND-PRT_di
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;

LEXICON Harm-Neutr_KAOTAMA-PSS-IND-PRT_di !!= * @CODE@ +Pss+Ind+Prt+Sg3: kaotõdi
+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>dit K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>diq K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>did%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>di K ;
+Pss+Ind+Prt+Pl1:%>dimiq K ;
+Pss+Ind+Prt+Pl2:%>ditiq K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>div%{aä%}q K ;
+Use/NG+Err/Orth+Pss+Ind+Prt+Sg2:%>di K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prt+Sg2:%>did%{eõ%} K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl1:%>dimi K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl2:%>diti K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl3:%>di K ; !!= * @CODE@ no q
+Use/NG+Err/Orth+Pss+Ind+Prt+Pl3:%>div%{aä%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_V0-PSS-PRSPRC_tAv
+Pss+PrsPrc:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>t%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>t%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>t%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>vihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>viid%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>viid K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>viihe K ;
+Use/NG+Err/Orth+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_V0-PSS-PRSPRC_ttAv
+Pss+PrsPrc:%>tt%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>tt%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>tt%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>tt%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>tt%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>tt%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>tt%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>tt%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>tt%{aä%}%>vihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Gen:%>tt%{aä%}%>viid%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Par:%>tt%{aä%}%>viid K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Ill:%>tt%{aä%}%>viihe K ;
+Use/NG+Err/Orth+Pss+PrsPrc+Pl+Nom:%>tt%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_V0-PSS-PRSPRC_dAv
+Pss+PrsPrc:%>d%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>d%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>d%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>d%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>d%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>d%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>d%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>d%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>d%{aä%}%>vihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Gen:%>d%{aä%}%>viid%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Par:%>d%{aä%}%>viid K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Ill:%>d%{aä%}%>viihe K ;
+Use/NG+Err/Orth+Pss+PrsPrc+Pl+Nom:%>d%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_KAOTAMA-PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_d
+Pss+PrfPrc:%>d K ;
+Pss+PrfPrc+Sg+Nom:%>d K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Sg+Nom:%>d%{uü%} K ; !!= * @CODE@
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_t-ONLY
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>t%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;


LEXICON Harm-Neutr_PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_VISKAMA-PSS-PRFPRC_t
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON Harm-Neutr_JUUMA-PSS-PRFPRC_d
+Pss+PrfPrc:%>d K ;
+Pss+PrfPrc+Sg+Nom:%>d K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>d%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>d%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Gen:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}id K ;
+Pss+PrfPrc+Pl+Par:%>d%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}isihe K ;
+Pss+PrfPrc+Pl+Ill:%>d%{uü%}ihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q

LEXICON PSS-PRFPRC_tt
+Pss+PrfPrc:%>tt K ;
+Pss+PrfPrc+Sg+Nom:%>tt K ;
+Pss+PrfPrc+Sg+Gen:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>tt%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>tt%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>t%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>tt%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Gen:%>tt%{uü%}isi K ;
+Pss+PrfPrc+Pl+Par:%>tt%{uü%}id K ;
+Pss+PrfPrc+Pl+Par:%>tt%{uü%}isi K ;
+Pss+PrfPrc+Pl+Ill:%>tt%{uü%}isihe K ;
+Pss+PrfPrc+Pl+Ill:%>tt%{uü%}ihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>t%{uü%} K ; !!= * @CODE@ no q
!CHECKME below 2019-01-09
+Pss+Ind+Prt+Sg1:%>ti K ;
+Pss+Ind+Prt+Sg2:%>ttit K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>ttid%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>tti K ;
+Pss+Ind+Prt+Pl1:%>ttimiq K ;
+Pss+Ind+Prt+Pl2:%>ttitiq K ;
+Pss+Ind+Prt+Pl3:%>tiq K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>ttiv%{aä%}q K ;


LEXICON PSS-PRFPRC_ttU-SG-NOM
+Pss+PrfPrc:%>tt%{uü%} K ;
+Pss+PrfPrc+Sg+Nom:%>tt%{uü%} K ;


LEXICON PSS-PRFPRC_tU-SG-NOM
+Pss+PrfPrc:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Nom:%>t%{uü%} K ;

LEXICON PSS-PRFPRC_dU-SG-NOM
+Pss+PrfPrc:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Nom:%>d%{uü%} K ;

!!
LEXICON Harm-Neutr_ACT-PRSPRC_v_LUGWMA !!= * @CODE@ lugõma:lugõ
!! Retain consonant and stem vowel
+Inf/mA:%>m%{aä%} K ;
 V2N_minE ;
!

+Act+PrsPrc+Sg+Gen:%>v%{aä%} K ;
+Act+PrsPrc+Sg+Par:%>v%{aä%}t K ;
+Act+PrsPrc+Sg+Ill:%>v%{aä%}he K ;
+Act+PrsPrc+Pl+Nom:%>v%{aä%}q K ;
+Act+PrsPrc+Pl+Gen:%>vid%{eõ%} K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Gen:%>viid%{eõ%} K ;
+Act+PrsPrc+Pl+Par:%>vit K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Par:%>viid K ;
+Act+PrsPrc+Pl+Ill:%>vihe K ;
+Err/Orth+Use/NG+Act+PrsPrc+Pl+Ill:%>viihe K ;
+Use/NG+Err/Orth+Act+PrsPrc+Pl+Nom:%>v%{aä%} K ; !!= * @CODE@ no q
!


LEXICON Harm-Neutr_ACT-IND-PRS-3_LUGWMA !!= * @CODE@ (6) lugõma:lugõ
 ACT-IND-PRS-SG3_ZERO ;
+Act+Ind+Prs+Pl3:%>v%{aä%}q K ; 

LEXICON Harm-Neutr_ACT-PRFPRC_v_LUGWMA
+Act+PrfPrc:%>n%{uü%}q K ;
+Act+PrfPrc:%>n%{uü%} NMN_2KASUNUQ/ELAENUEQ ; !FIX ME
+Use/NG+Err/Orth+Act+PrfPrc:%>n%{uü%} K ; !!= * @CODE@ no q


LEXICON Harm-Neutr_LUGWMA_IND-PRT-SG3
+Act+Ind+Prt+Sg3: K ;

LEXICON Harm-Neutr_ACT-PRSPRC-SG-NOM_v
+Act+PrsPrc+Sg+Nom:%>v K ;
LEXICON Harm-Neutr_ACT-PRSPRC-SG-NOM_vA
+Act+PrsPrc+Sg+Nom:%>v%{aä%} K ;

LEXICON Harm-Neutr_LUGWMA_IND-CONNEGII
!
 ACT-IND-PRS-SG1_ZERO ;
 ACT-IND-PRS-SG2_t ;
 ACT-IND-PRS-SG2_dEq ;
 ACT-IND-PRS-PL1_miq ;
 ACT-IND-PRS-PL2_tiq ;
 ACT-IND-CONNEG ;

LEXICON Harm-Neutr_LUGWMA_PSS-PRC
+Pss+PrfPrc:%>t K ;
+Pss+PrfPrc+Sg+Nom:%>t K ;
+Err/Orth+Use/NG+Pss+PrfPrc:%>t%{uü%} K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Sg+Nom:%>t%{uü%} K ;
+Pss+PrfPrc+Sg+Gen:%>d%{uü%} K ;
+Pss+PrfPrc+Sg+Par:%>t%{uü%}t K ;
+Pss+PrfPrc+Sg+Ill:%>t%{uü%}he K ;
+Pss+PrfPrc+Pl+Nom:%>d%{uü%}q K ;
+Pss+PrfPrc+Pl+Gen:%>t%{uü%}id%{eõ%} K ;
+Pss+PrfPrc+Pl+Par:%>t%{uü%}id K ;
+Pss+PrfPrc+Pl+Ill:%>t%{uü%}ihe K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Gen:%>t%{uü%}isi K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Par:%>t%{uü%}isi K ;
+Err/Orth+Use/NG+Pss+PrfPrc+Pl+Ill:%>t%{uü%}isihe K ;
+Use/NG+Err/Orth+Pss+PrfPrc+Pl+Nom:%>d%{uü%} K ; !!= * @CODE@ no q
!
+Pss+PrsPrc:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Nom:%>t%{aä%}v K ;
+Pss+PrsPrc+Sg+Gen:%>t%{aä%}%>v%{aä%} K ;
+Pss+PrsPrc+Sg+Par:%>t%{aä%}%>v%{aä%}t K ;
+Pss+PrsPrc+Sg+Ill:%>t%{aä%}%>v%{aä%}he K ;
+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%}q K ;
+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>vid%{eõ%} K ;
+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>vit K ;
+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>vihe K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Gen:%>t%{aä%}%>viid%{eõ%} K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Par:%>t%{aä%}%>viid K ;
+Err/Orth+Use/NG+Pss+PrsPrc+Pl+Ill:%>t%{aä%}%>viihe K ;
+Use/NG+Err/Orth+Pss+PrsPrc+Pl+Nom:%>t%{aä%}%>v%{aä%} K ; !!= * @CODE@ no q
!
+Pss+Ind+Prs+Sg1:%>d%{aä%} K ;
+Pss+Ind+Prs+Sg2:%>t%{aä%}t K ;
+Err/Orth+Use/NG+Pss+Ind+Prs+Sg2:%>t%{aä%}d%{eõ%}q K ;
+Pss+Ind+Prs+Sg3:%>t%{aä%}s K ;
+Pss+Ind+Prs+Pl1:%>t%{aä%}miq K ;
+Pss+Ind+Prs+Pl2:%>t%{aä%}tiq K ;
+Pss+Ind+Prs+Pl3:%>t%{aä%}s%{eõ%}q K ;
:%>d%{aä%} PSS-IND-CONNEG ;

+Pss+Ind+Prt+Sg1:%>di K ;
+Pss+Ind+Prt+Sg2:%>tit K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Sg2:%>tid%{eõ%}q K ;
+Pss+Ind+Prt+Sg3:%>ti K ;
+Pss+Ind+Prt+Pl1:%>timiq K ;
+Pss+Ind+Prt+Pl2:%>titiq K ;
+Pss+Ind+Prt+Pl3:%>diq K ;
+Err/Orth+Use/NG+Pss+Ind+Prt+Pl3:%>tiv%{aä%}q K ;


LEXICON Harm-Neutr_LOGÕMA_ACT-IND-PRT-SG1
+Act+Ind+Prt+Sg1: K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg1:%>e K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>%{eõ%}d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl1:%>%{eõ%}miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl2:%>%{eõ%}tiq K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>%{eõ%}q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>%{eõ%}v%{aä%}q K ;

LEXICON TULWMA_ACT-IND-PRT-SG1
+Act+Ind+Prt+Sg1: K ;
+Act+Ind+Prt+Sg2:%>t K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Sg2:%>d%{eõ%}q K ;
+Act+Ind+Prt+Pl1:%>miq K ;
+Act+Ind+Prt+Pl2:%>tiq K ;
+Act+Ind+Prt+Pl3:%>q K ;
+Err/Orth+Use/NG+Act+Ind+Prt+Pl3:%>v%{aä%}q K ;


! vim: set ft=xfst-lexc:

