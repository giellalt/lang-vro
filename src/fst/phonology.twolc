!  ===============================================
!! # The Võro morphophonological/twolc rules file 
!  ===============================================


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

́ ! U+0301 instead of the following two lines (testing)

!
b́ d́ f́ h́ t́ v́



!
B́ D́ F́ H́ T́ V́

! cf. https://github.com/hfst/hfst/issues/497 Trond.

 ʼ

 ǵ ḱ ĺ ḿ ń ṕ ŕ ś ź
 Ǵ Ḱ Ĺ Ḿ Ń Ṕ Ŕ Ś Ź


!! ## Special letters
u̬  !!≈ * **@CODE@** u plus U+032C COMBINING CARON BELOW
U̬  !!≈ * **@CODE@** u plus U+032C COMBINING CARON BELOW
ü̬  !!≈ * **@CODE@** ü plus U+032C COMBINING CARON BELOW
Ü̬  !!≈ * **@CODE@** ü plus U+032C COMBINING CARON BELOW
i̬  !!≈ * **@CODE@** i plus U+032C COMBINING CARON BELOW
I̬  !!≈ * **@CODE@** i plus U+032C COMBINING CARON BELOW
õ̭  !!≈ * **@CODE@** õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
Õ̭  !!≈ * **@CODE@** õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
ˋ  !!≈ * **@CODE@** U+02CB MODIFIER LETTER GRAVE ACCENT other parts of stem
%{ˋØ%}:0 !!≈ ```@CODE@ — U+02CB MODIFIER LETTER GRAVE ACCENT used in rules```
ˋ:0 !!≈ **@CODE@** — U+02CB MODIFIER LETTER GRAVE ACCENT used outside of rules, this is temporary removal 2017-04-20


!! ## Vowel harmony with "(t)a/ä"
!! ```
 %{aä%}:0   !!= @CODE@ — Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0
 %{ae%}:a  !!= @CODE@ — Vowel harmony with "a/e/õ" passive tahetu
 %{aõ%}:a  !!= @CODE@ — Vowel harmony with "a/e/õ" passive sõidõtu
 %{äe%}:ä   !!= @CODE@ — Vowel harmony with "ä/e/õ" passive
 %{eõ%}:0   !!= @CODE@ — Vowel harmony with "e/õ"
 %{uü%}:0   !!= @CODE@ — Vowel harmony with "u/ü"
 %{öü%}:ö   !!= @CODE@ — Vowel raising
 %{ou%}:o   !!= @CODE@ — Vowel raising
 %{ei%}:e   !!= @CODE@ — Vowel raising
 %{õy%}:õ   !!= @CODE@ — Vowel raising
 %{ao%}:a   !!= @CODE@ — Vowel raising

 %{eØ%}:e   !!= @CODE@ — ütlemä:üt%{eØ%}l  
 %{õØ%}:õ   !!= @CODE@ — ütlemä:üt%{eØ%}l  
 %{Øõ%}:0   !!= @CODE@ — juurdlõma:juur%{dØ%}%{0õ%}l

 %{dØ%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dv%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dn%}:d   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}
 %{dl%}:d   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}

 %{ij%}:i   !!= @CODE@ ellä%{ij%}
 %{gv%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gl%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gØ%}:g   !!= @CODE@ — HJK and KimmoK ideas argnõma:ar%{gØ%}
 %{uv%}:u   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{üv%}:ü   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{hØ%}:h   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{jØ%}:j   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kØ%}:k   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lØ%}:l   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{mØ%}:m   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{nØ%}:n   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pØ%}:p   !!= @CODE@ — HJK and KimmoK ideas oppama:o%{pØ%}pama
 %{rØ%}:r   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sØ%}:s   !!= @CODE@ — HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{vØ%}:v   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{pØ%}:0   !!= @CODE@ — häbü:häbü+N:hä%{pØ%}%{pbØ%}ü
 %{tØ%}:0   !!= @CODE@ — koda:ko%{tØ%}%{tdØ%}a
 %{kØ%}:0   !!= @CODE@ — nägo:nä%{kØ%}%{kgØ%}o

 %{bv%}:b   !!= @CODE@ — HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{dr%}:d   !!= @CODE@ — HJK and KimmoK ideas parras:par%{dr%}a%{sØ%}
 %{bm%}:b   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}
 %{pb%}:p   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pb%}:b   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{tØ%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{t́Ø%}:t   !!= @CODE@ — HJK and KimmoK ideas jaht́lõma:jah%{t́Ø%}%{eØ%}%{lĺ%}
 %{td%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{t́d́%}:t́   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kg%}:k   !!= @CODE@ — HJK and KimmoK ideas kaigas:kai%{kg%}as

 %{pbØ%}:p  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pbØ%}:b  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pbØ%}:0  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{pbv%}:p   %{pbv%}:b   %{pbv%}:v  !!= @CODE@ — tõbi: tõvõ tõpõ tõppõ

 %{tdØ%}:d  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{kgØ%}:k  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kgØ%}:g  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kgØ%}:0  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{jiØ%}:i  !!= @CODE@ — HJK and KimmoK ideas vari:var%{jiØ%}o
 %{qmn%}:q  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{qn%}:q   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{dd́Ø%}:d   !!= @CODE@
 %{dd́n%}:d   !!= @CODE@
 %{dd́r%}:d   !!= @CODE@
 %{dd́v%}:d   !!= @CODE@
 %{dd́Ø%}:d   !!= @CODE@
 %{gǵv%}:g   !!= @CODE@
 %{gǵØ%}:g   !!= @CODE@
 %{kḱg%}:k    %{kḱg%}:ḱ    %{kḱg%}:g   !!= @CODE@
 %{kḱØ%}:k   !!= @CODE@
 %{pṕb%}:p   %{pṕb%}:ṕ    %{pṕb%}:b   !!= @CODE@
 %{tt́d%}:t    %{tt́d%}:t́    %{tt́d%}:d   !!= @CODE@
 %{tt́Ø%}:t   !!= @CODE@ täh%{tt́Ø%}
 %{pṕØ%}:p   !!= @CODE@

!! ```

!! ## Palatalization of consonants
!! ```
 %{bb́%}:b   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dd́%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{ff́%}:f   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gǵ%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{hh́%}:h   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kḱ%}:k   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lĺ%}:l    !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lĺ%}:ĺ    !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{mḿ%}:m   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{nń%}:n   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pṕ%}:p   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{rŕ%}:r   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sś%}:s   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sś%}:ś   !!= @CODE@ — HJK and KimmoK ideas vaśma:va%{sØ%}%{sś%}
 %{tt́%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{vv́%}:v   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{zź%}:z   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dd́n%}:d !!= @CODE@
!! ```

!! ## Miscellaneous other symbols
!! ```
 %{XV%}:0   !!= @CODE@ — This is used for echoing the previous vowel
 %{XC%}:0   !!= @CODE@ — This is used for lengthening a consonant
 %^I7:0     !!= @CODE@ — This appears in stem vaoma:va%^I7o for vaio
 %^K7:0     !!= @CODE@ — This appears in stem väemä:vä%^K7e for väkeq
 %^V7:0     !!= @CODE@ — This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0     !!= @CODE@ — This appears in stem kaoma:ka%^T7o for katoq
 %^Y7:õ     !!= @CODE@ — This appears for syna = s%^Y7na and is rendered as õ in the norm
!! ```

!! ## Triggers
!! ```
   %^OO2Õ:0   !!= @CODE@ — joo%^OO2Õ%>i:j0õ0%>i
   %^CC2C:0   !!= @CODE@ — att%^CC2C%>m%{aä%} atma
 %^PSS:0      !!= @CODE@ vowel in passive tahetu, sõidõtu, eletü
 %^ÄI2ÄÄ:0   !!= @CODE@ — päiv%^ÄI2ÄÄ%>ä: päävä
 %{front%}:0   !!= @CODE@ — front harmony
 %{back%}:0   !!= @CODE@ — back harmony
%^ErrorBack:0 !!= @CODE@ — +Err/Orth+Clt:%>kinaq in front harmony context BHARM disallowance
 %{PrsSg1%}:0 !!= @CODE@ — this helps with %{eõ%}:i̬
!! ```

!! ```

 %{td%}:t 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kg%}:k 	!!= @CODE@ HJK and KimmoK ideas kaigas:kai%{kg%}as

 %{qmn%}:q 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{qn%}:q 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel
 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant
 %^I7:0         !!= @CODE@ This appears in stem vaoma:va%^I7o for vaio
 %^K7:0         !!= @CODE@  This appears in stem väemä:vä%^K7e for väkeq
 %^V7:0         !!= @CODE@ This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0         !!= @CODE@ This appears in stem kaoma:ka%^T7o for katoq

%^Y7:õ  !!= **@CODE@**  This appears for syna = s%^Y7na and is rendered as õ in the norm
!! ```



! this k is not effected by gradation
K1:k
!! ```
%^NoGrad:0    !!= @CODE@ — This will be placed after a stem to break Gradation
%^APOCH:0     !!= @CODE@ — This causes apochope: puhksama vs puhastaq
%^StrD2T:0    !!= @CODE@ — This changes g,d,b => k,t,p

%^G1:0	      !!= @CODE@ — This is used with %{pØ%} %{pbØ%} for 0 0, also t, k
%^G2:0	      !!= @CODE@ — This is used with %{pØ%} %{pbØ%} for 0 b, also t, k
%^G3:0	      !!= @CODE@ — This is used with %{pØ%} %{pbØ%} for 0 p, also t, k
%^G4:0	      !!= @CODE@ — This is used with %{pØ%} %{pbØ%} for p p, also t, k

%^WGStem:0    !!= @CODE@ — This weakens "kipõń" to "kibõna", "ompel" to "ommel"
%^StrGStem:0  !!= @CODE@ — This strengthens "perädü" to "perätüt"
%^ShortGStem:0  !!= @CODE@ — This shortens "pu%{tØ%}tu" to "putma", an orthographic convension
%^LongGStem:0    !!= @CODE@ — This lengthens "pu%{tØ%}tu" to "puttuq"


%^Pen:0       !!= @CODE@ — This moves us to penultimate coda
%^PAL:0	      !!= @CODE@ — Palatalization
%^NoPAL:0	      !!= @CODE@ — NoPalatalization

%^JI20:0	      !!= @CODE@ — in vari: vaŕo
%^JI2I:0	      !!= @CODE@ — in vari vari
%^JI2J:0	      !!= @CODE@ — in vari: varjo

%^PenWGStem:0 !!= @CODE@ — This weakens "kipõń" to "kibõna"
%^PenVowRM:0  !!= @CODE@ — syncope tapõld : taplõma 
%^D2S:0       !!= @CODE@ — The ti => si
%^TS2S:0      !!= @CODE@ — The -ts- => -s-
%^I2J:0       !!= @CODE@ — The i => j change
%^PLPRT:0     !!= @CODE@ — The a:o attested in Plural kana:kanno and prt
%^VOWRaise:0  !!= @CODE@ — Raises vowel
%^VOWLower:0  !!= @CODE@ — Lowers vowel
%^XLowerVow:0 !!= @CODE@ — Lowers vowel two levels
%^VOWLowerDelab:0  !!= @CODE@ — Lowers vowel and delabializes it
%^XLowerVowDelab:0 !!= @CODE@ — Lowers vowel two levels and delabializes it
%^U2E:0       !!= @CODE@ — lowers u:õ and ü:e delabializes and lowers
%^U2A:0       !!= @CODE@ — lowers u:a and ü:ä delabializes and lowers
%^VowRM:0     !!= @CODE@ — this will remove stem final vowel
%^CnsRM:0     !!= @CODE@ — this will remove stem final consonant tervüs:tervü
!! ```

!Outlining 2 grades (3,4) (audibly, orthographically)  ku%{pØ%}p : kupu kuppu
!Outlining 2 grades (audibly, orthographically)  tä%{tØ%}%{tdØ%}i : tädi täti tätti
!Outlining 2 grades (audibly, orthographically)  tü%{kØ%}k : tükü tükkü

!Outlining 2 grades (audibly, NOT orthographically)  kasv : kasvo kasvo

!Outlining 3 grades (2,3,4) (audibly, orthographically)  tö%{pØ%}%{pbØ%}i : töbi töpi töppi
!Outlining 3 grades (audibly, orthographically)  tä%{tØ%}%{tdØ%}i : tädi täti tätti
!Outlining 3 grades (audibly, orthographically)  pi%{kØ%}%{kgØ%}i : pigi piki pikki

!Outlining 3 grades (audibly, NOT orthographically)  pi%{nØ%}ni : pini pinni pinni

!Outlining 4 grades (1,2,3,4) (audibly, orthographically)  u%{pØ%}%{pbØ%}a : ua uba upa uppa
!Outlining 4 grades (audibly, orthographically)  la%{tØ%}%{tdØ%}o : lao lado lato latto
!Outlining 4 grades (audibly, orthographically)  la%{kØ%}%{kgØ%}o : lao lago lako lakko

!Outlining 4 grades (audibly, NOT orthographically)  u%{pØ%}%{pbØ%}a : ua uba upa uppa


%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y õ̭ u̬ ;
VowFront = ä ö ü ü̬ ;
VowNeutral = e i i̬ ; 
VowNonHigh = a o ä ö e õ ;
Vow = a o u ä ö ü e i õ y õ̭ ü̬ u̬ i̬ ;

VowInZero = a o u ä ö ü e i õ ;

Cns = b d g j k l m p r s t v  f h n q š z ž 
b́
d́
ǵ
ń ḱ ĺ ḿ ṕ ŕ ś ź 
t́ v́ f́ h́ 
;

CnsInZero = b d g j k l m p r s t v ;

CnsVoiced = j l m n r v ;

ArchVow = %{ae%} %{aõ%} %{äe%} %{eõ%} %{uü%} %{öü%} %{ou%} %{ei%} %{õy%} %{ao%} %{eØ%} %{õØ%} ;

ArchCns =  %{hØ%} %{jØ%} %{kØ%} %{lØ%} %{mØ%} %{nØ%} %{pØ%} %{rØ%} %{sØ%} %{tØ%} %{t́Ø%} %{vØ%} %{bv%} %{bm%} %{pb%} %{td%} %{gv%} %{gl%} %{gØ%} %{kg%} %{dØ%} %{dv%} %{dl%} %{dr%} %{dn%} %{pbØ%} %{tdØ%} %{kgØ%} %{sś%} %{lĺ%} %{rŕ%} %{nń%} %{ij%} %{kḱ%} %{hh́%} %{t́d́%} %{gǵ%} %{dd́Ø%} %{dd́n%} %{dd́r%} %{dd́v%} %{gǵv%} 
%{jiØ%} 
%{pṕb%} 
%{dd́%}
%{vv́%}
%{ff́%}
%{tt́%}
%{tt́Ø%}
%{tt́d%}
%{kḱg%}
%{kḱØ%}
%{mḿ%}
; 






ArchCnsInZero =  %{gØ%} %{hØ%} %{jØ%} %{kØ%} %{lØ%} %{mØ%} %{nØ%} %{pØ%} %{rØ%} %{sØ%} %{tØ%} %{t́Ø%} %{vØ%} %{pbØ%} %{tdØ%} %{kgØ%} ;

Letters = Vow Cns ;

HarmDummies = %{back%} %{front%} ;

Definitions

!! Onset consonant or word boundary
OnSetC = [:Cns (Cns:) |#|%>] ;

! Back Harmony
BHARM = [ %{back%}: \[ # | %{front%}: ]* ] ;

BHARMnotA = [[[Vow - [a]] Cns:+ ArchCnsInZero: ]  %{back%}: \[ # | %{front%}: ]* ] ;


! Front Harmony
FHARM = [ %{front%}: \[ # | %{back%}: ]* ] ;


! Front Trigger
FT =   # Cns* [[ VowFront: | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;

! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ]+ Cns:* | ü:+ Cns:+ i Cns:+) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
!!€ füüsi{kg}a{back}^Pen^StrGStem^VowRM>i>dõ
!!€ füüsik00000>i>dõ
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Optional vowel height in final coda
VOWHite = ( %^VOWLower: | %^VOWRaise: ) ;

! Optional vowel height in penultimate coda
PenVOWHite = ( %^Pen: %^VOWLower: | %^Pen: %^VOWRaise: ) ;

! Optional gradation in final coda
GRAD = (%^G1:|%^G2:|%^G3:|%^G4:|%^StrGStem:|%^WGStem:|%^CC2C:) ;

! Optional gradation in penultimate coda
PenGRAD = (%^Pen: [%^G1:|%^G2:|%^G3:|%^G4:|%^StrGStem:|%^WGStem:|%^CC2C:]) ;

! Optional Palatalization in penultimate coda
PenPAL = (%^Pen: [%^NoPAL:| %^PAL:]) ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;




PenBetweenStemAndStemFinal = [(HarmDummies:) ((%^Pen:) %^VOWLower:|(%^Pen:) %^VOWRaise:) (%^Pen: %^WGStem:)] ;


!! # Rules

Rules

!! ## VOWEL HARMONY 
!! ### Vowel harmony suffixes Front
"%{aä%}:a"
!! **@RULENAME@**
%{aä%}:a <=>
 BHARM _ ;

"%{aä%}:ä"
!! **@RULENAME@**
%{aä%}:ä <=>
 FHARM _ ;

"%{uü%}:u"
!! **@RULENAME@**
%{uü%}:u <=>
 BHARM _ ;

"%{uü%}:ü"
!! **@RULENAME@**
%{uü%}:ü <=>
 FHARM _ ;

"%{eõ%}:õ"
!! **@RULENAME@**
%{eõ%}:õ <=>
 BHARM _ ;

"%{eõ%}:e"
!! **@RULENAME@**
%{eõ%}:e <=>
 FHARM _ \[%{PrsSg1%}:] ;
! FHARM _ \[#|.#.|%>|%-] ;

"%{ae%}:e"
!! **@RULENAME@**
%{ae%}:e <=>
  _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:) %^PSS: ;
  a [Cns:+ ArchCns:* ] _ %{back%} %^Pen %^VOWLower %^Pen %^WGStem %> %{eõ%} %> t ;
!! tahtma+V+Pss+PrfPrc+Sg+Nom: **want/haluta**
!!€ tah{tt́Ø}{ae}{back}^Pen^VOWLower^Pen^WGStem>{eõ}>t{uü}
!!€ tah0e00000>0>tu

"%{aõ%}:õ"
!! **@RULENAME@**
%{aõ%}:õ <=>
  _ (HarmDummies:) (%^Pen: %^WGStem:) %^PSS: ;

"%{äe%}:e"
!! **@RULENAME@**
%{äe%}:e <=>
  _ (HarmDummies:) (%^Pen: %^WGStem:) %^PSS: ;


!! ### VOWEL LOWERING

"u:o"
!! **@RULENAME@**
 u:o <=>
!   u: _ (HarmDummies:) %^VOWLower: ;
! OnSetC   _ u: (HarmDummies:) %^VOWLower: ;
 OnSetC _ [CnsInZero:|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] VowInZero: (HarmDummies:) %^Pen: %^VOWLower: %^Pen: %^WGStem: %^VowRM:    ;
 OnSetC _ [CnsInZero: |%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow:e (HarmDummies:) %^Pen: %^VOWLower: %^Pen: %^WGStem:    ;
!!€ lugõ%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i
!!€ lo00000000%>i
 OnSetC _ (HarmDummies:) e %^VOWLower: ;

"ü:ö"
!! **@RULENAME@**
 ü:ö <=>
!  ü: _ (HarmDummies:) %^VOWLower: ;
! OnSetC _ ü: (HarmDummies:) %^VOWLower: (%^WGStem:) [ # | %> [ :Cns | [ :Vow - :i ]] ] ;
   # Cns:* _ [Cns:|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow: (HarmDummies:) %^Pen: %^VOWLower: ;
!!€ pü{kgØ}ä{front}^Pen^VOWLower^Pen^WGStem^VowRM>e>t
!!€ pö00000000>e>t
!!€ süü%{front%}%^VOWLower%^WGStem%>nüq
!!€ söö000%>nüq
 OnSetC _ (HarmDummies:) e %^VOWLower: ;

"o2õ"
!! **@RULENAME@**
 o:õ <=> [Cns - [m|p|b|v]] _ o: (HarmDummies:) %^OO2Õ: %> i ;
!!€ joo%{back%}%^OO2Õ%>i
!!€ jõ000%>i
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ aigo%{back%}%^OO2Õ
!!€ aigõ00

"u2õ"
!! **@RULENAME@**
 u:õ <=>
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ laulu%{back%}%^OO2Õ
!!€ laulõ00

"ö2e"
!! **@RULENAME@**
 ö:e <=> [Cns - [m|p|b|v]] _ ö: (HarmDummies:) %^OO2Õ: %> i ;
!!€ söö%{front%}%^OO2Õ%>i
!!€ se000%>i

"Delabializing o and ö"
!! **@RULENAME@**
 Vx:Vy <=> [Cns - [m|p|b|v]] _ Vx: (HarmDummies:) %^OO2Õ: %> i ;
           where Vx in (%{ou%} %{öü%})
	   	 Vy in (  õ      e   ) matched ;

!FIX-ME 2019-05-8 
!! ### VOWEL RAISING
!"ö:ü"
!! **@RULENAME@**
! ö:ü <=> Vow %^V7: 0:? _ (HarmDummies:) %^StrGStem: %> i: ; ! 0:
!!!€ hä%^V70ö%^StrGStem%>i
!!!€ hävvü0%>0

!! ### PALATALIZATION
"n2ń palatalization all"
!! **@RULENAME@**
 Cx:Cy <=>
 _ (HarmDummies:) VOWHite GRAD %^PAL: ;
! _ (HarmDummies:) VOWHite (%^CC2C:|%^G3:|%^G2:|%^StrGStem:|%^WGStem:) %^PAL: ;
 _ (HarmDummies:) PenVOWHite PenGRAD %^PAL: ;
! _ (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^G2:|%^G3:|%^StrGStem:|%^WGStem:] %^PAL: ;
 _ Vow: Cns:* (HarmDummies:) PenVOWHite PenGRAD %^Pen: %^PAL: ;
! _ Vow: Cns:* (HarmDummies:) PenVOWHite %^Pen: %^PAL: ;
 _ Vow: (HarmDummies:) PenVOWHite PenGRAD (%^VowRM:|%^Pen:) %^PAL: ;
 _ (%{jiØ%}:) [(HarmDummies:) %^PAL:| Vow: (HarmDummies:) %^Pen: %^PAL:]  ;

      where Cx in (%{bb́%} %{dd́%} %{ff́%} %{gǵ%} %{hh́%}  %{lĺ%} %{mḿ%} %{nń%} %{pṕ%} %{rŕ%} %{sś%} %{tt́%}  %{vv́%} %{zź%})
            Cy in (b́   d́   f́  ǵ  h́   ĺ  ḿ  ń  ṕ  ŕ  ś  t́  v́  ź) matched ;

!! akaŕ+A+Sg+Nom
!!€ a%{kg%}a%{rŕ%}%{back%}%^Pen%^G2%^PAL
!!€ akaŕ0000

!! asi+N+Sg+Gen:
!!€ a%{sś%}%{jiØ%}%{back%}%^PAL%^VowRM%>%{aä%}
!!€ aś0000%>a

!! alostama+V+Act+Ind+Prt+Sg3:
!!€ alos{tt́}a^Pen^VOWRaise^Pen^PAL^VowRM
!!€ alost́000000

"n2n no palatalization all"
!! **@RULENAME@**
 Cx:Cy <=>
 _ (HarmDummies:) VOWHite GRAD (%>) %^NoPAL: ;
! _ (HarmDummies:) VOWHite (%^CC2C:|%^G3:|%^G2:|%^StrGStem:|%^WGStem:) (%>) %^NoPAL: ;
 _ (HarmDummies:) PenVOWHite PenGRAD (%^Pen: %^VowRM: ) (%>) %^NoPAL: ;
! _ (HarmDummies:) PenVOWHite (%^Pen: [%^G1:|%^G2:|%^G3:|%^StrGStem:|%^WGStem:|%^CC2C:]) (%^Pen: %^VowRM: ) (%>) %^NoPAL: ;
 _ [Vow:|ArchVow:] Cns:* (HarmDummies:) PenVOWHite PenGRAD %^Pen: %^NoPAL: ;
 _ [Vow:|ArchVow:] (HarmDummies:) PenVOWHite PenGRAD (%^VowRM:) %^NoPAL: ;
 _ (%{jiØ%}:) [(HarmDummies:) %^NoPAL:| Vow: (HarmDummies:) PenVOWHite (%^Pen: [%^JI2J:|%^JI2I:] ) %^Pen: %^NoPAL:]  ;

      where Cx in (%{bb́%} %{dd́%} %{ff́%} %{gǵ%} %{hh́%}  %{lĺ%} %{mḿ%} %{nń%} %{pṕ%} %{rŕ%} %{sś%} %{tt́%}  %{vv́%} %{zź%})
            Cy in (b   d   f   g   h  l  m   n   p  r  s  t  v  z) matched ;

!! rehksämä+V+Inf/mA:
!!€ reh{kØ}ä{sś}{front}^Pen^StrGStem^Pen^VowRM^NoPAL>{aä}>m{aä}
!!€ rehk0s000000>ä>mä
!! hanǵ+N+Sg+Gen: **snow pack/hanki**
!!€ han{gǵ}{back}^NoPAL>%{eõ%}
!!€ hang000>e


"{dd́n}:d́ palatalization for 3-way"
!! **@RULENAME@**
 Cx:Cy <=> _ (HarmDummies:) VOWHite (%^CC2C:|%^G2:|%^StrGStem:)  %^PAL:  ;
           where Cx in (%{dd́n%} %{dd́v%} %{dd́Ø%}  %{gǵv%}   %{gǵØ%} )
                 Cy in (d́ d́ d́  ǵ  ǵ ) matched ;
! %{dd́n%}:d́ <=> _ (HarmDummies:)  %^PAL:  ;

!! särǵ+N+Sg+Nom: **roach/särki**
!!€ sär{gǵØ}{front}^StrGStem^PAL
!!€ särǵ000
!! andma+V+Act+Ind+Prs+Sg3
!!€ an%{dd́n%}%{back%}%^PAL
!!€ and́00

"{dd́n}:n weaken 3-way"
!! **@RULENAME@**
 Cx:Cy <=> _ (HarmDummies:) VOWHite [ %^WGStem: | %^G1: ]  ;
           where Cx in (%{dd́n%} %{dd́v%} %{dd́Ø%} )
                 Cy in (n v 0) matched ;
! %{dd́n%}:n <=> _   (HarmDummies:)  %^WGStem:  ;

!! andma+V+Act+Ind+Prs+Sg1
!!€ an%{dd́n%}%{back%}%^WGStem%>m%{aä%}
!!€ ann00%>ma

!! püüdmä+V+Act+Ind+Prs+Sg1
!!€ pü%{üv%}%{dd́v%}%{front%}%^WGStem%>%{aä%}
!!€ püvv00%>ä



"%{pṕb%}:p"
!! **@RULENAME@**
 %{pṕb%}:p <=> Vow _  (HarmDummies:) VOWHite [%^StrGStem:] %^NoPAL:  ;
!! loroṕ+N+Sg+Par:
!!€ loro{pṕb}{back}^StrGStem^NoPAL>i
!!€ lorop000>i

"%{tt́d%}:t"
!! **@RULENAME@**
 %{tt́d%}:t <=> 
 _  (HarmDummies:) VOWHite [ %^G2:|%^StrGStem:] %^NoPAL:  ;

 _ Vow: (HarmDummies:) PenVOWHite %^Pen: [%^G2:| %^G3:|%^StrGStem:] %^Pen:  %^NoPAL:  ;

!! hainatama+V+Inf/mA
!!€ haina{tt́d}a{back}^Pen^VOWRaise^Pen^StrGStem>m{aä}
!!€ hainata00000>ma

"%{kḱg%}:k"
!! **@RULENAME@**
 %{kḱg%}:k <=> _  (HarmDummies:) VOWHite [%^G2:|%^StrGStem:] %^NoPAL:  ;
!!€ pisla%{kḱg%}%{back%}%^G2%>
!!€ pislaḱ00%>



"%{pṕb%}:ṕ"
!! **@RULENAME@**
 %{pṕb%}:ṕ <=>  _  (HarmDummies:) VOWHite  [%^G3:|%^StrGStem:] %^PAL:  ;
!! loroṕ
!!€ loro{pṕb}{back}^StrGStem^PAL
!!€ loroṕ000

"%{tt́d%}:t́"
!! **@RULENAME@**
 %{tt́d%}:t́ <=>
 _  (HarmDummies:) VOWHite  [ %^G3:|%^StrGStem:] %^PAL:  ;
 _ [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [ %^G3:|%^StrGStem:] %^Pen: %^PAL:  ;

"%{kḱg%}:ḱ"
!! **@RULENAME@**
 %{kḱg%}:ḱ <=> _ (HarmDummies:) VOWHite [%^G3:|%^StrGStem:]  %^PAL:  ;
!!€ pisla%{kḱg%}%{back%}%^StrGStem%^PAL
!!€ pislaḱ000

!! kõiḱ+Pron+Sg+Nom
!!€ kõ̭i%{kØ%}%{kḱg%}%{back%}%^CC2C%^PAL
!!€ kõ̭i0ḱ000



!! ### VOWEL CHANGE WITH PLURAL


 
!"e2i̬"
!!! **@RULENAME@**
! e:i̬ <=>  [ # Cns* | Cns ] _ e: ( s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
! [ # Cns* | Cns ] e: _ [ s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:] (HarmDummies:) %^VOWRaise: ;
! [ # Cns* | Cns ] e: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;

!      # Cns:* _ [CnsInZero:|ArchCnsInZero:] (HarmDummies:) (%^WGStem:) %> [e:|%{eõ%}:] ;
!      # Cns:* e: [CnsInZero:|ArchCnsInZero:] (HarmDummies:) (%^WGStem:) %> _ [#|.#.|%>|%-] ;

"%{eõ%}:i̬"
 %{eõ%}:i̬ <=> # Cns:* e: [CnsInZero:|ArchCnsInZero: ] %{front%}: (%^WGStem:) %> _ %{PrsSg1%}: ;
!! tegemä+V+Act+Ind+Prs+Sg1: **do**
!!€ teg%{front%}%^WGStem%>%{eõ%}
!!€ ti̬000%>i̬

 
!!€ k%{ei%}%{ei%}%{lĺ%}%{front%}%^VOWRaise%^PAL
!!€ ki̬i̬ĺ000


"õ2õ̭"
!! **@RULENAME@**
 õ:õ̭ <=>  [ # Cns* | Cns ] _ õ: ( s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ [ s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;
  > OnSetC  _ CnsInZero: a (HarmDummies:) (%^WGStem:) ;
!!€ sõda%{back%}%^WGStem
!!€ sõ̭0a00


"o2u̬"
!! **@RULENAME@**
 o:u̬ <=>  [ # Cns* | Cns ] _ o: ( s | %{sś%}: | t | %{tt́%}: | %{gǵ%}: | %{pṕb%}: | %{dd́%}: | %{vv́%}: | %{ff́%}:| k | %{kḱ%}: | l (%{dd́Ø%}:|%{tt́Ø%}:) | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ [ s | %{sś%}: | t | %{tt́%}: | %{gǵ%}: | %{pṕb%}: | %{dd́%}: | %{vv́%}: | %{ff́%}:| k | %{kḱ%}: | l (%{dd́Ø%}:|%{tt́Ø%}:) | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}: ] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;

"Vx%{ou%}:Vyo"
!! **@RULENAME@**
 Vx:Vy <=> 
 [ # Cns* | Cns ] _ Vx: [ Cns (Cns) | ArchCns: ( ArchCns: | Cns: ) | Cns ArchCns: ] [ (HarmDummies:) %^VOWLower: | [ Vow: | ArchVow: ] ( ArchCns: | ArchCns: Cns ) (HarmDummies:) %^Pen: %^VOWLower: ] ;
 [ # Cns* | Cns ] Vx: _ [ Cns (Cns) | ArchCns: ( ArchCns: | Cns: ) | Cns ArchCns: ] [ (HarmDummies:) %^VOWLower: | [ Vow: | ArchVow: ] ( ArchCns: | ArchCns: Cns ) (HarmDummies:) %^Pen: %^VOWLower: ];
!! hoolas+A+Sg+Nom:
!!€ h%{ou%}%{ou%}la%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^G1
!!€ hoolas00000
 [ # Cns* | Cns ] Vx: _ (HarmDummies:) %^VOWLower: (%^WGStem:|%^G1:) [#|.#.|%>|%-] ;
!  [ # Cns* | Cns ] _ Vx: ( ArchCns: | Cns | ArchCns: ArchCns: | Cns ArchCns:) Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^VOWLower: ;
! [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns | ArchCns: ArchCns: | Cns ArchCns:] Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^VOWLower: ;
         where Vx in (%{ou%} %{öü%} %{õy%} %{ei%} )
               Vy in (  o      ö      õ      e ) matched ;

"Vx%{ou%}2Vyu̬"
!! **@RULENAME@**
 Vx:Vy <=>
 [ # Cns* | Cns ] _ Vx: ( ArchCns: | ArchCns:  ArchCns: | Cns (Cns) | Cns ArchCns:) ( Vow: | ArchVow: ) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | ArchCns:  ArchCns: | Cns (Cns) | Cns ArchCns: ] ( Vow: | ArchVow: ) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;
!! nuuĺ+N+Sg+Nom: **arrow**
!!€ n%{ou%}%{ou%}%{lĺ%}%{back%}%^VOWRaise%^PAL
!!€ nu̬u̬ĺ000
  [ # Cns* | Cns ] _ Vx: ( ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns+ ArchCns: ) [ Vow: | ArchVow: | ArchVow: ArchCns: ] ( Cns:+ |ArchCns: Cns | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^Pen: %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns+ ArchCns: ] [ Vow: | ArchVow: | ArchVow: ArchCns: ] ( Cns:+ |ArchCns: Cns | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^Pen: %^VOWRaise: ;
!
  [ # Cns* | Cns ] _ Vx: [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns: ] (HarmDummies:)  %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns: ]  (HarmDummies:)  %^VOWRaise: ;
!juuḱ
     	  where Vx in (%{ou%} %{öü%} %{õy%} %{ei%} )
	  	Vy in ( u̬ ü̬ õ̭ i̬ ) matched ;
!! kiiĺ+N+Sg+Gen: **tongue/kieli**
!!€ k%{ei%}%{ei%}{lĺ}%{front%}%^VOWRaise%^PAL
!!€ ki̬i̬ĺ000
!!kiirdlemä+V:
!!€ k%{ei%}%{ei%}r%{dØ%}%{eØ%}%{lĺ%}%{front%}%^Pen%^VOWRaise%^Pen%^VowRM%>%{eõ%}%>m%{aä%}
!!€ ki̬i̬rd0l00000%>e%>mä
!echo 'k {ei}:i {ei}:i r {dØ}:d {eØ}:0 {lĺ}:l {front}:0 ^Pen:0 ^VOWRaise:0 ^Pen:0 ^VowRM:0 > {eõ}:e > m {aä}:ä' | hfst-pair-test src/phonology/vro-phon.compose.hfst

"i2e"
!! **@RULENAME@**
 i:e <=> Cns _ (HarmDummies:) (%^StrGStem: ) %^PLPRT: ;
! Cns 0:? _ (HarmDummies:) (  %^StrGStem: ) %^PLPRT: ;
!! pini+N+Pl+Par: **dog/koira**
!!€ pi%{nØ%}ni%{front%}%^Pen%^StrGStem%^VowRM%>%{eõ%}
!!€ pinn00000%>e
! # Cns* _ i: l (HarmDummies:) %^VOWLower: ;
! # Cns* i: _ l (HarmDummies:) %^VOWLower: ;
!! kiiĺ+N+Sg+Gen: **tongue/kieli**
!!€ k%{ei%}%{ei%}{lĺ}%{front%}%^VOWLower%^NoPAL%>%{eõ%}
!!€ keel000%>e
     e _ [Cns:|%{bv%}:] (HarmDummies:) %^ÄI2ÄÄ: ;
!! leib+N+Sg+Gen: **bread/leipä**
!!€ lei{bv}{front}^ÄI2ÄÄ^WGStem>{aä}
!!€ leev000>ä
  Cns* _ %{tdØ%}: ä: (HarmDummies:) %^Pen: %^VOWLower: ;
!!pidämä+V+Pss+Ind+Prs+Sg3: 
!!€ pi%{tdØ%}ä%{front%}%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%}%>t%{aä%}s
!!€ pe00000000e%>täs


"i:ä"
!! **@RULENAME@**
 i:ä <=> ä _ Cns: (HarmDummies:) %^ÄI2ÄÄ: ;
!! päiv+N+Sg+Gen: **day/päivä**
!!€ päiv%{front%}%^ÄI2ÄÄ%>ä
!!€ pääv00%>ä


"a2o"!prepare for deprication to {ao}:o
!! **@RULENAME@**
!! ```
 a:o <=> OnSetC  _ (HarmDummies:) ( %^StrGStem: | PenVOWHite %^Pen: %^WGStem: ) %^PLPRT: ;
!!€ ka%{nØ%}na%{back%}%^Pen%^StrGStem%^PLPRT
!!€ kanno0000
!! ```

"{ao}o"
!! **@RULENAME@**
!! ```
 %{ao%}:o <=> OnSetC  _ (HarmDummies:) ( %^G4: | %^G3: | PenVOWHite %^Pen: %^G1: |PenVOWHite PenGRAD) %^PLPRT: ;
!!€ ka%{nØ%}n%{ao%}%{back%}%^G3%^PLPRT
!!€ kanno000
!! ```



!! ### VOWEL LOSS
"a:0"
!! **@RULENAME@**
 a:0 <=>
  [ Cns: | :Cns | ArchCns:] _ (s:|%{sś%}:) (HarmDummies:) PenVOWHite [ PenGRAD PenPAL [ %^VowRM: | %^Pen:0 %^JI2I: | %^Pen: %^VowRM:]| CnsInZero: %^VowRM: %^WGStem:  %> | [%^StrGStem:  ] %> i  ] ;  
!  [ Cns: | :Cns | ArchCns:] _ (s:|%{sś%}:) (HarmDummies:) (PenVOWHite) [ (%^Pen: %^CC2C:|%^Pen: %^StrGStem:|%^Pen: %^G2:| %^Pen: %^WGStem:) (%^Pen: %^NoPAL:) [ %^VowRM: | %^Pen: %^VowRM:]| CnsInZero: %^VowRM: %^WGStem:  %> | [%^StrGStem: ] %> i  ] ; 
[ Cns:| ArchCns: ] _ (HarmDummies:) [ (%^StrGStem:|PenVOWHite [%^Pen: %^WGStem:| PenPAL ] |%^Pen: %^CC2C:|%^Pen: %^G2:) %^VowRM: | CnsInZero: %^VowRM: %^WGStem: %> ]  ; 
! [ Cns:| ArchCns: ] _ (HarmDummies:) [ (%^StrGStem:|PenVOWHite %^Pen: %^WGStem:|%^Pen: %^CC2C:) (0:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM: %> ]  ; 
      a _ (HarmDummies:) %>  i  ; !!= @CODE@ 
!!€ saa%{back%}%>i
!!€ sa00%>i
!! sõimama+V+Act+Ind+Prt+Sg1
!!€ sõima%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>ssi
!!€ sõim0000000%>ssi
  [Cns:| ArchCns: ]  _ Cns: (HarmDummies:) %^Pen: %^VowRM: ;
!!€ puhkas%{back%}%^Pen%^VowRM%>a%>ma
!!€ puhk0s000%>a%>ma
  _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;
    _ (HarmDummies:) PenVOWHite PenGRAD PenPAL %^VowRM: ;
!    _ (HarmDummies:) PenVOWHite (%^Pen: (%^WGStem:|%^StrGStem:|%^G4:|%^G3:)) (%^Pen %^NoPAL:) %^VowRM: ;
!! võtma+V+Inf/mA: **to take/ottaa**
!!€ võ{tØ}{tt́}a{back}^Pen^WGStem^Pen^NoPAL^VowRM>m{aä}
!!€ võ0t0000000>ma
!! võtma+V+Ind+Prt+Sg3: **to take/ottaa**
!!€ võ{tØ}{tt́}a{back}^Pen^StrGStem^Pen^PAL^VowRM
!!€ võtt́0000000

!! sõda+N+Pl+Par:
!!€ sõ%{tØ%}%{tdØ%}a%{back%}%^Pen%^G3%^VowRM%>o
!!€ sõ0t00000%>o

"ä:0"
!! **@RULENAME@**
 ä:0 <=>  [ Cns: | :Cns | ArchCns:] _ (s:|%{sś%}:) (HarmDummies:) (PenVOWHite) [ PenGRAD (%^Pen: %^PAL:|%^Pen: %^NoPAL:) [ %^VowRM: | %^Pen: %^VowRM:]| CnsInZero: %^VowRM: %^WGStem:  %> | [%^StrGStem:  ] %> i  ] ; 
!! pügämä+V+Pss+PrfPrc:
!!€ pü%{kgØ%}ä%{front%}%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%}%>t
!!€ pö00000000e%>t
!! rehksämä+V+Inf/mA:
!!€ reh{kØ}ä{sś}{front}^Pen^StrGStem^Pen^VowRM^NoPAL>{aä}>m{aä}
!!€ rehkäs000000>ä>mä

!  [ Cns: | :Cns | ArchCns:] _ (s:) (HarmDummies:) [ (0:) (%^Pen: %^CC2C:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM:  %> | [%^StrGStem:  ] %> i  ] ; 
    _ (HarmDummies:) ((%^Pen: ) %^VOWLower:) (%^Pen: (%^WGStem:|%^StrGStem:)) (%^NoPAL:) %^VowRM: ;
!  [ Cns: | :Cns ] _ (HarmDummies:) [ (0:|:0+) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM:  %> | [%^StrGStem:  ] %> i  ] ; 
                        ä _ (HarmDummies:) VOWHite %^VowRM:  ;
!!€ jää%{front%}%^VOWRaise%^VowRM%>i
!!€ jä0000%>i
!!€ elä%{front%}%>et
!!€ el00%>et
  [Cns:| ArchCns: ] _ Cns: (HarmDummies:) %^Pen: %^VowRM: ;
!! tütär+N+Sg+Gen
!!€ tütä%{rŕ%}%{front%}%^Pen%^VowRM%>e
!!€ tüt0r000%>e
 _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;

"U:0 Vx"
!! **@RULENAME@**
!! ```
 Vx:0 <=> 
  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:)  PenVOWHite (%^Pen: [%^G1:|%^G2:|%^G3:|%^G4:| %^CC2C:|%^StrGStem:|%^ShortGStem:]) (%^Pen: %^NoPAL:) %^VowRM: (õ|e)    ; 

!!€ hirnu{back}^Pen^CC2C^VowRM>m{aä}
!!€ hirn00000>ma
!!€ tervüs{front}^VowRM^CnsRM>i>t
!!€ terv00000>i>t
!! juusk+N+Sg+Nom: ____
!!€ j{ou}{ou}s{kØ}u{back}^VOWRaise^VowRM
!!€ ju̬u̬sk0000

   OnSetC Vx: _ (HarmDummies:) (%^Pen: %^CC2C:) (%^NoPAL:) %^VowRM:  ;

!!€ kuu{back}^VowRM>i>d
!!€ ku000>i>d
    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:|%^Pen: %^StrGStem: |%^Pen: %^ShortGStem:) (%^NoPAL:) %^VowRM: ;
        where Vx in (u ü) ;
!! ```

!"ü:0"
!!! __@RULENAME@__
!!! {{{
! ü:0 <=>
!  [ Cns: |ArchCns:| Vow: ] _ (s:) (HarmDummies:) PenVOWHite [(%^Pen: %^CC2C:|%^Pen: %^G1:|%^Pen: %^G2:|%^Pen: %^G3:|%^Pen: %^G4:|%^Pen: %^StrGStem:|%^Pen: %^ShortGStem:) %^VowRM: | CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^WGStem: ]  ; 
!!  [ Cns: |ArchCns:| Vow: ] _ (s:) (HarmDummies:) [(%^Pen: %^CC2C:|%^Pen: %^G3:|%^Pen: %^G4:|PenVOWHite %^Pen: %^G1:) %^VowRM: ]( CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: )  ; 
!!€ tüü%{front%}%^VowRM%>hüq
!!€ tü000%>hüq
!!€ tervüs%{front%}%^VowRM%^WGStem%>i%>t
!!€ terv00000%>i%>t
!   OnSetC ü: _ (HarmDummies:) VOWHite %> i ;
!    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:|%^Pen: %^StrGStem: |%^Pen: %^ShortGStem:) %^VowRM: ;
!    _ (HarmDummies:) PenVOWHite (%^Pen: %^G3:|%^Pen: %^G4:) %^VowRM: ;
!!! }}}

"e:0"
!! **@RULENAME@**
 e:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [(%^Pen: %^CC2C:|%^Pen: %^StrGStem:|PenVOWHite %^Pen: %^WGStem:) %^VowRM: | CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^WGStem: ]  ; 
           [ Cns: | :Cns ] _ l (HarmDummies:) %^Pen: %^VowRM:  ;
           e: _ VOWHite %^VowRM: ;
	   _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;
!!€ herne%{hØ%}%{front%}%^VowRM%^WGStem%>id%{eõ%}
!!€ hern00000%>ide
    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:) (%^NoPAL:) %^VowRM: ;

"o:0"
!! **@RULENAME@**
 o:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [ (PenVOWHite (%^Pen: %^NoPAL:) %^Pen: [%^G1:|%^G2:|%^G3:|%^G4:|%^StrGStem:|%^JI2I:]|%^Pen: %^CC2C:) %^VowRM: (õ) ]   ; 
           o: _ (HarmDummies:) [VOWHite %^VowRM:|%^OO2Õ:] ;
!! juuma+V+Inf
!!€ j{ou}{ou}{back}^VOWRaise^VowRM>vv>{aä}q
!!€ ju̬0000>vv>aq
    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:|%^Pen: %^StrGStem:) (%^NoPAL:) %^VowRM: ;
    _ (HarmDummies:) VOWHite  (%^NoPAL:) %^VowRM: ;

"Vx%{ou%}:0"
!! **@RULENAME@**
 Vx:0 <=>
     Vx: _ (HarmDummies:) [VOWHite %^VowRM:|%^OO2Õ:] ;
!! juuma+V+Inf
!!€ j{ou}{ou}{back}^VOWRaise^VowRM>vv>{aä}q
!!€ ju̬0000>vv>aq
          where Vx in (%{ou%} %{öü%} %{ei%} %{õy%} ) ;

"Vx%{äe%}:0 Passive stem vowel"
!! **@RULENAME@**
 Vx:0 <=>
     _ (HarmDummies:) [(VOWHite (%^NoPAL:)| PenVOWHite (%^Pen: %^NoPAL:) |PenVOWHite %^Pen: %^WGStem:) %^VowRM:] ;
          where Vx in (%{aõ%} %{ae%} %{äe%} ) ;
!%^Pen%^NoPAL%^VowRM
!! nõstma+V+Inf/mA
!!€ nõs{tt́}{aõ}{back}^Pen^NoPAL^VowRM>m{aä}
!!€ nõst00000>ma

"ö:0"
!! **@RULENAME@**
 ö:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [  (PenVOWHite %^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^CC2C:) %^VowRM: (e) ]   ; 
           ö: _ (HarmDummies:) [VOWHite %^VowRM:|%^OO2Õ:] ;
    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:) (%^NoPAL:) %^VowRM: ;

"i:0"
!! **@RULENAME@**
 i:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [ (%^Pen: %^CC2C:|PenVOWHite %^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^TS2S:) %^VowRM: (õ|e) | CnsInZero: %^Pen: %^VowRM: %^WGStem: ]  ; 
!! hüdsi+N+Sg+Par:
!!€ hüdsi%{front%}%^TS2S%^VowRM%>t
!!€ hü0s0000%>t
          Vow %{lØ%}:l l %>: _ (HarmDummies:) [õ|e] ;
!          Vow l 0:l %>: _ (HarmDummies:) [õ|e] ;
       a _ g: (HarmDummies:) %^WGStem: %> [o:|e:] ;
       a _ g: [o:|e:] (HarmDummies:) PenVOWHite %^Pen: %^WGStem:  ;
!!€ aigo%{back%}%^Pen%^WGStem
!!€ a00o000

!2019-01-19    Vow [ %^I7: | %^V7: 0: ] Vow: (HarmDummies:) %^StrGStem: %> _ ;
!!!€ va%^I7o%^StrGStem%>i
!!!€ vaio0%>0
!!!€ hä%^V70ö%^StrGStem%>i
!!!€ hävvü0%>0
    _ (HarmDummies:) PenVOWHite (%^Pen: %^WGStem:) (%^NoPAL:) %^VowRM: ;


"õ:0"
!! **@RULENAME@**
 õ:0 <=>  [ Cns: | :Cns |ArchCns:] _ [ l | r | %{lĺ%}: | %{rŕ%}: ] (HarmDummies:) (%^Pen: %^StrGStem:|PenVOWHite %^Pen: %^WGStem:) %^Pen: %^VowRM:  ;
!!€ tapõl{back}^Pen^VowRMõ>m{aä}
!!€ tap0l000õ>ma
!!€ kan%{dn%}õ%{lĺ%}{back}^StrGStem^Pen^VowRM>õ
!!€ kand0l0000>õ
!! kogõr+N
!!€ ko%{kg%}õr{back}^StrGStem^Pen^VowRM>õ
!!€ kok0r0000>õ
 [Cns:|ArchCns:] _ (s:) (HarmDummies:) (%^WGStem:|%^Pen: %^CC2C:|PenVOWHite PenGRAD) %^VowRM:  ;
!! hamõq+N+Pl+Ill
!!€ ha%{mØ%}mõ%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem%^VowRM%>ihe
!!€ ha0m0000000%>ihe
	   _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;
    _ (%{lĺ%}:|r) (HarmDummies:) PenVOWHite PenGRAD %^Pen:0 %^NoPAL: %^VowRM: ;
!    _ (HarmDummies:) PenVOWHite PenGRAD (%^NoPAL:) %^VowRM: ;

"%{eØ%}: 0"
!! **@RULENAME@**
 %{eØ%}:0 <=> _ [%{lĺ%}:|r] (HarmDummies:) PenVOWHite PenGRAD %^Pen: %^VowRM: ;

"%{õØ%}: 0"
!! **@RULENAME@**
 %{õØ%}:0 <=> _ (%{lĺ%}:|l|r) (HarmDummies:) PenVOWHite PenGRAD (%^Pen:0 %^NoPAL:) %^VowRM: ;
 _ (%{lĺ%}:|l|r) (HarmDummies:) PenVOWHite PenGRAD  %^Pen: %^VowRM: ;


!! VOWEL LENGTHENING

"%{XV%}:u"
!! **@RULENAME@**
 %{XV%}:u <=> [ :u (HarmDummies:) PenBetweenStemAndStemFinal | %{ou%}:u̬ (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
       [ u (HarmDummies:) | u: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) | u̬: PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;

     
"%{XV%}:ü"
!! **@RULENAME@**
 %{XV%}:ü <=> [:ü (HarmDummies:) PenBetweenStemAndStemFinal | %{öü%}:ü̬  (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
       [ ü (HarmDummies:) | ü: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) |ü̬  (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

"%{XV%}:o"
!! **@RULENAME@**
 %{XV%}:o <=> :o (HarmDummies:) (PenBetweenStemAndStemFinal) ( %- ) _  ;
!!€ taso%{back%}%-%{XV%}iq
!!€ taso0%-oiq
     
"%{XV%}:a"
!! **@RULENAME@**
 %{XV%}:a <=> :a (HarmDummies:) PenBetweenStemAndStemFinal ( %- ) _  ;
       [ o (HarmDummies:) | a (HarmDummies:) | a: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           a: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:ä"
!! **@RULENAME@**
 %{XV%}:ä <=> :ä (HarmDummies:) PenBetweenStemAndStemFinal ( %- ) _  ;
       [ ö (HarmDummies:) | ä (HarmDummies:) | ä: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           ä: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:õ"
!! **@RULENAME@**
 %{XV%}:õ <=>
   [:õ (HarmDummies:) PenBetweenStemAndStemFinal| %{õy%}: (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
        [õ  (HarmDummies:) | õ:  (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           õ: [r|l| %{lĺ%}: | %{rŕ%}: ] (HarmDummies:) %^Pen: %^VowRM: (%^NoPAL:) %> _ ; 
!! kannõĺ+N+Sg+Gen: **kantele**
!!€ kan%{dn%}õ%{lĺ%}%{back%}%^Pen%^VowRM%^NoPAL%>%{XV%}
!!€ kand0l0000%>õ

"%{XV%}:e"
!! **@RULENAME@**
 %{XV%}:e <=>
       [:e (HarmDummies:) PenBetweenStemAndStemFinal|:e ] ( %- ) _  ;
!! käskmä+V+Act+Ind+Prs+Neg **command/käskeä**
!!€ käs%{kØ%}%{front%}%^WGStem%>%{eõ%}%-%{XV%}iq
!!€ käs000%>e%-eiq
          [ e  (HarmDummies:) | e: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;
           e: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:i"
!! **@RULENAME@**
 %{XV%}:i <=> [:i (HarmDummies:) PenBetweenStemAndStemFinal| :i (HarmDummies:) VOWHite ] ( %- ) _  ;


!!
"i2j"
!! **@RULENAME@**
 i:j <=> Vow Cns:+ Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
! Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
         Cns _ (HarmDummies:) %^I2J: ;
!!€ asi%{back%}%^I2J%>a
!!€ asj00%>a
         Vow: _  PenBetweenStemAndStemFinal %^I2J: ;
!! elläi+N+Sg+Gen
!!€ e{lØ}lä{ij}{front}^Pen^WGStem^I2J>{aä}
!!€ e0läj0000>ä
!         Cns _ (HarmDummies:) %^StrGStem: %> :Vow ;

"%{ij%}:j"
!! **@RULENAME@**
 %{ij%}:j  <=> Vow _ (HarmDummies:) (PenVOWHite %^Pen: %^G1:) %^I2J: ;


"%{jiØ%}:j"
!! **@RULENAME@**
 %{jiØ%}:j  <=>
    [Cns:|ArchCns:] _ [(HarmDummies:) (%^NoPAL:|%^PAL:) %^I2J: | Vow: (HarmDummies:) %^Pen:  %^NoPAL: %^Pen: %^JI2J: ] ;

"%{jiØ%}:i"
!! **@RULENAME@**
 %{jiØ%}:i  <=>
    [Cns:|ArchCns:] _  Vow: (HarmDummies:) %^Pen: (%^NoPAL:|%^PAL:) %^Pen: %^JI2I:  ;
    [Cns:|ArchCns:] _  (HarmDummies:) (%^NoPAL:|%^PAL:) %^JI2I:  ;
!!€ ki{rŕ}{jiØ}{front}^NoPAL^JI2I
!!€ kiri000

"%{jiØ%}:0"
!! **@RULENAME@**
 %{jiØ%}:0  <=>
    [Cns:|ArchCns:] _ (HarmDummies:) (%^PAL:) %^VowRM: ;
    [Cns:|ArchCns:] _ Vow: (HarmDummies:) PenVOWHite (%^Pen: %^PAL:) %^Pen: %^JI20: ;
!! vari+N+Sg+Gen: **shadow/varjo**
!!€ va%{rŕ%}%{jiØ%}o%{back%}%^Pen%^PAL%^Pen%^WGStem
!!€ vaŕ0o00000

"%{jØ%}:0"
!! **@RULENAME@**
 %{jØ%}:0  <=>
    [Vow:|ArchVow:] _ j: Vow: (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:] ;
!! vari+N+Sg+Gen: **shadow/varjo**
!!€ va%{rŕ%}%{jiØ%}o%{back%}%^Pen%^PAL%^Pen%^WGStem
!!€ vaŕ0o00000
 _ Cns  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ] %^WGStem: ;
 [Vow:|ArchVow:] _ (HarmDummies:) (%^VowRM: ) %^WGStem: ;
 j _ Vow: (Cns | ArchCns: ) (HarmDummies:) %^Pen: %^WGStem:  ;


"u2v"
!! **@RULENAME@** depricate to "%{uv%}:v"
 u:v <=> u _ %> v a q ;
         u _ d: (HarmDummies:) %^WGStem: ;

"%{uv%}:v"
!! **@RULENAME@**
 %{uv%}:v <=> u _ %> v a q ;
         u _ [ %{dv%}:|%{gv%}:] (HarmDummies:) %^WGStem: ;
!!€ lu{uv}{dv}{back}^WGStem>{aä}
!!€ luvv00>a
         [o|õ] _ [ %{dd́v%}:|%{gǵv%}:] (HarmDummies:) %^WGStem: ;
!! joudma+V+Act+Ind+Prt+Sg1
!!€ jõ%{uv%}%{dd́v%}%{back%}%^WGStem%>i
!!€ jõvv00%>i

"%{üv%}:v"
!! **@RULENAME@**
 %{üv%}:v <=> ü _ (HarmDummies:) %> v ä q ;
         ü _ [%{dv%}: | %{dd́v%}:] (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dd́v%}%{front%}%^WGStem%>et
!!€ püvv00%>et
!!€ sü%{üv%}%{dv%}%{front%}%^WGStem%>ä
!!€ süvv00%>ä
         ö _ [ %{dd́v%}:|%{gǵv%}:] (HarmDummies:) %^WGStem: ;

"%^I7:i"
!! **@RULENAME@**
 %^I7:i <=> Vow _ Vow (HarmDummies:) %^StrGStem:  ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0

!FIXME 2019-05-8
!"%^V7:v with 0:v"
!! **@RULENAME@**
! %^V7:v <=> Vow _ 0: Vow (HarmDummies:) %^StrGStem:  ;
!!!€ hä%^V70ö%^StrGStem
!!!€ hävvü0

!! CONSONANT
"%{pṕØ%}:ṕ"
!! **@RULENAME@**
%{pṕØ%}:ṕ <=>
 p _ (HarmDummies:) VOWHite [%^G2:|%^StrGStem:] %^PAL: ;
!!€ pap%{pṕØ%}%{back%}%^G2%^PAL
!!€ papṕ000

"%{tt́Ø%}:t́ "
!! **@RULENAME@**
%{tt́Ø%}:t́ <=>
 [t|h] _ (HarmDummies:) VOWHite [%^G2:|%^StrGStem:] %^PAL: ;
!!täht́+N+Sg+Nom:
!!€ täh%{tt́Ø%}%{front%}%^StrGStem%^PAL
!!€ täht́000

"%{kḱØ%}:ḱ"
!! **@RULENAME@**
%{kḱØ%}:ḱ <=>
 [s|h|k] _ (HarmDummies:)  VOWHite [%^G2:|%^StrGStem:] %^PAL: ;



!! SECONDARY CONSONANT LENGTHENING


!CHECKME 2020-02-13
"%{pØ%}:p"
!! **@RULENAME@**
!! ```
 %{pØ%}:p <=> Vow: _ [%{pbØ%}:|%{pb%}:] [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: %^G4: ;
!!€ hä%{pØ%}%{pbØ%}ü%{front%}%^Pen%^G4
!!€ häppü000
   Vow: _ [%{pbv%}:|%{pb%}:] (HarmDummies:) VOWHite %^G4: ;
!!tõbi+N+Sg+Ill:
!!€ tõ%{pØ%}%{pbv%}%{back%}%^G4%>%{eõ%}
!!€ tõpp00%>õ
   Vow: _ [p|%{pṕ%}:] (HarmDummies:) VOWHite %^StrGStem: ;
!!sepp+N+Sg+Nom
!!€ se%{pØ%}p%{front%}%^StrGStem
!!€ sepp00
Vow: _ [ p ] Vow: (HarmDummies:) PenVOWHite %^Pen:  %^StrGStem: ;
!!nupp+N+Sg+Nom
!!€ nu%{pØ%}pu%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem%^VowRM
!!€ nupp0000000
!! ```

"%{tØ%}:t"
!! **@RULENAME@**
 %{tØ%}:t <=>
 Vow _ [%{tdØ%}:|%{td%}:|t́] [Vow:|ArchVow:] (HarmDummies:) PenVOWHite  %^Pen: [ %^G4:|%^StrGStem:] ;
!!€ sõ%{tØ%}da%{back%}%^G4%^PLPRT
!!€ sõtto000
   Vow: _ [t|%{tt́%}:] (HarmDummies:) VOWHite %^StrGStem: ;
!!sitt+N+Sg+Nom
!!€ si%{tØ%}t%{front%}%^StrGStem
!!€ sitt00
!?? _ t Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ ü%{tØ%}te%{front%}%^Pen%^StrGStem
!!€ ütte000
Vow: _ [ t |%{tt́%}: ] Vow: (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: ;
!!mütt+N+Sg+Nom
!!€ mü%{tØ%}tü%{front%}%^Pen%^VOWRaise%^Pen%^StrGStem%^VowRM
!!€ mütt0000000
  _ %{tdØ%}: [Vow:|ArchVow:] (HarmDummies:)  %^Pen: [ %^G4: ];
!! sõda+N+Sg+Ill:
!!€ sõ%{tØ%}%{tdØ%}a%{back%}%^Pen%^G3
!!€ sõtta000
  Vow _ %{tdØ%}: (HarmDummies:) [%^G4:] ;
  [h|s] _ (HarmDummies:) %^StrGStem: ;
 _ [Vow:| ArchVow:] (Cns:| ArchCns:) (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: ;
!!€ uh{tØ}a{sś}{back}^Pen^StrGStem^Pen^VowRM^NoPAL>{aä}>m{aä}
!!€ uht0s000000>a>ma

"%{t́Ø%}:t́"
!! **@RULENAME@**
 %{t́Ø%}:t́ <=> _ [Vow:| ArchVow:] [Cns:| ArchCns:] (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: ;

"%{Øk%}:k"
!! **@RULENAME@**
 %{Øk%}:k <=> Vow _ [%{kgØ%}:|%{kg%}:|g:] (HarmDummies: | [Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^G4: ;
!! igä+N+Sg+Ill
!!€ i{kØ}{kgØ}ä{front}^Pen^G4
!!€ ikkä00
!??   Vow k _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
   Vow: _ [%{kgØ%}:|%{kg%}:] (HarmDummies:) %^G4: ;
!!mägi+N+Sg+Ill:
!!€ mä%{Øk%}%{kgØ%}%{front%}%^G4%>%{eõ%}
!!€ mäkk00%>e


"%{XC%}:s"
!! **@RULENAME@**
 %{XC%}:s <=> [ s: | %{sś%}:s ] (HarmDummies:) PenVOWHite (%^Pen: %^WGStem: |%^Pen: %^StrGStem:) _ ;

"%{XC%}:l"
!! **@RULENAME@**
 %{XC%}:l <=> l: (HarmDummies:) _  %^NoPAL:  ;

"%{XC%}:ĺ"
!! **@RULENAME@**
 %{XC%}:ĺ <=> l: (HarmDummies:) _ %^PAL: ;

"%{XC%}:k"
!! **@RULENAME@**
 %{XC%}:k <=> [%{kg%}:k | k: ] (HarmDummies:) _ ;

"%{kḱ%}:ḱ"
!! **@RULENAME@**
 %{kḱ%}:ḱ <=> %{kØ%}:k  (HarmDummies:) _ Vow:0 %^VowRM: %^PAL: ;
 _ [(HarmDummies:) VOWHite (%^CC2C: | %^WGStem: |%^StrGStem: ) %^PAL:| (HarmDummies:) PenVOWHite [%^Pen: %^WGStem:|%^Pen: %^StrGStem: ] %^PAL:| Vow: Cns:* (HarmDummies:) %^Pen: %^PAL:] ;
!! kakma
!!€ ka%{kØ%}%{kḱ%}u%{back%}%^VowRM%^PAL
!!€ kakḱ0000


!! ### Consonant weakening 

! aika: ajan
"kToZero"
!! **@RULENAME@**  
k:0 <=>
  [h|k|s] _ [Vow:|ArchVow:] (s) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
!!€ puhkas%{back%}%^WGStem%>taq
!!€ puh0as00%>taq

 s _ a (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ [Vow:|ArchVow:] | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
  k _ [(HarmDummies:)|[Vow:|ArchVow:] (HarmDummies:) %^Pen:] [ %^CC2C:0 | %^WGStem: ] ;
  [h|s] _ [(HarmDummies:) VOWHite |[Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen:] %^WGStem:  ;
!!€ ikkõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ ik000000%>ma

"%{pṕØ%}:0"
!! **@RULENAME@**  
 %{pṕØ%}:0 <=>
 p _ (HarmDummies:) VOWHite  [%^WGStem: | %^G1: ] ;
 p _ (HarmDummies:) PenVOWHite %^Pen:  [%^WGStem: | %^G1: ] ;

"%{tt́Ø%}:0"
!! **@RULENAME@**  
 %{tt́Ø%}:0 <=>
  [t|h] _ (HarmDummies:) VOWHite [%^WGStem: | %^G1: ] ;
  [t|h] _ (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ] ;

"%{kḱØ%}:0"
!! **@RULENAME@**  
 %{kḱØ%}:0 <=>
 [k|h|s] _ (HarmDummies:) VOWHite [%^WGStem: | %^G1: ] ;
 [k|h|s] _ (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ] ;

!!juuskma+V+Act+Ind+Prs+Sg1:
!!€ j{ou}{ou}s{kḱØ}{back}^VOWLower^WGStem>{eõ}
!!€ joos0000>õ
!!$ j{ou}{ou}s{kḱØ}{back}^VOWLower^WGStem>{eõ}
!!$ joosk000>õ

"%{sØ%}:0"
!! **@RULENAME@**  
 %{sØ%}:0 <=>
 _ [s|ś|ArchCns:] [Vow:|ArchVow:] (Cns:|ArchCns:) (HarmDummies:) PenVOWHite %^Pen:  [%^G1:|%^G2:|%^CC2C:|%^WGStem:] ;

!!€ su%{sØ%}śo%{back%}%^Pen%^WGStem
!!€ su0śo000
    _  [s|ś|ArchCns:] (HarmDummies:) VOWHite  [ %^CC2C:|%^WGStem:] ;
!! vaśma+V+Inf/mA: **answer/vastata**
!!€ va{sØ}{sś}{back}^VOWRaise^WGStem^PAL>m{aä}
!!€ va0ś0000>ma
!kraśma
    _  (HarmDummies:) PenVOWHite (%^Pen:  %^G1:|%^Pen:  %^WGStem:|%^Pen: %^StrGStem: ) %^WGStem: ;
!!hammas+N+Sg+Gen: __tooth/hammas__
!!€ ham%{bm%}a%{sØ%}%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem%^WGStem
!!€ hamba0000000


"%{vØ%}:0"
!! **@RULENAME@**  
 %{vØ%}:0 <=>
 _ [v|v́|ArchCns:] [Vow:|ArchVow:] (Cns:|ArchCns:) (HarmDummies:) PenVOWHite %^Pen:  [%^WGStem:|%^CC2C:] ;
    _  [v|v́|ArchCns:] (HarmDummies:) VOWHite  [ %^CC2C:|%^WGStem:] ;
!! kruv́ma+V+Inf/mA
!!€ kru{vØ}{vv́}{back}^VOWRaise^WGStem^PAL>m{aä}
!!€ kru0v́0000>ma


"%{rØ%}:0"
!! **@RULENAME@**  
 %{rØ%}:0 <=>
 [Vow:|ArchVow:] _ (HarmDummies:) PenVOWHite (%^Pen: %^G1: |%^Pen: %^WGStem:) (%^VowRM: ) %^WGStem: ;
  _ [r|ŕ | ArchCns: |%{rŕ%}:] (HarmDummies:) [PenVOWHite|VOWHite] [%^G1:|%^WGStem:] ;
!sekretäŕ
  _ [r|ŕ|%{rŕ%}:] [Vow:|ArchVow:] (ArchCns:|Cns:* |i:) (HarmDummies:) PenVOWHite %^Pen: [ %^G1: | %^G2: |%^WGStem:] ;
!!€ a%{rØ%}ro%{back%}%^Pen%^G2
!!€ a0ro000
  _ [r|ŕ |%{rŕ%}:]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:|%^WGStem:]  ;

 [Vow:|ArchVow:]  _ [ ArchCns: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:|%^WGStem:]  ;


 [Vow:|ArchVow:] _ [r|ŕ |%{rŕ%}:] Vow (:Cns |:Vow) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:] ;
  _ [r|ŕ|%{rŕ%}:] Vow: (Cns | ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [%^G1:|%^WGStem:]  ;


"%{nØ%}:0"
!! **@RULENAME@**  
 %{nØ%}:0 <=>  _ [n|ń|%{nń%}:] [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [ %^G1: | %^G2: |%^WGStem:| %^CC2C: ] ;
!!€ su%{nØ%}ńo%{back%}%^Pen%^WGStem
!!€ su0ńo000
!!€ va{nØ}{nń}u{back}^Pen^VOWRaise^Pen^CC2C^Pen^NoPAL^VowRM>m{aä}
!!€ va0n000000000>m{aä}
 [Vow:|ArchVow:]  _ [n|ń |%{nń%}:]  [[Vow:|ArchVow:] (Cns:| ArchCns: |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:  | %^G2: |%^WGStem:] ;
 [Vow:|ArchVow:]  _ [ ArchCns: ]  [[Vow:|ArchVow:] (Cns:| ArchCns: |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [ %^G1:  | %^G2: |%^WGStem:]  ;
 [Vow:|ArchVow:] _ (HarmDummies:) PenVOWHite (%^Pen: %^G1: |%^Pen: %^WGStem:) (%^VowRM: ) %^WGStem: ;

 [Vow:|ArchVow:] _ [n|ń|%{nń%}: ] Vow (:Cns |:Vow) (HarmDummies:) PenVOWHite %^Pen: [ %^G1:  | %^G2: |%^WGStem:] ;
  _ [n|ń | ArchCns: ] (HarmDummies:) VOWHite [%^G1:  | %^G2: |%^WGStem:] ;
!jońma
  _ [n|ń|%{nń%}:] Vow: (Cns | ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: [ %^G1:  | %^G2: |%^WGStem:]  ;


"%{lØ%}:0"
!! **@RULENAME@**  
 %{lØ%}:0 <=>  _ [l|ĺ | %{lĺ%}: ] [Vow:|ArchVow:] (Cns:|ArchCns:) (HarmDummies:) PenVOWHite %^Pen: [ %^G1: | %^G2: |%^WGStem:|%^CC2C:] ;
!!€ ta%{lØ%}lo%{back%}%^Pen%^WGStem
!!€ ta0lo000
 [Vow:|ArchVow:]  _ [l|ĺ | %{lĺ%}: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) (PenVOWHite|VOWHite) ]  [%^G1:|%^WGStem:|%^CC2C:]  ;
 [Vow:|ArchVow:]  _ [ ArchCns: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:|%^WGStem:]  ;
 [Vow:|ArchVow:] _ (HarmDummies:) PenVOWHite (%^Pen: %^G1:|%^Pen: %^WGStem: ) (%^VowRM: ) %^WGStem: ;

 [Vow:|ArchVow:] _ [l|ĺ | %{lĺ%}: ] Vow (:Cns |:Vow) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:|%^CC2C:] ;
  _ [l|ĺ | %{lĺ%}: ] (HarmDummies:) [VOWHite|PenVOWHite] [%^G1:|%^WGStem:] ;
  _ [l|ĺ| %{lĺ%}: ] Vow: (Cns | ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:|%^CC2C:]  ;


"%{mØ%}:0"
!! **@RULENAME@**  
 %{mØ%}:0 <=>  _ m [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [ %^G1: | %^G2:|%^WGStem:|%^CC2C:] ;
!!€ su%{sØ%}śo%{back%}%^Pen%^WGStem
!!€ su0śo000
 [Vow:|ArchVow:]  _ [m|ḿ |ArchVow:]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:|%^WGStem:|%^CC2C:]  ;
 [Vow:|ArchVow:]  _ [ ArchCns: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ]  [%^G1:|%^WGStem:|%^CC2C:]  ;
 [Vow:|ArchVow:] _ (HarmDummies:) PenVOWHite (%^Pen: %^G1:|%^Pen: %^WGStem: ) (%^VowRM: ) %^WGStem: ;

 [Vow:|ArchVow:] _ [m|ḿ|ArchCns: ] Vow (:Cns |:Vow) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:|%^CC2C:] ;
  _ [m|ḿ | ArchCns: ] (HarmDummies:) VOWHite [%^G1:|%^WGStem:|%^CC2C:] ;
  _ [m|ḿ|ArchCns:] Vow: (Cns | ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:|%^CC2C:]  ;


"%{kØ%}:0"
!! **@RULENAME@**  
 %{kØ%}:0 <=> [h|s] _ [Vow:|ArchVow:] (s|%{sØ%}:|ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
!!€ puh%{kØ%}as%{back%}%^WGStem%>taq
!!€ puh0as00%>taq
!!€ vis%{kØ%}a%{back%}%^WGStem%>aq
!!€ vis0a00%>aq

 [s|h] _  (HarmDummies:) ( %^VOWLower:) %^WGStem: ;
!! nätsk+A+Sg+Gen
!!€ nä%{td%}s%{kØ%}%{front%}%^G1%>%{aä%}
!!€ näds000%>ä

!  k _ (HarmDummies:) VOWHite [ %^G1: | %^G2: ] ;
 s _ a (HarmDummies:) PenVOWHite %^Pen: [%^WGStem:] ;
 
   _ [k | ḱ | %{kḱ%}: ] (HarmDummies:) VOWHite [ %^CC2C:0 | %^WGStem: ] ;
   _ [ %{kḱ%}: ] (HarmDummies:) [ %^CC2C:0 | %^WGStem: |%^G3: ] ;
   _ [k | ḱ | %{kḱ%}: ] [Vow:|ArchVow:] ( %{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen: [ %^CC2C:0 | %^WGStem:] ;
   _ [ %{kḱ%}: ] [Vow:|ArchVow:] ( %{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen: [ %^CC2C:0 | %^WGStem: | %^G3: ] ;

   _ [ %{kg%}: ] (HarmDummies:) [%^G2:|%^CC2C:]  ;
  [h|s] _ [(HarmDummies:)|[Vow:|ArchVow:] (HarmDummies:) %^Pen:] [%^WGStem:]  ;
!!€ i%{kØ%}kõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ i0k00000%>ma
 
!! kakma:
!!€ ka%{kØ%}%{kḱ%}u%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ ka0k00000%>ma
  _ %{kḱg%}: (HarmDummies:) [ %^CC2C: | %^G2: | %^G3: ] ;

!! kõiḱ+Pron+Sg+Nom
!!€ kõi%{kØ%}%{kḱg%}%{back%}%^CC2C%^PAL
!!€ kõi0ḱ000
 Vow  _ [ k | ḱ |ArchCns:] [Vow:|ArchVow:] (ArchCns:) (HarmDummies:) %^Pen:  [%^WGStem:|%^CC2C:]  ;
 Vow  _ [ %{kgØ%}: ] [Vow:|ArchVow:] (ArchCns:) (HarmDummies:) %^Pen: [%^G1: | %^G2: | %^G3: |%^CC2C:] ;
!!€ ri%{kØ%}ka%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ ri0kas00000
 Vow  _  %{kgØ%}:  (HarmDummies:)  [%^G1: | %^G2: | %^G3: |%^CC2C:] ;
!! jõgi+N+Sg+Gen: **river/joki**
!!€ jõ%{kØ%}%{kgØ%}%{back%}%^G1%>%{eõ%}
!!€ jõ0000%>õ

_ %{kg%}: (HarmDummies:) [ %^G2: | %^G3: |%^CC2C:] ;

"pToZero"
!! **@RULENAME@**
 p:0 <=>
  p _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  p _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ tapp%{back%}%^CC2C%>m%{aä%}
!!€ tap000%>ma

"%{pØ%}:0"
!! **@RULENAME@**
 %{pØ%}:0 <=>
  _ [Cns|%{pṕ%}:|%{pṕb%}:] (HarmDummies:) VOWHite [%^CC2C:0|%^WGStem:|%^G3: ] ;
!klaṕma
!!sepp+N+Sg+Gen
!!€ se%{pØ%}p%{front%}%^WGStem
!!€ se0p00

!  _ p (HarmDummies:) [%^CC2C:0|%^WGStem:| %^G1: | %^G2: | %^G3:] ;
  _ [p|%{pṕ%}:] [Vow:|ArchVow:] (Cns|%{ij%}:|%{sØ%}:|ArchCns:) (HarmDummies:) PenVOWHite %^Pen: [ %^G3: | %^CC2C:0|%^WGStem:] ;!2019-11-05
!  _ p [Vow:|ArchVow:] (HarmDummies:) %^Pen: [ %^G1: | %^G2: | %^G3: | %^CC2C:0|%^WGStem:] ;!2019-11-05
  p _ [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [ %^G3: | %^CC2C:0] ;
!!€ tap%{pØ%}%{back%}%^CC2C%>m%{aä%}
!!€ tap000%>ma
Vow: _ [ %{pbØ%}: | %{pbv%}: ] Vow: (HarmDummies:) PenVOWHite %^Pen: [ %^G1: |%^G2: | %^G3: |%^CC2C:] ;
Vow: _  %{pbv%}: (HarmDummies:) [ %^G1: |%^G2: | %^G3: |%^CC2C:] ;


"XØToZero"
!! **@RULENAME@**
 Cx:0 <=>
 [Vow:|ArchVow:]  _ [Cy ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ] [ %^WGStem: | %^G1: ] ;
 [Vow:|ArchVow:]  _ [ ArchCns: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) PenVOWHite %^Pen:|(HarmDummies:) ] [ %^WGStem: | %^G1: ] ;
 [Vow:|ArchVow:] _ (HarmDummies:) PenVOWHite (%^Pen: %^G1:|%^Pen: %^WGStem: ) (%^VowRM: ) %^WGStem: ;
!! agras+A+Sg+Gen
!!€ a%{kg%}ra%{sØ%}%{back%}%^Pen%^VOWRaise%^Pen%^G2%^WGStem
!!€ akra0000000
 [Vow:|ArchVow:] _ Cns [Vow|ArchVow:] (:Cns |:Vow|ArchCns:) (HarmDummies:) PenVOWHite %^Pen: [%^G1:|%^WGStem:] ;
  _ [Cy | ArchCns: ] (HarmDummies:) [%^G1: | PenVOWHite %^WGStem:] ;
  _ Cy Vow: (Cns | ArchCns: ) (HarmDummies:) %^Pen: [%^G1:|%^WGStem:]  ;
!!€ ve%{rØ%}rev%{front%}%^Pen%^WGStem%>%{aä%}
!!€ ve0rev000%>ä
         where Cx in (%{hØ%}) 
               Cy in (h     ) matched ;
!! elläi+N+Sg+Gen
!!€ e{lØ}lä{ij}{front}^Pen^WGStem^I2J>{aä}
!!€ e0läj0000>ä
!! hamõh+N+Sg+Nom
!!€ ha%{mØ%}mõ%{hØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ ha0mõh000000

"XØToSelf"
!! **@RULENAME@**
 Cx:Cy <=>
  _ Cy (HarmDummies:) PenVOWHite [ %^G3: |%^G4: |%^StrGStem:] ;
  _ Cy Vow: (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) %^Pen: [%^G3:|%^G4: |%^StrGStem:]  ;
! Cy _ Vow: (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) %^Pen: %^G2:  ;
     where Cx in (%{lØ%} %{mØ%} %{rØ%} %{vØ%}) 
     	   Cy in ( l       m      r      v) matched ;
!! villui+A+Sg+Nom
!!€ vi{lØ}lui{back}^Pen^G3
!!€ villui000


!! kevväi+N+Sg+Gen: **spring**
!!€ ke%{vØ%}vä%{ij%}%{front%}%^Pen%^WGStem%>ä
!!€ ke0väj000%>ä


"%{sØ%}:s"
!! **@RULENAME@**
 %{sØ%}:s <=>
  _ [s|ś] Vow: (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) PenVOWHite %^Pen: %^StrGStem:  ;
  _ [s|ś] (HarmDummies:) VOWHite %^StrGStem:  ;
! s _ Vow: (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) %^Pen: %^G2:  ;
 _ Vow: (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) PenVOWHite (%^Pen: %^StrGStem:|%^Pen: %^WGStem:) %^StrGStem:  ;
 _ (HarmDummies:) PenVOWHite (%^Pen: %^StrGStem:|%^Pen: %^WGStem:) %^StrGStem:  ;
!! ratas+N+Sg+Nom
!!€ ra%{tØ%}ta%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ ra0tas000000
!! kaardas+N+Sg+Nom
!!€ kaar%{dØ%}a%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ kaardas000000
! _ (HarmDummies:) %^Pen: %^G2:  ;
!! agras+A+Sg+Nom
!!€ a%{kg%}ra%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ agras000000

"%{hØ%}:h"
!! **@RULENAME@**
 %{hØ%}:h <=>
 _ (HarmDummies:) PenVOWHite (%^Pen: %^StrGStem:|%^Pen: %^WGStem:) %^StrGStem:  ;
!! hamõh+N+Sg+Nom
!!€ ha%{mØ%}mõ%{hØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ ha0mõh000000
 _ [h|h́] [Vow:|ArchVow:] (Cns: | ArchCns: |%{ij%}:) (HarmDummies:) PenVOWHite %^Pen: %^StrGStem:  ;


"%{kØ%}:k"
!! **@RULENAME@**
 %{kØ%}:k <=>
   [s|h] _ (HarmDummies:) VOWHite [ %^StrGStem:] ;
  Vow _ [k|%{kḱ%}:] (HarmDummies:) VOWHite [ %^StrGStem:] ;
 [Vow|Cns] _ [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem:] ;
!! rehksämä+V+Inf/mA:
!!€ reh{kØ}ä{sś}{front}^Pen^StrGStem^Pen^VowRM^NoPAL>{aä}>m{aä}
!!€ rehk0s000000>ä>mä
  Vow _ k Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem:] ;
!!€ as%{kØ%}o%{back%}%^Pen%^G2
!!€ asko000
 Vow _ Vow: (HarmDummies:) [n|ń] PenVOWHite  %^Pen: [ %^G2:] ;
  Vow _ %{kg%}: (HarmDummies:) %^G4: ;
  Vow _ %{kgØ%}: (Vow:) (HarmDummies:) %^G4: ;
  Vow _ %{kḱ%}: (HarmDummies:) %^StrGStem: ;
!! makḱ+N+Pl+All
!!€ ma{kØ}{kḱ}{back}^StrGStem^NoPAL>{eõ}>l{eõ}
!!€ makk000>õ>lõ

"%{pb%}:p"
!! **@RULENAME@**
 %{pb%}:p <=>
 Cns _ (HarmDummies:) VOWHite [ %^G3:|%^StrGStem:] ;
 [Vow|ArchVow:] _ (r) (HarmDummies:) VOWHite [ %^G3:|%^StrGStem:] ;
 [Vow|Cns|ArchVow:] _ (j) [ Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^G3: | %^StrGStem: ] ;
!!kauplõma+V+Inf
!!€ kau%{pb%}%{õØ%}%{lĺ%}%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem%^Pen%^VowRM%>%{eõ%}%>m%{aä%}
!!€ kaup0l0000000%>õ%>ma

!!€ li%{pb%}õ%{back%}%^Pen%^G2
!!€ lipõ000
 [Vow|ArchVow:] _ Vow: (HarmDummies:) [:Cns+|ArchCns:] PenVOWHite  %^Pen: [ %^G3:|%^StrGStem:] ;

!"%{hØ%}:0"
!!! __@RULENAME@__
! %{hØ%}:0 <=>
! Vow _ Cns Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen:  %^WGStem: ;

"%{t́d́%}:d́"
!! **@RULENAME@**
 %{t́d́%}:d́ <=>
 Vow _ (Cns) Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen:  %^WGStem: ;

"%{t́d́%}:t́"
!! **@RULENAME@**
 %{t́d́%}:t́ <=>
 Vow _ (Cns) Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;


"%{td%}:t"
!! **@RULENAME@**
 %{td%}:t <=>
 Vow _ (%{sś%}:|s (ArchCns: )) (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;
 [Vow|Cns|ArchVow:] _ (Cns|%{sś%}:|s (ArchCns: )) [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;
!hõtsk
!kitsin
!!€ lu̬u̬dusõkai{td}sõ^Pen^VOWLower^Pen^WGStem>q
!!€ lu̬u̬dusõkaitsõ0000>q
  _ (s ) [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;
!! võitlõma+V+Inf/mA
!!€ või{td}{õØ}{lĺ}{back}^Pen^VOWRaise^Pen^StrGStem^Pen^VowRM^NoPAL>{eõ}>m{aä}
!!€ võit0l00000000>õ>ma
  _ (:Cns+|(Cns) ArchCns: ) (HarmDummies:) VOWHite   [%^StrGStem: ] ;
!! nätsk+A+Sg+Nom
!!€ nä%{td%}s%{kØ%}%{front%}%^G2
!!€ nätsk00
!! hõrts+N+Sg+Nom
!!€ hõr%{td%}so%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem%^VowRM
!!€ hõrts0000000


"%{kg%}:k"
!! **@RULENAME@**
 %{kg%}:k <=>
 Vow _ (r|l) (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^G3:|%^StrGStem:] ;
 Vow _ (HarmDummies:) VOWHite [ %^StrGStem:] ;
 [Vow|ArchVow:] _ [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^G3:|%^StrGStem:] ;
!! akaŕ+A+Sg+Nom
!!€ a%{kg%}a%{rŕ%}%{back%}%^Pen%^G2%^PAL
!!€ akaŕ0000
 Vow [ r| l | n ] _  (HarmDummies:)  [ %^StrGStem:]  ;
!!€ rän%{kg%}%{front%}%^G3
!!€ ränk00
 Vow [ r| l | n ] _ Vow (HarmDummies:) PenVOWHite %^Pen: [%^StrGStem:  ]  ;
 [Vow|ArchVow:] _ [ r| l | n ] Vow (HarmDummies:) PenVOWHite %^Pen: [%^StrGStem:  ]  ;

 Vow (%{kØ%}:)  _  (HarmDummies:)  [  %^G3: |  %^G4: ]  ;
!!€ avali%{kØ%}%{kg%}%{back%}%^G2
!!€ avali0k00

"%{kg%}:g"
!! **@RULENAME@**
 %{kg%}:g <=>
 %{kØ%}: _ (HarmDummies:) VOWHite %^G2: ;
!! apteḱ+N+Sg+Gen:
!!€ apte{kØ}{kḱg}{back}^VOWLower^G2>i
!!€ apte0g000%>i
 Vow _ [r|l] Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^WGStem: | %^G1: ] ;
!! agras+A+Sg+Nom:
!!€ a%{kg%}ra%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ agras00000
 [Vow|ArchVow:] ( l | r | n ) _ Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^WGStem: | %^G1: ] ;
!! 
!!€ hõl%{kg%}a%{sØ%}%{back%}%^Pen%^G2
!!€ hõlgas000
 [Vow|ArchVow:] ( l | r | n ) _ (HarmDummies:) ( %^VOWLower: )  [ %^WGStem: ] ;
!!€ rän%{kg%}%{front%}%^G2%{aä%}
!!€ räng00ä
 Cns _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) ( %^Pen: %^VOWLower: | %^VOWLower:)  %^Pen:|(HarmDummies:) ( %^VOWLower:) ] [%^WGStem: | %^G1: ] ;
 Vow _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) ( %^Pen: %^VOWLower: | %^VOWLower:)  %^Pen:|(HarmDummies:) ( %^VOWLower:) ] [%^WGStem: | %^G1: ] ;
  _ Cns Vow %{sØ%}: (HarmDummies:) PenVOWHite %^Pen: [%^WGStem:| %^G1: ] ;	
!      where Cx in ( %{kg%} )
!            Cy in (   g   )   matched ;

!! nõkõś+N+Sg+Ill
!!€ nõ%{kg%}õ%{sś%}%{back%}%^Pen%^WGStem%{XC%}%>õ%>he
!!€ nõgõs000s%>õ%>he

!!agras+A+Sg+Nom
!!€ a%{kg%}ra%{sØ%}%{back%}%^Pen%^WGStem
!!€ agras000


"%{td%}:d"
!! **@RULENAME@**
 %{td%}:d <=>
 [ArchVow:|Vow] _ [s|ś] (ArchCns:) (HarmDummies:)  VOWHite [%^WGStem: |%^G2: ] ;

 [ArchVow:|Vow] _ (Cns:+ |ArchCns:) [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) PenVOWHite  %^Pen: [%^G2: |%^WGStem:] ;
!kaidsõq
!leedeq
!! kaotama+V+Act+Ind+Prs+Sg1:
!!€ kao%{td%}a%{back%}%^Pen%^G2
!!€ kaoda000
   Vow (%{tØ%}:) _ [ [Vow|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] [%^G2:|%^WGStem:]  ;
   Cns _ [Vow:|ArchVow:] Cns (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ];
    _ Cns Vow %{sØ%}: (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ];
 Vow _ r Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: [ %^WGStem: | %^G1: ] ;


"%{tt́d%}:d"
!! **@RULENAME@**
 %{tt́d%}:d <=>
         [Vow|Cns] _ (HarmDummies:) ( %^VOWLower:) %^WGStem:  ;
!anõkduut́
         Vow _ [[Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ]|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kergütämä+V+Act+Ind+Prs+Sg1:
!!€ kergü%{tt́d%}ä%{front%}%^Pen%^WGStem
!!€ kergüdä000
   Vow _ [ [Vow|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] [%^WGStem: | %^G1: ];
   Cns _ [Vow:|ArchVow:] Cns (HarmDummies:) PenVOWHite %^Pen: [ %^WGStem: | %^G1: ] ;



"tToZero"
!! **@RULENAME@**
 t:0 <=> [ h | t ] _ Vow (Cns:* |i:) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
         Vow _ s (HarmDummies:) (i:) %^TS2S: ;
!! hüdsi+N+Sg+Par:
!!€ hüdsi%{front%}%^TS2S%^VowRM%>t
!!€ hü0s0000%>t
  t _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  t _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;

!!€ att%{back%}%^CC2C%>m%{aä%}
!!€ at000%>ma


"%{tØ%}:0"
!! **@RULENAME@**
 %{tØ%}:0 <=> h _ [ [Vow:|ArchVow:] (Cns:* | ArchCns:* ) (HarmDummies:) PenVOWHite %^Pen:| (HarmDummies:) VOWHite] [%^WGStem: | %^G1: ] ;
!!€ puh%{tØ%}a%{sØ%}%{back%}%^Pen%^WGStem
!!€ puh0as000
  _ [t|ArchCns:] (HarmDummies:) VOWHite [%^CC2C: | %^WGStem:|%^ShortGStem:];
  _ [t|ArchCns:]  [Vow:|ArchVow:] (ArchCns:) (HarmDummies:) PenVOWHite %^Pen: [ %^CC2C: |%^G2:|%^G3:|%^G1:| %^WGStem: | %^ShortGStem:] ;
!! ratas+N+Sg+Nom
!!€ ra%{tØ%}ta%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem%^StrGStem
!!€ ra0tas00000

!  _ t [(HarmDummies:)| [Vow:|ArchVow:] (ArchCns:*) (HarmDummies:) %^Pen:] [%^WGStem: | %^G1: | %^G2: | %^G3:  ];
  _ %{tdØ%}: [Vow:|ArchVow:] (HarmDummies:) %^Pen: [ %^G1: | %^G2: | %^G3: ] ;
!! sõda+N+Sg+Gen:
!!€ sõ%{tØ%}%{tdØ%}a%{back%}%^Pen%^G1
!!€ sõ00a000

_ [%{td%}: | %{tt́d%}: ] (HarmDummies:) [ %^G2: | %^G3:| %^G4: ] ;


"%{t́Ø%}:0"
!! **@RULENAME@**
 %{t́Ø%}:0 <=> _ [Vow:| ArchVow:] [Cns:| ArchCns:] (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;

!! ### CONSONANT QUALITY CHANGE



"%{pṕb%}:b"
!! **@RULENAME@**
 %{pṕb%}:b <=>
 Vow _ (HarmDummies:) ( %^VOWLower: ) [ %^WGStem: | %^G1: ] ;
!!€ loro%{pṕb%}%{back%}%^G2%>i
!!€ lorob00%>i

"%{pb%}:b"
!! **@RULENAME@**
 %{pb%}:b <=>
 Vow _ [r|l] [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite %^Pen: [ %^WGStem: | %^G1: ] ;
!! habras+A+Sg+Nom
!!€ ha%{pb%}ra%{sØ%}%{back%}%^Pen%^WGStem%^StrGStem
!!€ habras0000

    _ [Vow:|ArchVow:] (Cns:+ | ArchCns:+|i:) (HarmDummies:) PenVOWHite  %^Pen:  [%^G2:|%^WGStem:] ;
    _ (HarmDummies:) PenVOWHite [%^G2:|%^WGStem:] ;

         Cns _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] [%^WGStem: | %^G1: ] ;
!         [Vow|Cns] _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] [%^WGStem: | %^G1: ] ;
!!kipõń+N+Sg+Gen:
!!€ ki%{pb%}õ%{nń%}%{back%}%^Pen%^WGStem%>%{aä%}
!!€ kibõn000%>a
 _ Cns Vow %{sØ%}: (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1: ] ;

"p2b"
!! **@RULENAME@**
 p:b <=> Vow _ Vow Cns:* (HarmDummies:)  PenVOWHite %^Pen: %^WGStem: ;

"b20"
!! **@RULENAME@**
 b:0 <=> Vow _ Vow  (HarmDummies:) [PenVOWHite %^Pen: %^WGStem:|%^WGStem:] ;

"%{pbØ%}:b"
!! **@RULENAME@**
 %{pbØ%}:b <=>
     %{pØ%}: _ Vow  (HarmDummies:) PenVOWHite %^Pen: %^G2: ;
 
 
"%{dr%}:r"
!! **@RULENAME@**
 %{dr%}:r <=>
 r _   (HarmDummies:) ( %^VOWLower: )  %^WGStem:  ;
 r _  Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite %^Pen:  %^WGStem:  ;
!! murrõq+N+Sg+Nom
!!€ mur%{dr%}õ%{back%}%^Pen%^VOWLower%^Pen%^WGStem%>q
!!€ murrõ00000%>q

"%{dr%}:d"
!! **@RULENAME@**
 %{dr%}:d <=>
 r _  (HarmDummies:) ( %^VOWLower: |%^Pen:)  %^StrGStem: ;
 r _  Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite %^Pen:  %^StrGStem: ;
!! murrõq+N+Sg+Gen
!!€ mur%{dr%}õ%{back%}%^Pen%^VOWRaise%^Pen%^StrGStem
!!€ murdõ00000


"%{tdØ%}:d"
!! **@RULENAME@**
 %{tdØ%}:d <=>
     (%{tØ%}:) _ Vow  (HarmDummies:) PenVOWHite %^Pen:  %^G2: ;
     Vow _ [s|ś]  (HarmDummies:) VOWHite  %^G2: ;
 
"%{dØ%}:d"
!! **@RULENAME@**
 %{dØ%}:d <=>
     [Cns|Vow] _ [Vow:|ArchVow:] (%{sØ%}:|ArchCns:|r|l) (HarmDummies:) PenVOWHite %^Pen:  %^StrGStem: ;
!! väärdlemä+V+Inf/mA
!!€ väär{dØ}{eØ}{lĺ}{front}^Pen^VOWRaise^Pen^StrGStem^Pen^VowRM^NoPAL>{eõ}>m{aä}
!!€ väärd0l00000000>e>mä

!! kaardas+N+Sg+Nom
!!€ kaar%{dØ%}a%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^StrGStem%^StrGStem
!!€ kaardas000000
     Vow _ (HarmDummies:) VOWHite %^StrGStem: ;


"%{kgØ%}:g"
!! **@RULENAME@**
 %{kgØ%}:g <=>
     (%{kØ%}:) _ Vow  (HarmDummies:) PenVOWHite %^Pen: %^G2: ;
     (%{kØ%}:) _  (HarmDummies:) ( %^VOWLower:) %^G2: ;
!! jõgi+N+Sg+Nom: **river / joki**
!!€ jõ{kØ}{kgØ}{back}^G2>i
!!€ jõ0g00>i
!! lugõma+V+Act+Ind+Prs+Sg3
!!€ lu{kgØ}õ{back}^Pen^G2
!!€ lugõ000
 
"%{pbv%}:b"
!! **@RULENAME@**
 %{pbv%}:b <=> 
   Vow: %{pØ%}: _ (HarmDummies:) %^G2: ;
!!tõbi+N+Sg+Nom:
!!€ tõ%{pØ%}%{pbv%}%{back%}%^G2i
!!€ tõ0b00i


!! hammas
!!€ ham%{bm%}a%{sØ%}%{back%}%^Pen%^WGStem
!!€ hammas000

"%{bm%}:m"
!! **@RULENAME@**
 %{bm%}:m <=> _  (HarmDummies:) %^WGStem: ;
 m _ [Vow:|ArchVow:] (Cns:+| ArchCns:) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
!!hammas+N+Sg+Nom: __tooth/hammas__
!!€ ham%{bm%}a%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ hammas00000

"%{bm%}:b"
!! **@RULENAME@**
 %{bm%}:b <=> _  (HarmDummies:) %^StrGStem: ;
 m _ [Vow:|ArchVow:] (Cns:+| ArchCns:) (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: ;
!!hammas+N+Sg+Gen: __tooth/hammas__
!!€ ham%{bm%}a%{sØ%}%^Pen%^VOWRaise%^Pen%^G2%^WGStem
!!€ hamba000000

"%{bv%}:v"
!! **@RULENAME@**
 %{bv%}:v <=> _  (HarmDummies:) %^WGStem: ;
  _ [Vow:|ArchVow:] (Cns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) PenVOWHite %^Pen: [%^WGStem: | %^G1:  ] ;
!!närväs+N+Sg+Nom: __tooth/hammas__
!!€ när%{bv%}ä%{sØ%}%{back%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ närväs00000
     Vow: _ (HarmDummies:) (%^ÄI2ÄÄ:) %^WGStem: ;
!! leib+N+Sg+Gen: **bread/leipä**
!!€ lei{bv}{front}^ÄI2ÄÄ^WGStem>{aä}
!!€ leev000>ä



"%{dn%}:n"
!! **@RULENAME@**
 %{dn%}:n <=> # Cns* Vow n (HarmDummies:) %> _ [a|ä] q ;
 n _ (HarmDummies:) VOWHite %^WGStem: ;
 n _ [Vow:|ArchVow:] (Cns:+|ArchCns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;
!! kannõĺ+N+Sg+Nom: **kantele**
!!€ kan%{dn%}õ%{lĺ%}%{back%}%^Pen%^WGStem%^PAL
!!€ kannõĺ0000


"%{dl%}:l"
!! **@RULENAME@**
 %{dl%}:l <=> # Cns* Vow [l (HarmDummies:)|(HarmDummies:) l] %> _ [a|ä] q PenVOWHite %^Pen: %^WGStem: ;
!!€ tul%{back%}%>%{dl%}aq%^Pen%^WGStem
!!€ tul0%>laq00
!!€ kõnõl%{back%}%>%{dl%}aq%^Pen%^WGStem
!!€ kõnõl0%>laq00
    l _ (HarmDummies:) %^WGStem: ;
!!€ val%{dl%}%{back%}%^G1%>n
!!€ vall00%>n
   l _ [Vow:|ArchVow:] (Cns:+) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;


"%{dv%}:v"
!! **@RULENAME@**
 %{dv%}:v <=> [ %{üv%}: |%{uv%}: ] _ (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dv%}%{front%}%^G1%>et
!!€ püvv00%>et
!!€ lu{uv}{dv}{back}^WGStem>{aä}
!!€ luvv00>a
!!€ sü%{üv%}%{dv%}%{front%}%^G1%>ä
!!€ süvv00%>ä



!"dTot" 2021-04-08
!!! __@RULENAME@__
! d:t <=> [ s | h ] (HarmDummies:|Vow: (HarmDummies:) %^WGStem: %^VowRM:) %> _ ;
!    Vow _ (s) (Vow:) (HarmDummies:) %^StrD2T: ;
!!!käsi+N+Sg+Par __hand, arm__
!!!€ käd%{front%}%^StrD2T%>t
!!!€ kät00%>t
!!!€ hüdsi%{front%}%^StrD2T%>l
!!!€ hütsi00%>l
!    Vow _ (s) (HarmDummies:|[Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^StrGStem: ;


"dTos"
!! **@RULENAME@**
 d:s <=> Vow: _ (HarmDummies:) %^D2S: ;


"tTos"
!! **@RULENAME@**
 t:s <=> _ (HarmDummies:) %^D2S: ;

"tTod"
!! **@RULENAME@**
 t:d <=>
 Vow _ s (HarmDummies:) %^WGStem: ;
         Vow _ [[Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1: 
!!€ kaota%{back%}%^Pen%^WGStem
!!€ kaoda000

!2015-09-02 Vow [(%^StrGStem:| CnsInZero: %^WGStem:) (%>) Vow | Vow (%^WGStem:)] %> _ [%> | # | :Vow ] ;
!! There should always be a trigger



" %{dn%}:d"
!! **@RULENAME@**
  %{dn%}:d <=> n _ Vow: (l) (HarmDummies:) PenVOWHite  %^Pen: %^StrGStem:  ;

!!€ kan%{dn%}õl%{back%}%^Pen%^G2%^Pen%^VowRM%>õ
!!€ kand0l00000%>õ

 n _  (HarmDummies:) VOWHite %^StrGStem: ;

"j2i"
!! **@RULENAME@**
 j:i <=> :Vow (HarmDummies:) %> _ [ # | %- ];

"{kḱg}:g "
!! **@RULENAME@**
 %{kḱg%}:g <=> _ (HarmDummies:) VOWHite [%^WGStem: | %^G1: ] ;

!! kõiḱ+Pron+Sg+Gen
!!€ kõi%{kØ%}%{kḱg%}%{back%}%^G1%>%{eõ%}
!!€ kõi0g00%>õ


"k2g"
!! **@RULENAME@**
 k:g <=> [Vow|Cns - [h|k|s]] _ [(HarmDummies:)|Vow Cns:+ (HarmDummies:)  %^Pen: ] %^G1: ;
!!€ hõrak%{back%}%^G1%>a
!!€ hõrag00%>a
!!$ hõrak%{back%}%^G1%>a
!!$ hõrak00%>a
!!€ kisk%{back%}%^G1%>%{uü%}%>t%{aä%}v
!!€ kis000%>u%>tav

!!€ süküs%{front%}%^Pen%^WGStem%>e
!!€ sügüs000%>e



!"gTok"
!!! **@RULENAME@**
! g:k <=>
!    Vow _ (HarmDummies:) %^StrGStem:  ; 
!    Vow _ [Vow:|ArchVow:] ( l | r ) (HarmDummies:) %^Pen: %^StrGStem: ;
!    Vow _ Vow: (HarmDummies:) %^StrGStem: ;

!   Vow _ [Vow:|ArchVow:] [ l | r ] (HarmDummies:) %^StrGStem: %^Pen: %^VowRM: ;
!   [t ] (VowInZero:) (HarmDummies:) (%^VowRM:) %> _ u ;
!! igä+N+Sg+Ill
!!€ i{kØ}{kgØ}ä{front}^Pen^G4
!!€ ikkä00
!!€ ko%{kg%}õr%{back%}%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r0000%>õ


"bTop"
!! **@RULENAME@**
 b:p <=> 
    Vow _ Vow: (HarmDummies:) %^StrGStem: ;

"%{pbv%}:p"
!! **@RULENAME@**
 %{pbv%}:p <=>
   Vow: %{pØ%}: _ (HarmDummies:) %^G4: ;
!!tõbi+N+Sg+Ill:
!!€ tõ%{pØ%}%{pbv%}%{back%}%^G4%>%{eõ%}
!!€ tõpp00%>e


"%{pbØ%}:p"
!! **@RULENAME@**
 %{pbØ%}:p <=> 
    Vow ( %{pØ%}: ) _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^StrGStem: ;
    %{pØ%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [ %^G3: | %^G4: ] ; 

"%{tdØ%}:t"
!! **@RULENAME@**
 %{tdØ%}:t <=>
  Vow ( %{tØ%}: ) _ (s) [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: [%^G4:|%^G3:] ;
  Vow ( %{tØ%}: ) _ (s) (HarmDummies:) VOWHite [%^G4:|%^G3:] ;
    Vow %{tØ%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^G4:|%^G3:] ;
    %{tØ%}: _ [Vow:|ArchVow:] (HarmDummies:)  %^Pen: [%^G3: | %^G4: ];

"%{kgØ%}:k"
!! **@RULENAME@**
 %{kgØ%}:k <=>
  Vow ( %{kØ%}: ) _ [[Vow:|ArchVow:] (HarmDummies:) VOWHite %^Pen:|(HarmDummies:) VOWHite ] %^StrGStem: ;
    Vow %{kØ%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^G4: ;
   Vow: %{kØ%}: _ (HarmDummies:) [ %^G4: | %^G3: ] ;
!!mägi+N+Sg+Ill:
!!€ mä%{kØ%}%{kgØ%}%{front%}%^G4%>%{eõ%}
!!€ mäkk00%>e
    %{kØ%}: _ [Vow:|ArchVow:] (HarmDummies:)  %^Pen: [%^G3: | %^G4: ];

!! ### STEM-FINAL CONSONANT LOSS
"s20"
!! **@RULENAME@**
 s:0 <=> [ u: | ü: | a: | ä: ] _ (HarmDummies:) (%^VowRM:)  %^CnsRM:  ;
 ![ u: | ü: | a: | ä: ] _ (HarmDummies:) (%^VowRM:)  %^WGStem:  ;
!! kirotus+N+Pl+Gen:
!!€ kirotus%{back%}%^CnsRM%>isi
!!€ kirotu000%>isi

         s _ %^TS2S: ;
!! usś+N+Sg+Par **door**
!!€ uss%{back%}%^TS2S%>t
!!€ us000%>t

  s _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
!FIXME 2019-05-08  s _  [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!FIXME
!! vaśma+V+Inf/mA
!!€ va{sØ}{sś}{back}^VOWRaise^WGStem^PAL>m{aä}
!!€ va0ś0000>ma

"%{bv%}:b"
!! **@RULENAME@**
 %{bv%}:b <=> 
   [ r| l | n | i] _ [ Vow: | ArchVow: ] ( ArchCns: | ArchCns: Cns ) (HarmDummies:) PenVOWHite  %^Pen: %^StrGStem: ;
!! närväs+A+Sg+Gen:
!!€ när%{bv%}ä%{sØ%}%{front%}%^Pen%^VOWLower%^Pen%^StrGStem%^WGStem
!!€ närbä0000000
  Vow [ r| l | n | i] _  (HarmDummies:)  [ %^G4: | %^G3: | %^G2: |%^StrGStem:]  ;
 Vow _ (s (ArchCns: )) (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ (HarmDummies:) VOWHite [ %^StrGStem:] ;
 Cns _ Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;
 [Vow|Cns|ArchVow:] _ (Cns|s (ArchCns: )) [Vow:|ArchVow:] (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;
  _  Vow: (:Cns+|ArchCns: ) (HarmDummies:) PenVOWHite  %^Pen: [ %^StrGStem: ] ;




"%{gØ%}:g"
!! **@RULENAME@**
 %{gØ%}:g <=> 
    _ (HarmDummies:) VOWHite %^StrGStem: ;
!! liig+A+Sg+Nom:
!!€ lii%{gØ%}%{back%}%^StrGStem
!!€ liig00
    _ Vow: (:Cns+|ArchCns:) (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: ;
!! huug

  Vow [ r| l | n ] _  (HarmDummies:)  [ %^G4: | %^G3: | %^G2: |%^StrGStem:]  ;

"d20"
!! **@RULENAME@**
 d:0 <=>
         Vow _ s ([Vow:|ArchVow:]) (HarmDummies:) %^TS2S: ;
!!€ hüdsi%{front%}%^TS2S%^VowRM%>te
!!€ hü0s0000%>te
!2019-06-02          Vow: _ [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;

"%{dØ%}:0"
!! **@RULENAME@**
 %{dØ%}:0 <=>
          _ [Vow:|ArchVow:] (ArchCns: |Cns:+|:Cns+ ) (HarmDummies:) PenVOWHite %^Pen: %^WGStem: ;


"g20"
!! **@RULENAME@** deprication to {gǵØ}:0
 g:0 <=> [Cns: - [n:]|Vow:]  _ (HarmDummies:)  %^WGStem: ;
!!€ aig%{back%}%^WGStem%>o
!!€ a0000%>o
   [Cns: - [n:]|Vow:] _ [[Vow:|ArchVow:] (HarmDummies:)|(HarmDummies:) %{eõ%}:] PenVOWHite %^Pen: %^WGStem: ;
!! argnõma+V+Inf
!!€ ar%{gØ%}{back}{eõ}^Pen^WGStem>d{aä}q
!!€ ar00õ00>daq

"%{gØ%}:0"
!! **@RULENAME@**
 %{gØ%}:0 <=> [Cns: - [n:]|Vow:]  _ (HarmDummies:)  %^WGStem: ;
!!€ aig{back}^WGStem>o
!!€ a0000>o
   [Cns: - [n:]|Vow:] _ [[Vow:|ArchVow:] (Cns:|ArchCns:) (HarmDummies:)|(HarmDummies:) %{eõ%}:] PenVOWHite %^Pen: %^WGStem: ;
!! igä+N+Sg+Gen
!!€ i{kØ}{kgØ}ä{front}^Pen^G1
!!€ i00ä000
!! lugõma+V+Pss+PrfPrc **read/lukea**
!!€ lu{kgØ}õ{back}^Pen^VOWLower^Pen^WGStem^VowRM>e>t
!!€ lo00000000>e>t
!! argnõma+V+Inf
!!€ ar{gØ}{back}{eõ}^Pen^WGStem>d{aä}q
!!€ ar00õ00>daq


!2017-02-03 Vow l _ Vow (HarmDummies:) %^WGStem: ;
!!€ palgõ%{back%}%^WGStem%>h
!!€ pal0õ00%>h

"{gǵØ}:0"
!! **@RULENAME@**
 %{gǵØ%}:0 <=> [Cns: - [n:]|Vow:]  _ (HarmDummies:)  %^WGStem: ;
!! särǵ+N+Sg+Gen: **roach/särki**
!!€ sär{gǵØ}{front}^WGStem^NoPAL>%{eõ%}
!!€ sär0000>e

"{gǵØ}:g"
!! **@RULENAME@**
 %{gǵØ%}:g <=> n _ (HarmDummies:)  %^WGStem: %^NoPAL: ;
              [Cns|Vow] _ (HarmDummies:) VOWHite %^StrGStem: %^NoPAL: ;
!! särǵ+N+Sg+Ill: **roach/särki**
!!€ sär{gǵØ}{front}^StrGStem^NoPAL>%{eõ%}
!!€ särg000>e
     [Cns|Vow] _ Vow: (HarmDummies:) PenVOWHite %^Pen: %^StrGStem: %^Pen: %^NoPAL: ;


"%{pbv%}:v"
!! **@RULENAME@**
 %{pbv%}:v <=> 
   Vow: %{pØ%}: _ (HarmDummies:) %^G1: ;
   Vow: %{pØ%}: _ (HarmDummies:) %^WGStem: ;
!!tõbi+N+Sg+Gen:
!!€ tõ%{pØ%}%{pbv%}%{back%}%^G1%>%{eõ%}
!!€ tõ0v00%>õ


"%{pbØ%}:0"
!! **@RULENAME@**
 %{pbØ%}:0 <=>
     %{pØ%}: _ Vow  (HarmDummies:) PenVOWHite %^Pen: %^G1: ;
! Vow ( %{pØ%}: ) _ Vow  (HarmDummies:) [PenVOWHite %^Pen: %^WGStem:|%^WGStem:|%^G1:] ;
 

"%{tdØ%}:0"
!! **@RULENAME@**
 %{tdØ%}:0 <=>
         Vow _ s [ [Vow:|ArchVow:] (HarmDummies:) PenVOWHite %^Pen:| (HarmDummies:) VOWHite] %^G1:  ;
!!€ hüdsi%{front%}%^TS2S%^VowRM%>te
!!€ hü0s0000%>te
          Vow:  _ [[Vow:|ArchVow:] (HarmDummies:) (PenVOWHite|VOWHite) %^Pen:|(HarmDummies:) VOWHite ] %^G1: ;
          Vow: %{tØ%}:  _ [[Vow:|ArchVow:] (HarmDummies:) (PenVOWHite|VOWHite) %^Pen:|(HarmDummies:) VOWHite ] %^G1: ;


"%{kgØ%}:0"
!! **@RULENAME@**
 %{kgØ%}:0 <=> 
   Vow: _ [[Vow:|ArchVow:] (HarmDummies:) ((%^Pen:) %^VOWLower:) %^Pen:| (HarmDummies:) VOWHite ] %^WGStem: ;
    Vow: %{kØ%}: _ (HarmDummies:) %^G1: ;
!!mägi+N+Sg+Gen: __hill/mäki__
!!€ mä%{kØ%}%{kgØ%}%{front%}%^G1%>%{eõ%}
!!€ mä0000%>e

   %{kØ%}: _ [Vow:|ArchVow:] (HarmDummies:) ((%^Pen:) %^VOWLower:) %^Pen: %^G1: ;

!!€ u%{jØ%}jo%{back%}%^Pen%^WGStem%>m%{aä%}
!!€ u0jo000%>ma


"Disallow %^ErrorBack:0 in BHARM"
%^ErrorBack: /<= BHARM _ ;





!!TESTS
!echo 'v a s t a l i {back}:0 > n {eõ}:õ' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'vallalinõ+A+Sg+Nom' | hfst-lookup src/morphology/lexicon.hfst
!echo 'k a u {pb}:p {õØ}:0 {lĺ}:l {back}:0 ^Pen:0 ^VOWRaise:0 ^Pen:0 ^StrGStem:0 ^Pen:0 ^VowRM:0 > {eõ}:õ > m {aä}:a ' | hfst-pair-test src/fst/phonology.compose.hfst

!OLD


!! püüdmä+V+Act+Ind+Prs+Sg3
!!€ pü%{üv%}%{dd́v%}%{front%}%^PAL
!!€ püüd́00


!! pereq
!!€ pe%{rØ%}re%{front%}%^WGStem%>h
!!€ pe0re00%>h

!! naŕma
!!€ na%{rØ%}%{rŕ%}%{back%}%^CC2C%^PAL%>m%{aä%}
!!€ na0r000%>ma


!! ### Other marks
!FIXME
!!€ ki%{pb%}õ%{nń%}%{back%}%>a
!!€ kipõn0%>a

!"mToZero"
!! **@RULENAME@**
! m:0 <=> m_ [Vow:|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;

!"vToZero"
!! **@RULENAME@**
! v:0 <=> v_ [Vow:|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;

