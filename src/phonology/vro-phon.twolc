! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ś

! Vowel harmony with "(t)a/ä"
AÄ1:a
AÄ1:ä
AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o
OÖ1:ö

! Vowel harmony with "ttu/tty"
UY1:u
UY1:y

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0

! The ti => si
%^TS:0

! Vowel raising
%^RVws:0

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>:0
#:0   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VwsBack = a o u õ ;
VwsFront = ä ö y ü ;
VwsNeutral = e i ; 
VwsNonHigh = a o ä ö e ;
Vws = a o u ä ö y ü e i õ ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

Letters = Vws Cns ;



Definitions

! Front Trigger
FT =  [ #: Cns* [ VwsFront | VwsNeutral | i: ] | :VwsFront ] \[ #: | VwsBack: | :VwsBack ]*  ;


! Back Trigger 
BT =   ( #: Cns* ( [ VwsNeutral | i: ] \[ #: | VwsFront: | :VwsFront ]* ) ) [ VwsBack: | :VwsBack ]  \[ #: | VwsFront: | :VwsFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vws : Cns:+ | Cns* Vws+ Cns:+  Vws:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %>:0 ) AÄ1: | OÖ1: | UY1: | Vws: | :Vws ]  ( %>:0 i ) |  %>:0  i ] %>:0 %^WCns:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vws [ ( %>:0 %^LCns:0 %^LVws:0 | %>:0 %^LCns:0 | %>:0 i %>:0 | %>:0 ) [ V1:Vws | AÄ1: ] |  0:Vws %>:0 ( %^LCns:0 | %^WCns: ) %^LVws: | %>:0 %^LCns:0  ] ] ;

!! !!!Rules

Rules


! Stem vowel loss before plural "i"
"StemVowLoss"
!! __@RULENAME@__  
 X:Y <=> X _  %>:0 i  %>:0 ;
!( %>:0  %^WCns:0 |  %>:0  %^LCns:0 ) %>:0 [ i | i:j ] ;
          where X in (e o ö)
                Y in (0 0 0) matched ;

! A loss
"StemALoss"
!! __@RULENAME@__  
 X:Y <=>  _   ( %>:0 %^LCns:0 ) %>:0 i: %>:0 ;
          where X in (a ä)
                Y in (0 0) matched ;

!€ osa%>^WCns%>i%>st
!€ os0000i0st

!%>%^LCnsi%>V1
! AÄ1 loss
"StemAÄLoss"
!! __@RULENAME@__  
 AÄ1:0 <=> [ AÄ1: | Cns: | :Cns ] _ ( %>:0 %^LCns:0 ) %>:0 i %>:0 ;


! U loss [u:0 | y:0]
"StemULoss"
!! __@RULENAME@__  
 X:Y <=> X _ %>:0  i  %>:0 ;
! ( %>:0  %^WCns:0 |  %>:0  %^LCns:0 ) %>:0 i %>:0 ;
         [ e | ä | a | o | ö ] _  [ t: | k: ] 0:v ;
          where X in (u y ü)
                Y in (0 0 0) matched ;
!Vws:0 %>:0  %^WCns:0 %>:0 i: %>:0 

!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
!! __@RULENAME@__  
 i:0 <=> i _ %>:0 i  %>:0 ;

! iToj between vowels
"i:j"
!! __@RULENAME@__  
 i:j <=> :Vws ( %>:0  %^LCns:0 ) %>:0 _ %>:0 :Vws ;
         Vws _ Cns:0 Vws: ;
!veitte%>%^WCnsttUY1%>%^LCns%>i%>AÄ1
!€ em0o0i%>%^LCnsAÄ1
!€ emmooj00a

! a to e in comparatives
"a:e"
!! __@RULENAME@__  
 a:e <=> _ %>:0 %^WCns:0 %^E1:0 ;

! ä to e in comparatives
"ä:e"
!! __@RULENAME@__  
 ä:e <=> _ %>:0 %^WCns:0 %^E1:0 ;


!
! Vowel harmony suffixes Front
"AÄ1:ä"
!! __@RULENAME@__  
 AÄ1:ä <=> FT _  ( %>:0  %^WCns:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:0  %^LCns:0 ( %^LVws:0 ) ) [ ( %>:0 ) [ Cns | %>:0 %^WCns:0 [ Cns ] | [[ Vws: ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 | i | %>:0 ( %^WCns:0 | %^LCns:0 ) [ V1: | V3: ] ]  ;

! mato : mattooa
! Vowel harmony suffixes Back
"AÄ1:a"
!! __@RULENAME@__  
 AÄ1:a <=> BT _   ( %>:0  %^WCns:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:0  %^LCns:0 ( %^LVws:0 ) ) [ ( %>:0 ) [ Cns | %>:0 %^WCns:0 [ Cns ] | [[ Vws: ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 | i | %>:0 ( %^WCns:0 | %^LCns:0 ) [ V1: | V3: ] ]  ;

!
! Vowel harmony suffixes Front
"OÖ1:ö"
!! __@RULENAME@__  
 OÖ1:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"OÖ1:o"
!! __@RULENAME@__  
 OÖ1:o <=>  BT _ ;

!
! Vowel harmony suffixes Front
"UY1:y"
!! __@RULENAME@__  
 UY1:y <=>  FT _ ;

! Vowel harmony suffixes Back
"UY1:u"
!! __@RULENAME@__  
 UY1:u <=>  BT _ ;

! Vws %>:0 %^LCns: V1:
! Vowel agreement in illative 
"V1:aeouüäö"
!! __@RULENAME@__  
 V1:Vx <=> :Vx %>:0 [ h | ( %^LCns: %>:0 ) ] _ ;
        where Vx in (a o u ä ö ü y) ;

! Vowel agreement in illative e
"V1:e"
!! __@RULENAME@__  
 V1:e <=> [ e | i ] %>:0  h  _ ;
          e ( %>:0 %^LCns: ) %>:0  _ ;

! Illative i with no intermittent _h_
"V1:i"
!! __@RULENAME@__  
 V1:i <=> i ( %>:0 %^LCns: ) %>:0 _ ;

!  _mato:maool_ not allowed
! Used with patsas type nominals
"V2:aeiouüäö"
!! __@RULENAME@__  
 V2:Vx <=>  Vx ( h ( :h ) ) %>:0  ( %^LCns:0 )  _ [ V2: | ( %>:0 ([ %^WCns:0 | %^LCns:0 ] ( V3: ) ) )  Cns ] ;
            Vx ( h ( :h ) ) %>:0  ( %^LCns:0 | %^WCns:0 ) V2: _ [ ( %>:0 )  Cns ]  ;
        where Vx in (a o u ä ö ü e i y) ;


"V2:Zero"
!! __@RULENAME@__  
 V2:0 <=> Vws %>:0 _ %>:0 i: %>:0 ;

!FROM HERE

! Vowel lengthening for the inesive and adessive with V3
!! This deals with secondary or perhaps pertary vowel lengthening
! e.g. gen plural, ine, ade
"V3:a"
!! __@RULENAME@__  
 V3:a <=> :Cns :0* :a :0* _ ;

"V3:e"
!! __@RULENAME@__  
 V3:e <=> :Cns :0* :e :0* _ ;

"V3:i"
!! __@RULENAME@__  
 V3:i <=> :Cns :0* :i :0* _ ;

"V3:o"
!! __@RULENAME@__  
 V3:o <=> :Cns :0* :o :0* _ ;

"V3:u"
!! __@RULENAME@__  
 V3:u <=> :Cns :0* :u :0* _ ;

"V3:ä"
!! __@RULENAME@__  
 V3:ä <=> :Cns :0* :ä :0* _ ;

"V3:ö"
!! __@RULENAME@__  
 V3:ö <=> :Cns :0* :ö :0* _ ;

"V3:y"
!! __@RULENAME@__  
 V3:y <=> :Cns :0* :y :0* _ ;

"V3:Zero"
!! __@RULENAME@__  
 V3:0 <=> :Vws :0* :Vws :0* _ ;

! ! Vowel Lengthening
! mato : mattoon,
 "Zero:Vx"
!! __@RULENAME@__  
 0:Vx <=> ( Vws+ :Cns+ ) Vx _ %>:0 ( %^LCns: | %^WCns:0 ) %^LVws: ;
          #: Cns* Vws [ Cns 0: | 0:i j ] Vx _ Cns ( Cns:0 ) :Vws %^StretchSyll2:0 ; ! ommeena<omena
! 
         [ #: ] Cns* Vws Cns 0: Vx _ Cns :0 Vws [ Cns | [ #: ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
!! __@RULENAME@__  
 0:Cx <=> 
         [ #: Cns* Vws+ Cns:+ Vws+ :0* Cns+ :Vws ( %>:0 %^LCns:0 ) | #: Cns*  Vws | %^OddSyll:0 ] Cx _ ( [ Vws: | :Vws ] ( :Vws ) ) %>:0 %^LCns:0 ; !
         #: Cns* Vws Cx _ [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen
           where Cx in (m p t s n r l k g h) ;
!
! oja: oija
"j lengthening"
!! __@RULENAME@__  
0:i <=> [ #: ] Cns* Vws _ j Vws ( 0: ) %>:0 %^LCns:0  ;
          [ [ :Cns | :Cns Vws:0 ] ( %>:0 ) | Cns: ] i _ %>:0 ( %^LCns: | %^WCns:0 ) %^LVws: ;
         [ #: ] Cns* Vws Cns 0: i _ Cns :0 Vws [ Cns | [ #: ] ] ;
         #: Cns* Vws _ j [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen, kaijaava


! TO HERE
!
!! Consonant weakening 
!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mp:mm, nt:nn, st:ss,
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv



"Ut:vv Uk:vv weakening"
!! __@RULENAME@__  
 Cx:Cy <=> [ #: ] Cns* Vws [ u: | y: ] _ 0:v [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;
              where Cx in (k t)
                    Cy in (v v) matched ;

"0:v Lengthening with Ut:vv weakening"
!! __@RULENAME@__  
 0:v <=> [ #: ] Cns* Vws [ u: | y: ] [ t: | k: ]  _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;
         [ #: ][ Cns* Vws | Cns* Vws+ Cns:+ Vws+ :0* Cns+ :Vws ] v _  ( :Vws | AÄ1: ) [ 0:Vws %>:0 %^LCns: | %>:0 %^LCns: :0* :Vws ] ; ! arvattava:arvattavvaa
        #: Cns* Vws v _ Vws [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! kevät:kevväen, tavvaara




! viis: viijes
"it:j"
!! __@RULENAME@__  
 t:j <=> [ i | o ] i _ [ e | o ] %>: %^WCns:0 ;

"st:ss weakening"
!! __@RULENAME@__  
t:s <=> Vws+ s _ ( %^TS:0 ) GRC ;
! last-minute verbs
! "ti:si in verbs"
   [ n | l | r | Vws Vws ] _ %^TS:0 ; !  pittää: piti 


"rt:rr weakening"
!! __@RULENAME@__  
t:r <=>  Vws r _ GRC ;

"lt:ll weakening"
!! __@RULENAME@__  
t:l <=>  Vws l _ GRC ;

"nt:nn weakening"
!! __@RULENAME@__  
t:n <=>  Vws ( %>:0 %^WCns:0 ) n _ GRC ;

!! p

"mp:mm weakening"
!! __@RULENAME@__  
p:m <=> [ :Vws ( %>:0 %^WCns:0 ) | [ a: | ä: ] %>:0 %^WCns:0 %^E1:0 | AÄ1: | Vws+ :Cns+ Vws  %^SyllBr:0 e %^StretchSyll2:0 ] m _ [ GRC | AÄ1: %>:0 %^WCns:0 ( V3: ) Cns | %^TS:0 i %>:0 %^WCns:0 ] ;
        m _ Vws l %^WGStem:0 ; ! ompel- to ommel-
! [ ( %>:0 ) :Vws ( 0: ) %>:0 %^WCns:0 | Vws:0 %>:0 i: %>:0 %^WCns:0 | Vws:0 %>:0  %^WCns:0 %>:0 i: %>:0 ] ;
!%> %^WCns %^E1 mp


"nk:ng weakening"
!! __@RULENAME@__  
k:g <=> [ #: ] Cns* Vws n _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;


! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> [  #:  Cns* Vws (( r | i | VwsNonHigh ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;
![ [ #: ] Cns* Vws (( r | i ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ Vws: | :Vws | ( Vws ) %>:0 i ] ( %>:0 i: | %^StretchSyll2:0 ) %>:0 %^WCns:0 ;
        [ #: ] Cns* VwsBack i: _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;

! liika:liijan, reikä:reijän
"kToj"
!! __@RULENAME@__  
 k:j <=> [ #: ] Cns* [ VwsFront | VwsNeutral ] i: _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;

! mato : maot, ! mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
!! __@RULENAME@__  
t:0 <=> [ #: Cns* Vws | #: Cns* Vws [ Vws - [ i | u | y ] ] | Vws h [ y | u ] ] _  GRC ;
        [ #: Cns*  Vws+ [ h | (r) t ] | Vws ( %>:0 %^WCns:0 ) t | Cns* Vws+ Cns+ Vws+ ( n ) t | Vws t ] _  [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;
        t _ Vws l %^WGStem:0 ; ! ottel- to otel-

! lupa : luvan
"p:v"
!! __@RULENAME@__  
p:v <=> [ #: ] Cns* Vws ( r | l | Vws ) _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;

"double pp to p"
!! __@RULENAME@__  
p:0 <=> [ #: Cns* Vws ( m ) p | #: Cns* Vws Vws p | Cns* Vws+ Cns+ Vws+ p ] _ [ GRC | %^TS:0 i %>:0 %^WCns:0 ] ;

!! Vowel raising
"o:u ö:y in Inf"
!! __@RULENAME@__  
 Vx:Vy <=> _ Vx: %>:0 v v AÄ1: %^RVws:0 ;
           Vx: _ %>:0 v v AÄ1: %^RVws:0 ;
          where Vx in (o ö)
                Vy in (u y) matched ;


!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>:0 _ ;

!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vws :0* :Vws :0* _ ;

