! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ǵ ḱ ĺ ń ś  ʼ

! Vowel harmony with "(t)a/ä"
 %{AÄ%}:0 	!!= @CODE@ Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
 %{OÖ%}:0 	!!= @CODE@ Vowel harmony with oö OÖ1:o OÖ1:ö

! Vowel harmony with "ttu/tty"
 %{UÜ%}:0 	!!= @CODE@ Vowel harmony with uü UY1:u UY1:y

 %{ÕE%}:0       !!= @CODE@ Vowel harmony with õe

 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel

 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^SGStem:0 !!= @CODE@ This strengthens "perädü" to "perätüt"



! The ti => si
%^TS:0


%^RVow:0 !!= @CODE@ Raises vowel
%^LowerVow:0 !!= @CODE@ Lowers vowel

%^VowRM:0 !!= @CODE@ this will remove stem final vowel
%^CnsRM:0 !!= @CODE@ this will remove stem final consonant tervüs:tervü

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ ;
VowFront = ä ö y ü ;
VowNeutral = e i ; 
VowNonHigh = a o ä ö e ;
Vow = a o u ä ö y ü e i õ ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

Letters = Vow Cns ;

VowDummies = %^WGStem %> ;

Definitions

! Front Trigger
FT =  [ # Cns* [ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   ( # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %> ) %{AÄ%}: | %{OÖ%}: | %{UÜ%}: | Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vow [ ( %> %^LCns:0 %^LVow:0 | %> %^LCns:0 | %> i %> | %> ) [ V1:Vow | %{AÄ%}: ] |  0:Vow %> ( %^LCns:0 | %^WCns: ) %^LVow: | %> %^LCns:0  ] ] ;

!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front
"%{AÄ%}:ä"
!! __@RULENAME@__  
 %{AÄ%}:ä <=> FT %> [ :j | m |(s i) v | t | d ] _   ;
              FT  :Cns (ʼ:) (%^WGStem:) %> _ ;

!! !Vowel harmony suffixes Back
"%{AÄ%}:a"
!! __@RULENAME@__  
 %{AÄ%}:a <=> BT %> [ :j | m |(s i) v | t | d ] _   ;
              BT  :Cns (ʼ:) (%^WGStem:) %> _ ;

!
! Vowel harmony suffixes Front
"%{UÜ%}:ü"
!! __@RULENAME@__  
 %{UÜ%}:ü <=>  FT _ ;

! Vowel harmony suffixes Back
"%{UÜ%}:u"
!! __@RULENAME@__  
 %{UÜ%}:u <=>  BT _ ;

! Vowel harmony suffixes Front
"%{ÕE%}:e"
!! __@RULENAME@__
 %{ÕE%}:e <=>  FT _ ;

! Vowel harmony suffixes Back
"%{ÕE%}:õ"
!! __@RULENAME@__
 %{ÕE%}:õ <=>  BT _ ;


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>  u: _ %^LowerVow: ;
    _ u: %^LowerVow: ;

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>  ü: _ %^LowerVow: ;
    _ ü: %^LowerVow: ;

"u2õ"
!! __@RULENAME@__
 u:õ <=> u: _ %> i ;

"ü2e"
!! __@RULENAME@__
 ü:e <=> ü: _ %> i ;


!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
 a:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 

"ä:0"
!! __@RULENAME@__
 ä:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 

"e:0"
!! __@RULENAME@__
 e:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 

"u20"
!! __@RULENAME@__
 u:0 <=> _ u: %> i ;

"ü20"
!! __@RULENAME@__
 ü:0 <=> _ ü: %> i ;

!!

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> :u %- _ [i q|s] ;

"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a %- _ [i q|s] ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow _ (%^WGStem:) %> :Vow ;

"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v %{AÄ%}: q ;

"ü2v"
!! __@RULENAME@__
 ü:v <=> ü _ %> v %{AÄ%}: q ;




!! Secondary Consonant lengthening

"0:l"
!! __@RULENAME@__
 0:l <=> Vow l _ :Vow %> j:i ;

"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> s _ ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> k _ ;


!! !Consonant weakening 



! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> s _ a %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ Vow: | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;

"ll2l"
!! __@RULENAME@__
 l:0 <=> l _ Vow i: %^WGStem: ;


"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow [ n (ʼ:) ] %^WGStem: ;


!! !CONSONANT QUALITY CHANGE

"j2i"
!! __@RULENAME@__
 j:i <=> :Vow %> _ [ # | %- ];

"k2g"
!! __@RULENAME@__
 k:g <=> Vow _ %^WGStem: ;
!€ hõrak%^WGStem>%{AÄ%}
!€ hõrag00a



"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] %> _ ;
     _ Vow %^SGStem: ;

"tTod"
!! __@RULENAME@__
 t:d <=> Vow [(%^SGStem:| Cns:0 %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ ;

!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u | ü | a: | ä: ] _ (%^VowRM:)  %^CnsRM:  ;


!! !Other marks
"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=>  :Cns _ (%^WGStem:) %> :Vow ;
!€ kipõnʼ>%{AÄ%}
!€ kipõn00a
