! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ǵ ḱ ĺ ń ś  ʼ

!! special letters
u̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
U̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
Ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
i̬	 !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
I̬	 !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
õ̭	 !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
Õ̭	 !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
ˋ	 !!≈ * __@CODE@__ U+02CB MODIFIER LETTER GRAVE ACCENT other parts of stem
%{ˋØ%}:0 !!≈ * __@CODE@__ U+02CB MODIFIER LETTER GRAVE ACCENT used in rules

! Vowel harmony with "(t)a/ä"
 %{aä%}:0 	!!= @CODE@ Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0
 %{eõ%}:0 	!!= @CODE@ Vowel harmony with "e/õ"
 %{uü%}:0 	!!= @CODE@ Vowel harmony with "u/ü"


 
 %{dØ%}:d 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{tØ%}:t 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dv%}:d 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{uv%}:u 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{üv%}:ü 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na

 %{hØ%}:h 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{jØ%}:j 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kØ%}:k 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lØ%}:l 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{mØ%}:m 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{nØ%}:n 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{rØ%}:r 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sØ%}:s 	!!= @CODE@ HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{vØ%}:v 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{bv%}:b 	!!= @CODE@ HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{dr%}:d 	!!= @CODE@ HJK and KimmoK ideas parras:par%{dr%}a%{sØ%}
 %{bm%}:b 	!!= @CODE@ HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}
 %{dn%}:d 	!!= @CODE@ HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}


 %{pb%}:p 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{td%}:t 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kg%}:k 	!!= @CODE@ HJK and KimmoK ideas kaigas:kai%{kg%}as

 %{pbØ%}:b 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{tdØ%}:d 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kgØ%}:g 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na



 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel

 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant

 %^I7:0         !!= @CODE@ This appears in stem vaoma:va%^I7o for vaio
 %^K7:0         !!= @CODE@  This appears in stem väemä:vä%^K7e for väkeq
 %^V7:0         !!= @CODE@ This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0         !!= @CODE@ This appears in stem kaoma:ka%^T7o for katoq

%^Y7:õ  !!= __@CODE@__  This appears for syna = s%^Y7na and is rendered as õ in the norm

!! Triggers
 %^Õ2E:0	 !!= @CODE@ lugõ%^Pen%^WGStem%^Õ2E loe
 %^OO2Õ:0	!!= @CODE@  joo%^OO2Õ%>i:j0õ0%>i
 %^CC2C:0 	!!= @CODE@ att%^CC2C%>m%{aä%} atma

 %^ÄI2ÄÄ:0 	!!= @CODE@ päiv%^ÄI2ÄÄ%>ä: päävä
%{front%}:0	!!= @CODE@ front harmony
%{back%}:0	!!= @CODE@ back harmony

!
%^ErrorBack:0	!!= @CODE@ +Err/Orth+Clt:%>kinaq in front harmony context BHARM disallowance


! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! puhksama but puhastaq
%^APOCH:0 !!= @CODE@ This causes apochope

! 2015-03-26
%^StrD2T:0 !!= @CODE@ This changes g,d,b => k,t,p

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^StrGStem:0 !!= @CODE@ This strengthens "perädü" to "perätüt"

%^XStrGStem:0 !!= @CODE@ This extra-strengthens "sõda" to "sõtta"

%^Pen:0	      !!= @CODE@ This moves us to penultimate coda

%^PenWGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^PenVowRM:0 !!= @CODE@ syncope tapõld : taplõma 

%^D2S:0 !!= @CODE@ The ti => si
%^TS2S:0 !!= @CODE@ The -ts- => -s-

%^ʼ2Õ:0 !!= @CODE@ The ʼ => õ change
%^ʼ2E:0 !!= @CODE@ The ʼ => e change
%^ʼ2I:0 !!= @CODE@ The ʼ => i change
%^I2ʼ:0 !!= @CODE@ The i => ʼ change
%^I2J:0 !!= @CODE@ The i => j change

%^PLPRT:0 !!= @CODE@ The a:o attested in Plural kana:kanno and prt

%^VOWRaise:0 !!= @CODE@ Raises vowel
%^VOWLower:0 !!= @CODE@ Lowers vowel
%^XLowerVow:0 !!= @CODE@ Lowers vowel two levels
%^VOWLowerDelab:0 !!= @CODE@ Lowers vowel and delabializes it
%^XLowerVowDelab:0 !!= @CODE@ Lowers vowel two levels and delabializes it

%^U2E:0 !!= @CODE@ lowers u:õ and ü:e delabializes and lowers
%^U2A:0 !!= @CODE@ lowers u:a and ü:ä delabializes and lowers

%^VowRM:0 !!= @CODE@ this will remove stem final vowel
%^CnsRM:0 !!= @CODE@ this will remove stem final consonant tervüs:tervü

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y õ̭ u̬ ;
VowFront = ä ö ü ü̬ ;
VowNeutral = e i i̬ ; 
VowNonHigh = a o ä ö e õ ;
Vow = a o u ä ö ü e i õ y õ̭ ü̬ u̬ i̬ ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

CnsVoiced = j l m n r v ;

ArchCns =  %{hØ%} %{jØ%} %{kØ%} %{lØ%} %{mØ%} %{nØ%} %{rØ%} %{sØ%} %{tØ%} %{vØ%} %{pb%} %{td%} %{kg%} %{dr%} %{dn%} ;

Letters = Vow Cns ;

VowDummies = %^WGStem %> %^PenVowRM ;

HarmDummies = %{back%} %{front%} ;

Definitions

!! Onset consonant or word boundary
OnSetC = [:Cns (Cns:) |#|%>] ;

! Back Harmony
BHARM = [ %{back%}: \[ # | %{front%}: ]* ] ;

! Front Harmony
FHARM = [ %{front%}: \[ # | %{back%}: ]* ] ;


! Front Trigger
FT =   # Cns* [[ VowFront: | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;

! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ]+ Cns:* | ü:+ Cns:+ i Cns:+) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
!!€ viska%^WGStem%>aq
!!€ vis0a0%>aq
!!€ füüsiga%^StrGStem%^VowRM%>i%>dõ
!!€ füüsik000%>i%>dõ
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;



PenBetweenStemAndPenGrade = [(%^Pen: %^VOWLower:)] ;

PenBetweenStemAndStemFinal = [(HarmDummies:) ((%^Pen:) %^VOWLower:) (%^Pen: %^WGStem:)] ;


!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front
"%{aä%}:a"
!! __@RULENAME@__
%{aä%}:a <=>
 BHARM _ ;

"%{aä%}:ä"
!! __@RULENAME@__
%{aä%}:ä <=>
 FHARM _ ;

"%{uü%}:u"
!! __@RULENAME@__
%{uü%}:u <=>
 BHARM _ ;

"%{uü%}:ü"
!! __@RULENAME@__
%{uü%}:ü <=>
 FHARM _ ;

"%{eõ%}:õ"
!! __@RULENAME@__
%{eõ%}:õ <=>
 BHARM _ ;

"%{eõ%}:e"
!! __@RULENAME@__
%{eõ%}:e <=>
 FHARM _ ;


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>
   u: _ (HarmDummies:) %^VOWLower: ;
 OnSetC   _ u: (HarmDummies:) %^VOWLower: ;
 OnSetC _ [Cns:0|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow:0 (HarmDummies:) %^VOWLower: %^Pen: %^WGStem: %^VowRM:    ;
 OnSetC _ [Cns:0 |%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow:e (HarmDummies:) %^VOWLower: %^Pen: %^WGStem:    ;
!!€ lugõ%^VOWLower%^Pen%^WGStem%^VowRM%>i
!!€ lo000000%>i

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>  ü: _ (HarmDummies:) %^VOWLower: ;
 OnSetC _ ü: (HarmDummies:) %^VOWLower: (%^WGStem:) [ # | %> [ :Cns | [ :Vow - :i ]] ] ;
   # Cns:* _ [Cns:|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow: (HarmDummies:) %^VOWLower: ;
!!€ pügä%^VOWLower%^WGStem%^VowRM%>e%>t
!!€ pö00000%>e%>t
!!€ süü%^VOWLower%^WGStem%>nüq
!!€ söö00%>nüq

"ü2e"
!! __@RULENAME@__
!! {{{
 ü:e <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^U2E: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpe00
    ü: _ (HarmDummies:) %> i ;
!! }}}

"ü2ä"
!! __@RULENAME@__
!! {{{
 ü:ä <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^U2A: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpä00
!! }}}

"u2a"
!! __@RULENAME@__
 u:a <=>
       Cns: _ (HarmDummies:) (%^Pen: %^WGStem:) %^U2A:0  ;
!!€ laulu%^U2A
!!€ laulõ0

"o2a"
!! __@RULENAME@__
 o:a <=>
       Cns: _ (HarmDummies:) (%^Pen: %^WGStem:) %^U2A:0  ;
!!€ aigo%^U2A
!!€ aiga0

"o2õ"
!! __@RULENAME@__
 o:õ <=> [Cns - [m|p|b|v]] _ o: (HarmDummies:) %^OO2Õ: %> i ;
!!€ joo%^OO2Õ%>i
!!€ jõ00%>i
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ aigo%^OO2Õ
!!€ aigõ0

"u2õ"
!! __@RULENAME@__
 u:õ <=>
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ laulu%^OO2Õ
!!€ laulõ0

"ö2e"
!! __@RULENAME@__
 ö:e <=> [Cns - [m|p|b|v]] _ ö: (HarmDummies:) %^OO2Õ: %> i ;
!!€ söö%^OO2Õ%>i
!!€ se00%>i

!! !VOWEL RAISING
"ö:ü"
!! __@RULENAME@__
 ö:ü <=> Vow %^V7: 0: _ (HarmDummies:) %^StrGStem: %> i: ;
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0

!! !VOWEL CHANGE WITH PLURAL

"ʼ2õ"
!! __@RULENAME@__
!! {{{
 ʼ:õ <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %^ʼ2Õ:  ;
!!€ hangʼ%^WGStem%^ʼ2Õ#
!!€ hangõ00#
!! }}}

"ʼ2e"
!! __@RULENAME@__
!! {{{
 ʼ:e <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %^ʼ2E:  ;
!!€ särgʼ%^WGStem#
!!€ sär0e0#
!! }}}

"ʼ2i"
!! __@RULENAME@__
!! {{{
 ʼ:i <=> OnSetC  _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | %^WGStem: ) %^ʼ2I: ;
!!€ särgʼ%^WGStem%^ʼ2I
!!€ särgi00
!! }}}

 
"e2i̬"
!! __@RULENAME@__
 e:i̬ <=>  [ # Cns* | Cns ] _ e: ( s | t | k | l | n | r | m | t: s | r t: | v) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] e: _ [ s | t | k | l | n | r | m | t: s | r t: | v] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] e: _ (HarmDummies:) %^VOWRaise: [#|%>] ;
!!€ keel%^VOWRaiseʼ
!!€ ki̬i̬l0ʼ
      # Cns:* _ Cns:0 (HarmDummies:) (%^WGStem:) %> e: ;
      # Cns:* e: Cns:0 (HarmDummies:) (%^WGStem:) %> _  ;
!! tegemä+V+Act+Ind+Prs+Sg1: __do__
!!€ teg%^WGStem%>e
!!€ ti̬00%>i̬

"õ2õ̭"
!! __@RULENAME@__
 õ:õ̭ <=>  [ # Cns* | Cns ] _ õ: ( s | t | k | l | n | r | m | t: s | r t: | v) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ [ s | t | k | l | n | r | m | t: s | r t: | v] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ (HarmDummies:) %^VOWRaise: [#|%>] ;


"o2u̬"
!! __@RULENAME@__
 o:u̬ <=>  [ # Cns* | Cns ] _ o: ( s | t | k | l | n | r | m | t: s | r t: | v) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ [ s | t | k | l | n | r | m | t: s | r t: | v ] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ (HarmDummies:) %^VOWRaise: [#|%>] ;


"ö2ü̬"
!! __@RULENAME@__
 ö:ü̬ <=>  [ # Cns* | Cns ] _ ö: ( s | t | k | l | n | r | m | t: s | r t: | v) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] ö: _ [ s | t | k | l | n | r | m | t: s | r t: | v] (HarmDummies:) %^VOWRaise:  ;
[ # Cns* | Cns ] ö: _ (HarmDummies:) %^VOWRaise: [#|%>] ;
  [ # Cns* | Cns ] _ ö: [ s | t | k | l | n | r | m | t: s | r t: | v| r d:] Vow: (HarmDummies:)  %^Pen:  %^VOWRaise: ;
 [ # Cns* | Cns ] ö: _ [ s | t | k | l | n | r | m | t: s | r t: | v] Vow: (HarmDummies:) %^Pen: %^VOWRaise:  ;

"i2e"
!! __@RULENAME@__
 i:e <=> Cns 0: _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€ pin0i%^StrGStem%^PLPRT
!!€ pinne00
 # Cns* _ i: l (HarmDummies:) %^VOWLower: ;
 # Cns* i: _ l (HarmDummies:) %^VOWLower: ;
!!€ kiil%^VOWLower%>e
!!€ keel0%>e
     e _ Cns: (HarmDummies:) %^ÄI2ÄÄ: ;
!!€ leib%^ÄI2ÄÄ%^WGStem%>ä
!!€ leev0%>ä



"i:ä"
!! __@RULENAME@__
 i:ä <=> ä _ Cns: (HarmDummies:) %^ÄI2ÄÄ: ;
!!€ päiv%^ÄI2ÄÄ%>ä
!!€ pääv0%>ä


"a2o"
!! __@RULENAME@__
!! {{{
 a:o <=> OnSetC  _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | %^Pen: %^WGStem: ) %^PLPRT: ;
!!€ kan0a%^StrGStem%^PLPRT
!!€ kanno00
!! }}}


"õ2y"
!! __@RULENAME@__
!! {{{
 õ:y <=> OnSetC  _ Cns:0 a (HarmDummies:) (%^WGStem:) ;
!!€ sõda%^WGStem
!!€ sy0a0
!! }}}

"õ2e"
!! __@RULENAME@__
 õ:e <=>  _ (HarmDummies:) (%^Pen: %^WGStem:) %^Õ2E:;
!!€ lugõ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t

!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
 a:0 <=> [ Cns:| ArchCns: ] _ (HarmDummies:) [ (%^StrGStem:|%^Pen: %^WGStem:|%^Pen: %^CC2C:) (0:) %^VowRM: | Cns:0 %^VowRM: %^CnsRM: %> ]  ; 
      a _ (HarmDummies:) %>  i  ; !!= @CODE@ 
!!€ saa%>i
!!€ sa0%>i
!!€ sõima0%^VowRM%>si
!!€ sõim0s0%>si
  [Cns:| ArchCns: ]  _ Cns: (HarmDummies:) (ʼ:) %^Pen: %^VowRM: ;
!!€ puhkas%^Pen%^VowRM%>a%>ma
!!€ puhk0s00%>a%>ma
  _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"ä:0"
!! __@RULENAME@__
 ä:0 <=>  [ Cns: | :Cns | ArchCns:] _ (HarmDummies:) [ (0:) (%^Pen: %^CC2C:) %^VowRM: | Cns:0 %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
!  [ Cns: | :Cns ] _ (HarmDummies:) [ (0:|:0+) %^VowRM: | Cns:0 %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
                        ä _ (%^VOWRaise:) %^VowRM:  ;
!!€ jää%^VOWRaise%^VowRM%>i
!!€ jä000%>i
!!€ elä%>et
!!€ el0%>et
  [Cns:| ArchCns: ] _ Cns: (HarmDummies:) (ʼ:) %^Pen: %^VowRM: ;
!!€ tütär%^Pen%^VowRM%>e
!!€ tüt0r0%>e
 _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;

"u:0"
!! __@RULENAME@__
!! {{{
 u:0 <=>  Cns: _ (HarmDummies:) [ (%^Pen: %^CC2C:) %^VowRM: | Cns:0 (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
   OnSetC u: _ (HarmDummies:) (%^Pen: %^CC2C:) %^VowRM: %> i ;
!!€ kuu%^VowRM%>i%>d
!!€ ku00%>i%>d
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
!! }}}

"ü:0"
!! __@RULENAME@__
!! {{{
 ü:0 <=>  [ Cns: | Vow: ] _ (s:) (HarmDummies:) [(%^Pen: %^CC2C:) %^VowRM: | Cns:0 (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
!!€ tüü%^VowRM%>hüq
!!€ tü00%>hüq
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
   OnSetC ü: _ (HarmDummies:) (%^VOWLower:) %> i ;
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
!! }}}

"e:0"
!! __@RULENAME@__
 e:0 <=>  [ Cns: | :Cns ] _ (HarmDummies:) [(%^Pen: %^CC2C:) %^VowRM: | Cns:0 (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
           [ Cns: | :Cns ] _ l (HarmDummies:) %^Pen: %^VowRM:  ;
           e: _ (%^VOWRaise:) %^VowRM: ;
	   _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;
!!€ herne%{hØ%}%{front%}%^VowRM%^WGStem%>id%{eõ%}
!!€ hern00000%>ide
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"o:0"
!! __@RULENAME@__
 o:0 <=>  [ Cns: | :Cns ] _ (HarmDummies:) [ (%^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^CC2C:) %^VowRM: (õ) ]   ; 
           o: _ (HarmDummies:) [(%^VOWRaise:) %^VowRM:|%^OO2Õ:] ;
!!€ joo%^VOWRaise%^VowRM%>vv%>aq
!!€ ju000%>vv%>aq
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"ö:0"
!! __@RULENAME@__
 ö:0 <=>  [ Cns: | :Cns ] _ (HarmDummies:) [  (%^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^CC2C:) %^VowRM: (e) ]   ; 
           ö: _ (HarmDummies:) [(%^VOWRaise:) %^VowRM:|%^OO2Õ:] ;
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"i:0"
!! __@RULENAME@__
 i:0 <=>  [ Cns: | :Cns ] _ (HarmDummies:) [ (%^Pen: %^CC2C:|%^Pen: %^WGStem:|%^TS2S:) %^VowRM: (õ|e) | Cns:0 %^Pen: %^VowRM: %^CnsRM: ]  ; 
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t
          Vow l 0:l %>: _ (HarmDummies:) [õ|e] ;
       a _ g: (HarmDummies:) %^WGStem: %> [o:|e:] ;
       a _ g: [o:|e:] (HarmDummies:) %^Pen: %^WGStem:  ;
!!€ aigo%^Pen%^WGStem
!!€ a00o00
    Vow [ %^I7: | %^V7: 0: ] Vow: (HarmDummies:) %^StrGStem: %> _ ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;


"õ:0"
!! __@RULENAME@__
 õ:0 <=>  [ Cns: | :Cns ] _ [ l | r ] (HarmDummies:) (%^StrGStem:) %^Pen: %^VowRM:  ;
!!€ tapõl%^Pen%^VowRMõ%>ma
!!€ tap0l0õ%>ma
!!€ kannõl%^StrGStem%^Pen%^VowRM%>õ
!!€ kand0l00%>õ
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
 Cns: _ (HarmDummies:) (%^Pen: %^VOWLower: %^Pen: %^WGStem:|%^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^CC2C:|%^Pen: %^WGStem:) %^VowRM:  ;
!!€ hammõ%^WGStem%^VowRM%>ihe
!!€ ham0000%>ihe
	   _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;


!! VOWEL LENGTHENING

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> :u PenBetweenStemAndStemFinal ( %- ) _  ;
       [ u | u: PenBetweenStemAndStemFinal (%^VowRM:) | u̬: PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;

     
"%{XV%}:ü"
!! __@RULENAME@__
 %{XV%}:ü <=> :ü PenBetweenStemAndStemFinal ( %- ) _  ;
       [ ü | ü: PenBetweenStemAndStemFinal (%^VowRM:) |ü̬: PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

     
"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a PenBetweenStemAndStemFinal ( %- ) _  ;
       [ o | a | a: PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

"%{XV%}:ä"
!! __@RULENAME@__
 %{XV%}:ä <=> :ä PenBetweenStemAndStemFinal ( %- ) _  ;
       [ ö | ä | ä: PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

"%{XV%}:õ"
!! __@RULENAME@__
 %{XV%}:õ <=> :õ PenBetweenStemAndStemFinal ( %- ) _  ;
        [õ | õ: PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

"%{XV%}:e"
!! __@RULENAME@__
 %{XV%}:e <=>  [ e | e: PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;

"%{XV%}:i"
!! __@RULENAME@__
 %{XV%}:i <=> :i PenBetweenStemAndStemFinal ( %- ) _  ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow Cns:+ Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
! Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
         Cns _ (HarmDummies:) %^I2J: ;
!!€ asi%^I2J%>a
!!€ asj0%>a
         Vow: _  PenBetweenStemAndStemFinal %^I2J: ;
!!€ elläi%^Pen%^WGStem%^I2J%>ä
!!€ el0äj00%>ä
!         Cns _ (HarmDummies:) %^StrGStem: %> :Vow ;

"i2ʼ"
!! __@RULENAME@__
 i:ʼ <=>
  Cns: _ (HarmDummies:) (%^Pen: %^CC2C:) %^I2ʼ: ;
! Cns _ %^WGStem: %> :Vow ;


"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v a q ;
         u _ d: (HarmDummies:) %^WGStem: ;
!!€ juud%^WGStem%>a
!!€ juvv0%>a

"%{uv%}:v"
!! __@RULENAME@__
 %{uv%}:v <=> u _ %> v a q ;
         u _ %{dv%}: (HarmDummies:) %^WGStem: ;
!!€ ju%{uv%}%{dv%}%^WGStem%>a
!!€ juvv0%>a

"%{üv%}:v"
!! __@RULENAME@__
 %{üv%}:v <=> ü _ (HarmDummies:) %> v ä q ;
         ü _ %{dv%}: (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dv%}%^WGStem%>et
!!€ püvv0%>et
!!€ sü%{üv%}%{dv%}%^WGStem%>ä
!!€ süvv0%>ä

"%^I7:i"
!! __@RULENAME@__
 %^I7:i <=> Vow _ Vow (HarmDummies:) %^StrGStem:  ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0


"%^V7:v with 0:v"
!! __@RULENAME@__
 %^V7:v <=> Vow _ 0: Vow (HarmDummies:) %^StrGStem:  ;
!!€ hä%^V70ö%^StrGStem
!!€ hävvü0


!! SECONDARY CONSONANT LENGTHENING

"0:m"
!! __@RULENAME@__
 0:m <=>
         Vow m _ (HarmDummies:) (Vow: (HarmDummies:) %^Pen:) %^StrGStem: ;
!!nimi+N+Sg+Par __name__
!!€ nim0%^StrGStem%>e
!!€ nimm0%>e

"0:p"
!! __@RULENAME@__
!! {{{
 0:p <=> Vow b: _ Vow: (HarmDummies:) %^Pen: %^XStrGStem: ;
!!€ häb0ü%^XStrGStem
!!€ häppü00
!! }}}

"0:v"
!! __@RULENAME@__
!! {{{
 0:v <=> Vow %^V7: _ Vow: (HarmDummies:) %^StrGStem: ;
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0
 Vow v _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ kiv0i%^StrGStem
!!€ kivvi0
!! }}}


"0:n"
!! __@RULENAME@__
 0:n <=> Vow n _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;

!        Vow n _ :Vow %> j:i ;
!	 Vow n _ %> i: (õ|e) [ %> | # ] ;

"0:t"
!! __@RULENAME@__
 0:t <=> Vow d: _ Vow: (HarmDummies:) %^Pen: %^XStrGStem: ;
!!€ sõd0a%^XStrGStem%^PLPRT
!!€ sõtto00
 t _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ üt0e%^Pen%^StrGStem
!!€ ütte00

"0:s"
!! __@RULENAME@__
 0:s <=> CnsVoiced Vow: _ (HarmDummies:) %^VowRM: %> s i ;
!!€ sõima0%^VowRM%>si
!!€ sõim0s0%>si
   Vow s: _ (ʼ:) (HarmDummies:) (%^Pen: %^WGStem:) %^StrGStem: ;
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ
   Vow s: _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ kos0i%^StrGStem%>q
!!€ kossi0%>q

"0:l"
!! __@RULENAME@__
 0:l <=> Vow l _ :Vow (HarmDummies:) %> j:i ;
         Vow l _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!	 Vow l _ (HarmDummies:) %> i: (õ|e) [ %> | # ] ;

"0:r"
!! __@RULENAME@__
 0:r <=>
         Vow r _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;

"0:h"
!! __@RULENAME@__
 0:h <=> Vow h _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;


"0:k"
!! __@RULENAME@__
 0:k <=> Vow g: _ (HarmDummies: | Vow: (HarmDummies:) %^Pen:) %^XStrGStem: ;
!!€ ig0ä %^Pen%^XStrGStem
!!€ ikkä0
   Vow k _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ mak0õ%^StrGStem%^VowRM%>i%>dõ
!!€ makkõ00%>i%>dõ

"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> s: (HarmDummies:) _ ;

"%{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=> l: (HarmDummies:) _ ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> k: (HarmDummies:) _ ;


!! !Consonant weakening 

! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> [h|k|s] _ Vow: (s) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ puhkas%^WGStem%>taq
!!€ puh0as0%>taq

 s _ a (HarmDummies:) %^Pen: %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ Vow: | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
  k _ [(HarmDummies:)|Vow: (HarmDummies:) %^Pen:] [ %^CC2C:0 | %^WGStem: ] ;
  [h|s] _ [(HarmDummies:)|Vow: (HarmDummies:) %^Pen:] %^WGStem:  ;
!!€ ikkõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ ik000000%>ma

"%{kØ%}:0"
!! __@RULENAME@__  
 %{kØ%}:0 <=> [h|k|s] _ Vow: (s) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ puhkas%^WGStem%>taq
!!€ puh0as0%>taq

 s _ a (HarmDummies:) %^Pen: %^WGStem: ;

   _ k [(HarmDummies:)|Vow: (HarmDummies:) %^Pen:] [ %^CC2C:0 | %^WGStem: ] ;
  [h|s] _ [(HarmDummies:)|Vow: (HarmDummies:) %^Pen:] %^WGStem:  ;
!!€ i%{kØ%}kõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ i0k00000%>ma

"pToZero"
!! __@RULENAME@__
 p:0 <=>
  p _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  p _ Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ tapp%{back%}%^CC2C%>m%{aä%}
!!€ tap000%>ma

"%{pØ%}:0"
!! __@RULENAME@__
 %{pØ%}:0 <=>
  _ p (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  _ p Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ ta%{pØ%}p%{back%}%^CC2C%>m%{aä%}
!!€ ta0p00%>ma

"mToZero"
!! __@RULENAME@__
 m:0 <=> m_ Vow: (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ hammõ%^Pen%^WGStem%>h
!!€ ham0õ0%>h

"vToZero"
!! __@RULENAME@__
 v:0 <=> v_ Vow: (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
!! kevväi+N+Sg+Gen: __spring__
!!€ kevväi%^Pen%^WGStem%>ä
!!€ kev0äj00%>ä


"lToZero"
!! __@RULENAME@__
 l:0 <=> l _ Vow (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ elläi%^Pen%^WGStem%^I2J%>ä
!!€ el0äj00%>ä
         _ %{XC%}:l [õ|e] v (HarmDummies:) %^Pen: %^WGStem: ;

  l _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  l _ (ʼ) Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ võlli%{back%}%^CC2C%^I2ʼ%>m%{aä%}
!!€ võl0ʼ000%>ma
  l _ Vow:0 l ;
!!€ kullõl^Pen%^VowRM%>%{eõ%}%>ma
!!€ kul00l00%>õ%>ma

"XØToZero"
!! __@RULENAME@__
 Cx:0 <=>
 _ Cns (ʼ) [Vow: (Cns:* |i:) (%{hØ%}:|%{sØ%}:) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
 Vow: _ (HarmDummies:) (%^VowRM: ) %^WGStem: ;
         where Cx in (%{hØ%} %{jØ%} %{lØ%} %{mØ%} %{nØ%} %{rØ%} %{sØ%} %{vØ%}) ;
!! elläi+N+Sg+Gen
!!€ e%{lØ%}läi%^Pen%^WGStem%^I2J%>ä
!!€ e0läj00%>ä
!!€ ha%{mØ%}mõ%{hØ%}%{back%}%^Pen%^WGStem
!!€ ha0mõh%{back%}%^Pen%^WGStem

"StrongToWeak"
!! __@RULENAME@__
 Cx:Cy <=> Vow _ (ʼ)[ Vow (Cns:* |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
      where Cx in (%{pb%} %{td%} %{kg%} )
            Cy in (   b       d     g   )   matched ;

"rToZero"
!! __@RULENAME@__
 r:0 <=> r _ Vow: (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ perre%^WGStem%>h
!!€ per0e0%>h
  r _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  r _ (ʼ:) Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ narri%{back%}%^CC2C%>m%{aä%}
!!€ nar0ʼ00%^I2ʼ%>ma


"tToZero"
!! __@RULENAME@__
 t:0 <=> [ h | t ] _ Vow (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
         Vow _ s (HarmDummies:) (ʼ:|i:) %^TS2S: ;
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t
  t _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  t _ Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;

!!€ att%{back%}%^CC2C%>m%{aä%}
!!€ at000%>ma



!! !CONSONANT QUALITY CHANGE

"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow Cns:* (HarmDummies:) (ʼ:) %^Pen: %^WGStem: ;

"b20"
!! __@RULENAME@__
 b:0 <=> Vow _ Vow  (HarmDummies:) [%^Pen: %^WGStem:|%^WGStem:] ;

"b2v"
!! __@RULENAME@__
 b:v <=>
     Vow: _ (HarmDummies:) (%^ÄI2ÄÄ:) %^WGStem: ;
!!€ leib%^ÄI2ÄÄ%^WGStem%>ä
!!€ leev0%>ä

"bTop"
!! __@RULENAME@__
 b:p <=> 
    Vow _ Vow: (HarmDummies:) %^StrGStem: ;
    Vow _ 0: Vow: (HarmDummies:) %^XStrGStem: ;


"b2m"
!! __@RULENAME@__
 b:m <=> m _  (HarmDummies:) %^WGStem: ;
 m _ Vow: (Cns:+) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ hamba%^Pen%^WGStem%>s
!!€ hamma0%>s

"%{bm%}:m"
!! __@RULENAME@__
 %{bm%}:m <=> _  (HarmDummies:) %^WGStem: ;
 m _ Vow: (Cns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ ham%{bm%}a%{sØ%}%^Pen%^WGStem
!!€ hammas00


"d2n"
!! __@RULENAME@__
 d:n <=> # Cns* Vow n (HarmDummies:) %> _ [a|ä] q ;
 n _ (HarmDummies:) %^WGStem: ;
 n _ Vow: (Cns:+) (HarmDummies:) %^Pen: %^WGStem: ;

"%{dn%}:n"
!! __@RULENAME@__
 %{dn%}:n <=> # Cns* Vow n (HarmDummies:) %> _ [a|ä] q ;
 n _ (HarmDummies:) %^WGStem: ;
 n _ Vow: (Cns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) %^Pen: %^WGStem: ;


"dTol"
!! __@RULENAME@__
 d:l <=> # Cns* Vow [l (HarmDummies:)|(HarmDummies:) l] %> _ [a|ä] q ;
!!€ tul%>daq
!!€ tul%>laq
!!€ kõnõl%>daq
!!€ kõnõl%>daq
    l _ (HarmDummies:) %^WGStem: ;
!!€ vald%{back%}%^WGStem%>n
!!€ vall00%>n
   l _ Vow: (Cns:+) (HarmDummies:) %^Pen: %^WGStem: ;


"d2v"
!! __@RULENAME@__
 d:v <=> [ ü: |u: ] _ (HarmDummies:) %^WGStem: ;
!!€ püüd%^WGStem%>et
!!€ püvv0%>et
!!€ juud%^WGStem%>a
!!€ juvv0%>a
!!€ süüd%^WGStem%>ä
!!€ süvv0%>ä

"%{dv%}:v"
!! __@RULENAME@__
 %{dv%}:v <=> [ %{üv%}: |%{uv%}: ] _ (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dv%}%^WGStem%>et
!!€ püvv0%>et
!!€ ju%{uv%}%{dv%}%^WGStem%>a
!!€ juvv0%>a
!!€ sü%{üv%}%{dv%}%^WGStem%>ä
!!€ süvv0%>ä



"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] (HarmDummies:|Vow: (HarmDummies:) %^WGStem: %^VowRM:) %> _ ;
    Vow _ (s) (Vow:) (HarmDummies:) %^StrD2T: ;
!!käsi+N+Sg+Par __hand, arm__
!!€ käd%^StrD2T%>t
!!€ kät0%>t
!!€ hüdsi%^StrD2T%>l
!!€ hütsi0%>l
    Vow _ (s) (HarmDummies:|Vow: (HarmDummies:) %^Pen:) %^StrGStem: ;
    Vow _ 0: (HarmDummies:|Vow: (HarmDummies:) %^Pen:) %^XStrGStem: ;


"dTos"
!! __@RULENAME@__
 d:s <=> Vow: _ (HarmDummies:) %^D2S: ;


"tTos"
!! __@RULENAME@__
 t:s <=> _ (HarmDummies:) %^D2S: ;

"tTod"
!! __@RULENAME@__
 t:d <=>
 Vow _ s (HarmDummies:) (ʼ:) %^WGStem: ;
         Vow _ [Vow: (HarmDummies:) %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1: 
!!€ kaota%^Pen%^WGStem
!!€ kaoda0

!2015-09-02 Vow [(%^StrGStem:| Cns:0 %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ [%> | # | :Vow ] ;
!! There should always be a trigger

"%{td%}:d"
!! __@RULENAME@__
 %{td%}:d <=>
 Vow _ s (HarmDummies:) (ʼ:) %^WGStem: ;
         Vow _ [Vow: (HarmDummies:) %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1:
!!€ kao%{td%}a%^Pen%^WGStem
!!€ kaoda0


!!This should be dealt with in %{dn%}
"nTod"
!! __@RULENAME@__
 n:d <=> n _ õ: l (HarmDummies:) %^Pen: %^VowRM: ;

!!€ kannõl%^Pen%^VowRM%>õ
!!€ kand0l0%>õ


"j2i"
!! __@RULENAME@__
 j:i <=> :Vow (HarmDummies:) %> _ [ # | %- ];

"k2g"
!! __@RULENAME@__
 k:g <=> [Vow|Cns - [h|k|s]] _ (HarmDummies:) %^WGStem: ;
!!€ hõrak%^WGStem%>a
!!€ hõrag0%>a
!!$ hõrak%^WGStem%>a
!!$ hõraK0%>a
!!€ kisk%^WGStem%>%{uü%}%>t%{aä%}v
!!€ kis0%>u%>tav

     Vow (Cns) _ Vow Cns:+ (HarmDummies:) (ʼ:) %^Pen: %^WGStem: ;
!!€ süküs%^Pen%^WGStem%>e
!!€ sügüs0%>e
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ

"gTok"
!! __@RULENAME@__
 g:k <=>
    Vow _ (HarmDummies:) %^StrGStem:  ; 
    Vow _ Vow: ( l | r ) (HarmDummies:) %^Pen: %^StrGStem: ;
    Vow _ Vow: (HarmDummies:) %^StrGStem: ;
    Vow _ 0: (HarmDummies:|Vow: (HarmDummies:) %^Pen:) %^XStrGStem: ;
!!€ ig0ä%^XStrGStem
!!€ ikkä0
   Vow _ Vow: [ l | r ] (HarmDummies:) %^StrGStem: %^Pen: %^VowRM: ;
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
   [t ] (Vow:0) (HarmDummies:) (%^VowRM:) %> _ u ;

"%{kgØ%}:k"
!! __@RULENAME@__
 %{kgØ%}:k <=>
  Vow _ Vow: (HarmDummies:) (%^VOWLower:) %^Pen: %^StrGStem: ;


"%{tdØ%}:t"
!! __@RULENAME@__
 %{tdØ%}:t <=>
  Vow _ Vow: (HarmDummies:) (%^VOWLower:) %^Pen: %^StrGStem: ;

!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u: | ü: | a: | ä: ] _ (HarmDummies:) (%^VowRM:)  %^CnsRM:  ;
         s _ %^TS2S: ;
!! ussʼ+N+Sg+Par __door__
!!€ uss%^TS2S%>t
!!€ us00%>t

  s _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  s _ (ʼ:) Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ vassi%{back%}%^CC2C%^I2ʼ%>m%{aä%}
!!€ vas0ʼ000%>ma


"d20"
!! __@RULENAME@__
 d:0 <=>
         Vow _ s (ʼ:|Vow:) (HarmDummies:) %^TS2S: ;
!!€ hüdsi%^TS2S%^VowRM%>te
!!€ hü0s000%>te
          Vow: _ Vow: (HarmDummies:) PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;

"g20"
!! __@RULENAME@__
 g:0 <=> [Cns:|Vow:]  _ (HarmDummies:) ( ʼ: ) %^WGStem: ;
!!€ särgʼ%^WGStem%>#
!!€ sär0e0%>0
!!€ aig%^WGStem%>o
!!€ a000%>o
   [Cns:|Vow:] _ [Vow: (HarmDummies:)|(HarmDummies:) %{eõ%}:] PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;
!!€ igä%^Pen%^WGStem
!!€ i0ä00
!! lugõma+V+Pss+PrfPrc __read/lukea__
!!€ lugõ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t
!! argnõma+V+Inf
!!€ arg%{back%}%{eõ%}%^Pen%^WGStemd%{aä%}q
!!€ ar00õ00daq

!2017-02-03 Vow l _ Vow (HarmDummies:) %^WGStem: ;
!!€ palgõ%^WGStem%>h
!!€ pal0õ0%>h

"%{tdØ%}:0"
!! __@RULENAME@__
 %{tdØ%}:0 <=> 
   Vow: _ Vow: (HarmDummies:) (%^VOWLower:) %^Pen: %^WGStem: ;

"%{kgØ%}:0"
!! __@RULENAME@__
 %{kgØ%}:0 <=> 
   Vow: _ Vow: (HarmDummies:) (%^VOWLower:) %^Pen: %^WGStem: ;

"j20"
!! __@RULENAME@__
 j:0 <=>
  j _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  j _ (ʼ:) Vow: (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ ujjo%{back%}%^Pen%^WGStem%>m%{aä%}
!!€ uj0o000%>ma

!! !Other marks
"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=>  :Cns _ (HarmDummies:) (%^WGStem:|%^Pen: %^WGStem:) (%^StrGStem:|%^TS2S:) %> [:Vow|t] ;
!!€ kipõnʼ%>a
!!€ kipõn0%>a
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ

"Disallow %^ErrorBack:0 in BHARM"
%^ErrorBack: /<= BHARM _ ;
