!  ===============================================
!! !!!The Võro morphophonological/twolc rules file 
!  ===============================================


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

b́ d́ f́ ǵ h́ ḱ ĺ ḿ ń ṕ ŕ ś t́ v́ ź

B́ D́ F́ Ǵ H́ Ḱ Ĺ Ḿ Ń Ṕ Ŕ Ś T́ V́ Ź

 ʼ

!! !!Special letters
u̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
U̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
Ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
i̬  !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
I̬  !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
õ̭  !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
Õ̭  !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
ˋ  !!≈ * __@CODE@__ U+02CB MODIFIER LETTER GRAVE ACCENT other parts of stem
%{ˋØ%}:0 !!≈ {{{@CODE@ — U+02CB MODIFIER LETTER GRAVE ACCENT used in rules}}}
ˋ:0 !!≈ __@CODE@__ — U+02CB MODIFIER LETTER GRAVE ACCENT used outside of rules, this is temporary removal 2017-04-20

!! !!Vowel harmony with "(t)a/ä"
!! {{{
 %{aä%}:0   !!= @CODE@ — Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0
 %{ae%}:a  !!= @CODE@ — Vowel harmony with "a/e/õ" passive tahetu
 %{aõ%}:a  !!= @CODE@ — Vowel harmony with "a/e/õ" passive sõidõtu
 %{äe%}:ä   !!= @CODE@ — Vowel harmony with "ä/e/õ" passive
 %{eõ%}:0   !!= @CODE@ — Vowel harmony with "e/õ"
 %{uü%}:0   !!= @CODE@ — Vowel harmony with "u/ü"
 %{öü%}:ö   !!= @CODE@ — Vowel raising
 %{ou%}:o   !!= @CODE@ — Vowel raising
 %{ei%}:e   !!= @CODE@ — Vowel raising
 %{õy%}:õ   !!= @CODE@ — Vowel raising
 %{ao%}:a   !!= @CODE@ — Vowel raising

 %{eØ%}:e   !!= @CODE@ — ütlemä:üt%{eØ%}l  
 %{õØ%}:õ   !!= @CODE@ — ütlemä:üt%{eØ%}l  

 %{dØ%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dv%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dn%}:d   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}
 %{dl%}:d   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}

 %{ij%}:i   !!= @CODE@ ellä%{ij%}
 %{gv%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gl%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gØ%}:g   !!= @CODE@ — HJK and KimmoK ideas argnõma:ar%{gØ%}
 %{uv%}:u   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{üv%}:ü   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{hØ%}:h   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{jØ%}:j   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kØ%}:k   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lØ%}:l   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{mØ%}:m   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{nØ%}:n   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pØ%}:p   !!= @CODE@ — HJK and KimmoK ideas oppama:o%{pØ%}pama
 %{rØ%}:r   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sØ%}:s   !!= @CODE@ — HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{vØ%}:v   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{Øp%}:0   !!= @CODE@ — häbü:häbü+N:hä%{Øp%}%{pbØ%}ü
 %{Øt%}:0   !!= @CODE@ — koda:ko%{Øt%}%{tdØ%}a
 %{Øk%}:0   !!= @CODE@ — nägo:nä%{Øk%}%{kgØ%}o

 %{bv%}:b   !!= @CODE@ — HJK and KimmoK ideas närväs:när%{bv%}ä%{sØ%}
 %{dr%}:d   !!= @CODE@ — HJK and KimmoK ideas parras:par%{dr%}a%{sØ%}
 %{bm%}:b   !!= @CODE@ — HJK and KimmoK ideas lammas:lam%{bm%}a%{sØ%}
 %{pb%}:p   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{tØ%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{td%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{t́d́%}:t́   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kg%}:k   !!= @CODE@ — HJK and KimmoK ideas kaigas:kai%{kg%}as
 %{pbØ%}:b  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pbv%}:b  !!= @CODE@ — tõbi: tõvõ tõpõ tõppõ
 %{tdØ%}:d  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kgØ%}:g  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{jiØ%}:i  !!= @CODE@ — HJK and KimmoK ideas vari:var%{jiØ%}o
 %{qmn%}:q  !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{qn%}:q   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na

 %{dd́Ø%}:d   !!= @CODE@
 %{dd́n%}:d   !!= @CODE@
 %{dd́r%}:d   !!= @CODE@
 %{dd́v%}:d   !!= @CODE@
 %{dd́Ø%}:d   !!= @CODE@
 %{gǵv%}:g   !!= @CODE@
 %{gǵØ%}:g   !!= @CODE@
 %{kḱg%}:k   !!= @CODE@
 %{kḱØ%}:k   !!= @CODE@
 %{pṕb%}:p   !!= @CODE@
 %{tt́d%}:t   !!= @CODE@
 %{tt́Ø%}:t   !!= @CODE@ täh%{tt́Ø%}
 %{pṕØ%}:p   !!= @CODE@

!! }}}

!! !!Palatalization of consonants
!! {{{
 %{bb́%}:b   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dd́%}:d   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{ff́%}:f   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{gǵ%}:g   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{hh́%}:h   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kḱ%}:k   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{lĺ%}:l    !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{mḿ%}:m   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{nń%}:n   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{pṕ%}:p   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{rŕ%}:r   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{sś%}:s   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{tt́%}:t   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{vv́%}:v   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{zź%}:z   !!= @CODE@ — HJK and KimmoK ideas kana:ka%{nØ%}na
 %{dd́n%}:d !!= @CODE@
!! }}}

!! !!Miscellaneous other symbols
!! {{{
 %{XV%}:0   !!= @CODE@ — This is used for echoing the previous vowel
 %{XC%}:0   !!= @CODE@ — This is used for lengthening a consonant
 %^I7:0     !!= @CODE@ — This appears in stem vaoma:va%^I7o for vaio
 %^K7:0     !!= @CODE@ — This appears in stem väemä:vä%^K7e for väkeq
 %^V7:0     !!= @CODE@ — This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0     !!= @CODE@ — This appears in stem kaoma:ka%^T7o for katoq
 %^Y7:õ     !!= @CODE@ — This appears for syna = s%^Y7na and is rendered as õ in the norm
!! }}}

!! !!Triggers
!! {{{
   %^OO2Õ:0   !!= @CODE@ — joo%^OO2Õ%>i:j0õ0%>i
   %^CC2C:0   !!= @CODE@ — att%^CC2C%>m%{aä%} atma
 %^PSS:0      !!= @CODE@ vowel in passive tahetu, sõidõtu, eletü
 %^ÄI2ÄÄ:0   !!= @CODE@ — päiv%^ÄI2ÄÄ%>ä: päävä
 %{front%}:0   !!= @CODE@ — front harmony
 %{back%}:0   !!= @CODE@ — back harmony
%^ErrorBack:0 !!= @CODE@ — +Err/Orth+Clt:%>kinaq in front harmony context BHARM disallowance
 %{PrsSg1%}:0 !!= @CODE@ — this helps with %{eõ%}:i̬
!! }}}

!! {{{

 %{td%}:t 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{kg%}:k 	!!= @CODE@ HJK and KimmoK ideas kaigas:kai%{kg%}as

 %{qmn%}:q 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{qn%}:q 	!!= @CODE@ HJK and KimmoK ideas kana:ka%{nØ%}na
 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel
 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant
 %^I7:0         !!= @CODE@ This appears in stem vaoma:va%^I7o for vaio
 %^K7:0         !!= @CODE@  This appears in stem väemä:vä%^K7e for väkeq
 %^V7:0         !!= @CODE@ This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0         !!= @CODE@ This appears in stem kaoma:ka%^T7o for katoq

%^Y7:õ  !!= __@CODE@__  This appears for syna = s%^Y7na and is rendered as õ in the norm
!! }}}



! this k is not effected by gradation
K1:k
!! {{{
%^NoGrad:0    !!= @CODE@ — This will be placed after a stem to break Gradation
%^APOCH:0     !!= @CODE@ — This causes apochope: puhksama vs puhastaq
%^StrD2T:0    !!= @CODE@ — This changes g,d,b => k,t,p
%^WGStem:0    !!= @CODE@ — This weakens "kipõń" to "kibõna", "ompel" to "ommel"
%^StrGStem:0  !!= @CODE@ — This strengthens "perädü" to "perätüt"
%^XStrGStem:0 !!= @CODE@ — This extra-strengthens "sõda" to "sõtta"
%^Pen:0       !!= @CODE@ — This moves us to penultimate coda
%^PenWGStem:0 !!= @CODE@ — This weakens "kipõń" to "kibõna"
%^PenVowRM:0  !!= @CODE@ — syncope tapõld : taplõma 
%^PAL:0	      !!= @CODE@ — Palatalization
%^D2S:0       !!= @CODE@ — The ti => si
%^TS2S:0      !!= @CODE@ — The -ts- => -s-
%^I2J:0       !!= @CODE@ — The i => j change
%^PLPRT:0     !!= @CODE@ — The a:o attested in Plural kana:kanno and prt
%^VOWRaise:0  !!= @CODE@ — Raises vowel
%^VOWLower:0  !!= @CODE@ — Lowers vowel
%^XLowerVow:0 !!= @CODE@ — Lowers vowel two levels
%^VOWLowerDelab:0  !!= @CODE@ — Lowers vowel and delabializes it
%^XLowerVowDelab:0 !!= @CODE@ — Lowers vowel two levels and delabializes it
%^U2E:0       !!= @CODE@ — lowers u:õ and ü:e delabializes and lowers
%^U2A:0       !!= @CODE@ — lowers u:a and ü:ä delabializes and lowers
%^VowRM:0     !!= @CODE@ — this will remove stem final vowel
%^CnsRM:0     !!= @CODE@ — this will remove stem final consonant tervüs:tervü
!! }}}

!Outlining 2 grades (3,4) (audibly, orthographically)  ku%{Øp%}p : kupu kuppu
!Outlining 2 grades (audibly, orthographically)  tä%{tØ%}%{tdØ%}i : tädi täti tätti
!Outlining 2 grades (audibly, orthographically)  tü%{kØ%}k : tükü tükkü

!Outlining 2 grades (audibly, NOT orthographically)  kasv : kasvo kasvo

!Outlining 3 grades (2,3,4) (audibly, orthographically)  tö%{pØ%}%{pbØ%}i : töbi töpi töppi
!Outlining 3 grades (audibly, orthographically)  tä%{tØ%}%{tdØ%}i : tädi täti tätti
!Outlining 3 grades (audibly, orthographically)  pi%{kØ%}%{kgØ%}i : pigi piki pikki

!Outlining 3 grades (audibly, NOT orthographically)  pi%{nØ%}ni : pini pinni pinni

!Outlining 4 grades (1,2,3,4) (audibly, orthographically)  u%{pØ%}%{pbØ%}a : ua uba upa uppa
!Outlining 4 grades (audibly, orthographically)  la%{tØ%}%{tdØ%}o : lao lado lato latto
!Outlining 4 grades (audibly, orthographically)  la%{kØ%}%{kgØ%}o : lao lago lako lakko

!Outlining 4 grades (audibly, NOT orthographically)  u%{pØ%}%{pbØ%}a : ua uba upa uppa


%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y õ̭ u̬ ;
VowFront = ä ö ü ü̬ ;
VowNeutral = e i i̬ ; 
VowNonHigh = a o ä ö e õ ;
Vow = a o u ä ö ü e i õ y õ̭ ü̬ u̬ i̬ ;

VowInZero = a o u ä ö ü e i õ ;

Cns = b d g j k l m p r s t v  f h n q š z ž 
b́
d́
ǵ
ń ḱ ĺ ḿ ṕ ŕ ś ź 
t́ v́ f́ h́ 
;

CnsInZero = b d g j k l m p r s t v ;

CnsVoiced = j l m n r v ;

ArchVow = %{ae%} %{aõ%} %{äe%} %{eõ%} %{uü%} %{öü%} %{ou%} %{ei%} %{õy%} %{ao%};

ArchCns =  %{hØ%} %{jØ%} %{kØ%} %{lØ%} %{mØ%} %{nØ%} %{pØ%} %{rØ%} %{sØ%} %{tØ%} %{vØ%} %{bv%} %{bm%} %{pb%} %{td%} %{gv%} %{gl%} %{gØ%} %{kg%} %{dØ%} %{dv%} %{dl%} %{dr%} %{dn%} %{pbØ%} %{tdØ%} %{kgØ%} %{sś%} %{lĺ%} %{rŕ%} %{nń%} %{ij%} %{kḱ%} %{hh́%} %{t́d́%} %{gǵ%} %{dd́Ø%} %{dd́n%} %{dd́r%} %{dd́v%} %{gǵv%} 
%{pṕb%} 
%{dd́%}
%{vv́%}
%{ff́%}
%{tt́%}
%{mḿ%}
; 






ArchCnsInZero =  %{gØ%} %{hØ%} %{jØ%} %{kØ%} %{lØ%} %{mØ%} %{nØ%} %{pØ%} %{rØ%} %{sØ%} %{tØ%} %{vØ%} %{pbØ%} %{tdØ%} %{kgØ%} ;

Letters = Vow Cns ;

HarmDummies = %{back%} %{front%} ;

Definitions

!! Onset consonant or word boundary
OnSetC = [:Cns (Cns:) |#|%>] ;

! Back Harmony
BHARM = [ %{back%}: \[ # | %{front%}: ]* ] ;

BHARMnotA = [[[Vow - [a]] Cns:+ ArchCnsInZero: ]  %{back%}: \[ # | %{front%}: ]* ] ;


! Front Harmony
FHARM = [ %{front%}: \[ # | %{back%}: ]* ] ;


! Front Trigger
FT =   # Cns* [[ VowFront: | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;

! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ]+ Cns:* | ü:+ Cns:+ i Cns:+) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
!!€ viska%^WGStem%>aq
!!€ vis0a0%>aq
!!€ füüsiga%^StrGStem%^VowRM%>i%>dõ
!!€ füüsik000%>i%>dõ
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;



PenBetweenStemAndPenGrade = [(%^Pen: %^VOWLower:)] ;

PenBetweenStemAndStemFinal = [(HarmDummies:) ((%^Pen:) %^VOWLower:|(%^Pen:) %^VOWRaise:) (%^Pen: %^WGStem:)] ;


!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front
"%{aä%}:a"
!! __@RULENAME@__
%{aä%}:a <=>
 BHARM _ ;

"%{aä%}:ä"
!! __@RULENAME@__
%{aä%}:ä <=>
 FHARM _ ;

"%{uü%}:u"
!! __@RULENAME@__
%{uü%}:u <=>
 BHARM _ ;

"%{uü%}:ü"
!! __@RULENAME@__
%{uü%}:ü <=>
 FHARM _ ;

"%{eõ%}:õ"
!! __@RULENAME@__
%{eõ%}:õ <=>
 BHARM _ ;

"%{eõ%}:e"
!! __@RULENAME@__
%{eõ%}:e <=>
 FHARM _ \[%{PrsSg1%}:] ;
! FHARM _ \[#|.#.|%>|%-] ;

"%{ae%}:e"
!! __@RULENAME@__
%{ae%}:e <=>
  _ (HarmDummies:) (%^Pen: %^WGStem:) %^PSS: ;
!!€ tah%{tØ%}%{ae%}%^Pen%^WGStem%^PSS%>t%{uü%}
!!€ tah0e000%>tu

"%{aõ%}:õ"
!! __@RULENAME@__
%{aõ%}:õ <=>
  _ (HarmDummies:) (%^Pen: %^WGStem:) %^PSS: ;

"%{äe%}:e"
!! __@RULENAME@__
%{äe%}:e <=>
  _ (HarmDummies:) (%^Pen: %^WGStem:) %^PSS: ;


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>
!   u: _ (HarmDummies:) %^VOWLower: ;
! OnSetC   _ u: (HarmDummies:) %^VOWLower: ;
 OnSetC _ [CnsInZero:|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] VowInZero: (HarmDummies:) %^Pen: %^VOWLower: %^Pen: %^WGStem: %^VowRM:    ;
 OnSetC _ [CnsInZero: |%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow:e (HarmDummies:) %^Pen: %^VOWLower: %^Pen: %^WGStem:    ;
!!€ lugõ%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%>i
!!€ lo0000000%>i
 OnSetC _ (HarmDummies:) e %^VOWLower: ;

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>
!  ü: _ (HarmDummies:) %^VOWLower: ;
! OnSetC _ ü: (HarmDummies:) %^VOWLower: (%^WGStem:) [ # | %> [ :Cns | [ :Vow - :i ]] ] ;
   # Cns:* _ [Cns:|%{pbØ%}:|%{tdØ%}:|%{kgØ%}:] Vow: (HarmDummies:) %^VOWLower: ;
!!€ pügä%^VOWLower%^WGStem%^VowRM%>e%>t
!!€ pö00000%>e%>t
!!€ süü%^VOWLower%^WGStem%>nüq
!!€ söö00%>nüq
 OnSetC _ (HarmDummies:) e %^VOWLower: ;

"o2õ"
!! __@RULENAME@__
 o:õ <=> [Cns - [m|p|b|v]] _ o: (HarmDummies:) %^OO2Õ: %> i ;
!!€ joo%^OO2Õ%>i
!!€ jõ00%>i
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ aigo%^OO2Õ
!!€ aigõ0

"u2õ"
!! __@RULENAME@__
 u:õ <=>
       [Cns: - [m:|p:|b:|v:]] _ (HarmDummies:) (%^Pen: %^WGStem:) %^OO2Õ:  ;
!!€ laulu%^OO2Õ
!!€ laulõ0

"ö2e"
!! __@RULENAME@__
 ö:e <=> [Cns - [m|p|b|v]] _ ö: (HarmDummies:) %^OO2Õ: %> i ;
!!€ söö%^OO2Õ%>i
!!€ se00%>i

"Delabializing o and ö"
!! __@RULENAME@__
 Vx:Vy <=> [Cns - [m|p|b|v]] _ Vx: (HarmDummies:) %^OO2Õ: %> i ;
           where Vx in (%{ou%} %{öü%})
	   	 Vy in (  õ      e   ) matched ;

!FIX-ME 2019-05-8 
!! !VOWEL RAISING
!"ö:ü"
!! __@RULENAME@__
! ö:ü <=> Vow %^V7: 0:? _ (HarmDummies:) %^StrGStem: %> i: ; ! 0:
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0

!! !PALATALIZATION
"n2ń palatalization all"
!! __@RULENAME@__
 Cx:Cy <=> _ [(HarmDummies:) (%^VOWRaise:|%^CC2C:) %^PAL:| (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: %^PAL:| Vow: Cns:* (HarmDummies:) %^Pen: %^PAL: | Vow: (HarmDummies:) %^VowRM: %^PAL: | (%{jiØ%}:) (HarmDummies:) %^PAL: ] ;
       	     where Cx in (%{bb́%} %{dd́%} %{ff́%} %{gǵ%} %{hh́%}  %{lĺ%} %{mḿ%} %{nń%} %{pṕ%} %{rŕ%} %{sś%} %{tt́%} %{tt́d%} %{vv́%} %{zź%})
	           Cy in (b́   d́   f́  ǵ  h́   ĺ  ḿ  ń  ṕ  ŕ  ś  t́ t́  v́  ź) matched ;

!! asi+N+Sg+Gen:
!!€ a%{sś%}%{jiØ%}%^PAL%^VowRM%>%{aä%}
!!€ aś000%>a


"{dd́n}:d́ palatalization for 3-way"
!! __@RULENAME@__
 Cx:Cy <=> _ (HarmDummies:) (%^VOWRaise:|%^VOWLower:) (%^CC2C:)  %^PAL:  ;
           where Cx in (%{dd́n%} %{dd́v%} %{dd́Ø%} %{tt́Ø%} %{gǵv%}  %{kḱØ%} %{gǵØ%} %{pṕb%} %{kḱg%} )
                 Cy in (d́ d́ d́ t́ ǵ ḱ  ǵ  ṕ ḱ ) matched ;
! %{dd́n%}:d́ <=> _ (HarmDummies:)  %^PAL:  ;

!! andma+V+Act+Ind+Prs+Sg3
!!€ an%{dd́n%}%{back%}%^PAL
!!€ and́00
!!täht́+N+Sg+Nom:
!!€ täh%{tt́Ø%}
!!€ täht́

"{dd́n}:n weaken 3-way"
!! __@RULENAME@__
 Cx:Cy <=> _ (HarmDummies:) (%^VOWRaise:|%^VOWLower:)  %^WGStem:  ;
           where Cx in (%{dd́n%} %{dd́v%} %{dd́Ø%} %{tt́Ø%})
                 Cy in (n v 0 0) matched ;
! %{dd́n%}:n <=> _   (HarmDummies:)  %^WGStem:  ;

!! andma+V+Act+Ind+Prs+Sg1
!!€ an%{dd́n%}%{back%}%^WGStem%>%{aä%}
!!€ ann00m%a

!! püüdmä+V+Act+Ind+Prs+Sg1
!!€ pü%{üv%}%{dd́v%}%{front%}%^WGStem%>%{aä%}
!!€ püvv00%>ä

!"%{pṕb%}:ṕ"
!! __@RULENAME@__
! %{pṕb%}:ṕ <=> _   (HarmDummies:)  (%^CC2C:) %^PAL:  ;


!"%{kḱg%}:ḱ"
!! __@RULENAME@__
! %{kḱg%}:ḱ <=> _  (HarmDummies:) ( %^CC2C: ) %^PAL:  ;

!! kõiḱ+Pron+Sg+Nom
!!€ kõi%{kØ%}%{kḱg%}%^CC2C%^PAL
!!€ kõi0ḱ00



!! !VOWEL CHANGE WITH PLURAL


 
"e2i̬"
!! __@RULENAME@__
 e:i̬ <=>  [ # Cns* | Cns ] _ e: ( s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] e: _ [ s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] e: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;
!!€ keel%^VOWRaise%^PAL
!!€ ki̬i̬ĺ00
      # Cns:* _ [CnsInZero:|ArchCnsInZero:] (HarmDummies:) (%^WGStem:) %> [e:|%{eõ%}:] ;
      # Cns:* e: [CnsInZero:|ArchCnsInZero:] (HarmDummies:) (%^WGStem:) %> _ [#|.#.|%>|%-] ;
!! tegemä+V+Act+Ind+Prs+Sg1: __do__
!!€ teg%{front%}%^WGStem%>e
!!€ ti̬000%>i̬

"%{eõ%}:i̬"
 %{eõ%}:i̬ <=> # Cns:* e: [CnsInZero:|ArchCnsInZero: ] %{front%}: (%^WGStem:) %> _ %{PrsSg1%}: ;
!! tegemä+V+Act+Ind+Prs+Sg1: __do__
!!€ teg%^WGStem%>%{eõ%}
!!€ ti̬00%>i̬

 
!!€ k%{ei%}%{ei%}l%^VOWRaise%^PAL
!!€ ki̬i̬ĺ00


"õ2õ̭"
!! __@RULENAME@__
 õ:õ̭ <=>  [ # Cns* | Cns ] _ õ: ( s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ [ s | %{sś%}: | t | %{tt́%}: | k | %{kḱ%}: | l | %{lĺ%}: | n | %{nń%}: | r | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;
  > OnSetC  _ CnsInZero: a (HarmDummies:) (%^WGStem:) ;
!!€ sõda%^WGStem
!!€ sõ̭0a0


"o2u̬"
!! __@RULENAME@__
 o:u̬ <=>  [ # Cns* | Cns ] _ o: ( s | %{sś%}: | t | %{tt́%}: | %{gǵ%}: | %{pṕb%}: | %{dd́%}: | %{vv́%}: | %{ff́%}:| k | %{kḱ%}: | l (%{dd́Ø%}:|%{tt́Ø%}:) | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}:) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ [ s | %{sś%}: | t | %{tt́%}: | %{gǵ%}: | %{pṕb%}: | %{dd́%}: | %{vv́%}: | %{ff́%}:| k | %{kḱ%}: | l (%{dd́Ø%}:|%{tt́Ø%}:) | %{lĺ%}: | n | %{nń%}: | r (%{dd́Ø%}:|%{tt́Ø%}:) | %{rŕ%}: | m | %{mḿ%}: | %{td%}: s | %{td%}: %{sś%}: | r t: | r %{tt́%}: | v | %{vv́%}: ] (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;

"Vx%{ou%}:Vyo"
!! __@RULENAME@__
 Vx:Vy <=> 
 [ # Cns* | Cns ] _ Vx: [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns:] [ (HarmDummies:) %^VOWLower: | Vow: ArchCns: (HarmDummies:) %^Pen: %^VOWLower: ] ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns:] [ (HarmDummies:) %^VOWLower: | Vow: ArchCns: (HarmDummies:) %^Pen: %^VOWLower: ];
 [ # Cns* | Cns ] Vx: _ (HarmDummies:) %^VOWLower: (%^WGStem:) [#|.#.|%>|%-] ;
!  [ # Cns* | Cns ] _ Vx: ( ArchCns: | Cns | ArchCns: ArchCns: | Cns ArchCns:) Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^VOWLower: ;
! [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns | ArchCns: ArchCns: | Cns ArchCns:] Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^VOWLower: ;
         where Vx in (%{ou%} %{öü%} %{õy%} %{ei%} )
               Vy in (  o      ö      õ      e ) matched ;

"Vx%{ou%}2Vyu̬"
!! __@RULENAME@__
 Vx:Vy <=>
  [ # Cns* | Cns ] _ Vx: ( ArchCns: | ArchCns:  ArchCns: | Cns (Cns) | Cns ArchCns:) ( Vow: ) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | ArchCns:  ArchCns: | Cns (Cns) | Cns ArchCns: ] ( Vow: ) (HarmDummies:) %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ (HarmDummies:) %^VOWRaise: [#|.#.|%>|%-] ;
!! nuuĺ+N+Sg+Nom: __arrow__
!!€ n%{ou%}%{ou%}l%^VOWRaise%^PAL
!!€ nu̬u̬ĺ
  [ # Cns* | Cns ] _ Vx: ( ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns:) Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^Pen: %^VOWRaise: ;
 [ # Cns* | Cns ] Vx: _ [ ArchCns: | Cns (Cns) | ArchCns: ArchCns: | Cns ArchCns: ] Vow: ( Cns:+ | %{sØ%}: | %{hØ%}: ) (HarmDummies:) %^Pen: %^VOWRaise: ;
     	  where Vx in (%{ou%} %{öü%} %{õy%} %{ei%} )
	  	Vy in ( u̬ ü̬ õ̭ i̬ ) matched ;


"i2e"
!! __@RULENAME@__
 i:e <=> Cns _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
! Cns 0:? _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€ pin0i%^StrGStem%^PLPRT
!!€ pinne00
! # Cns* _ i: l (HarmDummies:) %^VOWLower: ;
! # Cns* i: _ l (HarmDummies:) %^VOWLower: ;
!!€ kiil%^VOWLower%>e
!!€ keel0%>e
     e _ Cns: (HarmDummies:) %^ÄI2ÄÄ: ;
!!€ leib%^ÄI2ÄÄ%^WGStem%>ä
!!€ leev0%>ä
  Cns* _ %{tdØ%}: ä: (HarmDummies:) %^Pen: %^VOWLower: ;
!!pidämä+V+Pss+Ind+Prs+Sg3: 
!!€ pi%{tdØ%}ä%{front%}%^Pen%^VOWLower%^Pen%^WGStem%^VowRM%{eõ%}%>t%{aä%}s
!!€ pe0000000e%>täs


"i:ä"
!! __@RULENAME@__
 i:ä <=> ä _ Cns: (HarmDummies:) %^ÄI2ÄÄ: ;
!!€ päiv%^ÄI2ÄÄ%>ä
!!€ pääv0%>ä


"a2o"!prepare for deprication to {ao}:o
!! __@RULENAME@__
!! {{{
 a:o <=> OnSetC  _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ) %^PLPRT: ;
!!€ kan0a%^StrGStem%^PLPRT
!!€ kanno00
!! }}}

"{ao}o"
!! __@RULENAME@__
!! {{{
 %{ao%}:o <=> OnSetC  _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: | (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ) %^PLPRT: ;
!!€ ka%{nØ%}n%{ao%}%^StrGStem%^PLPRT
!!€ kanno00
!! }}}



!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
 a:0 <=> [ Cns:| ArchCns: ] _ (HarmDummies:) [ (%^StrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^Pen: %^CC2C:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM: %> ]  ; 
! [ Cns:| ArchCns: ] _ (HarmDummies:) [ (%^StrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^Pen: %^CC2C:) (0:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM: %> ]  ; 
      a _ (HarmDummies:) %>  i  ; !!= @CODE@ 
!!€ saa%>i
!!€ sa0%>i
!!€ sõima0%^VowRM%>si
!!€ sõim0s0%>si
  [Cns:| ArchCns: ]  _ Cns: (HarmDummies:) %^Pen: %^VowRM: ;
!!€ puhkas%^Pen%^VowRM%>a%>ma
!!€ puhk0s00%>a%>ma
  _  [%{hØ%}: | %{sØ%}:] (HarmDummies:) %^VowRM:  ;
    _ (HarmDummies:) (%^VOWLower:) (%^Pen: (%^WGStem:|%^StrGStem:|%^XStrGStem:)) %^VowRM: ;

"ä:0"
!! __@RULENAME@__
 ä:0 <=>  [ Cns: | :Cns | ArchCns:] _ (s:) (HarmDummies:) [ (%^Pen: %^CC2C:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
!  [ Cns: | :Cns | ArchCns:] _ (s:) (HarmDummies:) [ (0:) (%^Pen: %^CC2C:) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
    _ (HarmDummies:) ((%^Pen: ) %^VOWLower:) (%^Pen: (%^WGStem:|%^StrGStem:|%^XStrGStem:)) %^VowRM: ;
!  [ Cns: | :Cns ] _ (HarmDummies:) [ (0:|:0+) %^VowRM: | CnsInZero: %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
                        ä _ (HarmDummies:) (%^VOWRaise:) %^VowRM:  ;
!!€ jää%^VOWRaise%^VowRM%>i
!!€ jä000%>i
!!€ elä%>et
!!€ el0%>et
  [Cns:| ArchCns: ] _ Cns: (HarmDummies:) %^Pen: %^VowRM: ;
!!€ tütär%^Pen%^VowRM%>e
!!€ tüt0r0%>e
 _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;

"u:0"
!! __@RULENAME@__
!! {{{
 u:0 <=>  [Cns:|ArchCns:| Vow: ] _ (s:) (HarmDummies:) [ (%^Pen: %^CC2C:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^VOWRaise:) %^VowRM: | CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
!!€ hirnu%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ hirn00000%>ma
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
!! juusk+N+Sg+Nom: ____
!!€ j%{ou%}%{ou%}s%{kØ%}u%^VOWRaise%^VowRM
!!€ juusk000

   OnSetC u: _ (HarmDummies:) (%^Pen: %^CC2C:) %^VowRM:  ;
!   OnSetC u: _ (HarmDummies:) (%^Pen: %^CC2C:) %^VowRM: %> i ;
!!€ kuu%^VowRM%>i%>d
!!€ ku00%>i%>d
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
!! }}}

"ü:0"
!! __@RULENAME@__
!! {{{
 ü:0 <=>  [ Cns: |ArchCns:| Vow: ] _ (s:) (HarmDummies:) [(%^Pen: %^CC2C:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^VowRM: | CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
!!€ tüü%^VowRM%>hüq
!!€ tü00%>hüq
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
   OnSetC ü: _ (HarmDummies:) (%^VOWLower:) %> i ;
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
!! }}}

"e:0"
!! __@RULENAME@__
 e:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [(%^Pen: %^CC2C:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^VowRM: | CnsInZero: (%^Pen: %^CC2C:) %^VowRM: %^CnsRM: ]  ; 
           [ Cns: | :Cns ] _ l (HarmDummies:) %^Pen: %^VowRM:  ;
           e: _ (%^VOWRaise:) %^VowRM: ;
	   _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;
!!€ herne%{hØ%}%{front%}%^VowRM%^WGStem%>id%{eõ%}
!!€ hern00000%>ide
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"o:0"
!! __@RULENAME@__
 o:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [ ((%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|%^Pen: %^CC2C:) %^VowRM: (õ) ]   ; 
           o: _ (HarmDummies:) [(%^VOWRaise:) %^VowRM:|%^OO2Õ:] ;
!!€ joo%^VOWRaise%^VowRM%>vv%>aq
!!€ ju000%>vv%>aq
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;
    _ (HarmDummies:) (%^VOWRaise:)  %^VowRM: ;

"Vx%{ou%}:0"
!! __@RULENAME@__
 Vx:0 <=>
     Vx: _ (HarmDummies:) [(%^VOWRaise:|%^VOWLower:) %^VowRM:|%^OO2Õ:] ;
!!€ joo%^VOWRaise%^VowRM%>vv%>aq
!!€ ju000%>vv%>aq
          where Vx in (%{ou%} %{öü%} %{ei%} %{õy%} ) ;

"Vx%{äe%}:0 Passive stem vowel"
!! __@RULENAME@__
 Vx:0 <=>
     _ (HarmDummies:) [(%^VOWRaise:|%^VOWLower:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^VowRM:] ;
          where Vx in (%{aõ%} %{ae%} %{äe%} ) ;


"ö:0"
!! __@RULENAME@__
 ö:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [  ((%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|%^Pen: %^CC2C:) %^VowRM: (e) ]   ; 
           ö: _ (HarmDummies:) [(%^VOWRaise:) %^VowRM:|%^OO2Õ:] ;
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"i:0"
!! __@RULENAME@__
 i:0 <=>  [ Cns: | :Cns |ArchCns:] _ (s:) (HarmDummies:) [ (%^Pen: %^CC2C:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|%^TS2S:) %^VowRM: (õ|e) | CnsInZero: %^Pen: %^VowRM: %^CnsRM: ]  ; 
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t
          Vow %{lØ%}:l l %>: _ (HarmDummies:) [õ|e] ;
!          Vow l 0:l %>: _ (HarmDummies:) [õ|e] ;
       a _ g: (HarmDummies:) %^WGStem: %> [o:|e:] ;
       a _ g: [o:|e:] (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem:  ;
!!€ aigo%^Pen%^WGStem
!!€ a00o00

!2019-01-19    Vow [ %^I7: | %^V7: 0: ] Vow: (HarmDummies:) %^StrGStem: %> _ ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;


"õ:0"
!! __@RULENAME@__
 õ:0 <=>  [ Cns: | :Cns |ArchCns:] _ [ l | r | %{lĺ%}: | %{rŕ%}: ] (HarmDummies:) (%^Pen: %^StrGStem:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^Pen: %^VowRM:  ;
!!€ tapõl%^Pen%^VowRMõ%>ma
!!€ tap0l0õ%>ma
!!€ kannõl%^StrGStem%^Pen%^VowRM%>õ
!!€ kand0l00%>õ
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
 [Cns:|ArchCns:] _ (s:) (HarmDummies:) (%^Pen: %^VOWLower: %^Pen: %^WGStem:|%^WGStem:|%^Pen: %^StrGStem:|%^Pen: %^XStrGStem:|%^Pen: %^CC2C:|(%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^VowRM:  ;
!!€ hammõ%^WGStem%^VowRM%>ihe
!!€ ham0000%>ihe
	   _  [%{hØ%}: | %{sØ%}: ] (HarmDummies:) %^VowRM:  ;
    _ (HarmDummies:) (%^Pen: %^VOWLower:) (%^Pen: %^WGStem:) %^VowRM: ;

"%{eØ%}: 0"
!! __@RULENAME@__
 %{eØ%}:0 <=> _ %{lĺ%}: (HarmDummies:) %^Pen: %^VowRM: ;

"%{õØ%}: 0"
!! __@RULENAME@__
 %{õØ%}:0 <=> _ %{lĺ%}: (HarmDummies:) %^Pen: %^VowRM: ;


!! VOWEL LENGTHENING

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> [ :u (HarmDummies:) PenBetweenStemAndStemFinal | %{ou%}:u̬ (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
       [ u (HarmDummies:) | u: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) | u̬: PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;

     
"%{XV%}:ü"
!! __@RULENAME@__
 %{XV%}:ü <=> [:ü (HarmDummies:) PenBetweenStemAndStemFinal | %{öü%}:ü̬  (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
       [ ü (HarmDummies:) | ü: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) |ü̬  (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;

"%{XV%}:o"
!! __@RULENAME@__
 %{XV%}:o <=> :o (HarmDummies:) (PenBetweenStemAndStemFinal) ( %- ) _  ;
!!€ taso%{back%}%-%{XV%}iq
!!€ taso0%-oiq
     
"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a (HarmDummies:) PenBetweenStemAndStemFinal ( %- ) _  ;
       [ o (HarmDummies:) | a (HarmDummies:) | a: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           a: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:ä"
!! __@RULENAME@__
 %{XV%}:ä <=> :ä (HarmDummies:) PenBetweenStemAndStemFinal ( %- ) _  ;
       [ ö (HarmDummies:) | ä (HarmDummies:) | ä: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           ä: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:õ"
!! __@RULENAME@__
 %{XV%}:õ <=>
   [:õ (HarmDummies:) PenBetweenStemAndStemFinal| %{õy%}: (HarmDummies:) %^VOWRaise: ] ( %- ) _  ;
        [õ  (HarmDummies:) | õ:  (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:) ] %> h _ ;
           õ: [r|l| %{lĺ%}: | %{rŕ%}: ] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 
!! kannõĺ+N+Sg+Gen: __kantele__
!!€ kan%{dn%}õ%{lĺ%}%^Pen%^VowRM%>%{XV%}
!!€ kand0l00%>õ

"%{XV%}:e"
!! __@RULENAME@__
 %{XV%}:e <=>
       [:e (HarmDummies:) PenBetweenStemAndStemFinal|:e ] ( %- ) _  ;
!! käskmä+V+Act+Ind+Prs+Neg __command/käskeä__
!!€ käs%{kØ%}%{front%}%^WGStem%>%{eõ%}%-%{XV%}iq
!!€ käs000%>e%-eiq
          [ e  (HarmDummies:) | e: (HarmDummies:) PenBetweenStemAndStemFinal (%^VowRM:)] %> h _ ;
           e: [r|l] (HarmDummies:) %^Pen: %^VowRM: %> _ ; 

"%{XV%}:i"
!! __@RULENAME@__
 %{XV%}:i <=> [:i (HarmDummies:) PenBetweenStemAndStemFinal| :i (HarmDummies:) (%^VOWRaise:) ] ( %- ) _  ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow Cns:+ Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
! Vow _ (HarmDummies:) (%^WGStem:) %> :Vow ;
         Cns _ (HarmDummies:) %^I2J: ;
!!€ asi%^I2J%>a
!!€ asj0%>a
         Vow: _  PenBetweenStemAndStemFinal %^I2J: ;
!!€ elläi%^Pen%^WGStem%^I2J%>ä
!!€ el0äj00%>ä
!         Cns _ (HarmDummies:) %^StrGStem: %> :Vow ;

"%{ij%}:j"
!! __@RULENAME@__
 %{ij%}:j  <=> Vow _ (HarmDummies:) ((%^Pen: %^VOWLower:) %^Pen: %^WGStem:) %^I2J: ;


"%{jiØ%}:j"
!! __@RULENAME@__
 %{jiØ%}:j  <=>
    [Cns:|ArchCns:] _ [(HarmDummies:) %^I2J: | Vow: (HarmDummies:) %^Pen: %^XStrGStem: ] ;

"%{jiØ%}:0"
!! __@RULENAME@__
 %{jiØ%}:0  <=>
    [Cns:|ArchCns:] _ (HarmDummies:) (%^PAL:) %^VowRM: ;
    [Cns:|ArchCns:] _ Vow: (HarmDummies:) %^Pen: %^PAL: (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!! vari+N+Sg+Gen: __shadow/varjo__
!!€ va%{rŕ%}%{jiØ%}o%^Pen%^PAL%^Pen%^WGStem
!!€ vaŕ0o0000

"%{jØ%}:0"
!! __@RULENAME@__
 %{jØ%}:0  <=>
    [Vow:|ArchVow:] _ j: Vow: (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!! vari+N+Sg+Gen: __shadow/varjo__
!!€ va%{rŕ%}%{jiØ%}o%^Pen%^PAL%^Pen%^WGStem
!!€ vaŕ0o0000
 _ Cns  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
 [Vow:|ArchVow:] _ (HarmDummies:) (%^VowRM: ) %^CnsRM: ;



"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v a q ;
         u _ d: (HarmDummies:) %^WGStem: ;
!!€ juud%^WGStem%>a
!!€ juvv0%>a

"%{uv%}:v"
!! __@RULENAME@__
 %{uv%}:v <=> u _ %> v a q ;
         u _ [ %{dv%}:|%{gv%}:] (HarmDummies:) %^WGStem: ;
!!€ ju%{uv%}%{dv%}%^WGStem%>a
!!€ juvv0%>a
         [o|õ] _ [ %{dd́v%}:|%{gǵv%}:] (HarmDummies:) %^WGStem: ;
!! joudma+V+Act+Ind+Prt+Sg1
!!€ jõ%{uv%}%{dd́v%}back%^WGStem%>i
!!€ jõvv00%>i

"%{üv%}:v"
!! __@RULENAME@__
 %{üv%}:v <=> ü _ (HarmDummies:) %> v ä q ;
         ü _ [%{dv%}: | %{dd́v%}:] (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dd́v%}%^WGStem%>et
!!€ püvv0%>et
!!€ sü%{üv%}%{dv%}%^WGStem%>ä
!!€ süvv0%>ä
         ö _ [ %{dd́v%}:|%{gǵv%}:] (HarmDummies:) %^WGStem: ;

"%^I7:i"
!! __@RULENAME@__
 %^I7:i <=> Vow _ Vow (HarmDummies:) %^StrGStem:  ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0

!FIXME 2019-05-8
!"%^V7:v with 0:v"
!! __@RULENAME@__
! %^V7:v <=> Vow _ 0: Vow (HarmDummies:) %^StrGStem:  ;
!!€ hä%^V70ö%^StrGStem
!!€ hävvü0


!! SECONDARY CONSONANT LENGTHENING

"%{Øp%}:p"
!! __@RULENAME@__
!! {{{
 %{Øp%}:p <=> Vow: _ [%{pbØ%}:|%{pb%}:] [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^XStrGStem: ;
!!€ hä%{Øp%}%{pbØ%}ü%^Pen%^XStrGStem
!!€ häppü00
   Vow: _ [%{pbv%}:|%{pb%}:] (HarmDummies:) %^XStrGStem: ;
!!tõbi+N+Sg+Ill:
!!€ tõ%{Øp%}%{pbv%}%{back%}%^XStrGStem%>%{eõ%}
!!€ tõpp00%>õ
!! }}}

"%{Øt%}:t"
!! __@RULENAME@__
 %{Øt%}:t <=> Vow _ [%{tdØ%}:|%{td%}:] [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^XStrGStem: ;
!!€ sõ%{Øt%}da%^XStrGStem%^PLPRT
!!€ sõtto00
!?? _ t Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ ü%{Øt%}te%^Pen%^StrGStem
!!€ ütte00


"%{Øk%}:k"
!! __@RULENAME@__
 %{Øk%}:k <=> Vow _ [%{kgØ%}:|%{kg%}:|g:] (HarmDummies: | [Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^XStrGStem: ;
!!€ ig0ä %^Pen%^XStrGStem
!!€ ikkä0
!??   Vow k _ Vow: (HarmDummies:) %^Pen: %^StrGStem: ;
!!€ mak0õ%^StrGStem%^VowRM%>i%>dõ
!!€ makkõ00%>i%>dõ
   Vow: _ [%{kgØ%}:|%{kg%}:] (HarmDummies:) %^XStrGStem: ;
!!mägi+N+Sg+Ill:
!!€ mä%{Øk%}%{kgØ%}%{front%}%^XStrGStem%>%{eõ%}
!!€ mäkk00%>e


"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> [ s: | %{sś%}:s ] (HarmDummies:) ((%^Pen: %^VOWLower:) %^Pen: %^WGStem: ) _ ;

"%{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=> l: (HarmDummies:) _ \[ %^PAL: ] ;

"%{XC%}:ĺ"
!! __@RULENAME@__
 %{XC%}:ĺ <=> l: (HarmDummies:) _ %^PAL: ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> [%{kg%}:k | k: ] (HarmDummies:) _ ;

"%{kḱ%}:ḱ"
!! __@RULENAME@__
 %{kḱ%}:ḱ <=> %{kØ%}:k  (HarmDummies:) _ Vow:0 %^VowRM: %^PAL: ;
 _ [(HarmDummies:) (%^VOWRaise:) (%^CC2C: | %^WGStem: ) %^PAL:| (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: %^PAL:| Vow: Cns:* (HarmDummies:) %^Pen: %^PAL:] ;
!! kakma
!!€ ka%{kØ%}%{kḱ%}u%^VowRM%^PAL
!!€ kakḱ000


!! !Consonant weakening 

! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=>
  [h|k|s] _ [Vow:|ArchVow:] (s) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!!€ puhkas%^WGStem%>taq
!!€ puh0as0%>taq

 s _ a (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ [Vow:|ArchVow:] | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
  k _ [(HarmDummies:)|[Vow:|ArchVow:] (HarmDummies:) %^Pen:] [ %^CC2C:0 | %^WGStem: ] ;
  [h|s] _ [(HarmDummies:)|[Vow:|ArchVow:] (HarmDummies:) %^Pen:] %^WGStem:  ;
!!€ ikkõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ ik000000%>ma

"%{kØ%}:0"
!! __@RULENAME@__  
 %{kØ%}:0 <=> [h|s] _ [Vow:|ArchVow:] (s|%{sØ%}:|ArchCns: ) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!!€ puhkas%^WGStem%>taq
!!€ puh0as0%>taq

 s _ a (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;

   _ [k | ḱ | %{kḱ%}: ] [(HarmDummies:)|[Vow:|ArchVow:] ( %{sØ%}: ) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen:] [ %^CC2C:0 | %^WGStem: ] ;
  [h|s] _ [(HarmDummies:)|[Vow:|ArchVow:] (HarmDummies:) %^Pen:] %^WGStem:  ;
!!€ i%{kØ%}kõ%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ i0k00000%>ma

!! kakma:
!!€ ka%{kØ%}%{kḱ%}u%{back%}%^Pen%^CC2C%^VowRM%>m%{aä%}
!!€ ka0k00000%>ma
  _ %{kḱg%}: (HarmDummies:) [ %^CC2C: | %^WGStem: ] ;

!! kõiḱ+Pron+Sg+Nom
!!€ kõi%{kØ%}%{kḱg%}%^CC2C%^PAL
!!€ kõi0ḱ00

"pToZero"
!! __@RULENAME@__
 p:0 <=>
  p _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  p _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ tapp%{back%}%^CC2C%>m%{aä%}
!!€ tap000%>ma

"%{pØ%}:0"
!! __@RULENAME@__
 %{pØ%}:0 <=>
  _ p (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  _ p [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ ta%{pØ%}p%{back%}%^CC2C%>m%{aä%}
!!€ ta0p00%>ma

!!hamõh+N+Sg+Nom
!!€ ha%{mØ%}mõ%{hØ%}%^Pen%^WGStem
!!€ ha0mõ0%>h

!! kevväi+N+Sg+Gen: __spring__
!!€ ke%{vØ%}vä%{ij%}%^Pen%^WGStem%>ä
!!€ ke0väj00%>ä


"XØToZero"
!! __@RULENAME@__
 Cx:0 <=>
  _ [Cns | ArchCns: ]  [[Vow:|ArchVow:] (Cns:* |i:) (%{hØ%}:|%{sØ%}: ) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
 [Vow:|ArchVow:] _ (HarmDummies:) (%^Pen: %^VOWLower: | %^Pen: %^VOWRaise: ) (%^Pen: %^WGStem: ) (%^VowRM: ) %^CnsRM: ;
 [Vow:|ArchVow:] _ Cns Vow (:Cns |:Vow) (HarmDummies:) (%^Pen: %^VOWLower: | %^Pen: %^VOWRaise: ) %^Pen: %^WGStem: ;
         where Cx in (%{hØ%} %{lØ%} %{mØ%} %{nØ%} %{rØ%} %{sØ%} %{vØ%}) ;
!! elläi+N+Sg+Gen
!!€ e%{lØ%}lä%{ij%}%^Pen%^WGStem%^I2J%>ä
!!€ e0läj00%>ä
!!€ ha%{mØ%}mõ%{hØ%}%{back%}%^Pen%^WGStem
!!€ ha0mõh%{back%}%^Pen%^WGStem

"StrongToWeak"
!! __@RULENAME@__
 Cx:Cy <=> [Vow|Cns] _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) ( %^Pen: %^VOWLower: | %^VOWLower:)  %^Pen:|(HarmDummies:) ( %^VOWLower:) ] %^WGStem: ;
  _ Cns Vow %{sØ%}: (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;	
      where Cx in ( %{kg%} )
            Cy in (   g   )   matched ;

!! nõkõś+N+Sg+Ill
!!€ nõ%{kg%}õ%{sś%}%^Pen%^WGStem%{XC%}%>õ%>he
!!€ nõgõs00s%>õ%>he

!!agras+A+Sg+Nom
!!€ a%{kg%}ra%{sØ%}%^Pen%^WGStem
!!€ agras00


"%{td%}:d"
!! __@RULENAME@__
 %{td%}:d <=>
 Vow _ s (HarmDummies:)  %^WGStem: ;
         Vow _ [[Vow:|ArchVow:] (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1:
!!€ kao%{td%}a%^Pen%^WGStem
!!€ kaoda0
   Vow _ [ [Vow|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
   Cns _ [Vow:|ArchVow:] Cns (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
    _ Cns Vow %{sØ%}: (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;	   

"%{tt́d%}:d"
!! __@RULENAME@__
 %{tt́d%}:d <=>
         Vow _ [[Vow:|ArchVow:] (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kergütämä+V+Act+Ind+Prs+Sg1:
!!€ kergü%{tt́d%}ä%^Pen%^WGStem
!!€ kergüdä00
   Vow _ [ [Vow|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
   Cns _ [Vow:|ArchVow:] Cns (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;



"tToZero"
!! __@RULENAME@__
 t:0 <=> [ h | t ] _ Vow (Cns:* |i:) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
         Vow _ s (HarmDummies:) (i:) %^TS2S: ;
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t
  t _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
  t _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;

!!€ att%{back%}%^CC2C%>m%{aä%}
!!€ at000%>ma


"%{tØ%}:0"
!! __@RULENAME@__
 %{tØ%}:0 <=> h _ [ [Vow:|ArchVow:] (Cns:* | ArchCns:* ) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen:| (HarmDummies:) (%^VOWLower:)] %^WGStem: ;
  _ t [(HarmDummies:)| [Vow:|ArchVow:] (ArchCns:*) (HarmDummies:) %^Pen:] %^WGStem: ;


!! !CONSONANT QUALITY CHANGE

"%{pb%}:b"
!! __@RULENAME@__
 %{pb%}:b <=> Vow _ Vow [Cns:* | ArchCns:*] (HarmDummies:) (%^Pen: %^VOWLower:)  %^Pen: %^WGStem: ;
         [Vow|Cns] _ [ [Vow:|ArchVow:] (Cns:* |:Cns+|ArchCns: |i:) (HarmDummies:) %^Pen:|(HarmDummies:) ] %^WGStem: ;
!!kipõń+N+Sg+Gen:
!!€ ki%{pb%}õ%{nń%}%{back%}%^Pen%^WGStem%>%{aä%}
!!€ kibõn000%>a
 _ Cns Vow %{sØ%}: (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;

"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow Cns:* (HarmDummies:)  (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;

"b20"
!! __@RULENAME@__
 b:0 <=> Vow _ Vow  (HarmDummies:) [(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^WGStem:] ;

"%{pbØ%}:0"
!! __@RULENAME@__
 %{pbØ%}:0 <=> Vow ( %{Øp%}: ) _ Vow  (HarmDummies:) [(%^Pen: %^VOWLower:) %^Pen: %^WGStem:|%^WGStem:] ;

"%{pbv%}:v"
!! __@RULENAME@__
 %{pbv%}:v <=> 
   Vow: %{Øp%}: _ (HarmDummies:) %^WGStem: ;
!!tõbi+N+Sg+Gen:
!!€ tõ%{Øp%}%{pbv%}%{back%}%^WGStem%>%{eõ%}
!!€ tõ0v00%>õ

"b2v"
!! __@RULENAME@__
 b:v <=>
     Vow: _ (HarmDummies:) (%^ÄI2ÄÄ:) %^WGStem: ;
!!€ leib%^ÄI2ÄÄ%^WGStem%>ä
!!€ leev0%>ä


!! hammas
!!€ ham%{bm%}a%{sØ%}%^Pen%^WGStem
!!€ hammas00

"%{bm%}:m"
!! __@RULENAME@__
 %{bm%}:m <=> _  (HarmDummies:) %^WGStem: ;
 m _ [Vow:|ArchVow:] (Cns:+| ArchCns:) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!!hammas+N+Sg+Nom: __tooth/hammas__
!!€ ham%{bm%}a%{sØ%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ hammas0000

"%{bv%}:v"
!! __@RULENAME@__
 %{bv%}:v <=> _  (HarmDummies:) %^WGStem: ;
 Cns: _ [Vow:|ArchVow:] (Cns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!!närväs+N+Sg+Nom: __tooth/hammas__
!!€ när%{bv%}ä%{sØ<%}%^Pen%^VOWLower%^Pen%^WGStem
!!€ närväs0000


"d2n"
!! __@RULENAME@__
 d:n <=> # Cns* Vow n (HarmDummies:) %> _ [a|ä] q ;
 n _ (HarmDummies:) %^WGStem: ;
 n _ [Vow:|ArchVow:] (Cns:+) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;

"%{dn%}:n"
!! __@RULENAME@__
 %{dn%}:n <=> # Cns* Vow n (HarmDummies:) %> _ [a|ä] q ;
 n _ (HarmDummies:) %^WGStem: ;
 n _ [Vow:|ArchVow:] (Cns:+|ArchCns:+| %{hØ%}:| %{sØ%}:) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!! kannõĺ+N+Sg+Nom: __kantele__
!!€ kan%{dn%}õ%{lĺ%}%^Pen%^WGStem%^PAL
!!€ kannõĺ000

"dTol"
!! __@RULENAME@__
 d:l <=> # Cns* Vow [l (HarmDummies:)|(HarmDummies:) l] %> _ [a|ä] q ;
!!€ tul%>daq
!!€ tul%>laq
!!€ kõnõl%>daq
!!€ kõnõl%>daq
    l _ (HarmDummies:) %^WGStem: ;
!!€ vald%{back%}%^WGStem%>n
!!€ vall00%>n
   l _ [Vow:|ArchVow:] (Cns:+) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;

"%{dl%}:l"
!! __@RULENAME@__
 %{dl%}:l <=> # Cns* Vow [l (HarmDummies:)|(HarmDummies:) l] %> _ [a|ä] q (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;
!!€ tul%>%{dl%}aq%^Pen%^WGStem
!!€ tul%>laq00
!!€ kõnõl%>%{dl%}aq%^Pen%^WGStem
!!€ kõnõl%>laq00
    l _ (HarmDummies:) %^WGStem: ;
!!€ val%{dl%}%{back%}%^WGStem%>n
!!€ vall00%>n
   l _ [Vow:|ArchVow:] (Cns:+) (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem: ;


"d2v"
!! __@RULENAME@__
 d:v <=> [ ü: |u: ] _ (HarmDummies:) %^WGStem: ;
!!€ püüd%^WGStem%>et
!!€ püvv0%>et
!!€ juud%^WGStem%>a
!!€ juvv0%>a
!!€ süüd%^WGStem%>ä
!!€ süvv0%>ä

"%{dv%}:v"
!! __@RULENAME@__
 %{dv%}:v <=> [ %{üv%}: |%{uv%}: ] _ (HarmDummies:) %^WGStem: ;
!!€ pü%{üv%}%{dv%}%^WGStem%>et
!!€ püvv0%>et
!!€ ju%{uv%}%{dv%}%^WGStem%>a
!!€ juvv0%>a
!!€ sü%{üv%}%{dv%}%^WGStem%>ä
!!€ süvv0%>ä



"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] (HarmDummies:|Vow: (HarmDummies:) %^WGStem: %^VowRM:) %> _ ;
    Vow _ (s) (Vow:) (HarmDummies:) %^StrD2T: ;
!!käsi+N+Sg+Par __hand, arm__
!!€ käd%^StrD2T%>t
!!€ kät0%>t
!!€ hüdsi%^StrD2T%>l
!!€ hütsi0%>l
    Vow _ (s) (HarmDummies:|[Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^StrGStem: ;
!FIXME 2019-05-08    Vow _ 0: (HarmDummies:|[Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^XStrGStem: ;


"dTos"
!! __@RULENAME@__
 d:s <=> Vow: _ (HarmDummies:) %^D2S: ;


"tTos"
!! __@RULENAME@__
 t:s <=> _ (HarmDummies:) %^D2S: ;

"tTod"
!! __@RULENAME@__
 t:d <=>
 Vow _ s (HarmDummies:) %^WGStem: ;
         Vow _ [[Vow:|ArchVow:] (HarmDummies:) (%^Pen: %^VOWLower:) %^Pen: %^WGStem:|(HarmDummies:) %^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1: 
!!€ kaota%^Pen%^WGStem
!!€ kaoda0

!2015-09-02 Vow [(%^StrGStem:| CnsInZero: %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ [%> | # | :Vow ] ;
!! There should always be a trigger


!!This should be dealt with in %{dn%}
"nTod"
!! __@RULENAME@__
 n:d <=> n _ õ: l (HarmDummies:) %^Pen: %^VowRM: ;

!!€ kannõl%^Pen%^VowRM%>õ
!!€ kand0l0%>õ


"j2i"
!! __@RULENAME@__
 j:i <=> :Vow (HarmDummies:) %> _ [ # | %- ];

"{kḱg}:g "
!! __@RULENAME@__
 %{kḱg%}:g <=> _ (HarmDummies:) %^WGStem: ;
!! kõiḱ+Pron+Sg+Gen
!!€ kõi%{kØ%}%{kḱg%}%{back%}%^WGStem%>%{eõ%}
!!€ kõi0g00%>õ


"k2g"
!! __@RULENAME@__
 k:g <=> [Vow|Cns - [h|k|s]] _ [(HarmDummies:)|Vow Cns:+ (HarmDummies:)  %^Pen: ] %^WGStem: ;
!!€ hõrak%^WGStem%>a
!!€ hõrag0%>a
!!$ hõrak%^WGStem%>a
!!$ hõraK0%>a
!!€ kisk%^WGStem%>%{uü%}%>t%{aä%}v
!!€ kis0%>u%>tav

!!€ süküs%^Pen%^WGStem%>e
!!€ sügüs0%>e



"gTok"
!! __@RULENAME@__
 g:k <=>
    Vow _ (HarmDummies:) %^StrGStem:  ; 
    Vow _ [Vow:|ArchVow:] ( l | r ) (HarmDummies:) %^Pen: %^StrGStem: ;
    Vow _ Vow: (HarmDummies:) %^StrGStem: ;
!FIX-ME 2019-05-8    Vow 0: _ (HarmDummies:|[Vow:|ArchVow:] (HarmDummies:) %^Pen:) %^XStrGStem: ;
!!€ ig0ä%^XStrGStem
!!€ ikkä0
   Vow _ [Vow:|ArchVow:] [ l | r ] (HarmDummies:) %^StrGStem: %^Pen: %^VowRM: ;
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
   [t ] (VowInZero:) (HarmDummies:) (%^VowRM:) %> _ u ;

"bTop"
!! __@RULENAME@__
 b:p <=> 
    Vow _ Vow: (HarmDummies:) %^StrGStem: ;
!FIX-ME 2019-05-8     Vow _ 0: Vow: (HarmDummies:) %^XStrGStem: ;

"%{pbØ%}:p"
!! __@RULENAME@__
 %{pbØ%}:p <=> 
    Vow ( %{Øp%}: ) _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^StrGStem: ;
    Vow %{Øp%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^XStrGStem: ;


"%{pbv%}:p"
!! __@RULENAME@__
 %{pbv%}:p <=>
   Vow: %{Øp%}: _ (HarmDummies:) %^XStrGStem: ;
!!tõbi+N+Sg+Ill:
!!€ tõ%{Øp%}%{pbv%}%{back%}%^XStrGStem%>%{eõ%}
!!€ tõpp00%>e


"%{kgØ%}:k"
!! __@RULENAME@__
 %{kgØ%}:k <=>
  Vow ( %{Øk%}: ) _ [[Vow:|ArchVow:] (HarmDummies:) (%^VOWLower:) %^Pen:|(HarmDummies:) (%^VOWLower:) ] %^StrGStem: ;
    Vow %{Øk%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^XStrGStem: ;
   Vow: %{Øk%}: _ (HarmDummies:) %^XStrGStem: ;
!!mägi+N+Sg+Ill:
!!€ mä%{Øk%}%{kgØ%}%{front%}%^XStrGStem%>%{eõ%}
!!€ mäkk00%>e


"%{tdØ%}:t"
!! __@RULENAME@__
 %{tdØ%}:t <=>
  Vow ( %{Øt%}: ) _ (s) [[Vow:|ArchVow:] (HarmDummies:) (%^VOWLower:) %^Pen:|(HarmDummies:) (%^VOWLower:)] %^StrGStem: ;
    Vow %{Øt%}: _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: %^XStrGStem: ;

!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u: | ü: | a: | ä: ] _ (HarmDummies:) (%^VowRM:)  %^CnsRM:  ;
         s _ %^TS2S: ;
!! usś+N+Sg+Par __door__
!!€ uss%^TS2S%>t
!!€ us00%>t

  s _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
!FIXME 2019-05-08  s _  [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!FIXME
!!€ vassi%{back%}%^CC2C%^I2%^PAL%>m%{aä%}
!!€ vaś00000%>ma


"d20"
!! __@RULENAME@__
 d:0 <=>
         Vow _ s ([Vow:|ArchVow:]) (HarmDummies:) %^TS2S: ;
!!€ hüdsi%^TS2S%^VowRM%>te
!!€ hü0s000%>te
!2019-06-02          Vow: _ [Vow:|ArchVow:] (HarmDummies:) PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;

"%{dØ%}:0"
!! __@RULENAME@__
 %{dØ%}:0 <=>
         [ Vow: | ArchVow: ] _ [Vow:|ArchVow:] (ArchCns: |Cns:+|:Cns+ ) (HarmDummies:) PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;


"g20"
!! __@RULENAME@__
 g:0 <=> [Cns: - [n:]|Vow:]  _ (HarmDummies:)  %^WGStem: ;
!!€ särg%^WGStem%>#
!!€ sär0e0%>0
!!€ aig%^WGStem%>o
!!€ a000%>o
   [Cns: - [n:]|Vow:] _ [[Vow:|ArchVow:] (HarmDummies:)|(HarmDummies:) %{eõ%}:] PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;
!!€ igä%^Pen%^WGStem
!!€ i0ä00
!! lugõma+V+Pss+PrfPrc __read/lukea__
!!€ lugõ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t
!! argnõma+V+Inf
!!€ arg%{back%}%{eõ%}%^Pen%^WGStemd%{aä%}q
!!€ ar00õ00daq

"%{gØ%}:0"
!! __@RULENAME@__
 %{gØ%}:0 <=> [Cns: - [n:]|Vow:]  _ (HarmDummies:)  %^WGStem: ;
!!€ sär%{gØ%}%^WGStem%>e
!!€ sär00%>e
!!€ aig%^WGStem%>o
!!€ a000%>o
   [Cns: - [n:]|Vow:] _ [[Vow:|ArchVow:] (HarmDummies:)|(HarmDummies:) %{eõ%}:] PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;
!!€ i%{kgØ%}ä%^Pen%^WGStem
!!€ i0ä00
!! lugõma+V+Pss+PrfPrc __read/lukea__
!!€ lu%{kgØ%}õ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t
!! argnõma+V+Inf
!!€ arg%{back%}%{eõ%}%^Pen%^WGStemd%{aä%}q
!!€ ar00õ00daq


!2017-02-03 Vow l _ Vow (HarmDummies:) %^WGStem: ;
!!€ palgõ%^WGStem%>h
!!€ pal0õ0%>h

"%{tdØ%}:0"
!! __@RULENAME@__
 %{tdØ%}:0 <=>
         Vow _ s [ [Vow:|ArchVow:] (HarmDummies:) %^Pen:| (HarmDummies:)] %^WGStem:  ;
!!€ hüdsi%^TS2S%^VowRM%>te
!!€ hü0s000%>te
          Vow: _ [[Vow:|ArchVow:] (HarmDummies:) ((%^Pen:) %^VOWLower:) %^Pen:|(HarmDummies:) (%^VOWLower:) ] %^WGStem: ;


"%{kgØ%}:0"
!! __@RULENAME@__
 %{kgØ%}:0 <=> 
   Vow: _ [[Vow:|ArchVow:] (HarmDummies:) ((%^Pen:) %^VOWLower:) %^Pen:| (HarmDummies:) (%^VOWLower:) ] %^WGStem: ;
    Vow: %{Øk%}: _ (HarmDummies:) %^WGStem: ;
!!jõgi+N+Sg+Gen:
!!€ jõ%{Øk%}%{kgØ%}%{back%}%^WGStem%>%{eõ%}
!!€ jõ0000%>õ


!!€ u%{jØ%}jo%{back%}%^Pen%^WGStem%>m%{aä%}
!!€ u0jo000%>ma


"Disallow %^ErrorBack:0 in BHARM"
%^ErrorBack: /<= BHARM _ ;





!!TESTS
!echo 'v a s t a l i {back}:0 > n {eõ}:õ' | hfst-pair-test src/phonology/vro-phon.compose.hfst
!echo 'vallalinõ+A+Sg+Nom' | hfst-lookup src/morphology/lexicon.hfst

!!OLD
!"ü2e"
!! __@RULENAME@__
!! {{{
! ü:e <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^U2E: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpe00
!    ü: _ (HarmDummies:) %> i ;
!! }}}

!"ü2ä"
!! __@RULENAME@__
!! {{{
! ü:ä <=> OnSetC _ (HarmDummies:) ( %^XStrGStem: | %^StrGStem: ) %^U2A: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpä00
!! }}}

!"u2a"
!! __@RULENAME@__
! u:a <=>
!       Cns: _ (HarmDummies:) (%^Pen: %^WGStem:) %^U2A:0  ;
!!€ laulu%^U2A
!!€ laulõ0

!"o2a"
!! __@RULENAME@__
! o:a <=>
!       Cns: _ (HarmDummies:) (%^Pen: %^WGStem:) %^U2A:0  ;
!!€ aigo%^U2A
!!€ aiga0

!"{dd́v}:d́ "
!!! __@RULENAME@__
! %{dd́v%}:d́ <=> _ (HarmDummies:)  %^PAL:  ;

!! püüdmä+V+Act+Ind+Prs+Sg3
!!€ pü%{üv%}%{dd́v%}%{front%}%^PAL
!!€ püüd́00

!"{dd́v}:v"
!!! __@RULENAME@__
! %{dd́v%}:v <=> _   (HarmDummies:)  %^WGStem:  ;

!"lToZero"!FIXME 2019-04
!!! __@RULENAME@__
! l:0 <=>
! l _ Vow (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;
!!€ e%{lØ%}lä%{ij%}%^Pen%^WGStem%^I2J%>ä
!!€ e0läj00%>ä
!         _ %{XC%}:l [õ|e] v (HarmDummies:) %^Pen: %^WGStem: ;

!  l _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
!  l _  [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!!€ võ%{lØ%}%{lĺ%}%{back%}%^CC2C%^I2%^PAL%>m%{aä%}
!!€ võ0ĺ0000%>ma
!  l _ VowInZero: l ;
!! kullõma+V+Inf
!!€ ku%{lØ%}l%{õØ%}%{lĺ%}%{back%}^Pen%^VowRM%>%{eõ%}%>ma
!!€ ku0l0l000%>õ%>ma

!"rToZero"
!!! __@RULENAME@__
! r:0 <=> r _ [Vow:|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;

!! pereq
!!€ pe%{rØ%}re%^WGStem%>h
!!€ pe0re0%>h
!  r _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
!  r _  [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;
!! naŕma
!!€ na%{rØ%}%{rŕ%}%{back%}%^CC2C%^PAL%>m%{aä%}
!!€ na0r000%>ma

!"b2m"
!!! __@RULENAME@__
! b:m <=> m _  (HarmDummies:) %^WGStem: ;
! m _ [Vow:|ArchVow:] (Cns:+) (HarmDummies:) %^Pen: %^WGStem: ;

!"j20"
!!! __@RULENAME@__
! j:0 <=>
!  j _ (HarmDummies:) [%^CC2C:0|%^WGStem:] ;
!  j _ [Vow:|ArchVow:] (HarmDummies:) %^Pen: [%^CC2C:0|%^WGStem:] ;

!! !Other marks
!FIXME
!!€ ki%{pb%}õ%{nń%}%>a
!!€ kipõn%>a

!"mToZero"
!! __@RULENAME@__
! m:0 <=> m_ [Vow:|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;

!"vToZero"
!! __@RULENAME@__
! v:0 <=> v_ [Vow:|ArchVow:] (Cns:* |i:) (HarmDummies:) %^Pen: %^WGStem: ;

