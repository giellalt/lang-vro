! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ǵ ḱ ĺ ń ś  ʼ

!! special letters
u̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
U̬  !!≈ * __@CODE@__ u plus U+032C COMBINING CARON BELOW
ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
Ü̬  !!≈ * __@CODE@__ ü plus U+032C COMBINING CARON BELOW
i̬	 !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
I̬	 !!≈ * __@CODE@__ i plus U+032C COMBINING CARON BELOW
õ̭	 !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
Õ̭	 !!≈ * __@CODE@__ õ plus U+032D COMBINING CIRCUMFLEX ACCENT BELOW
ˋ	 !!≈ * __@CODE@__ U+02CB MODIFIER LETTER GRAVE ACCENT

! Vowel harmony with "(t)a/ä"
 %{AÄ%}:0 	!!= @CODE@ Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0


 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel

 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant

 %^I7:0         !!= @CODE@ This appears in stem vaoma:va%^I7o for vaio
 %^V7:0         !!= @CODE@ This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0         !!= @CODE@ This appears in stem kaoma:ka%^T7o for katoq

Y7:õ  !!= __@CODE@__  This appears for syna = s%^Y7na and is rendered as õ in the norm

!! Triggers
 %^Õ2E:0	 !!= @CODE@ lugõ%^Pen%^WGStem%^Õ2E loe
 %^UU2Õ:0	!!= @CODE@  juu%^UU2Õ%>i:j0õ0%>i

 %^ÄI2ÄÄ:0 	!!= @CODE@ päiv%^ÄI2ÄÄ%>ä: päävä


! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! puhksama but puhastaq
%^APOCH:0 !!= @CODE@ This causes apochope

! 2015-03-26
%^StrD2T:0 !!= @CODE@ This changes g,d,b => k,t,p

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^StrGStem:0 !!= @CODE@ This strengthens "perädü" to "perätüt"

%^XStrGStem:0 !!= @CODE@ This extra-strengthens "sõda" to "sõtta"

%^Pen:0	      !!= @CODE@ This moves us to penultimate coda

%^PenWGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^PenVowRM:0 !!= @CODE@ syncope tapõld : taplõma 

%^D2S:0 !!= @CODE@ The ti => si
%^TS2S:0 !!= @CODE@ The -ts- => -s-

%^ʼ2Õ:0 !!= @CODE@ The ʼ => õ change
%^ʼ2E:0 !!= @CODE@ The ʼ => e change
%^ʼ2I:0 !!= @CODE@ The ʼ => i change
%^I2ʼ:0 !!= @CODE@ The i => ʼ change
%^I2J:0 !!= @CODE@ The i => j change

%^PLPRT:0 !!= @CODE@ The a:o attested in Plural kana:kanno and prt

%^VOWRaise:0 !!= @CODE@ Raises vowel
%^VOWLower:0 !!= @CODE@ Lowers vowel
%^XLowerVow:0 !!= @CODE@ Lowers vowel two levels
%^VOWLowerDelab:0 !!= @CODE@ Lowers vowel and delabializes it
%^XLowerVowDelab:0 !!= @CODE@ Lowers vowel two levels and delabializes it

%^U2E:0 !!= @CODE@ lowers u:õ and ü:e delabializes and lowers
%^U2A:0 !!= @CODE@ lowers u:a and ü:ä delabializes and lowers

%^VowRM:0 !!= @CODE@ this will remove stem final vowel
%^CnsRM:0 !!= @CODE@ this will remove stem final consonant tervüs:tervü

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y õ̭ u̬ ;
VowFront = ä ö ü ü̬ ;
VowNeutral = e i i̬ ; 
VowNonHigh = a o ä ö e õ ;
Vow = a o u ä ö ü e i õ y õ̭ ü̬ u̬ i̬ ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

CnsVoiced = j l m n r v ;

Letters = Vow Cns ;

VowDummies = %^WGStem %> %^PenVowRM ;

Definitions

!! Onset consonant or word boundary
OnSetC = [:Cns (Cns:) |#|%>] ;


! Front Trigger
FT =   # Cns* [[ VowFront: | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;

! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ]+ Cns:* | ü:+ Cns:+ i Cns:+) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
!!€ viska%^WGStem%>aq
!!€ vis0a0%>aq
!!€ füüsiga%^StrGStem%^VowRM%>i%>dõ
!!€ füüsik000%>i%>dõ
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;

PenBetweenStemAndPenGrade = [(%^Pen: %^VOWLower:)] ;

PenBetweenStemAndStemFinal = [(%^Pen: %^WGStem:)] ;


!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>
   u: _ %^VOWLower: ;
 OnSetC   _ u: %^VOWLower: ;
 OnSetC _ Cns:0 Vow:0 %^Pen: %^WGStem: %^VowRM: %> i   ;
 OnSetC _ Cns:0 Vow:e %^Pen: %^WGStem:    ;
!!€ lugõ%^WGStem%^VowRM%>i
!!€ lo0000%>i

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>  ü: _ %^VOWLower: ;
 OnSetC _ ü: %^VOWLower: (%^WGStem:) [ # | %> [ :Cns | [ :Vow - :i ]] ] ;
   # Cns:* _ Cns: Vow: %^VOWLower: ;
!!€ pügä%^VOWLower%^WGStem%^VowRM%>e%>t
!!€ pö00000%>e%>t
!!€ süü%^VOWLower%^WGStem%>nüq
!!€ söö00%>nüq

"u2õ"
!! __@RULENAME@__
 u:õ <=> OnSetC u: _ %^UU2Õ: %> i ;
!!€ juu%^UU2Õ%>i
!!€ j0õ0%>i

!! !VOWEL RAISING
"ö:ü"
!! __@RULENAME@__
 ö:ü <=> Vow %^V7: 0: _ %^StrGStem: %> i: ;
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0

!! !VOWEL CHANGE WITH PLURAL

"ʼ2õ"
!! __@RULENAME@__
!! {{{
 ʼ:õ <=> OnSetC _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %^ʼ2Õ:  ;
!!€ hangʼ%^WGStem%^ʼ2Õ#
!!€ hangõ00#
!! }}}

"ʼ2e"
!! __@RULENAME@__
!! {{{
 ʼ:e <=> OnSetC _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %^ʼ2E:  ;
!!€ särgʼ%^WGStem#
!!€ sär0e0#
!! }}}

"ʼ2i"
!! __@RULENAME@__
!! {{{
 ʼ:i <=> OnSetC  _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: ) %^ʼ2I: ;
!!€ särgʼ%^WGStem%^ʼ2I
!!€ särgi00
!! }}}

!"e2i"
!!! __@RULENAME@__
! e:i <=>  [ # Cns* | Cns ] _ e: [ k | l | n | r ] %^VOWRaise: ;
!  [ # Cns* | Cns ] e: _ [ k | l | n | r ] %^VOWRaise: ;
 
"e2i̬"
!! __@RULENAME@__
 e:i̬ <=>  [ # Cns* | Cns ] _ e: [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
 [ # Cns* | Cns ] e: _ [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
!!€ keel%^VOWRaiseʼ
!!€ kiil0ʼ

"õ2õ̭"
!! __@RULENAME@__
 õ:õ̭ <=>  [ # Cns* | Cns ] _ õ: [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;



!"o2u"
!!! __@RULENAME@__
! o:u <=>  [ # Cns* | Cns ] _ o: [ k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
! [ # Cns* | Cns ] o: _ [ k | l | n | r | m | t: s | r t: | v ] %^VOWRaise: ;


"o2u̬"
!! __@RULENAME@__
 o:u̬ <=>  [ # Cns* | Cns ] _ o: [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
 [ # Cns* | Cns ] o: _ [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;


"ö2ü̬"
!! __@RULENAME@__
 ö:ü̬ <=>  [ # Cns* | Cns ] _ ö: [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
 [ # Cns* | Cns ] ö: _ [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;

"õ2õ̬"
!! __@RULENAME@__
 õ:õ̬ <=>  [ # Cns* | Cns ] _ õ: [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;
 [ # Cns* | Cns ] õ: _ [ s | t | k | l | n | r | m | t: s | r t: | v] %^VOWRaise: ;



"i2e"
!! __@RULENAME@__
 i:e <=> Cns 0: _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€ pin0i%^StrGStem%^PLPRT
!!€ pinne00
 # Cns* _ i: l %^VOWLower: ;
 # Cns* i: _ l %^VOWLower: ;
!!€ kiil%^VOWLower%>e
!!€ keel0%>e
     e _ Cns %^ÄI2ÄÄ: ;
!!€ leiv%^ÄI2ÄÄ%>ä
!!€ leev0%>ä


"i:ä"
!! __@RULENAME@__
 i:ä <=> ä _ Cns %^ÄI2ÄÄ: ;
!!€ päiv%^ÄI2ÄÄ%>ä
!!€ pääv0%>ä


"ü2e"
!! __@RULENAME@__
!! {{{
 ü:e <=> OnSetC _ ( %^XStrGStem: | %^StrGStem: ) %^U2E: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpe00
    ü: _ %> i ;
!! }}}

"ü2ä"
!! __@RULENAME@__
!! {{{
 ü:ä <=> OnSetC _ ( %^XStrGStem: | %^StrGStem: ) %^U2A: ;
!!€ häbü%^StrGStem%^PLPRT
!!€ häpä00
!! }}}

"a2o"
!! __@RULENAME@__
!! {{{
 a:o <=> OnSetC  _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€ kan0a%^StrGStem%^PLPRT
!!€ kanno00
!! }}}


"õ2y"
!! __@RULENAME@__
!! {{{
 õ:y <=> OnSetC  _ Cns:0 a (%^WGStem:) ;
!!€ sõda%^WGStem
!!€ sy0a0
!! }}}

"õ2e"
!! __@RULENAME@__
 õ:e <=>  _ (%^Pen: %^WGStem:) %^Õ2E:;
!!€ lugõ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t

!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
 a:0 <=>  Cns: _ [ (%^StrGStem:|%^Pen: %^WGStem:) (0:) %^VowRM: | Cns:0 %^VowRM: %^CnsRM: %> ]  ; 
      a _ %>  i  ; !!= @CODE@ 
!!€ saa%>i
!!€ sa0%>i
!!€ sõima0%^VowRM%>si
!!€ sõim0s0%>si
  Cns:  _ Cns: (ʼ:) %^Pen: %^VowRM: ;
!!€ puhkas%^Pen%^VowRM%>a%>ma
!!€ puhk0s0%>a%>ma


"ä:0"
!! __@RULENAME@__
 ä:0 <=>  [ Cns: | :Cns ] _ [ (0:|:0+) %^VowRM: | Cns:0 %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
 ä _ %>  i  ; !!= @CODE@ 
!!€ elä%>et
!!€ el0%>et
  Cns: _ Cns: (ʼ:) %^Pen: %^VowRM: ;
!!€ tütär%^Pen%^VowRM%>e
!!€ tüt0r0%>e

"u:0"
!! __@RULENAME@__
!! {{{
 u:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
   OnSetC _ u: [%^VowRM:|%^UU2Õ:] %> i ;
!!€ kuu%^VowRM%>i%>d
!!€ k0u0%>i%>d
!!€ juu%^UU2Õ%>i
!!€ j0õ0%>i
!! }}}

"ü:0"
!! __@RULENAME@__
!! {{{
 ü:0 <=>  [ Cns: | Vow: ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!!€ tüü%^VowRM%>hüq
!!€ tü00%>hüq
!!€ tervüs%^VowRM%^CnsRM%>i%>t
!!€ terv0000%>i%>t
   OnSetC _ ü: (%^VOWLower:) %> i ;
!! }}}

"e:0"
!! __@RULENAME@__
 e:0 <=>  [ Cns: | :Cns ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
           [ Cns: | :Cns ] _ l %^Pen: %^VowRM:  ;

"o:0"
!! __@RULENAME@__
 o:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:|%^StrGStem:) %^VowRM: (õ) ]   ; 

"i:0"
!! __@RULENAME@__
 i:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:|%^TS2S:) %^VowRM: (õ|e) | Cns:0 %^Pen: %^VowRM: %^CnsRM: ]  %> ; 
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t
          Vow l 0:l %>: _ [õ|e] ;
       a _ g: %^WGStem: %> [o:|e:] ;
!!€ aig%^WGStem%>o
!!€ a000%>o
    Vow [ %^I7: | %^V7: 0: ] Vow: %^StrGStem: %> _ ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0


"õ:0"
!! __@RULENAME@__
 õ:0 <=>  [ Cns: | :Cns ] _ [ l | r ] (%^StrGStem:) %^Pen: %^VowRM:  ;
!!€ tapõl%^Pen%^VowRMõ%>ma
!!€ tap0l0õ%>ma
!!€ kannõl%^StrGStem%^Pen%^VowRM%>õ
!!€ kand0l00%>õ
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
 Cns: _ (%^Pen: %^VOWLower: %^Pen: %^WGStem:|%^WGStem:|%^StrGStem:|%^Pen: %^WGStem:) %^VowRM:  ;
!!€ hammõ%^WGStem%^VowRM%>ihe
!!€ ham0000%>ihe

!! VOWEL LENGTHENING

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> :u ( %- ) _  ;
       [ u | u: (%^VowRM:) | u̬: (%^VowRM:)] %> h _ ;

     
"%{XV%}:ü"
!! __@RULENAME@__
 %{XV%}:ü <=> :ü ( %- ) _  ;
       [ ü | ü: (%^VowRM:) |ü̬: (%^VowRM:) ] %> h _ ;

     
"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a ( %- ) _  ;
       [ o | a | a: (%^VowRM:) ] %> h _ ;

"%{XV%}:ä"
!! __@RULENAME@__
 %{XV%}:ä <=> :ä ( %- ) _  ;
       [ ö | ä | ä: (%^VowRM:) ] %> h _ ;

"%{XV%}:õ"
!! __@RULENAME@__
 %{XV%}:õ <=> :õ ( %- ) _  ;
        [õ | õ: (%^VowRM:) ] %> h _ ;

"%{XV%}:e"
!! __@RULENAME@__
 %{XV%}:e <=>  [ e | e: (%^VowRM:)] %> h _ ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow Cns:+ Vow _ (%^WGStem:) %> :Vow ;
! Vow _ (%^WGStem:) %> :Vow ;
         Cns _ %^I2J: ;
!!€ asi%^I2J%>a
!!€ asj0%>a
         Vow: _ PenBetweenStemAndStemFinal %^I2J: ;
!!€ elläi%^Pen%^WGStem%^I2J%>ä
!!€ el0äj00%>ä
!         Cns _ %^StrGStem: %> :Vow ;

"i2ʼ"
!! __@RULENAME@__
 i:ʼ <=>
  Cns _ %^I2ʼ: ;
! Cns _ %^WGStem: %> :Vow ;


"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v a q ;
         u _ d: %^WGStem: ;
!!€ juud%^WGStem%>a
!!€ juvv0%>a

"ü2v"
!! __@RULENAME@__
 ü:v <=> ü _ %> v ä q ;
         ü _ d: %^WGStem: ;
!!€ püüd%^WGStem%>et
!!€ püvv0%>et
!!€ süüd%^WGStem%>ä
!!€ süvv0%>ä

"%^I7:i"
!! __@RULENAME@__
 %^I7:i <=> Vow _ Vow %^StrGStem: %> [ i: | q ] ;
!!€ va%^I7o%^StrGStem%>i
!!€ vaio0%>0

"%^V7:v with 0:v"
!! __@RULENAME@__
 %^V7:v <=> Vow _ 0: Vow %^StrGStem: %> i: ;
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0


!! SECONDARY CONSONANT LENGTHENING

"0:m"
!! __@RULENAME@__
 0:m <=>
         Vow m _ (Vow: %^Pen:) %^StrGStem: ;
!!nimi+N+Sg+Par __name__
!!€ nim0%^StrGStem%>e
!!€ nimm0%>e

"0:p"
!! __@RULENAME@__
!! {{{
 0:p <=> Vow b: _ Vow: %^Pen: %^XStrGStem: ;
!!€ häb0ü%^XStrGStem
!!€ häppü00
!! }}}

"0:v"
!! __@RULENAME@__
!! {{{
 0:v <=> Vow %^V7: _ Vow: %^StrGStem: ;
!!€ hä%^V70ö%^StrGStem%>i
!!€ hävvü0%>0
 Vow v _ Vow: %^Pen: %^StrGStem: ;
!!€ kiv0i%^StrGStem
!!€ kivvi0
!! }}}


"0:n"
!! __@RULENAME@__
 0:n <=> Vow n _ Vow: %^Pen: %^StrGStem: ;

!        Vow n _ :Vow %> j:i ;
!	 Vow n _ %> i: (õ|e) [ %> | # ] ;

"0:t"
!! __@RULENAME@__
 0:t <=> Vow d: _ Vow: %^Pen: %^XStrGStem: ;
!!€ sõd0a%^XStrGStem%^PLPRT
!!€ sõtto00
 t _ Vow: %^Pen: %^StrGStem: ;
!!€ üt0e%^Pen%^StrGStem
!!€ ütte00

"0:s"
!! __@RULENAME@__
 0:s <=> CnsVoiced Vow: _ %^VowRM: %> s i ;
!!€ sõima0%^VowRM%>si
!!€ sõim0s0%>si
   Vow s: _ (ʼ:) (%^Pen: %^WGStem:) %^StrGStem: ;
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ
   Vow s: _ Vow: %^Pen: %^StrGStem: ;
!!€ kos0i%^StrGStem%>q
!!€ kossi0%>q

"0:l"
!! __@RULENAME@__
 0:l <=> Vow l _ :Vow %> j:i ;
         Vow l _ Vow: %^Pen: %^StrGStem: ;
!	 Vow l _ %> i: (õ|e) [ %> | # ] ;

"0:r"
!! __@RULENAME@__
 0:r <=>
         Vow r _ Vow: %^Pen: %^StrGStem: ;

"0:h"
!! __@RULENAME@__
 0:h <=> Vow h _ Vow: %^Pen: %^StrGStem: ;


"0:k"
!! __@RULENAME@__
 0:k <=> Vow g: _ Vow: %^Pen: %^XStrGStem: ;
!!€ ig0ä%^XStrGStem
!!€ ikkä0
   Vow k _ Vow: %^Pen: %^StrGStem: ;
!!€ mak0õ%^StrGStem%^VowRM%>i%>dõ
!!€ makkõ00%>i%>dõ

"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> s: _ ;

"%{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=> l: _ ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> k: _ ;


!! !Consonant weakening 

! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> h _ Vow: s %^Pen: %^WGStem: ;
!!€ puhkas%^WGStem%>taq
!!€ puh0as0%>taq

 s _ a %^Pen: %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ Vow: | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^D2S:0 i %> %^WCns:0 ] ;

"mToZero"
!! __@RULENAME@__
 m:0 <=> m_ Vow: (Cns:* |i:) %^Pen: %^WGStem: ;
!!€ hammõ%^Pen%^WGStem%>h
!!€ ham0õ0%>h

"vToZero"
!! __@RULENAME@__
 v:0 <=> v_ Vow: (Cns:* |i:) %^Pen: %^WGStem: ;
!! kevväi+N+Sg+Gen: __spring__
!!€ kevväi%^Pen%^WGStem%>ä
!!€ kev0äj00%>ä

"lToZero"
!! __@RULENAME@__
 l:0 <=> l _ Vow (Cns:* |i:) %^Pen: %^WGStem: ;
!!€ elläi%^Pen%^WGStem%^I2J%>ä
!!€ el0äj00%>ä
         _ %{XC%}:l [õ|e] v %^Pen: %^WGStem: ;

"rToZero"
!! __@RULENAME@__
 r:0 <=> r _ Vow: (Cns:* |i:) %^Pen: %^WGStem: ;
!!€ perre%^WGStem%>h
!!€ per0e0%>h

"tToZero"
!! __@RULENAME@__
 t:0 <=> [ h | t ] _ Vow (Cns:* |i:) %^Pen: %^WGStem: ;
         Vow _ s (ʼ:|i:) %^TS2S: ;
!! hüdsi+N+Sg+Par:
!!€ hüdsi%^TS2S%^VowRM%>t
!!€ hü0s000%>t

!! !CONSONANT QUALITY CHANGE

"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow Cns:* (ʼ:) %^Pen: %^WGStem: ;

"b20"
!! __@RULENAME@__
 b:0 <=> Vow _ Vow  [%^Pen: %^WGStem:|%^WGStem:] ;

"bTop"
!! __@RULENAME@__
 b:p <=> 
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


"b2m"
!! __@RULENAME@__
 b:m <=> m _  %^WGStem: ;
 m _ Vow: (Cns:+) %^Pen: %^WGStem: ;
!!€ hamba%^Pen%^WGStem%>s
!!€ hamma0%>s

"d2n"
!! __@RULENAME@__
 d:n <=> n _ %^WGStem: ;
 n _ Vow: (Cns:+) %^Pen: %^WGStem: ;

"dTol"
!! __@RULENAME@__
 d:l <=> # Cns* Vow l %> _ [a|ä] q ;
!!€ tul%>daq
!!€ tul%>laq
!!€ kõnõl%>daq
!!€ kõnõl%>daq

"d2v"
!! __@RULENAME@__
 d:v <=> [ ü: |u: ] _ %^WGStem: ;
!!€ püüd%^WGStem%>et
!!€ püvv0%>et
!!€ juud%^WGStem%>a
!!€ juvv0%>a
!!€ süüd%^WGStem%>ä
!!€ süvv0%>ä

"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] (Vow: %^WGStem: %^VowRM:) %> _ ;
    Vow _ (s) (Vow:) %^StrD2T: ;
!!käsi+N+Sg+Par __hand, arm__
!!€ käd%^StrD2T%>t
!!€ kät0%>t
!!€ hüdsi%^StrD2T%>l
!!€ hütsi0%>l
    Vow _ (s) Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


"dTos"
!! __@RULENAME@__
 d:s <=> Vow: _ %^D2S: ;


"tTos"
!! __@RULENAME@__
 t:s <=> _ %^D2S: ;

"tTod"
!! __@RULENAME@__
 t:d <=>
 Vow _ s (ʼ:) %^WGStem: ;
         Vow _ [Vow: %^Pen: %^WGStem:|%^WGStem: (%>) :Vow ] ;
!! kaotama+V+Act+Ind+Prs+Sg1: 
!!€ kaota%^Pen%^WGStem
!!€ kaoda0

!2015-09-02 Vow [(%^StrGStem:| Cns:0 %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ [%> | # | :Vow ] ;
!! There should always be a trigger

"nTod"
!! __@RULENAME@__
 n:d <=> n _ õ: l %^Pen: %^VowRM: ;

!!€ kannõl%^Pen%^VowRM%>õ
!!€ kand0l0%>õ


"j2i"
!! __@RULENAME@__
 j:i <=> :Vow %> _ [ # | %- ];

"k2g"
!! __@RULENAME@__
 k:g <=> Vow _ %^WGStem: ;
!!€ hõrak%^WGStem%>a
!!€ hõrag0%>a
     Vow _ Vow Cns:+ (ʼ:) %^Pen: %^WGStem: ;
!!€ süküs%^Pen%^WGStem%>e
!!€ sügüs0%>e
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ

"gTok"
!! __@RULENAME@__
 g:k <=> 
    Vow _ Vow: ( l | r ) %^Pen: %^StrGStem: ;
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;
!!€ ig0ä%^XStrGStem
!!€ ikkä0
   Vow _ Vow: [ l | r ] %^StrGStem: %^Pen: %^VowRM: ;
!!€ kogõr%^StrGStem%^Pen%^VowRM%>õ
!!€ kok0r00%>õ
   [t ] (Vow:0) (%^VowRM:) %> _ u ;

!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u: | ü: | a: | ä: ] _ (%^VowRM:)  %^CnsRM:  ;
         s _ %^TS2S: ;
!! ussʼ+N+Sg+Par __door__
!!€ uss%^TS2S%>t
!!€ us00%>t

"d20"
!! __@RULENAME@__
 d:0 <=>
         Vow _ s (ʼ:|Vow:) %^TS2S: ;
!!€ hüdsi%^TS2S%^VowRM%>te
!!€ hü0s000%>te
          Vow: _ Vow: PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;

"g20"
!! __@RULENAME@__
 g:0 <=> [Cns:|Vow:]  _ ( ʼ: ) %^WGStem: ;
!!€ särgʼ%^WGStem%>#
!!€ sär0e0%>0
!!€ aig%^WGStem%>o
!!€ a000%>o
   Vow: _ Vow: PenBetweenStemAndPenGrade %^Pen: %^WGStem: ;
!!€ igä%^Pen%^WGStem
!!€ i0ä00
!! lugõma+V+Pss+PrfPrc __read/lukea__
!!€ lugõ%^Pen%^WGStem%^Õ2E%>t
!!€ lo0e000%>t


 Vow l _ Vow %^WGStem: ;
!!€ palgõ%^WGStem%>h
!!€ pal0õ0%>h

!! !Other marks
"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=>  :Cns _ (%^WGStem:|%^Pen: %^WGStem:) (%^StrGStem:|%^TS2S:) %> [:Vow|t] ;
!!€ kipõnʼ%>a
!!€ kipõn0%>a
!!€ nõkõs0ʼ%^Pen%^WGStem%^StrGStem%>õ
!!€ nõgõss000%>õ
