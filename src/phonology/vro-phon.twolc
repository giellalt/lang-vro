! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ǵ ḱ ĺ ń ś  ʼ

! Vowel harmony with "(t)a/ä"
 %{AÄ%}:0 	!!= @CODE@ Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
 %{OÖ%}:0 	!!= @CODE@ Vowel harmony with oö OÖ1:o OÖ1:ö

! Vowel harmony with "ttu/tty"
 %{UÜ%}:0 	!!= @CODE@ Vowel harmony with uü UY1:u UY1:y

 %{ÕE%}:0       !!= @CODE@ Vowel harmony with õe

 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel

 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant


! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^StrGStem:0 !!= @CODE@ This strengthens "perädü" to "perätüt"

%^XStrGStem:0 !!= @CODE@ This extra-strengthens "sõda" to "sõtta"


%^TS:0 !!= @CODE@ The ti => si

%^PLPRT:0 !!= @CODE@ The a:o attested in Plural kana:kanno and prt

%^RVow:0 !!= @CODE@ Raises vowel
%^LowerVow:0 !!= @CODE@ Lowers vowel

%^VowRM:0 !!= @CODE@ this will remove stem final vowel
%^CnsRM:0 !!= @CODE@ this will remove stem final consonant tervüs:tervü

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y;
VowFront = ä ö ü ;
VowNeutral = e i ; 
VowNonHigh = a o ä ö e ;
Vow = a o u ä ö ü e i õ y ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

Letters = Vow Cns ;

VowDummies = %^WGStem %> ;

Definitions

! Front Trigger
FT =   # Cns* [[ VowFront | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;
!€# füüsiga%^StrGStem%^VowRM%>i%>d%{ÕE%}
!€0 füüsik0000i0de
! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ] ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %> ) %{AÄ%}: | %{OÖ%}: | %{UÜ%}: | Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vow [ ( %> %^LCns:0 %^LVow:0 | %> %^LCns:0 | %> i %> | %> ) [ V1:Vow | %{AÄ%}: ] |  0:Vow %> ( %^LCns:0 | %^WCns: ) %^LVow: | %> %^LCns:0  ] ] ;

!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front
"%{AÄ%}:ä"
!! __@RULENAME@__  
 %{AÄ%}:ä <=> FT %> [ :j | m |(s i) v | t | d: ] _   ;
!              FT  :Cns (ʼ:) (%^StrGStem:|%^WGStem:) %> _ ;
	      FT  (%^StrGStem:|%^WGStem:) (%^VowRM:) ( %> i v | %> s i v ) _ ;
!%^VowRM%>iv%{AÄ%}q
!! !Vowel harmony suffixes Back
"%{AÄ%}:a"
!! __@RULENAME@__  
 %{AÄ%}:a <=> BT %> [ :j | m |(s i) v | t | d: ] _   ;
!              BT  :Cns (ʼ:) (%^StrGStem:|%^WGStem:) %> _ ;
	      BT  (%^StrGStem:|%^WGStem:) (%^VowRM:)  _  ;
!	      BT  (%^StrGStem:|%^WGStem:) (%^VowRM:) ( %> i v | %> s i v ) _  ;
!
! Vowel harmony suffixes Front
"%{UÜ%}:ü"
!! __@RULENAME@__  
 %{UÜ%}:ü <=>  FT _ ;

! Vowel harmony suffixes Back
"%{UÜ%}:u"
!! __@RULENAME@__  
 %{UÜ%}:u <=>  BT _ ;

"%{OÖ%}:ö"
!! __@RULENAME@__  
 %{OÖ%}:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"%{OÖ%}:o"
!! __@RULENAME@__  
 %{OÖ%}:o <=>  BT _ ;

! Vowel harmony suffixes Front
"%{ÕE%}:e"
!! __@RULENAME@__
 %{ÕE%}:e <=>  FT _ ;

! Vowel harmony suffixes Back
"%{ÕE%}:õ"
!! __@RULENAME@__
 %{ÕE%}:õ <=>  BT _ ;


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>  u: _ %^LowerVow: ;
    _ u: %^LowerVow: ;

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>  ü: _ %^LowerVow: ;
    _ ü: %^LowerVow: ;

"u2õ"
!! __@RULENAME@__
 u:õ <=> u: _ %> i ;


!! !VOWEL CHANGE WITH PLURAL

"ʼ2e"
!! __@RULENAME@__
!! {{{
 ʼ:e <=>  _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %> \[ :Vow | Vow: ]  ;
!€# särgʼ%^WGStem#
!€0 sär0e00
!! }}}

"ʼ2i"
!! __@RULENAME@__
!! {{{
 ʼ:i <=>  _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: ) %^PLPRT: ;
!€# särgʼ%^WGStem%^PLPRT
!€0 särgi00
!! }}}

"i2e"
!! __@RULENAME@__
!! {{{
 i:e <=>  _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!€# pin0i%^StrGStem%^PLPRT
!€0 pinne00
!! }}}

"ü2e"
!! __@RULENAME@__
!! {{{
 ü:e =>  _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!€# häbü%^StrGStem%^PLPRT
!€0 häpe00
    ü: _ %> i ;
!! }}}

"ü2ä"
!! __@RULENAME@__
!! {{{
 ü:ä =>  _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!€# häbü%^StrGStem%^PLPRT
!€0 häpä00
!! }}}

"a2o"
!! __@RULENAME@__
!! {{{
 a:o <=>   _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!€# kan0a%^StrGStem%^PLPRT
!€0 kanno00
!! }}}


"õ2y"
!! __@RULENAME@__
!! {{{
 õ:y <=>   _ Cns:0 a (%^WGStem:) ;
!€# sõda%^WGStem
!€0 sy0a0
!! }}}

!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
!! {{{
 a:0 <=>  Cns: _ [ (%^StrGStem:) %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
      a _ %> i ; !!= @CODE@ 
!€# saa>i
!€0 sa00i
!! }}}

"ä:0"
!! __@RULENAME@__
 ä:0 <=>  [ Cns: | :Cns ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 

"u:0"
!! __@RULENAME@__
!! {{{
 u:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!€# tervüs%^VowRM%^CnsRM%>i%>t
!€0 terv00000i0t
      _ u: %> i ;
!! }}}

"ü:0"
!! __@RULENAME@__
!! {{{
 ü:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!€# tervüs%^VowRM%^CnsRM%>i%>t
!€0 terv00000i0t
         _ ü: %> i ;
!! }}}

"e:0"
!! __@RULENAME@__
 e:0 <=>  [ Cns: | :Cns ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 

"o:0"
!! __@RULENAME@__
 o:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:|%^StrGStem:) %^VowRM: (%{ÕE%}:|%{AÄ%}:) ]   ; 

"i:0"
!! __@RULENAME@__
 i:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:) %^VowRM: (%{ÕE%}:) | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
          Vow l 0:l %>: _ %{ÕE%}: ;


!!

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> :u ( %- ) _  ;

"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a ( %- ) _  ;

"%{XV%}:ä"
!! __@RULENAME@__
 %{XV%}:ä <=> :ä ( %- ) _  ;

"%{XV%}:õ"
!! __@RULENAME@__
 %{XV%}:õ <=> :õ ( %- ) _  ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow _ (%^WGStem:) %> :Vow ;
         Cns _ %^StrGStem: %> :Vow ;

"i2ʼ"
!! __@RULENAME@__
 i:ʼ <=> Cns _ %^WGStem: %> :Vow ;


"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v %{AÄ%}: q ;

"ü2v"
!! __@RULENAME@__
 ü:v <=> ü _ %> v %{AÄ%}: q ;




!! Secondary Consonant lengthening

"0:p"
!! __@RULENAME@__
!! {{{
 0:p <=> Vow b: _ Vow: %^XStrGStem: ;
!€# häb0ü%^XStrGStem
!€0 häppü00
!! }}}

"0:n"
!! __@RULENAME@__
 0:n <=> Vow n _ Vow: %^StrGStem: ;

!        Vow n _ :Vow %> j:i ;
!	 Vow n _ %> i: (%{ÕE%}: ) [ %> | # ] ;

"0:t"
!! __@RULENAME@__
!! {{{
 0:t <=> Vow d: _ Vow: %^XStrGStem: ;
!€# sõd0a%^XStrGStem%^PLPRT
!€0 sõtto00
!! }}}

"0:l"
!! __@RULENAME@__
 0:l <=> Vow l _ :Vow %> j:i ;
         Vow l _ Vow: %^StrGStem: ;
	 Vow l _ %> i: (%{ÕE%}: ) [ %> | # ] ;

"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> s: _ ;

"%{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=> l: _ ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> k: _ ;


!! !Consonant weakening 

! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> s _ a %^WGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ Vow: | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;

"lToZero"
!! __@RULENAME@__
 l:0 <=> l _ Vow i: %^WGStem: ;
         _ %{XC%}:l %{ÕE%}: v %^WGStem: ;


!! !CONSONANT QUALITY CHANGE

"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow [ n (ʼ:) ] %^WGStem: ;

"b20"
!! __@RULENAME@__
 b:0 <=> Vow _ Vow  %^WGStem: ;

"bTop"
!! __@RULENAME@__
 b:p <=> 
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


"d2n"
!! __@RULENAME@__
 d:n <=> n _ %^WGStem: ;

"dTol"
!! __@RULENAME@__
 d:l <=> l %> _ %{AÄ%}: q ;

!%>d%{AÄ%}q

"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] (Vow: %^WGStem: %^VowRM:) %> _ ;
    Vow _ (s) Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;

"tTod"
!! __@RULENAME@__
 t:d <=> Vow [(%^StrGStem:| Cns:0 %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ ;

"j2i"
!! __@RULENAME@__
 j:i <=> :Vow %> _ [ # | %- ];

"k2g"
!! __@RULENAME@__
 k:g <=> Vow _ %^WGStem: ;
!€# hõrak%^WGStem>%{AÄ%}
!€0 hõrag00a

"gTok"
!! __@RULENAME@__
 g:k <=> 
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u: | ü: | a: | ä: ] _ (%^VowRM:)  %^CnsRM:  ;

"d20"
!! __@RULENAME@__
 d:0 <=>  Vow _ s Vow: %^WGStem: (%^VowRM:)  ;
          Vow: _ Vow: %^WGStem: ;

"g20"
!! __@RULENAME@__
!! {{{
 g:0 <=>  _ ʼ: %^WGStem: ;
!€# särgʼ%^WGStem>#
!€0 sär0e000
!! }}}

!! !Other marks
"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=>  :Cns _ (%^WGStem:) %> :Vow ;
!€# kipõnʼ>%{AÄ%}
!€0 kipõn00a
