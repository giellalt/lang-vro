! ============================================ 
!! !!! The Võro morphophonological/twolc rules file 
!  ============================================ 


Alphabet
a b d e f g h i j k l m n o p q r s š t u v y z ž õ ü ä ö
A B D E F G H I J K L M N O P Q R S Š T U V Y Z Ž Õ Ü Ä Ö

ǵ ḱ ĺ ń ś  ʼ

! Vowel harmony with "(t)a/ä"
 %{AÄ%}:0 	!!= @CODE@ Vowel harmony with "(t)a/ä" AÄ1:a AÄ1:ä AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
 %{OÖ%}:0 	!!= @CODE@ Vowel harmony with oö OÖ1:o OÖ1:ö

! Vowel harmony with "ttu/tty"
 %{UÜ%}:0 	!!= @CODE@ Vowel harmony with uü UY1:u UY1:y

 %{ÕE%}:0       !!= @CODE@ Vowel harmony with õe

 %{XV%}:0		!!= @CODE@ This is used for echoing the previous vowel

 %{XC%}:0	!!= @CODE@ This is used for lengthening a consonant

 %^I7:0         !!= @CODE@ This appears in stem vaoma:va%^I7o for vaio
 %^V7:0         !!= @CODE@ This appears in stem häömä:hä%^V7ö for hävvü
 %^T7:0         !!= @CODE@ This appears in stem kaoma:ka%^T7o for katoq

!! Triggers
! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! puhksama but puhastaq
%^APOCH:0 !!= @CODE@ This causes apochope

! 2015-03-26
%^StrD2T:0 !!= @CODE@ This changes g,d,b => k,t,p

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^StrGStem:0 !!= @CODE@ This strengthens "perädü" to "perätüt"

%^XStrGStem:0 !!= @CODE@ This extra-strengthens "sõda" to "sõtta"

%^PenWGStem:0 !!= @CODE@ This weakens "kipõnʼ" to "kibõna"

%^PenVowRM:0 !!= @CODE@ syncope tapõld : taplõma 

%^TS:0 !!= @CODE@ The ti => si
%^TS2S:0 !!= @CODE@ The -ts- => -s-

%^I2ʼ:0 !!= @CODE@ The i => ʼ change
%^I2J:0 !!= @CODE@ The i => j change

%^PLPRT:0 !!= @CODE@ The a:o attested in Plural kana:kanno and prt

%^VOWRaise:0 !!= @CODE@ Raises vowel
%^LowerVow:0 !!= @CODE@ Lowers vowel
%^XLowerVow:0 !!= @CODE@ Lowers vowel two levels
%^LowerVowDelab:0 !!= @CODE@ Lowers vowel and delabializes it
%^XLowerVowDelab:0 !!= @CODE@ Lowers vowel two levels and delabializes it

%^U2E:0 !!= @CODE@ lowers u:õ and ü:e delabializes and lowers
%^U2A:0 !!= @CODE@ lowers u:a and ü:ä delabializes and lowers

%^VowRM:0 !!= @CODE@ this will remove stem final vowel
%^CnsRM:0 !!= @CODE@ this will remove stem final consonant tervüs:tervü

%: !used with Proper nouns
%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowBack = a o u õ y;
VowFront = ä ö ü ;
VowNeutral = e i ; 
VowNonHigh = a o ä ö e ;
Vow = a o u ä ö ü e i õ y ;

Cns = b d f g h j k l m n p q r s š t v z ž ;

CnsVoiced = j l m n r v ;

Letters = Vow Cns ;

VowDummies = %^WGStem %> %^PenVowRM ;

Definitions

! Front Trigger
FT =   # Cns* [[ VowFront: | :VowFront ] \[ # | :VowBack ]* | [VowNeutral ] \[ # | :VowBack | VowBack: ]* ]  ;

! # Cns* [[ VowFront | VowNeutral ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;
! # Cns* [[ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


! Back Trigger 
BT =   # Cns* ( [ VowNeutral ]+ Cns:* | ü:+ Cns:+ i Cns:+) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;
!!€# viska%^WGStem%>aq
!!€0 vis0a0%>aq
!!€# füüsiga%^StrGStem%^VowRM%>i%>d%{ÕE%}
!!€0 füüsik000%>i%>dõ
! # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ %{OÖ%}: | %{UÜ%}: | Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;


!! !!!Rules

Rules

!! !!VOWEL HARMONY 
!! !Vowel harmony suffixes Front
!
! Vowel harmony suffixes Front
"%{UÜ%}:ü"
!! __@RULENAME@__  
 %{UÜ%}:ü <=>  FT _ ;

! Vowel harmony suffixes Back
"%{UÜ%}:u"
!! __@RULENAME@__  
 %{UÜ%}:u <=>  BT _ ;

"%{OÖ%}:ö"
!! __@RULENAME@__  
 %{OÖ%}:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"%{OÖ%}:o"
!! __@RULENAME@__  
 %{OÖ%}:o <=>  BT _ ;

! Vowel harmony suffixes Front
"%{ÕE%}:e"
!! __@RULENAME@__
 %{ÕE%}:e <=>  FT _ ;

! Vowel harmony suffixes Back
"%{ÕE%}:õ"
!! __@RULENAME@__
 %{ÕE%}:õ <=>  BT _ ;


!! !VOWEL LOWERING

"u:o"
!! __@RULENAME@__
 u:o <=>  u: _ %^LowerVow: ;
    _ u: %^LowerVow: ;

"ü:ö"
!! __@RULENAME@__
 ü:ö <=>  ü: _ %^LowerVow: ;
    _ ü: %^LowerVow: (%^WGStem:) [ # | %> [ :Cns | [ :Vow - :i ]] ] ;
   # Cns:* _ Cns: Vow: %^LowerVow: ;
!!€# pügä%^LowerVow%^WGStem%^VowRM%>%{ÕE%}%>t
!!€0 pö00000%>e%>t
!!€# süü%^LowerVow%^WGStem%>n%{UÜ%}q
!!€0 söö00%>nüq

"u2õ"
!! __@RULENAME@__
 u:õ <=> u: _ %> i ;

!! !VOWEL RAISING
"ö:ü"
!! __@RULENAME@__
 ö:ü <=> Vow %^V7: 0: _ %^StrGStem: %> i: ;
!!€# hä%^V70ö%^StrGStem%>i
!!€0 hävvü0%>0

!! !VOWEL CHANGE WITH PLURAL

"ʼ2e"
!! __@RULENAME@__
!! {{{
 ʼ:e <=>  _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: )  %> \[ :Vow | Vow: ]  ;
!!€# särgʼ%^WGStem#
!!€0 sär0e0#
!! }}}

"ʼ2i"
!! __@RULENAME@__
!! {{{
 ʼ:i <=>  _ ( %^XStrGStem: | %^StrGStem: | %^WGStem: ) %^PLPRT: ;
!!€# särgʼ%^WGStem%^PLPRT
!!€0 särgi00
!! }}}

"e2i"
!! __@RULENAME@__
 e:i <=>  # Cns* _ e: [ l | n | r ] %^VOWRaise: ;
  # Cns* e: _ [ l | n | r ] %^VOWRaise: ;
!!€# keel%^VOWRaiseʼ
!!€# kiil0ʼ
  

"o2u"
!! __@RULENAME@__
 o:u <=>  # Cns* _ o: [ l | n | r ] %^VOWRaise: ;
 # Cns* o: _ [ l | n | r ] %^VOWRaise: ;


"i2e"
!! __@RULENAME@__
!! {{{
 i:e <=> Cns 0: _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€# pin0i%^StrGStem%^PLPRT
!!€0 pinne00
 # Cns* _ i: l %^LowerVow: ;
 # Cns* i: _ l %^LowerVow: ;
!!€# kiil%^LowerVow%>%{ÕE%}
!!€# keel0%>e
!! }}}

"ü2e"
!! __@RULENAME@__
!! {{{
 ü:e <=>  _ ( %^XStrGStem: | %^StrGStem: ) %^U2E: ;
!!€# häbü%^StrGStem%^PLPRT
!!€0 häpe00
    ü: _ %> i ;
!! }}}

"ü2ä"
!! __@RULENAME@__
!! {{{
 ü:ä <=>  _ ( %^XStrGStem: | %^StrGStem: ) %^U2A: ;
!!€# häbü%^StrGStem%^PLPRT
!!€0 häpä00
!! }}}

"a2o"
!! __@RULENAME@__
!! {{{
 a:o <=>   _ ( %^XStrGStem: | %^StrGStem: ) %^PLPRT: ;
!!€# kan0a%^StrGStem%^PLPRT
!!€0 kanno00
!! }}}


"õ2y"
!! __@RULENAME@__
!! {{{
 õ:y <=>   _ Cns:0 a (%^WGStem:) ;
!!€# sõda%^WGStem
!!€0 sy0a0
!! }}}

!! !VOWEL LOSS
"a:0"
!! __@RULENAME@__
 a:0 <=>  Cns: _ [ (%^StrGStem:|%^WGStem:) (0:) %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
      a _ %>  i  ; !!= @CODE@ 
!!€# saa%>i
!!€0 sa0%>i
!!€# sõima0%^VowRM%>si
!!€0 sõim0s0%>si
  Cns:  _ Cns: (ʼ:) %^PenVowRM: ;
!!€# puhkas%^PenVowRM%>a%>ma
!!€0 puhk0s0%>a%>ma


"ä:0"
!! __@RULENAME@__
 ä:0 <=>  [ Cns: | :Cns ] _ [ (0:|:0+) %^VowRM: | Cns:0 %^VowRM: %^CnsRM:  %> | [%^StrGStem: | %^XStrGStem: ] %> i  ] ; 
 ä _ %>  i  ; !!= @CODE@ 
!!€# elä%>et
!!€0 el0%>et
  Cns: _ Cns: (ʼ:) %^PenVowRM: ;
!!€# tütär%^PenVowRM%>e
!!€0 tüt0r0%>e

"u:0"
!! __@RULENAME@__
!! {{{
 u:0 <=>  Cns: _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!!€# tervüs%^VowRM%^CnsRM%>i%>t
!!€0 terv0000%>i%>t
      _ u: (%^VowRM:) %> i ;
!!€# kuu%^VowRM%>i%>d
!!€0 k0u0%>i%>d
!! }}}

"ü:0"
!! __@RULENAME@__
!! {{{
 ü:0 <=>  [ Cns: | Vow: ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
!!€# tüü%^VowRM%>hüq
!!€0 tü00%>hüq
!!€# tervüs%^VowRM%^CnsRM%>i%>t
!!€0 terv0000%>i%>t
         _ ü: (%^LowerVow:) %> i ;
!! }}}

"e:0"
!! __@RULENAME@__
 e:0 <=>  [ Cns: | :Cns ] _ [ %^VowRM: | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
           [ Cns: | :Cns ] _ l %^PenVowRM:  ;

"o:0"
!! __@RULENAME@__
 o:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:|%^StrGStem:) %^VowRM: (%{ÕE%}:) ]   ; 

"i:0"
!! __@RULENAME@__
 i:0 <=>  [ Cns: | :Cns ] _ [ (%^WGStem:) %^VowRM: (%{ÕE%}:) | Cns:0 %^VowRM: %^CnsRM: ]  %> ; 
          Vow l 0:l %>: _ %{ÕE%}: ;
       a _ g: %^WGStem: %> %{OÖ%}: ;
!!€# aig%^WGStem%>%{OÖ%}
!!€0 a000%>o
    Vow [ %^I7: | %^V7: 0: ] Vow: %^StrGStem: %> _ ;
!!€# va%^I7o%^StrGStem%>i
!!€0 vaio0%>0
!!€# hä%^V70ö%^StrGStem%>i
!!€0 hävvü0%>0


"õ:0"
!! __@RULENAME@__
 õ:0 <=>  [ Cns: | :Cns ] _ [ l | r ] (%^StrGStem:) %^PenVowRM:  ;
!!€# tapõl%^PenVowRM%{ÕE%}%>ma
!!€0 tap0l0õ%>ma
!!€# kannõl%^StrGStem%^PenVowRM%>%{ÕE%}
!!€0 kand0l00%>õ
!!€# kogõr%^StrGStem%^PenVowRM%>%{ÕE%}
!!€0 kok0r00%>õ
   _ (%^WGStem:|%^StrGStem:) %^VowRM:  ;
!!€# hammõ%^WGStem%^VowRM%>ihe
!!€0 ham0000%>ihe

!! VOWEL LENGTHENING

"%{XV%}:u"
!! __@RULENAME@__
 %{XV%}:u <=> :u ( %- ) _  ;

"%{XV%}:a"
!! __@RULENAME@__
 %{XV%}:a <=> :a ( %- ) _  ;
       [ o | a ] %> h _ ;

"%{XV%}:ä"
!! __@RULENAME@__
 %{XV%}:ä <=> :ä ( %- ) _  ;
       [ ö | ä ] %> h _ ;

"%{XV%}:õ"
!! __@RULENAME@__
 %{XV%}:õ <=> :õ ( %- ) _  ;
        õ %> h _ ;

"%{XV%}:e"
!! __@RULENAME@__
 %{XV%}:e <=>  e %> h _ ;


!!
"i2j"
!! __@RULENAME@__
 i:j <=> Vow Cns:+ Vow _ (%^WGStem:) %> :Vow ;
! Vow _ (%^WGStem:) %> :Vow ;
         Cns _ %^I2J: ;
!         Cns _ %^StrGStem: %> :Vow ;

"i2ʼ"
!! __@RULENAME@__
 i:ʼ <=>
  Cns _ %^I2ʼ: ;
! Cns _ %^WGStem: %> :Vow ;


"u2v"
!! __@RULENAME@__
 u:v <=> u _ %> v a q ;
         u _ d: %^WGStem: ;

"ü2v"
!! __@RULENAME@__
 ü:v <=> ü _ %> v ä q ;
         ü _ d: %^WGStem: ;
!!€# püüd%^WGStem%>et
!!€0 püvv0%>et


"%^I7:i"
!! __@RULENAME@__
 %^I7:i <=> Vow _ Vow %^StrGStem: %> [ i: | q ] ;
!!€# va%^I7o%^StrGStem%>i
!!€0 vaio0%>0

"%^V7:v with 0:v"
!! __@RULENAME@__
 %^V7:v <=> Vow _ 0: Vow %^StrGStem: %> i: ;
!!€# hä%^V70ö%^StrGStem%>i
!!€0 hävvü0%>0


!! SECONDARY CONSONANT LENGTHENING

"0:m"
!! __@RULENAME@__
 0:m <=>
         Vow m _ (Vow:) %^StrGStem: ;
!!nimi+N+Sg+Par __name__
!!€# nim0%^StrGStem%>e
!!€0 nimm0%>e

"0:p"
!! __@RULENAME@__
!! {{{
 0:p <=> Vow b: _ Vow: %^XStrGStem: ;
!!€# häb0ü%^XStrGStem
!!€0 häppü00
!! }}}

"0:v"
!! __@RULENAME@__
!! {{{
 0:v <=> Vow %^V7: _ Vow: %^StrGStem: ;
!!€# hä%^V70ö%^StrGStem%>i
!!€0 hävvü0%>0
!! }}}


"0:n"
!! __@RULENAME@__
 0:n <=> Vow n _ Vow: %^StrGStem: ;

!        Vow n _ :Vow %> j:i ;
!	 Vow n _ %> i: (%{ÕE%}: ) [ %> | # ] ;

"0:t"
!! __@RULENAME@__
!! {{{
 0:t <=> Vow d: _ Vow: %^XStrGStem: ;
!!€# sõd0a%^XStrGStem%^PLPRT
!!€0 sõtto00
!! }}}

"0:s"
!! __@RULENAME@__
 0:s <=> CnsVoiced Vow: _ %^VowRM: %> s i ;
!!€# sõima0%^VowRM%>si
!!€0 sõim0s0%>si
   Vow s: _ (ʼ:) (%^PenWGStem:) %^StrGStem: ;
!!€# nõkõs0ʼ%^PenWGStem%^StrGStem%>õ
!!€0 nõgõss000%>õ
   Vow s: _ Vow: %^StrGStem: ;
!!€# kos0i%^StrGStem%>q
!!€0 kossi0%>q

"0:l"
!! __@RULENAME@__
 0:l <=> Vow l _ :Vow %> j:i ;
         Vow l _ Vow: %^StrGStem: ;
!	 Vow l _ %> i: (%{ÕE%}: ) [ %> | # ] ;

"0:k"
!! __@RULENAME@__
 0:k <=> Vow g: _ Vow: %^XStrGStem: ;
!!€# ig0ä%^XStrGStem
!!€0 ikkä0
   Vow k _ Vow: %^StrGStem: ;
!!€# mak0õ%^StrGStem%^VowRM%>i%>dõ
!!€0 makkõ00%>i%>dõ

"%{XC%}:s"
!! __@RULENAME@__
 %{XC%}:s <=> s: _ ;

"%{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=> l: _ ;

"%{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=> k: _ ;


!! !Consonant weakening 

! aika: ajan
"kToZero"
!! __@RULENAME@__  
k:0 <=> h _ Vow: s %^PenWGStem: ;
!!€# puhkas%^WGStem%>taq
!!€0 puh0as0%>taq

 s _ a %^PenWGStem: ;
! [  #  Cns* Vow (( r | i | VowNonHigh ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;
![ [ # ] Cns* Vow (( r | i ) k | t | h | l | r | s ) | Vow :Cns+ :Vow+ k ] _ [ Vow: | :Vow | ( Vow ) %> i ] ( %> i: | %^StretchSyll2:0 ) %> %^WCns:0 ;
!        [ # ] Cns* VowBack i: _ [ GRC | %^TS:0 i %> %^WCns:0 ] ;

"mToZero"
!! __@RULENAME@__
 m:0 <=> m_ Vow: %^PenWGStem: ;
!!€# hammõ%^PenWGStem%>h
!!€0 ham0õ0%>h


"lToZero"
!! __@RULENAME@__
 l:0 <=> l _ Vow i: %^PenWGStem: ;
         _ %{XC%}:l %{ÕE%}: v %^PenWGStem: ;

"rToZero"
!! __@RULENAME@__
 r:0 <=> r _ Vow %^PenWGStem: ;
!!€# perre%^WGStem%>h
!!€0 per0e0%>h

"tToZero"
!! __@RULENAME@__
 t:0 <=> [ h | t ] _ Vow %^PenWGStem: ;
         Vow _ s (ʼ:) %^TS2S: ;

!! !CONSONANT QUALITY CHANGE

"p2b"
!! __@RULENAME@__
 p:b <=> Vow _ Vow Cns:* (ʼ:) %^PenWGStem: ;

"b20"
!! __@RULENAME@__
 b:0 <=> Vow _ Vow  %^PenWGStem: ;

"bTop"
!! __@RULENAME@__
 b:p <=> 
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


"b2m"
!! __@RULENAME@__
 b:m <=> m _  %^WGStem: ;
 m _ Vow: %^PenWGStem: ;
!!€# hamba%^PenWGStem%>s
!!€0 hamma0%>s

"d2n"
!! __@RULENAME@__
 d:n <=> n _ %^WGStem: ;

"dTol"
!! __@RULENAME@__
 d:l <=> # Cns* Vow l %> _ [a|ä] q ;
!!€# tul%>daq
!!€0 tul%>laq
!!€# kõnõl%>daq
!!€0 kõnõl%>daq

"d2v"
!! __@RULENAME@__
 d:v <=> [ ü: |u: ] _ %^WGStem: ;
!!€# püüd%^WGStem%>et
!!€0 püvv0%>et

"dTot"
!! __@RULENAME@__
 d:t <=> [ s | h ] (Vow: %^WGStem: %^VowRM:) %> _ ;
    Vow _ (s) (Vow:) %^StrD2T: ;
!!käsi+N+Sg+Par __hand, arm__
!!€# käd%^StrD2T%>t
!!€0 kät0%>t
!!€# hüdsi%^StrD2T%>l
!!€0 hütsi0%>l
    Vow _ (s) Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;


"dTos"
!! __@RULENAME@__
 d:s <=> _ %^TS: ;


"tTos"
!! __@RULENAME@__
 t:s <=> _ %^TS: ;

"tTod"
!! __@RULENAME@__
 t:d <=>
 Vow _ s (ʼ:) %^WGStem: ;
 Vow [(%^StrGStem:| Cns:0 %^CnsRM:) (%>) Vow | Vow (%^WGStem:)] %> _ [%> | # | :Vow ] ;
         Vow _ [Vow: %^PenWGStem:|%^WGStem: (%>) :Vow ] ;

"nTod"
!! __@RULENAME@__
 n:d <=> n _ õ: l %^PenVowRM: ;

!!€# kannõl%^PenVowRM%>%{ÕE%}
!!€0 kand0l0%>õ


"j2i"
!! __@RULENAME@__
 j:i <=> :Vow %> _ [ # | %- ];

"k2g"
!! __@RULENAME@__
 k:g <=> Vow _ %^WGStem: ;
!!€# hõrak%^WGStem%>a
!!€0 hõrag0%>a
     Vow _ Vow Cns:+ (ʼ:) %^PenWGStem: ;
!!€# süküs%^PenWGStem%>e
!!€0 sügüs0%>e
!!€# nõkõs0ʼ%^PenWGStem%^StrGStem%>õ
!!€0 nõgõss000%>õ

"gTok"
!! __@RULENAME@__
 g:k <=> 
    Vow _ Vow: %^StrGStem: ;
    Vow _ 0: Vow: %^XStrGStem: ;
!!€# ig0ä%^XStrGStem
!!€0 ikkä0
   Vow _ Vow: [ l | r ] %^StrGStem: %^PenVowRM: ;
!!€# kogõr%^StrGStem%^PenVowRM%>%{ÕE%}
!!€0 kok0r00%>õ


!! !STEM-FINAL CONSONANT LOSS
"s20"
!! __@RULENAME@__
 s:0 <=> [ u: | ü: | a: | ä: ] _ (%^VowRM:)  %^CnsRM:  ;

"d20"
!! __@RULENAME@__
 d:0 <=>
         Vow _ s (ʼ:|Vow:) %^TS2S: ;
!!€# hüdsi%^TS2S%^VowRM%>te
!!€0 hü0s000%>te
          Vow: _ Vow: %^WGStem: ;

"g20"
!! __@RULENAME@__
 g:0 <=>  _ ( ʼ: ) %^WGStem: ;
!!€# särgʼ%^WGStem%>#
!!€0 sär0e0%>0
 # Cns:*  Vow: _ [ %{ÕE%}: |ä: ] (%^LowerVow:) %^WGStem: ;
!!€# igä%^WGStem
!!€0 i0ä0
!!€# aig%^WGStem%>%{OÖ%}
!!€0 a000%>o
 Vow l _ Vow %^WGStem: ;
!!€# palgõ%^WGStem%>h
!!€0 pal0õ0%>h

!! !Other marks
"ʼ:0"
!! __@RULENAME@__
 ʼ:0 <=>  :Cns _ (%^WGStem:|%^PenWGStem:) (%^StrGStem:|%^TS2S:) %> [:Vow|t] ;
!!€# kipõnʼ%>a
!!€0 kipõn0%>a
!!€# nõkõs0ʼ%^PenWGStem%^StrGStem%>õ
!!€0 nõgõss000%>õ
